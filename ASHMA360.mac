*          DATA SET ASHMA360E  AT LEVEL 004 AS OF 09/27/10
MACRO
* DATA DESCRIPTION
FILE FILEA
  PROPA       4   8 P     HEADING ('PIN')
  SFINDA     13   1 A
  TAXCDA     14   3 P
  SRFRZA     57   5 P 0   HEADING ('S F AMT')
FILE FILEB
  PROPB       1  14 N
FILE FILEC  SEQUENTIAL VBS (18710 6447)
* WORKING STORAGE FIELDS
 INCNT     W 4 P
 MTCCNT    W 4 P
 OUTCNT    W 4 P
 UPDCNT    W 4 P
 UNCNT     W 4 P
 WTXCD     W 5 N
 WTOWN  WTXCD +0 2 N      HEADING ('TOWN')
*
JOB INPUT (FILEA KEY(PROPA) +
           FILEB KEY(PROPB)) FINISH EOJ
IF FILEA
   INCNT = INCNT + 1
END-IF
IF MATCHED
   MTCCNT = MTCCNT + 1
   IF SRFRZA NE 0
      WTXCD = TAXCDA
      PRINT RPT
      SRFRZA = 0
      SFINDA = ' '
      UPDCNT = UPDCNT + 1
*     DISPLAY 'FILEC PROP UPD ' PROPA

      RECORD-LENGTH (FILEC) = RECORD-LENGTH (FILEA)
      PUT FILEC FROM FILEA
      OUTCNT   = OUTCNT + 1
      GOTO JOB
   ELSE
      RECORD-LENGTH (FILEC) = RECORD-LENGTH (FILEA)
      PUT FILEC FROM FILEA
      OUTCNT   = OUTCNT + 1
      GOTO JOB
   END-IF
END-IF

IF FILEA
   OUTCNT   = OUTCNT + 1
   RECORD-LENGTH (FILEC) = RECORD-LENGTH (FILEA)
   PUT FILEC FROM FILEA
   GOTO JOB
END-IF

IF FILEB
   UNCNT    = UNCNT + 1
*  DISPLAY 'UNMATCHED ''NO'' RECORD.  PIN = ' PROPB
END-IF
EOJ. PROC
DISPLAY 'TOTALS FOR ASHMA360 **********'
DISPLAY ' '
DISPLAY 'MATCHED RECORDS             ' MTCCNT
DISPLAY 'UPDATED RECORDS             ' UPDCNT
DISPLAY ' '
DISPLAY ' '
DISPLAY 'MASTER U RECS. READ       = ' INCNT
DISPLAY 'MATCHED ''NO'' RECORDS    = ' MTCCNT
DISPLAY 'MASTER U RECS. UPDATED    = ' UPDCNT
DISPLAY 'MASTER U RECS. WRITTEN    = ' OUTCNT
DISPLAY 'UNMATCHED ''NO'' RECORDS  = ' UNCNT
END-PROC
REPORT RPT
SEQUENCE
TITLE 1 '                              ASHMA360             '
TITLE 2 '                 OFFICE OF THE COOK COUNTY ASSESSOR'
TITLE 3 '          MASTER FILE RECORDS MATCHED TO THE ''NO'' FILE'
LINE 1 WTOWN PROPA SRFRZA
