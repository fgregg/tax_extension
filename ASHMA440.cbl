00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASHMA440.
00003  AUTHOR. EUGENE J HARBUT.
00004 *REMARKS. THIS PROGRAM WILL CREATE THE HOMESTEAD SEQUENTIAL MASTER
00005 *         USING THE VSAM HOMESTEAD MASTER AS INPUT.
00006  DATE-WRITTEN.  SEP 30,1977.
00007  DATE-COMPILED. OCT  3,1977.
00008  ENVIRONMENT DIVISION.
00009  CONFIGURATION SECTION.
00010  SOURCE-COMPUTER. IBM-370.
00011  OBJECT-COMPUTER. IBM-370.
00012  INPUT-OUTPUT SECTION.
00013  FILE-CONTROL.
00014      SELECT V-HOMESTEAD  ASSIGN TO DA-VSAMHMST
00015             ORGANIZATION IS INDEXED
00016             ACCESS MODE  IS SEQUENTIAL
00017             FILE STATUS  IS FILE-STATUS
00018             RECORD KEY   IS V-REC-KEY.
00019      SELECT PRINT-FILE   ASSIGN TO UT-S-PRINTER.
00020      SELECT HOMESTEAD    ASSIGN TO UT-S-HMSTMSTR.
00021  DATA DIVISION.
00022  FILE SECTION.
00023  COPY FDVSAMHMST.
00024  COPY FDHMSTDMS.
00025  FD  PRINT-FILE
00026      RECORDING  MODE IS  F
00027      RECORD CONTAINS 133 CHARACTERS
00028      BLOCK  CONTAINS 0   RECORDS
00029      LABEL  RECORDS  ARE OMITTED
00030      DATA   RECORD   IS  PRT-REC.
00031  01  PRINT-REC          PIC X(133).
00032  WORKING-STORAGE SECTION.
00033  77  SAVE-PROP          PIC 9(15)    COMP-3 VALUE 0.
00034  77  READ-COUNT         PIC S9(7)    COMP-3 VALUE +0.
00035  77  WRITE-COUNT        PIC S9(7)    COMP-3 VALUE +0.
00036  77  ERROR-COUNT        PIC S9(7)    COMP-3 VALUE +0.
00037  77  CO-OP-COUNT        PIC S9(7)    COMP-3 VALUE +0.
00038  77  NON-COOP-COUNT     PIC S9(7)    COMP-3 VALUE +0.
00039  77  TWN                PIC S9(4)    COMP   VALUE +0.
00040  77  CDE                PIC S9(4)    COMP   VALUE +0.
00041  77  SUB                PIC S9(4)    COMP   VALUE +0.
00042  77  FILE-STATUS        PIC 99              VALUE 0.
00043      88 NORMAL-I-O      VALUE 0.
00044  77  BLNK               PIC X               VALUE SPACE.
00045  01  TAXCODE-UNPK       PIC 9(5).
00046  01  TAXCODE-UNPKX REDEFINES TAXCODE-UNPK.
00047      05 TOWNX           PIC 9(2).
00048      05 FILLER          PIC X(3).
00049  01  PRINT-LINE.
00050      05 FILLER          PIC X(11)    VALUE SPACES.
00051      05 PRT-TOWN-NAME   PIC X(13).
00052      05 FILLER          PIC X(9)     VALUE SPACES.
00053      05 PRT-NON-COOP    PIC Z,ZZZ,ZZ9.
00054      05 FILLER          PIC X(22)    VALUE SPACES.
00055      05 PRT-CO-OP       PIC Z,ZZZ,ZZ9.
00056  01  HDR-1.
00057      05 FILLER          PIC X(9)     VALUE ' ASHMA440'.
00058      05 FILLER          PIC X(47)    VALUE SPACES.
00059      05 FILLER          PIC X(14)    VALUE 'OFFICE OF THE '.
00060      05 FILLER          PIC X(8)     VALUE 'ASSESSOR'.
00061  01  HDR-2.
00062      05 FILLER          PIC X(6)     VALUE ' DATE '.
00063      05 HDR-DATE        PIC X(8).
00064      05 FILLER          PIC X(40)    VALUE SPACES.
00065      05 FILLER          PIC X(19)    VALUE 'HOMESTEAD TOWNSHIP '.
00066      05 FILLER          PIC X(6)     VALUE 'COUNTS'.
00067  01  HDR-3.
00068      05 FILLER          PIC X(11)    VALUE SPACES.
00069      05 FILLER          PIC X(8)     VALUE 'TOWNSHIP'.
00070      05 FILLER          PIC X(9)     VALUE SPACES.
00071      05 FILLER          PIC X(19)    VALUE 'NON COOP EXEMPTIONS'.
00072      05 FILLER          PIC X(14)    VALUE SPACES.
00073      05 FILLER          PIC X(15)    VALUE 'COOP EXEMPTIONS'.
00074  01  TOWN-INFORMATION-TABLE.
00075  COPY TWNINFOTBL.
00076  01  TOWN-COUNT-TABLE.
00077      05 TWN-CNT-TAB OCCURS 77 TIMES.
00078         10 CODE-COUNT OCCURS 2 TIMES
00079                         PIC S9(7)    COMP-3.
00080      EJECT
00081  PROCEDURE DIVISION.
00082  100-KICK-OFF.
00083      OPEN    INPUT V-HOMESTEAD.
00084      IF      NORMAL-I-O
00085              NEXT SENTENCE ELSE
00086                  DISPLAY 'OPEN I-O ERROR - FILE STATUS = '
00087                  FILE-STATUS
00088                  MOVE 16 TO RETURN-CODE
00089                  STOP RUN.
00090      OPEN    OUTPUT PRINT-FILE
00091                     HOMESTEAD.
00092      MOVE    CURRENT-DATE TO HDR-DATE.
00093      MOVE    +1 TO TWN.
00094  110-ZERO-TABLE.
00095      MOVE    +0 TO CODE-COUNT (TWN, 1)
00096                    CODE-COUNT (TWN, 2).
00097      IF      TWN EQUAL +77
00098              NEXT SENTENCE ELSE
00099                  ADD +1 TO TWN
00100                  GO TO 110-ZERO-TABLE.
00101 ******************************************************************
00102 *               M A I N  L I N E  P R O G R A M                  *
00103 ******************************************************************
00104  120-READ-VSAM-FILE.
00105      READ    V-HOMESTEAD AT END
00106              GO TO 140-PROCESS-TABLE.
00107      IF      NORMAL-I-O
00108              NEXT SENTENCE ELSE
00109                  DISPLAY 'READ I-O ERROR - FILE STATUS = '
00110                  FILE-STATUS ' LAST GOOD RECORD = ' SAVE-PROP
00111                  MOVE 16 TO RETURN-CODE
00112                  STOP RUN.
00113      ADD     +1 TO READ-COUNT.
00114      IF      V-REC-CD EQUAL 1 OR 2
00115              NEXT SENTENCE ELSE
00116                  DISPLAY 'INVALID RECORD CODE - PROP-NO = ' V-PROP
00117                  ' RECORD CODE = ' V-REC-CD ' REC = ' READ-COUNT
00118                  ADD +1 TO ERROR-COUNT
00119                  GO TO 130-WRITE-REC.
00120      MOVE    V-PROP   TO SAVE-PROP.
00121      MOVE    V-TXCD   TO TAXCODE-UNPK.
00122      MOVE    TOWNX    TO TWN.
00123      MOVE    V-REC-CD TO CDE.
00124      ADD     +1 TO CODE-COUNT (TWN, CDE).
00125  130-WRITE-REC.
00126      MOVE    V-REC TO H-REC
00127      ADD     +0 TO H-VOL
00128      ADD     +0 TO H-PROP
00129      ADD     +0 TO H-TXCD
00130      WRITE H-REC.
00131      ADD     +1 TO WRITE-COUNT.
00132      GO      TO 120-READ-VSAM-FILE.
00133 ******************************************************************
00134  140-PROCESS-TABLE.
00135      MOVE    +1  TO SUB.
00136      MOVE    +10 TO TWN.
00137      PERFORM 160-HDR-RTE.
00138  150-PRINT-TOWN-TOTALS.
00139      MOVE    TOWN-NAME  (SUB)    TO PRT-TOWN-NAME.
00140      MOVE    CODE-COUNT (TWN, 1) TO PRT-NON-COOP.
00141      MOVE    CODE-COUNT (TWN, 2) TO PRT-CO-OP.
00142      ADD     CODE-COUNT (TWN, 1) TO NON-COOP-COUNT.
00143      ADD     CODE-COUNT (TWN, 2) TO CO-OP-COUNT.
00144      WRITE   PRINT-REC FROM PRINT-LINE AFTER ADVANCING 1.
00145      IF      TWN EQUAL +39
00146              ADD +31 TO TWN ELSE
00147                  ADD +1 TO TWN.
00148      IF      SUB EQUAL +38
00149              NEXT SENTENCE ELSE
00150                  ADD +1 TO SUB
00151                  GO TO 150-PRINT-TOWN-TOTALS.
00152      MOVE    'TOTALS       ' TO PRT-TOWN-NAME.
00153      MOVE    NON-COOP-COUNT  TO PRT-NON-COOP.
00154      MOVE    CO-OP-COUNT     TO PRT-CO-OP.
00155      WRITE   PRINT-REC FROM PRINT-LINE AFTER ADVANCING 3.
00156      WRITE   PRINT-REC FROM BLNK       AFTER PAGE.
00157      GO      TO 990-EOJ.
00158 ******************************************************************
00159 *                     S U B R O U T I N E S                      *
00160 ******************************************************************
00161  160-HDR-RTE.
00162      WRITE   PRINT-REC FROM HDR-1 AFTER PAGE.
00163      WRITE   PRINT-REC FROM HDR-2 AFTER ADVANCING 2.
00164      WRITE   PRINT-REC FROM HDR-3 AFTER ADVANCING 3.
00165      WRITE   PRINT-REC FROM BLNK  AFTER ADVANCING 1.
00166  990-EOJ.
00167      DISPLAY 'TOTAL RECORDS READ      = ' READ-COUNT.
00168      DISPLAY 'TOTAL RECORDS WRITTEN   = ' WRITE-COUNT.
00169      DISPLAY 'TOTAL NON-CO-OP RECORDS = ' NON-COOP-COUNT.
00170      DISPLAY 'TOTAL CO-OP RECORDS     = ' CO-OP-COUNT.
00171      DISPLAY 'TOTAL ERROR RECORDS     = ' ERROR-COUNT.
00172      CLOSE   V-HOMESTEAD.
00173      CLOSE   HOMESTEAD.
00174      CLOSE   PRINT-FILE.
00175      STOP    RUN.
