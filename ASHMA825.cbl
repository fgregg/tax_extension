00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASHMA825.
00003 *AUTHOR. SQUILLACE - MOLIS
00004 *DATE-WRITTEN.  APRIL,1995
00005 *REMARKS. THIS PROGRAM WILL UPDATE THE FREEZE FILE WITH
00006 *           HOMESTEAD DATA
00007      SKIP2
00008  ENVIRONMENT DIVISION.
00009  CONFIGURATION SECTION.
00010  SOURCE-COMPUTER. IBM-370.
00011  OBJECT-COMPUTER. IBM-370.
00012  INPUT-OUTPUT SECTION.
00013  FILE-CONTROL.
00014      SELECT  SR-HOMESTEAD-FILE
00015               ASSIGN  TO UT-S-SENIOR.
00016      SELECT  HOMEOWNER-EXEMPT
00017               ASSIGN TO UT-S-HOMEIN.
00018      SELECT  SENIORFREEZE
00019               ASSIGN TO UT-S-SRFREEZE.
00020      SELECT  PRNT-FILE
00021               ASSIGN  TO UT-S-PRNTOUT.
00022      SKIP2
00023  DATA DIVISION.
00024  FILE SECTION.
00025  COPY ASHMSTFD01.
00026      EJECT
00027  COPY ASHMOWFD01.
00028      EJECT
00029  COPY ASSFHHFD01.
00030      SKIP2
00031  FD  PRNT-FILE
00032      BLOCK CONTAINS 0 RECORDS
00033      RECORD CONTAINS 133 CHARACTERS
00034      LABEL RECORDS ARE STANDARD
00035      RECORDING MODE IS F
00036      DATA RECORD IS PRNT-REC.
00037  01  PRNT-REC               PIC X(133).
00038      EJECT
00039  WORKING-STORAGE SECTION.
00040      SKIP1
00041  77  PAGE-CNTR         PIC S9(5) VALUE +0    PACKED-DECIMAL.
00042  77  SUB               PIC S9(4) VALUE +0    BINARY.
00043  77  LINE-CNTR         PIC S9(3) VALUE +54   PACKED-DECIMAL.
00044      88  PRINT-HEADER            VALUE +54 THRU +59.
00045  77  HMSD-RD           PIC S9(7) VALUE +0    PACKED-DECIMAL.
00046  77  HOME-RD           PIC S9(7) VALUE +0    PACKED-DECIMAL.
00047  77  HOMEOWNER-USED-SW PIC X     VALUE 'N'.
00048  77  RECCD-CNT1        PIC S9(7) VALUE +0    PACKED-DECIMAL.
00049  77  RECCD-CNT2        PIC S9(7) VALUE +0    PACKED-DECIMAL.
00050  77  SRFREEZE-WR       PIC S9(7) VALUE +0    PACKED-DECIMAL.
00051  77  SET-HOME-SW       PIC X     VALUE 'N'.
00052      88  READ-NEXT-HOME          VALUE 'N'.
00053  77  SET-HMSD-SW       PIC X     VALUE 'N'.
00054      88  READ-NEXT-HMSD          VALUE 'N'.
00055  77  HOME-VALW-SW      PIC X     VALUE 'N'.
00056  77  CHECK-PROCESSING-SW PIC X   VALUE 'Y'.
00057      88  STOP-PROCESSING         VALUE 'N'.
00058      EJECT
00059  01  WS-ZIP            PIC 9(9).
00060  01  FILLER REDEFINES WS-ZIP.
00061      05  WS-ZIP-HO5    PIC X(5).
00062      05  WS-ZIP-LO4    PIC 9(4).
00063  01  WORK-AREA.
00064      05  WS-DATE.
00065          10  YY                      PIC 9(2)    VALUE ZEROS.
00066          10  MM                      PIC 9(2)    VALUE ZEROS.
00067          10  DD                      PIC 9(2)    VALUE ZEROS.
00068      05  WS-DATE-MMDDYY.
00069          10  MM                      PIC 9(2)    VALUE ZEROS.
00070          10  FILLER                  PIC X       VALUE '/'.
00071          10  DD                      PIC 9(2)    VALUE ZEROS.
00072          10  FILLER                  PIC X       VALUE '/'.
00073          10  YY                      PIC 9(2)    VALUE ZEROS.
00074      SKIP1
00075      05  HOME-KEY.
00076          88  HOME-EOF            VALUE HIGH-VALUES.
00077          10  CURR-HOME-KEY.
00078              15 HK-TWN      PIC 99.
00079              15 HK-VOL      PIC 9(3).
00080              15 HK-PROP     PIC 9(15).
00081              15 HK-TXTYP    PIC 9.
00082 *            15 HK-RECCD    PIC 9.
00083          10  PREV-HOME-KEY  PIC X(21) VALUE LOW-VALUES.
00084      SKIP1
00085      05  HMSD-KEY.
00086          88  HMSD-EOF            VALUE HIGH-VALUES.
00087          10  CURR-HMSD-KEY.
00088              15 SK-TWN      PIC 99.
00089              15 SK-VOL      PIC 9(3).
00090              15 SK-PROP     PIC 9(15).
00091              15 SK-TXTYP    PIC 9.
00092 *            15 SK-RECCD    PIC 9.
00093          10  PREV-HMSD-KEY  PIC X(21) VALUE LOW-VALUES.
00094          SKIP1
00095      05  BRK-TXCD                PIC 9(5).
00096      05  FILLER REDEFINES BRK-TXCD.
00097          10 BRK-TWN              PIC 99.
00098      SKIP1
00099      05  BRK-TXCD2               PIC 9(5).
00100      05  FILLER REDEFINES BRK-TXCD2.
00101          10 BRK-TWN2             PIC 99.
00102      SKIP1
00103      05  WS-PROP            PIC 9(14).
00104      05  WS-PROP-R REDEFINES WS-PROP.
00105          10  WS-PROP1       PIC 9(2).
00106          10  WS-PROP2       PIC 9(2).
00107          10  WS-PROP3       PIC 9(3).
00108          10  WS-PROP4       PIC 9(3).
00109          10  WS-PROP5       PIC 9(4).
00110      EJECT
00111  01  PRINT-LINES.
00112      SKIP1
00113      05  HDR-LINE1.
00114          10  FILLER    PIC X(3)  VALUE SPACES.
00115          10  H1-DATE   PIC X(8).
00116          10  FILLER    PIC X(44) VALUE SPACES.
00117          10  FILLER    PIC X(19) VALUE 'OFFICE OF THE COOK'.
00118          10  FILLER    PIC X(46) VALUE 'COUNTY ASSESSOR'.
00119          10  FILLER    PIC X(5)  VALUE 'PAGE '.
00120          10  H1-PAGE   PIC ZZ,ZZ9.
00121      SKIP1
00122      05  HDR-LINE2.
00123          10  FILLER    PIC X(53) VALUE '   ASHMA825'.
00124          10  FILLER    PIC X(24) VALUE 'SENIOR FREEZE FILE CREAT'.
00125          10  FILLER    PIC X(16) VALUE 'ION ERROR REPORT'.
00126      SKIP1
00127      05  HDR-LINE3.
00128          10  FILLER    PIC X(09) VALUE SPACES.
00129          10  FILLER    PIC X(52) VALUE
00130          'PERMANENT INDEX     TAX  RECORD   BIRTH'.
00131      SKIP1
00132      05  HDR-LINE4.
00133          10  FILLER    PIC X(02) VALUE SPACES.
00134          10  FILLER    PIC X(47) VALUE
00135          'VOL         NUMBER        TYPE   CODE     DATE'.
00136      SKIP1
00137      05  DETAIL-LINE             VALUE SPACES.
00138          10  FILLER    PIC X(2).
00139          10  D-VOL     PIC 999.
00140          10  FILLER    PIC XXX.
00141          10  D-PROP    PIC 99,99,999,999,9999.
00142          10  FILLER    PIC X(4).
00143          10  D-TXTYP   PIC 9.
00144          10  FILLER    PIC X(5).
00145          10  D-RECCD   PIC 9.
00146          10  FILLER    PIC X(5).
00147          10  D-BRTH-DTE PIC 99,99,99.
00148          10  FILLER    PIC X(6).
00149          10  D-MESG    PIC X(50).
00150      SKIP1
00151      05  FINAL-LINE.
00152          10  FILLER    PIC X(2)  VALUE SPACES.
00153          10  FIN-TOT   PIC Z,ZZZ,ZZ9.
00154          10  FILLER    PIC X(2)  VALUE SPACES.
00155          10  FIN-MSG   PIC X(50).
00156      EJECT
00157  PROCEDURE DIVISION.
00158  020-BEGIN.
00159      OPEN  INPUT  SR-HOMESTEAD-FILE
00160            INPUT  HOMEOWNER-EXEMPT
00161      OPEN  OUTPUT SENIORFREEZE
00162            OUTPUT PRNT-FILE
00163      ACCEPT WS-DATE FROM DATE
00164      MOVE CORR WS-DATE TO WS-DATE-MMDDYY
00165      MOVE WS-DATE-MMDDYY TO H1-DATE
00166      SKIP1
00167      PERFORM  700-RD-HOME
00168      PERFORM  750-RD-HMSD
00169      SKIP1
00170      PERFORM  100-MAINLINE
00171                UNTIL  (HMSD-EOF AND HOME-EOF)
00172                OR STOP-PROCESSING.
00173      SKIP1
00174      PERFORM 850-PRINT-TOTALS
00175      SKIP1
00176      CLOSE   SR-HOMESTEAD-FILE
00177              HOMEOWNER-EXEMPT
00178              SENIORFREEZE
00179              PRNT-FILE
00180      STOP RUN.
00181      SKIP3
00182  100-MAINLINE.
00183      IF CURR-HOME-KEY GREATER THAN CURR-HMSD-KEY
00184         PERFORM 300-HOME-HIGH-RTN
00185         MOVE 'N' TO HOMEOWNER-USED-SW
00186         MOVE 'Y' TO SET-HOME-SW
00187         MOVE 'N' TO SET-HMSD-SW
00188      ELSE
00189         IF CURR-HOME-KEY EQUAL CURR-HMSD-KEY
00190            MOVE 'Y' TO HOMEOWNER-USED-SW
00191            PERFORM 400-MATCH-ROUTINE
00192            MOVE 'Y' TO SET-HOME-SW
00193            MOVE 'N' TO SET-HMSD-SW
00194         ELSE
00195            IF HOMEOWNER-USED-SW = 'Y'
00196               MOVE 'N' TO HOMEOWNER-USED-SW
00197               MOVE 'N' TO SET-HOME-SW
00198               MOVE 'Y' TO SET-HMSD-SW
00199            ELSE
00200               MOVE HE-REC TO SENFRZ-REC
00201               MOVE ZEROS TO SR-HMCNTR
00202               WRITE SENFRZ-REC
00203               ADD +1 TO SRFREEZE-WR
00204               MOVE 'N' TO SET-HOME-SW
00205               MOVE 'Y' TO SET-HMSD-SW.
00206      SKIP1
00207      IF  READ-NEXT-HOME
00208          PERFORM 700-RD-HOME.
00209      SKIP1
00210      IF  READ-NEXT-HMSD
00211          PERFORM 750-RD-HMSD.
00212      SKIP3
00213  300-HOME-HIGH-RTN.
00214      PERFORM 650-MOVE-HMSD-TO-PRNT
00215      MOVE  'NO-MATCHING HOMEOWNER RECORD' TO D-MESG
00216      PERFORM  800-PRINT-DETAIL.
00217      SKIP3
00218  400-MATCH-ROUTINE.
00219      IF SF-REC-CD = 1
00220         ADD +1 TO RECCD-CNT1
00221         PERFORM 500-BUILD-FIX-SEG
00222         MOVE ZEROS TO SR-HMCNTR
00223         PERFORM 550-BUILD-ONE-SEG
00224         WRITE SENFRZ-REC
00225         ADD +1 TO SRFREEZE-WR
00226      ELSE
00227         ADD +1 TO RECCD-CNT2
00228         PERFORM 500-BUILD-FIX-SEG
00229         MOVE ZEROS TO SR-HMCNTR
00230         PERFORM 550-BUILD-ONE-SEG
00231         WRITE SENFRZ-REC
00232         ADD +1 TO SRFREEZE-WR
00233      END-IF.
00234      SKIP3
00235  500-BUILD-FIX-SEG.
00236      MOVE HE-REC TO SENFRZ-REC.
00237      MOVE SF-REC-CD TO SR-RECCD.
00238      SKIP3
00239  550-BUILD-ONE-SEG.
00240      MOVE 00001 TO SR-HMCNTR
00241      MOVE SF-BIRTSF-DATE TO SR-HSG-BRTHDTE (SR-HMCNTR)
00242      MOVE SF-NAME           TO SR-HSG-NAME (SR-HMCNTR)
00243      MOVE SF-ADDR           TO SR-HSG-ADDR (SR-HMCNTR)
00244      MOVE SF-CITY           TO SR-HSG-CITY (SR-HMCNTR)
00245      MOVE SF-STATE          TO SR-HSG-STATE(SR-HMCNTR)
00246      MOVE SF-ZIP            TO WS-ZIP-HO5
00247      MOVE 0                 TO WS-ZIP-LO4
00248      MOVE WS-ZIP            TO SR-HSG-ZIP  (SR-HMCNTR)
00249      MOVE SF-YEAR-APPLD     TO SR-HSG-YRAPPL(SR-HMCNTR)
00250      MOVE SF-BATCSF-NO      TO SR-HSG-BATCH (SR-HMCNTR).
00251      SKIP3
00252  580-BUILD-VAR-SEGS.
00253      ADD  00001  TO SR-HMCNTR
00254      IF SR-HMCNTR GREATER THAN 410
00255         DISPLAY 'HOMESTEAD SEGMENTS EXCEED 410'
00256         MOVE 16 TO RETURN-CODE
00257         MOVE 'N' TO CHECK-PROCESSING-SW
00258      ELSE
00259         MOVE SF-BIRTSF-DATE TO SR-HSG-BRTHDTE (SR-HMCNTR)
00260         MOVE SF-NAME        TO SR-HSG-NAME (SR-HMCNTR)
00261         MOVE SF-ADDR        TO SR-HSG-ADDR (SR-HMCNTR)
00262         MOVE SF-CITY        TO SR-HSG-CITY (SR-HMCNTR)
00263         MOVE SF-STATE       TO SR-HSG-STATE(SR-HMCNTR)
00264         MOVE SF-ZIP         TO WS-ZIP-HO5
00265         MOVE 0              TO WS-ZIP-LO4
00266         MOVE WS-ZIP         TO SR-HSG-ZIP  (SR-HMCNTR)
00267         MOVE SF-YEAR-APPLD  TO SR-HSG-YRAPPL(SR-HMCNTR)
00268         MOVE SF-BATCSF-NO   TO SR-HSG-BATCH (SR-HMCNTR).
00269      SKIP3
00270  650-MOVE-HMSD-TO-PRNT.
00271      MOVE  SF-VOL TO D-VOL
00272      MOVE  SF-PROP TO D-PROP
00273      INSPECT D-PROP REPLACING ALL ',' BY '-'
00274      MOVE  SF-TXTYP TO D-TXTYP
00275      MOVE  SF-REC-CD TO D-RECCD
00276      MOVE  SF-BIRTSF-DATE TO D-BRTH-DTE
00277      INSPECT D-BRTH-DTE REPLACING ALL ',' BY '-'.
00278      SKIP3
00279  670-MOVE-HOME-TO-PRNT.
00280      MOVE  HE-VOL TO D-VOL
00281      MOVE  HE-PROP TO D-PROP
00282      INSPECT D-PROP REPLACING ALL ',' BY '-'
00283      MOVE  HE-TXTYP TO D-TXTYP
00284      MOVE  HE-RECCD TO D-RECCD
00285      MOVE  SF-BIRTSF-DATE TO D-BRTH-DTE
00286      INSPECT D-BRTH-DTE REPLACING ALL ',' BY '-'.
00287      SKIP3
00288  700-RD-HOME.
00289      READ  HOMEOWNER-EXEMPT
00290             AT END  MOVE HIGH-VALUE TO HOME-KEY.
00291      IF  NOT HOME-EOF
00292           ADD +1 TO HOME-RD
00293           MOVE HE-TXCD TO  BRK-TXCD
00294           MOVE BRK-TWN TO  HK-TWN
00295           MOVE HE-VOL  TO  HK-VOL
00296           MOVE HE-PROP TO  HK-PROP
00297           MOVE HE-TXTYP TO  HK-TXTYP
00298 *         MOVE HE-RECCD TO  HK-RECCD
00299           IF CURR-HOME-KEY LESS THAN PREV-HOME-KEY
00300              DISPLAY 'HOMEOWNER FILE OUT OF SEQUENCE'
00301              DISPLAY 'CURR KEY  = ' CURR-HOME-KEY
00302              DISPLAY 'PREV KEY  = ' PREV-HOME-KEY
00303              MOVE 16 TO RETURN-CODE
00304              MOVE 'N' TO CHECK-PROCESSING-SW
00305           ELSE
00306              MOVE CURR-HOME-KEY TO PREV-HOME-KEY.
00307      SKIP3
00308  750-RD-HMSD.
00309      READ  SR-HOMESTEAD-FILE
00310             AT END MOVE HIGH-VALUES TO HMSD-KEY.
00311      IF  NOT HMSD-EOF
00312          ADD +1 TO HMSD-RD
00313          MOVE SF-TXCD TO BRK-TXCD2
00314          MOVE BRK-TWN2 TO SK-TWN
00315          MOVE SF-VOL  TO SK-VOL
00316          MOVE SF-PROP TO SK-PROP
00317          MOVE SF-TXTYP TO SK-TXTYP
00318 *        MOVE SF-REC-CD TO SK-RECCD
00319          IF CURR-HMSD-KEY LESS THAN PREV-HMSD-KEY
00320             DISPLAY 'HOMESTEAD FILE OUT OF SEQUENCE'
00321             DISPLAY 'CURR KEY  = ' CURR-HMSD-KEY
00322             DISPLAY 'PREV KEY  = ' PREV-HMSD-KEY
00323             MOVE 16 TO RETURN-CODE
00324             MOVE 'N' TO CHECK-PROCESSING-SW
00325          ELSE
00326             MOVE CURR-HOME-KEY TO PREV-HOME-KEY.
00327      SKIP3
00328  800-PRINT-DETAIL.
00329      IF  PRINT-HEADER
00330           PERFORM  870-HDR-ROUTINE.
00331      SKIP1
00332      WRITE  PRNT-REC FROM DETAIL-LINE AFTER ADVANCING   2
00333      ADD  +2 TO LINE-CNTR
00334      MOVE  SPACES TO DETAIL-LINE.
00335      SKIP3
00336  850-PRINT-TOTALS.
00337      IF  PRINT-HEADER
00338           PERFORM 870-HDR-ROUTINE.
00339      SKIP1
00340      MOVE  'TOTAL HOMEOWNERS RECORDS READ' TO FIN-MSG
00341      MOVE  HOME-RD TO FIN-TOT
00342      WRITE  PRNT-REC FROM FINAL-LINE AFTER ADVANCING   3
00343      DISPLAY FINAL-LINE
00344      SKIP1
00345      MOVE  RECCD-CNT1  TO FIN-TOT
00346      MOVE
00347      'TOTAL HOMESTEAD RECORDS READ WITH RECORD CODE OF 1'
00348                                  TO FIN-MSG
00349      WRITE  PRNT-REC FROM FINAL-LINE AFTER ADVANCING   1
00350      DISPLAY FINAL-LINE
00351      SKIP1
00352      MOVE  RECCD-CNT2  TO FIN-TOT
00353      MOVE
00354      'TOTAL HOMESTEAD RECORDS READ WITH RECORD CODE OF 2'
00355                                  TO FIN-MSG
00356      WRITE  PRNT-REC FROM FINAL-LINE AFTER ADVANCING   1
00357      DISPLAY FINAL-LINE
00358      SKIP1
00359      MOVE  SRFREEZE-WR TO   FIN-TOT
00360      MOVE  'TOTAL SENIOR FREEZE RECORDS WRITTEN' TO FIN-MSG
00361      WRITE  PRNT-REC FROM FINAL-LINE AFTER ADVANCING   1
00362      DISPLAY FINAL-LINE.
00363      SKIP3
00364  870-HDR-ROUTINE.
00365      ADD  +1 TO PAGE-CNTR
00366      MOVE  PAGE-CNTR TO H1-PAGE
00367      MOVE  +6 TO LINE-CNTR
00368      WRITE  PRNT-REC FROM HDR-LINE1 AFTER ADVANCING   PAGE
00369      WRITE  PRNT-REC FROM HDR-LINE2 AFTER ADVANCING   2
00370      WRITE  PRNT-REC FROM HDR-LINE3 AFTER ADVANCING   2
00371      WRITE  PRNT-REC FROM HDR-LINE4 AFTER ADVANCING   1.
