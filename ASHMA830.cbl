00001  IDENTIFICATION DIVISION.
00002      SKIP1
00003  PROGRAM-ID. ASHMA830.
00004 *AUTHORS. SQUILLACE-MOLIS.
00005 *DATE-WRITTEN. JUNE ,1995.
00006 *REMARKS. THIS PROGRAM CREATES SENIOR FREEZE PRORATION FILE.
00007      SKIP1
00008  ENVIRONMENT DIVISION.
00009  CONFIGURATION SECTION.
00010      SKIP1
00011  SOURCE-COMPUTER. IBM-370.
00012  OBJECT-COMPUTER. IBM-370.
00013      SKIP1
00014  INPUT-OUTPUT SECTION.
00015  FILE-CONTROL.
00016      SKIP1
00017      SELECT CNTRL-FILE       ASSIGN TO UT-S-CNTRLFLE.
00018      SELECT PRORATION-FILE   ASSIGN TO UT-S-PRORATF.
00019      SELECT PRORATION-REPORT ASSIGN TO UT-S-PRORATR.
00020      SKIP1
00021  DATA DIVISION.
00022  FILE SECTION.
00023      SKIP1
00024  FD  CNTRL-FILE
00025      LABEL RECORDS ARE STANDARD
00026      BLOCK CONTAINS 0 RECORDS
00027      RECORDING MODE IS F
00028      RECORD CONTAINS 80 CHARACTERS
00029      DATA RECORD IS CNTRL-CARD.
00030  01  CNTRL-CARD.
00031      05  CC-DESC            PIC X(4).
00032          88  VALID-DESC     VALUE 'YEAR'.
00033      05  CC-YEAR.
00034          10 CC-YEAR-CN      PIC 9(2).
00035          10 CC-YEAR-YR      PIC 9(2).
00036      05  CC-YEAR-N REDEFINES CC-YEAR
00037                             PIC 9(4).
00038      05  C-SPACE            PIC X(72).
00039      EJECT
00040  COPY ASPRTNFD01.
00041      SKIP3
00042  FD  PRORATION-REPORT
00043      RECORDING MODE IS F
00044      RECORD CONTAINS 133 CHARACTERS
00045      BLOCK CONTAINS 0 RECORDS
00046      LABEL RECORDS ARE STANDARD
00047      DATA RECORD IS PRORATION-REP.
00048  01  PRORATION-REP          PIC X(133).
00049      EJECT
00050  WORKING-STORAGE SECTION.
00051      SKIP1
00052  77  ERR-SW                 PIC X     VALUE 'N'.
00053      88  ERR                          VALUE 'Y'.
00054  77  CC-EOF-SW              PIC X     VALUE 'N'.
00055      88  CC-EOF                       VALUE 'Y'.
00056  77  TROOT-SEG-ACC          PIC S9(7) VALUE +0   PACKED-DECIMAL.
00057  77  TASMT-DATA-SEG-ACC     PIC S9(7) VALUE +0   PACKED-DECIMAL.
00058  77  TMSTR-SEG-ACC          PIC S9(7) VALUE +0   PACKED-DECIMAL.
00059  77  TEXEMP-SEG-ACC         PIC S9(7) VALUE +0   PACKED-DECIMAL.
00060  77  TEXEMP-SEG-SEL         PIC S9(7) VALUE +0   PACKED-DECIMAL.
00061  77  TKEYPCL-REC-WR         PIC S9(7) VALUE +0   PACKED-DECIMAL.
00062  77  LINE-CNT               PIC S999  VALUE +60  PACKED-DECIMAL.
00063  77  PAGE-CNT               PIC S9(5) VALUE +0   PACKED-DECIMAL.
00064  77  BLNK                   PIC X     VALUE SPACE.
00065      EJECT
00066  01  WORK-AREA.
00067      SKIP1
00068      05  WS-ASSMT-KEY       PIC X(5).
00069      05  FILLER REDEFINES WS-ASSMT-KEY.
00070          10  WS-PROCYR      PIC XX.
00071          10  WS-TXYR        PIC XX.
00072          10  WS-TXTYP       PIC X.
00073      SKIP1
00074      05  DSP-TIME                     PIC 99,99.
00075      05  DSP-DATE.
00076          10  DSP-MO                   PIC 99.
00077          10  FILLER                   PIC X       VALUE '/'.
00078          10  DSP-DA                   PIC 99.
00079          10  FILLER                   PIC X       VALUE '/'.
00080          10  DSP-YR                   PIC 99.
00081      05  ACPT-DATE                    PIC 9(6).
00082      05  ACPT-DATE-X REDEFINES ACPT-DATE.
00083          10  ACPT-YR                  PIC 99.
00084          10  ACPT-MO                  PIC 99.
00085          10  ACPT-DA                  PIC 99.
00086      05  ACPT-TIME-HOLD               PIC 9(8).
00087      05  ACPT-TIME-HOLD-X REDEFINES ACPT-TIME-HOLD.
00088          10  ACPT-TIME                PIC 9(4).
00089          10  FILLER                   PIC 9(4).
00090          SKIP1
00091      05  CURR-TIME-X.
00092          10  CURR-TIME              PIC  9(6).
00093          10  CURR-TIME-2            PIC  9(2).
00094      05  CURR-TIME-N   REDEFINES    CURR-TIME-X PIC 9(8).
00095          SKIP1
00096      05  WS-DATE            PIC 9(8).
00097      05  FILLER REDEFINES WS-DATE.
00098          10  WS-CC          PIC 99.
00099          10  WS-YY          PIC 99.
00100          10  WS-MM          PIC 99.
00101          10  WS-DD          PIC 99.
00102          SKIP1
00103      05  MO-DAY-YR          PIC 9(6)  VALUE ZERO.
00104          SKIP1
00105      05 WS-CLASS            PIC 9(7).
00106      05 FILLER REDEFINES WS-CLASS.
00107          10 WS-ZEROS        PIC 99.
00108      EJECT
00109  01  PRINT-LINES.
00110      SKIP1
00111      05  HL-1.
00112          10  FILLER         PIC XXX   VALUE SPACE.
00113          10  H1-DATE        PIC 99B99B99.
00114          10  FILLER         PIC X(39) VALUE SPACE.
00115          10  FILLER         PIC X(69) VALUE
00116          'OFFICE OF THE COOK COUNTY ASSESSOR'.
00117          10  FILLER         PIC X(5)  VALUE 'PAGE'.
00118          10  H1-PAGE        PIC ZZ,ZZ9.
00119          SKIP1
00120      05  HL-2.
00121          10  FILLER         PIC XXX   VALUE SPACE.
00122          10  FILLER         PIC X(46) VALUE 'ASHMA830'.
00123          10  FILLER         PIC X(57) VALUE
00124      'KEYPARCEL PRORATION FILE CREATION REPORT'.
00125          SKIP1
00126      05  HL-3.
00127          10  FILLER         PIC XXXX.
00128          10  FILLER         PIC X(65) VALUE
00129          'PERMANENT INDEX          KEYPARCEL       RECD'.
00130          10  FILLER         PIC X(51) VALUE
00131      'SENIOR FREEZE    HOMESTEAD    HOMEOWNER     PERCENT'.
00132          10  FILLER         PIC X(09) VALUE '    SPLIT'.
00133          SKIP1
00134      05  HL-4.
00135          10  FILLER         PIC X(9)  VALUE SPACE.
00136          10  FILLER         PIC X(63) VALUE
00137      'NUMBER               NUMBER         CODE     BIRTHDATE'.
00138          10  FILLER         PIC X(49) VALUE
00139      'STATUS         STATUS        STATUS      PRORATED'.
00140          10  FILLER         PIC X(09) VALUE '    CODE'.
00141          SKIP1
00142      05  DET-LN.
00143          10  FILLER         PIC XX.
00144          10  DL-PROP        PIC 99,99,999,999,9999.
00145          10  FILLER         PIC XXXX  VALUE SPACE.
00146          10  DL-KEYPCL      PIC 99,99,999,999,9999.
00147          10  FILLER         PIC X(5)  VALUE SPACE.
00148          10  DL-RC-CD       PIC X.
00149          10  FILLER         PIC X(5)  VALUE SPACE.
00150          10  DL-BIRTH-DTE   PIC 99,99,9999.
00151          10  FILLER         PIC X(12) VALUE SPACE.
00152          10  DL-FZ-ST       PIC X.
00153          10  FILLER         PIC X(14) VALUE SPACE.
00154          10  DL-HM-ST       PIC X.
00155          10  FILLER         PIC X(13) VALUE SPACE.
00156          10  DL-HO-ST       PIC X.
00157          10  FILLER         PIC X(08) VALUE SPACE.
00158          10  DL-PCT-PRO     PIC 9.999999.
00159          10  FILLER         PIC X(05) VALUE SPACE.
00160          10  DL-SPLT-CD     PIC X.
00161          SKIP1
00162      05  TOT-LN.
00163          10  FILLER     PIC XX    VALUE SPACE.
00164          10  TL-MSG     PIC X(40).
00165          10  FILLER     PIC XX    VALUE SPACE.
00166          10  TL-TOT     PIC Z,ZZZ,ZZ9.
00167          EJECT
00168  01  IO-AREA.
00169      SKIP1
00170  COPY PIROOTSEGM.
00171      SKIP3
00172  COPY PIASSESSMT.
00173      SKIP3
00174  COPY ASAIS145SG.
00175      SKIP3
00176  01  IO-AREA1.
00177  COPY ASAIS150SG.
00178      EJECT
00179 **************************************************************
00180 *             ASSESSOR ASSESSMENT INFORMATION                *
00181 *                       DATA BASE                            *
00182 *             PATH TO: 1) ROOT SEGMENT                       *
00183 *                      2) ASSESSMENT/TAX INFORMATION SEGMENT *
00184 *                      3) EXEMPTION MASTER SEGMENT           *
00185 *                      4) EXEMPTION DETAIL SEGMENT           *
00186 *                      5) SOCIAL SECURITY SEGMENT            *
00187 **************************************************************
00188 *
00189  01  SSAS.
00190 *--------------------- QUALIFIED SSA'S ----------------------*
00191      05  LVL1-QUAL-SSA.
00192          10  FILLER              PIC X(8)   VALUE 'PROPSEG '.
00193          10  FILLER              PIC X      VALUE '('.
00194          10  FILLER              PIC X(8)   VALUE 'PPROP   '.
00195          10  LVL1-RO             PIC XX     VALUE 'EQ'.
00196          10  LVL1-ARG            PIC 9(15)    COMP-3.
00197          10  FILLER              PIC X      VALUE ')'.
00198      05  LVL2-QUAL-SSA.
00199          10  FILLER              PIC X(17)
00200                                  VALUE 'ASSMTSEG(PYSRCH  '.
00201          10  LVL2-RO             PIC XX     VALUE 'EQ'.
00202          10  LVL2-ARG.
00203              15  LVL2-PROCYR     PIC 99.
00204              15  LVL2-TXYR       PIC 99.
00205              15  LVL2-TXTYP      PIC 9.
00206          10  FILLER              PIC X      VALUE ')'.
00207      05  LVL3-QUAL-SSA.
00208          10  FILLER              PIC X(8)   VALUE 'EXMASTSG'.
00209          10  FILLER              PIC X      VALUE '('.
00210          10  FILLER              PIC X(8)   VALUE 'EXRECCOD'.
00211          10  LVL3-RO             PIC XX     VALUE 'EQ'.
00212          10  LVL3-ARG            PIC X.
00213          10  FILLER              PIC X      VALUE ')'.
00214      05  LVL4-QUAL-SSA.
00215          10  FILLER              PIC X(8)   VALUE 'EXDETLSG'.
00216          10  FILLER              PIC X      VALUE '('.
00217          10  FILLER              PIC X(8)   VALUE 'EXBRTHDT'.
00218          10  LVL4-RO             PIC XX     VALUE 'EQ'.
00219          10  LVL4-ARG            PIC X(8).
00220          10  FILLER              PIC X      VALUE ')'.
00221      05  LVL5-QUAL-SSA.
00222          10  FILLER              PIC X(8)   VALUE 'EXSOSCSG'.
00223          10  FILLER              PIC X      VALUE '('.
00224          10  FILLER              PIC X(8)   VALUE 'EXSEQNUM'.
00225          10  LVL5-RO             PIC XX     VALUE 'EQ'.
00226          10  LVL5-ARG            PIC XX.
00227          10  FILLER              PIC X      VALUE ')'.
00228 *-------------------- UNQUALIFIED SSA'S ---------------------*
00229      05  LVL1-UNQUAL-SSA         PIC X(9)   VALUE 'PROPSEG'.
00230      05  LVL2-UNQUAL-SSA         PIC X(9)   VALUE 'ASSMTSEG'.
00231      05  LVL3-UNQUAL-SSA         PIC X(9)   VALUE 'EXMASTSG'.
00232      05  LVL4-UNQUAL-SSA         PIC X(9)   VALUE 'EXDETLSG'.
00233      05  LVL5-UNQUAL-SSA         PIC X(9)   VALUE 'EXSOSCSG'.
00234 *-------------------- PATH  SSA'S   -------------------------*
00235      05  LVL1-UNQUAL-SSA-PATH.
00236          10  FILLER              PIC X(08)  VALUE 'PROPSEG '.
00237          10  FILLER              PIC X      VALUE '*'.
00238          10  FILLER              PIC XX     VALUE 'D '.
00239      05  LVL2-QUAL-SSA-PATH.
00240          10  FILLER              PIC X(10)  VALUE 'ASSMTSEG*D'.
00241          10  FILLER              PIC X      VALUE '('.
00242          10  FILLER              PIC X(8)   VALUE 'PYSRCH  '.
00243          10  LVL2-RO-P           PIC XX     VALUE 'EQ'.
00244          10  LVL2-ARG-P.
00245              15  LVL2-PROCYR-P   PIC 99.
00246              15  LVL2-TXYR-P     PIC 99.
00247              15  LVL2-TXTYP-P    PIC 9.
00248          10  FILLER              PIC X      VALUE ')'.
00249      05  LVL3-UNQUAL-SSA-PATH.
00250          10  FILLER              PIC X(08)  VALUE 'EXMASTSG'.
00251          10  FILLER              PIC X      VALUE '*'.
00252          10  FILLER              PIC XX     VALUE 'D '.
00253      05  LVL4-QUAL-SSA-PATH.
00254          10  FILLER              PIC X(10)  VALUE 'EXDETLSG*D'.
00255          10  FILLER              PIC X      VALUE '('.
00256          10  FILLER              PIC X(8)   VALUE 'EXBRTHDT'.
00257          10  LVL4-RO-P           PIC XX     VALUE 'EQ'.
00258          10  LVL4-ARG-P          PIC X(8).
00259          10  FILLER              PIC X      VALUE ')'.
00260 **************************************************************
00261      SKIP3
00262  COPY IMSCALLS.
00263      EJECT
00264  LINKAGE SECTION.
00265      SKIP1
00266  01  IO-PCB.
00267      05  IP-TERM-NAME              PIC X(8).
00268      05  FILLER                    PIC X(2).
00269      05  IP-STATUS-CODE            PIC X(2).
00270          88  IP-GOOD-STATUS                    VALUE SPACES.
00271      05  IP-CURR-DATE-TIME         PIC X(8).
00272      05  IP-MSG-SEQ-NO             PIC X(4).
00273      05  IP-MSG-OUT-DES-NAME       PIC X(4).
00274      05  IP-USER-ID                PIC X(8).
00275      SKIP1
00276  01  PI-PCB.
00277  COPY PIPCB1DESC.
00278          10  PI-PROP-NO            PIC 9(15)  PACKED-DECIMAL.
00279          10  PI-ASS-KEY            PIC 9(5).
00280          10  PI-MSTR-KEY           PIC X.
00281          10  PI-DET-KEY            PIC X(8).
00282      EJECT
00283  PROCEDURE DIVISION.
00284      SKIP1
00285  000-START.
00286      ENTRY 'DLITCBL' USING IO-PCB PI-PCB
00287      OPEN INPUT CNTRL-FILE
00288      PERFORM 650-READ-EDIT-CC
00289      IF ERR
00290         MOVE 16 TO RETURN-CODE
00291         CLOSE  CNTRL-FILE
00292         GOBACK.
00293      SKIP1
00294      OPEN OUTPUT PRORATION-FILE
00295                  PRORATION-REPORT.
00296      SKIP1
00297      ACCEPT   CURR-TIME-N   FROM  TIME
00298      ACCEPT    ACPT-DATE    FROM  DATE
00299      MULTIPLY  ACPT-DATE    BY    100.0001
00300        GIVING  MO-DAY-YR
00301      MOVE      MO-DAY-YR    TO  H1-DATE
00302      INSPECT   H1-DATE   REPLACING  ALL  ' '  BY '/'.
00303      SKIP1
00304      WRITE PRORATION-REP FROM BLNK AFTER ADVANCING PAGE.
00305      SKIP1
00306      PERFORM 100-MAINLINE UNTIL PI-DB-END
00307                              OR ERR.
00308      SKIP1
00309      IF LINE-CNT GREATER THAN +55
00310         PERFORM 550-HEADING-RTN.
00311      DISPLAY SPACES
00312      MOVE TROOT-SEG-ACC TO TL-TOT
00313      MOVE 'TOTAL ROOT SEGMENTS ACCESSED...........:' TO TL-MSG
00314      WRITE PRORATION-REP FROM TOT-LN AFTER ADVANCING 3
00315      DISPLAY 'TOTAL ROOT SEGMENTS ACCESSED...........: '
00316                                             TROOT-SEG-ACC.
00317      SKIP1
00318      MOVE TASMT-DATA-SEG-ACC TO TL-TOT
00319      MOVE 'TOTAL ASSESSMENT DATA SEGMENTS ACCESSED:' TO TL-MSG
00320      WRITE PRORATION-REP FROM TOT-LN AFTER ADVANCING 1
00321      DISPLAY 'TOTAL ASSESSMENT DATA SEGMENTS ACCESSED: '
00322                                             TASMT-DATA-SEG-ACC.
00323      SKIP1
00324      MOVE TMSTR-SEG-ACC   TO TL-TOT
00325      MOVE 'TOTAL MASTER SEGMENTS ACCESSED.........:' TO TL-MSG
00326      WRITE PRORATION-REP FROM TOT-LN AFTER ADVANCING 1
00327      DISPLAY 'TOTAL MASTER SEGMENTS ACCESSED.........: '
00328                                             TMSTR-SEG-ACC.
00329      SKIP1
00330      MOVE TEXEMP-SEG-ACC  TO TL-TOT
00331      MOVE 'TOTAL EXEMPTION SEGMENTS ACCESSED......:' TO TL-MSG
00332      WRITE PRORATION-REP FROM TOT-LN AFTER ADVANCING 1
00333      DISPLAY 'TOTAL EXEMPTION SEGMENTS ACCESSED......: '
00334                                             TEXEMP-SEG-ACC.
00335      SKIP1
00336      MOVE TEXEMP-SEG-SEL  TO TL-TOT
00337      MOVE 'TOTAL EXEMPTION SEGMENTS SELECTED......:' TO TL-MSG
00338      WRITE PRORATION-REP FROM TOT-LN AFTER ADVANCING 1
00339      DISPLAY 'TOTAL EXEMPTION SEGMENTS SELECTED......: '
00340                                             TEXEMP-SEG-SEL.
00341      SKIP1
00342      MOVE TKEYPCL-REC-WR  TO TL-TOT
00343      MOVE 'TOTAL KEYPARCEL RECORDS WRITTEN........:' TO TL-MSG
00344      WRITE PRORATION-REP FROM TOT-LN AFTER ADVANCING 1
00345      DISPLAY 'TOTAL KEYPARCEL RECORDS WRITTEN........: '
00346                                             TKEYPCL-REC-WR.
00347      SKIP1
00348      IF ERR
00349         MOVE 16 TO RETURN-CODE.
00350      SKIP1
00351      CLOSE CNTRL-FILE PRORATION-FILE PRORATION-REPORT
00352      GOBACK.
00353      SKIP2
00354  100-MAINLINE.
00355      PERFORM 300-GET-MSTR-SEG.
00356          SKIP2
00357  300-GET-MSTR-SEG.
00358      SKIP1
00359      MOVE CC-YEAR-YR TO LVL2-PROCYR-P
00360                         LVL2-TXYR-P
00361      MOVE 0          TO LVL2-TXTYP-P
00362      SKIP1
00363      CALL 'CBLTDLI' USING GN
00364                           PI-PCB
00365                           IO-AREA
00366                           LVL1-UNQUAL-SSA-PATH
00367                           LVL2-QUAL-SSA-PATH
00368                           LVL3-UNQUAL-SSA
00369      IF PI-DB-GOOD-STATUS
00370         ADD +1 TO TROOT-SEG-ACC TASMT-DATA-SEG-ACC
00371                                 TMSTR-SEG-ACC
00372         IF (C145-PROPCT NOT LESS    0.000001 AND
00373             C145-PROPCT NOT GREATER 0.999999)
00374             ADD +1 TO TEXEMP-SEG-SEL
00375             PERFORM 400-GET-PROC-DETL-SEG UNTIL PI-SEG-NOTFND
00376         ELSE
00377            CONTINUE
00378      ELSE
00379         IF (PI-DB-END OR PI-SEG-NOTFND)
00380              CONTINUE
00381         ELSE
00382            PERFORM 800-DISPLAY-PI-PCB.
00383      SKIP3
00384  400-GET-PROC-DETL-SEG.
00385      CALL 'CBLTDLI' USING GNP
00386                           PI-PCB
00387                           C150-SENFRZDETAIL
00388                           LVL4-UNQUAL-SSA
00389      IF PI-DB-GOOD-STATUS
00390         ADD +1 TO TEXEMP-SEG-ACC
00391         PERFORM 450-WRITE-OUTPUT-REC
00392         PERFORM 500-WRITE-OUTPUT-REP
00393      ELSE
00394        IF PI-SEG-NOTFND
00395           CONTINUE
00396        ELSE
00397           PERFORM 800-DISPLAY-PI-PCB.
00398      SKIP3
00399  450-WRITE-OUTPUT-REC.
00400      MOVE SPACES        TO PRORATION-REC
00401      MOVE P-PROP        TO PR-PROP
00402      MOVE C145-KEYPCL   TO PR-KEYPCL
00403      MOVE C145-PROPCT   TO PR-PRORATE
00404      MOVE C145-RECCODE  TO PR-RECCD
00405      MOVE C145-SPLTCD   TO PR-SPLTCD
00406      MOVE C150-BIRTHDTE TO PR-BRTHDTE
00407      MOVE C150-SFSTAT   TO PR-SF-STAT
00408      MOVE C150-HSSTAT   TO PR-HM-STAT
00409      MOVE C150-HOSTAT   TO PR-HO-STAT
00410      WRITE PRORATION-REC
00411      ADD +1 TO TKEYPCL-REC-WR.
00412      SKIP3
00413  500-WRITE-OUTPUT-REP.
00414      IF LINE-CNT
00415         GREATER THAN +55
00416         PERFORM 550-HEADING-RTN.
00417      MOVE P-PROP        TO DL-PROP
00418      INSPECT DL-PROP REPLACING ALL ',' BY '-'
00419      MOVE C145-KEYPCL   TO DL-KEYPCL
00420      INSPECT DL-KEYPCL REPLACING ALL ',' BY '-'
00421      MOVE C145-RECCODE  TO DL-RC-CD
00422      MOVE C150-BIRTHDTE TO DL-BIRTH-DTE
00423      INSPECT DL-BIRTH-DTE REPLACING ALL ',' BY '/'
00424      MOVE C150-SFSTAT   TO DL-FZ-ST
00425      MOVE C150-HSSTAT   TO DL-HM-ST
00426      MOVE C150-HOSTAT   TO DL-HO-ST
00427      MOVE C145-PROPCT   TO DL-PCT-PRO
00428      MOVE C145-SPLTCD   TO DL-SPLT-CD
00429      WRITE PRORATION-REP FROM DET-LN AFTER ADVANCING 2
00430      ADD +2 TO LINE-CNT.
00431      SKIP3
00432  550-HEADING-RTN.
00433      ADD +1 TO PAGE-CNT
00434      MOVE PAGE-CNT TO H1-PAGE
00435      WRITE PRORATION-REP FROM HL-1 AFTER ADVANCING PAGE
00436      WRITE PRORATION-REP FROM HL-2 AFTER ADVANCING 2
00437      WRITE PRORATION-REP FROM HL-3 AFTER ADVANCING 2
00438      WRITE PRORATION-REP FROM HL-4 AFTER ADVANCING 1
00439      MOVE +6 TO LINE-CNT.
00440      SKIP2
00441  650-READ-EDIT-CC.
00442      PERFORM 670-READ-CARD-FILE
00443
00444      DISPLAY 'CONTROL CARD  '  CNTRL-CARD
00445
00446      IF NOT CC-EOF
00447         PERFORM 690-CNTRL-EDIT
00448      ELSE
00449         DISPLAY 'CONTROL CARD IS MISSING'
00450         MOVE 'Y' TO ERR-SW
00451      END-IF.
00452      SKIP2
00453  670-READ-CARD-FILE.
00454      READ CNTRL-FILE
00455      AT END
00456           MOVE 'Y' TO CC-EOF-SW.
00457      SKIP2
00458  690-CNTRL-EDIT.
00459      IF VALID-DESC
00460         IF CC-YEAR NUMERIC AND
00461            CC-YEAR-N GREATER THAN ZERO
00462            DISPLAY 'CONTROL CARD VALUES ARE: '
00463            CC-DESC SPACE CC-YEAR-N
00464         ELSE
00465             DISPLAY 'INVALID CONTROL CARD'
00466             DISPLAY 'POS 1-4 MUST BE EQUAL TO YEAR'
00467             DISPLAY
00468             'POS 5-8 MUST BE EQUAL TO NUMERIC VALUE'
00469             MOVE 16 TO RETURN-CODE
00470         END-IF
00471      ELSE
00472          DISPLAY 'INVALID CONTROL CARD'
00473          DISPLAY 'POS 1-4 MUST BE EQUAL TO YEAR'
00474          DISPLAY
00475          'POS 5-8 MUST BE EQUAL TO NUMERIC VALUE'
00476          MOVE 16 TO RETURN-CODE
00477      END-IF.
00478      SKIP3
00479  800-DISPLAY-PI-PCB.
00480      SKIP1
00481      MOVE 'N' TO ERR-SW
00482      MOVE 16  TO RETURN-CODE
00483      SKIP1
00484      DISPLAY 'THE DBD NAME IS                        '
00485        PI-DBD-NAME
00486      DISPLAY 'THE SEGMENT LEVEL IS                   '
00487        PI-SEGMENT-LVL
00488      DISPLAY 'THE STATUS CODE IS                     '
00489        PI-STATUS-CODE
00490      DISPLAY 'THE PROCESSING OPTIONS ARE             '
00491        PI-PROC-OPT
00492      DISPLAY 'THE SEGMENT NAME IS                    '
00493        PI-NAME-FDBK
00494      DISPLAY 'THE LENGTH OF THE KEY FEEDBACK AREA IS '
00495        PI-KEY-FDBK-LNG
00496      DISPLAY 'THE NUMBER OF SENSITIVE SEGMENTS IS    ' PI-SEN-SEG
00497      DISPLAY 'THE KEY-FEEDBACK AREA IS               '
00498      PI-PROP-NO  PI-ASS-KEY  PI-MSTR-KEY PI-DET-KEY
00499      PERFORM 830-ISSUE-ROLL.
00500      SKIP3
00501  830-ISSUE-ROLL.
00502      CALL 'CBLTDLI' USING ROLL.
