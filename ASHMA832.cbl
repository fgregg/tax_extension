00001  IDENTIFICATION DIVISION.
00002                          SKIP1
00003  PROGRAM-ID.  ASHMA832.
00004 *AUTHOR.  VINCENT P. FALK -- RICHARD KALEMBA.
00005 *DATE-WRITTEN.  JULY, 1995.
00006                          SKIP1
00007 *REMARKS.  THIS PROGRAM WILL UPDATE THE ASSESSOR'S PROPERTY
00008 *    INFORMATION DATA BASE (PIFDBP10).  IT WILL USE DATA
00009 *    FROM THE PRORATION SEQUENTIAL FILE (SORTED IN KEY PARCEL
00010 *    SEQUENCE) TO UPDATE THE EXEMPTION DETAIL AND SOCIAL
00011 *    SECURITY SEGMENTS OF THE DATA BASE.  THE SENIOR FREEZE
00012 *    STATUS CODE WILL DETERMINE THE UPDATE ACTION.
00013                          SKIP2
00014  ENVIRONMENT DIVISION.
00015                          SKIP1
00016  CONFIGURATION SECTION.
00017  SOURCE-COMPUTER.  IBM-370.
00018  OBJECT-COMPUTER.  IBM-370.
00019                          SKIP2
00020  INPUT-OUTPUT SECTION.
00021  FILE-CONTROL.
00022      SELECT CONTROL-FILE  ASSIGN TO UT-S-CNTLFILE.
00023      SELECT PRORATION-FILE  ASSIGN TO UT-S-PRORATE.
00024      SELECT PRINT-FILE  ASSIGN TO UT-S-RPTFILE.
00025                          SKIP2
00026  DATA DIVISION.
00027  FILE SECTION.
00028  FD  CONTROL-FILE
00029      RECORDING MODE IS  F
00030      RECORD CONTAINS 80 CHARACTERS
00031      BLOCK CONTAINS 0 RECORDS
00032      DATA RECORD IS  CONTROL-REC.
00033  01  CONTROL-REC.
00034      05  CICV-VALUEX.
00035          10  CICV-VALUE  PIC 9(4).
00036      05  PROCYR-VALUEX.
00037          10  PROCYR-VALUE PIC 9(4).
00038      05  FILLER          PIC X(72).
00039                          EJECT
00040  COPY ASPRTNFD01.
00041                          SKIP1
00042  COPY PRINTFILE.
00043                          EJECT
00044  WORKING-STORAGE SECTION.
00045 *****SWITCHES.
00046  77  EOF-PRORATE-SW      PIC X          VALUE 'N'.
00047      88  PRORATE-GOOD-READ       VALUE 'N'.
00048      88  EOF-PRORATE             VALUE 'Y'.
00049  77  EOF-SOSC-SW         PIC X          VALUE 'N'.
00050      88  EOF-SOSC-RESET          VALUE 'N'.
00051      88  EOF-SOSC                VALUE 'Y'.
00052  77  SEVERE-ERROR-SW     PIC X          VALUE 'N'.
00053      88  NO-SEVERE-ERROR         VALUE 'N'.
00054      88  SEVERE-ERROR            VALUE 'Y'.
00055  77  GOOD-REC-SW         PIC X          VALUE 'N'.
00056      88  BYPASS-REC              VALUE 'N'.
00057      88  GOOD-REC                VALUE 'Y'.
00058  77  STOP-KEYPCL-SW      PIC X          VALUE 'N'.
00059      88  STOP-KEYPCL-RESET       VALUE 'N'.
00060      88  STOP-KEYPCL             VALUE 'Y'.
00061  77  SPLTCD-2-SW         PIC X          VALUE 'N'.
00062      88  SPLTCD-2-RESET          VALUE 'N'.
00063      88  SPLTCD-2                VALUE 'Y'.
00064  77  FIRST-TABLFULL-SW   PIC X          VALUE 'Y'.
00065      88  TABLFULL-PROCESSED      VALUE 'N'.
00066      88  FIRST-TABLFULL          VALUE 'Y'.
00067  77  Q-SW                PIC X          VALUE 'N'.
00068      88  Q-RESET                 VALUE 'N'.
00069      88  Q-SET                   VALUE 'Y'.
00070  77  R-SW                PIC X          VALUE 'N'.
00071      88  R-RESET                 VALUE 'N'.
00072      88  R-SET                   VALUE 'Y'.
00073  77  D-SW                PIC X          VALUE 'N'.
00074      88  D-RESET                 VALUE 'N'.
00075      88  D-SET                   VALUE 'Y'.
00076  77  X-SW                PIC X          VALUE 'N'.
00077      88  X-RESET                 VALUE 'N'.
00078      88  X-SET                   VALUE 'Y'.
00079  77  ACCUM-PRORATE-SW    PIC X          VALUE 'N'.
00080      88  NO-ACCUM-PRORATE        VALUE 'N'.
00081      88  ACCUM-PRORATE           VALUE 'Y'.
00082                          SKIP1
00083 *****WORK VARIABLES.
00084  77  TABL-SUB            PIC S9(4)      VALUE +0     BINARY.
00085      88  MAX-TABL-ENTRIES        VALUE +2500.
00086  77  SOSC-SUB            PIC S9(4)      VALUE +0     BINARY.
00087      88  MAX-SOSC-ENTRIES        VALUE +2500.
00088  77  PRINT-SUB           PIC S9(4)      VALUE +0     BINARY.
00089  77  UPDT-SUB            PIC S9(4)      VALUE +0     BINARY.
00090  77  TABL-ENTRIES        PIC S9(4)      VALUE +0     BINARY.
00091  77  SOSC-ENTRIES        PIC S9(4)      VALUE +0     BINARY.
00092  77  KEYPCL-CHECK        PIC 9(4)       VALUE 0.
00093      88  CONDO-KEYPCL            VALUE 1000 THRU 2999
00094                                        4000 THRU 5999.
00095  77  SPLTCD-CHECK        PIC X          VALUE SPACE.
00096      88  SPLTCD-2-VALUE          VALUE '2'.
00097  77  PRORATE-CHECK       PIC V9(6)      VALUE 0.
00098      88  GOOD-PRORATE            VALUE 0.
00099  77  CICV-HOLD           PIC 9(4)       VALUE 0.
00100  77  PROCYR-HOLD         PIC 9(4)       VALUE 0.
00101  77  KEYPCL-HOLD         PIC 9(14)      VALUE 0.
00102  77  TOTRATE-HOLD        PIC 9V9(6)     VALUE 0.
00103  77  PROP-HOLD           PIC 9(14)      VALUE 0.
00104  77  MSG-HOLD            PIC X(60)      VALUE SPACES.
00105  77  SEG-LVL-HOLD        PIC XX         VALUE SPACES.
00106  77  PARA-NAME           PIC X(30)      VALUE SPACES.
00107  77  CTR-DISPLAY         PIC Z,ZZZ,ZZ9.
00108  77  SYSTEM-DATE         PIC 9(6)       VALUE 0.
00109  77  SYSTEM-TIME         PIC 9(7)       VALUE 0.
00110  77  CURRENT-DATE        PIC 99/99/99.
00111                          EJECT
00112 *****ERROR MESSAGES.
00113  77  TOTAL-PRORATE-MSG   PIC X(35)      VALUE
00114      'TOTAL PRORATION IS NOT EQUAL TO 100'.
00115  77  SPLTCD-2-MSG        PIC X(34)      VALUE
00116      'KEYPARCEL CONTAINS SPLIT CODE OF 2'.
00117  77  SF-STATUS-MSG       PIC X(46)      VALUE
00118      'KEY PARCEL CONTAINS DIFFERENT SF STATUS VALUES'.
00119  77  ROOT-NOTFND-MSG     PIC X(35)      VALUE
00120      'NO MATCHING ROOT (PROPERTY) SEGMENT'.
00121  77  ASMT-NOTFND-MSG     PIC X(35)      VALUE
00122      'NO MATCHING ASSESSMENT DATA SEGMENT'.
00123  77  MAST-NOTFND-MSG     PIC X(36)      VALUE
00124      'NO MATCHING EXEMPTION MASTER SEGMENT'.
00125  77  DETL-NOTFND-MSG     PIC X(36)      VALUE
00126      'NO MATCHING EXEMPTION DETAIL SEGMENT'.
00127  77  FINAL-MSG-1         PIC X(35)      VALUE
00128      'TOTAL KEY PARCEL RECORDS READ'.
00129  77  FINAL-MSG-2         PIC X(35)      VALUE
00130      'TOTAL DETAIL SEGMENTS UPDATED'.
00131  77  FINAL-MSG-2A        PIC X(36)      VALUE
00132      'TOTAL DETAIL SEGMENTS INSERTED'.
00133  77  FINAL-MSG-3         PIC X(35)      VALUE
00134      'TOTAL SOC. SEC. SEGMENTS ADDED'.
00135  77  FINAL-MSG-4         PIC X(35)      VALUE
00136      'TOTAL KEY PARCEL RECORDS REJECTED'.
00137                          SKIP1
00138  01  PACKED-DECIMAL-VARIABLES           PACKED-DECIMAL.
00139      05  CHKP-CTR        PIC S9(5)      VALUE +0.
00140      05  REC-READ-CTR    PIC S9(7)      VALUE +0.
00141          88  FIRST-REC           VALUE +1.
00142      05  REC-BYPASS-CTR  PIC S9(7)      VALUE +0.
00143      05  DETL-UPDT-CTR   PIC S9(7)      VALUE +0.
00144      05  DETL-ISRT-CTR   PIC S9(7)      VALUE +0.
00145      05  SOSC-UPDT-CTR   PIC S9(7)      VALUE +0.
00146      05  TOTAL-PRORATE   PIC S999V9(6)  VALUE +0.
00147      05  PAGE-CTR        PIC S9(5)      VALUE +0.
00148      05  LINE-CTR        PIC S999       VALUE +99.
00149          88  END-PAGE            VALUE +57 THRU +999.
00150      05  YMD-TO-MDY      PIC S999V9(4)  VALUE +100.0001.
00151                          SKIP1
00152  01  KEYPCL-TABLE.
00153      05  KEYPCL-ENTRY    OCCURS 2500 TIMES.
00154          10  KT-PROP     PIC 9(15)      PACKED-DECIMAL.
00155          10  KT-KEYPCL   PIC 9(15)      PACKED-DECIMAL.
00156          10  KT-RECCD    PIC 9.
00157          10  KT-BRTHDTE  PIC 9(8).
00158          10  KT-SF-STAT  PIC X.
00159          10  KT-HM-STAT  PIC X.
00160          10  KT-HO-STAT  PIC X.
00161          10  KT-PRORATE  PIC 9V9(6)     PACKED-DECIMAL.
00162          10  KT-SPLTCD   PIC X.
00163          10  KT-FILLER   PIC X(17).
00164                          SKIP1
00165  01  SOSC-TABLE.
00166      05  SOSC-ENTRY      OCCURS 2500 TIMES
00167                          PIC X(100).
00168                          EJECT
00169  01  GROUP-ITEMS.
00170 *****WORK AREAS.
00171      05  CUR-REC-KEY.
00172          10  CUR-KEYPCL  PIC 9(14)      VALUE 0.
00173          10  CUR-PROP    PIC 9(14)      VALUE 0.
00174          10  CUR-RECCD   PIC 9          VALUE 0.
00175      05  PREV-REC-KEY    PIC X(29)      VALUE SPACES.
00176      05  CHKP-ID-FLD.
00177          10  FILLER      PIC XX         VALUE 'AS'.
00178          10  CHKP-ID-CTR PIC 9(6)       VALUE 0.
00179      05  ASMT-ARG-HOLD.
00180          10  ASMT-PROCYR PIC 99         VALUE 0.
00181          10  ASMT-TAXYR  PIC 99         VALUE 0.
00182          10  ASMT-TXTYP  PIC 9          VALUE 0.
00183      05  MAX-TABL-MSG.
00184          10  FILLER      PIC X(31)      VALUE
00185              'KEY PARCEL CONTAINS MORE THAN '.
00186          10  MTM-AMT     PIC ZZ9.
00187          10  FILLER      PIC X(23)      VALUE
00188              ' ITEMS -- NOT PROCESSED'.
00189      05  UPDT-DATE.
00190          10  UPDT-CC     PIC 99         VALUE 0.
00191          10  UPDT-YMD.
00192              15  UPDT-YY PIC 99         VALUE 0.
00193              15  FILLER  PIC X(4)       VALUE SPACES.
00194                          SKIP1
00195  01  OUTPUT-LINES.
00196      05  BLANK-LINE      PIC X          VALUE SPACES.
00197                          SKIP1
00198      05  DETAIL-LINE.
00199          10  FILLER      PIC XX         VALUE SPACES.
00200          10  DL-KEYPCL   PIC 99,99,999,999,9(4).
00201          10  FILLER      PIC XX         VALUE SPACES.
00202          10  DL-PROP     PIC 99,99,999,999,9(4).
00203          10  FILLER      PIC XXX        VALUE SPACES.
00204          10  DL-TAXYR    PIC 99.
00205          10  FILLER      PIC XX         VALUE SPACES.
00206          10  DL-PRORATE  PIC 9.9(6).
00207          10  FILLER      PIC X(4)       VALUE SPACES.
00208          10  DL-SF-STAT  PIC X.
00209          10  FILLER      PIC X(5)       VALUE SPACES.
00210          10  DL-HM-STAT  PIC X.
00211          10  FILLER      PIC X(5)       VALUE SPACES.
00212          10  DL-HO-STAT  PIC X.
00213          10  FILLER      PIC X(4)       VALUE SPACES.
00214          10  DL-TOTRATE  PIC 9.9(6)     BLANK WHEN ZERO.
00215          10  FILLER      PIC X(4)       VALUE SPACES.
00216          10  DL-MSG      PIC X(60).
00217                          EJECT
00218      05  FINAL-LINE.
00219          10  FILLER      PIC XX         VALUE SPACES.
00220          10  FL-TOTAL    PIC Z,ZZZ,ZZ9.
00221          10  FILLER      PIC XX         VALUE SPACES.
00222          10  FL-MSG      PIC X(35).
00223                          SKIP1
00224      05  HDR-1.
00225          10  FILLER      PIC XXX        VALUE SPACES.
00226          10  H1-DATE     PIC X(8).
00227          10  FILLER      PIC X(39)      VALUE SPACES.
00228          10  FILLER      PIC X(68)      VALUE
00229              'OFFICE OF THE COOK COUNTY ASSESSOR'.
00230          10  FILLER      PIC X(5)       VALUE 'PAGE'.
00231          10  H1-PAGE     PIC ZZ,ZZ9.
00232                          SKIP1
00233      05  HDR-2.
00234          10  FILLER      PIC X(48)      VALUE '   ASHMA832'.
00235          10  FILLER      PIC X(38)      VALUE
00236              'SENIOR FREEZE DATA BASE UPDATE REPORT '.
00237          10  FILLER      PIC X(24)      VALUE
00238              '-- PRORATION DUPLICATION'.
00239                          SKIP1
00240      05  HDR-3.
00241          10  FILLER      PIC X(24)      VALUE
00242              '   KEY PARCEL INDEX'.
00243          10  FILLER      PIC X(18)      VALUE 'PERMANENT INDEX'.
00244          10  FILLER      PIC X(05)      VALUE 'TAX'.
00245          10  FILLER      PIC X(10)      VALUE 'PRO-RATED'.
00246          10  FILLER      PIC X(17)      VALUE 'SF    HM    HO'.
00247          10  FILLER      PIC X(09)      VALUE 'PRORATION'.
00248                          SKIP1
00249      05  HDR-4.
00250          10  FILLER      PIC X(27)      VALUE '       NUMBER'.
00251          10  FILLER      PIC X(15)      VALUE 'NUMBER'.
00252          10  FILLER      PIC X(06)      VALUE 'YEAR'.
00253          10  FILLER      PIC X(09)      VALUE 'AMOUNT'.
00254          10  FILLER      PIC X(18)      VALUE 'STAT  STAT  STAT'.
00255          10  FILLER      PIC X(15)      VALUE 'ACCUMULATOR'.
00256          10  FILLER      PIC X(07)      VALUE 'MESSAGE'.
00257                          EJECT
00258  01  SSAS.
00259      05  PROP-QUAL-SSA.
00260          10  FILLER      PIC X(8)       VALUE 'PROPSEG '.
00261          10  FILLER      PIC X          VALUE '('.
00262          10  FILLER      PIC X(8)       VALUE 'PPROP   '.
00263          10  PROP-RO     PIC XX         VALUE 'EQ'.
00264          10  PROP-ARG    PIC 9(15)      PACKED-DECIMAL.
00265          10  FILLER      PIC X          VALUE ')'.
00266      05  ASMT-QUAL-SSA.
00267          10  FILLER      PIC X(8)       VALUE 'ASSMTSEG'.
00268          10  FILLER      PIC X          VALUE '('.
00269          10  FILLER      PIC X(8)       VALUE 'PYSRCH  '.
00270          10  ASMT-RO     PIC XX         VALUE 'EQ'.
00271          10  ASMT-ARG    PIC X(5).
00272          10  FILLER      PIC X          VALUE ')'.
00273      05  MAST-QUAL-SSA.
00274          10  FILLER      PIC X(8)       VALUE 'EXMASTSG'.
00275          10  FILLER      PIC X          VALUE '('.
00276          10  FILLER      PIC X(8)       VALUE 'EXRECCOD'.
00277          10  MAST-RO     PIC XX         VALUE 'EQ'.
00278          10  MAST-ARG    PIC X.
00279          10  FILLER      PIC X          VALUE ')'.
00280      05  DETL-QUAL-SSA.
00281          10  FILLER      PIC X(8)       VALUE 'EXDETLSG'.
00282          10  FILLER      PIC X          VALUE '('.
00283          10  FILLER      PIC X(8)       VALUE 'EXBRTHDT'.
00284          10  DETL-RO     PIC XX         VALUE 'EQ'.
00285          10  DETL-ARG    PIC 9(8).
00286          10  FILLER      PIC X          VALUE ')'.
00287      05  MAST-UNQUAL-SSA.
00288          10  FILLER      PIC X(8)       VALUE 'EXMASTSG'.
00289          10  FILLER      PIC X          VALUE ' '.
00290      05  DETL-UNQUAL-SSA.
00291          10  FILLER      PIC X(8)       VALUE 'EXDETLSG'.
00292          10  FILLER      PIC X          VALUE ' '.
00293      05  SOSC-UNQUAL-SSA.
00294          10  FILLER      PIC X(8)       VALUE 'EXSOSCSG'.
00295          10  FILLER      PIC X          VALUE ' '.
00296                          SKIP1
00297  COPY IMSCALLS.
00298                          EJECT
00299  01  IO-AREAS.
00300  COPY ASAIS150SG.
00301                          EJECT
00302  COPY ASAIS155SG.
00303                          EJECT
00304  01  IO-2.
00305  COPY ASAIS150SG.
00306                          EJECT
00307  LINKAGE SECTION.
00308  COPY IOPCBDESC.
00309                          SKIP1
00310  01  PCB-1.
00311  COPY PCBDESC.
00312          10  FILLER      PIC X(22).
00313                          EJECT
00314  01  PCB-2.
00315  COPY PIPCB2DESC.
00316          10  FILLER      PIC X(22).
00317                          SKIP3
00318  PROCEDURE DIVISION.
00319      ENTRY 'DLITCBL'  USING
00320            IO-PCB  PCB-1  PCB-2.
00321                          EJECT
00322  0000-START.
00323      DISPLAY SPACES
00324      OPEN INPUT CONTROL-FILE
00325      PERFORM 9000-CONTROL-EDIT-RTN
00326      IF RETURN-CODE = 16
00327         CLOSE CONTROL-FILE
00328         GOBACK
00329      END-IF
00330      PERFORM 0100-INITIAL-CHECKPOINT-RTN
00331      IF RETURN-CODE = 16
00332         CLOSE CONTROL-FILE
00333         GOBACK
00334      END-IF
00335      OPEN INPUT PRORATION-FILE
00336           OUTPUT PRINT-FILE
00337      PERFORM 9100-INITIALIZE-RTN
00338      PERFORM 1000-MAINLINE-RTN  UNTIL
00339              EOF-PRORATE  OR SEVERE-ERROR
00340      IF NO-SEVERE-ERROR
00341         PERFORM 3000-KEYPCL-BREAK-RTN
00342         PERFORM 7000-FINAL-RTN
00343      END-IF
00344      CLOSE CONTROL-FILE  PRORATION-FILE  PRINT-FILE
00345      GOBACK.
00346                          SKIP2
00347  0100-INITIAL-CHECKPOINT-RTN.
00348      CALL 'CBLTDLI' USING CHKP
00349                           IO-PCB
00350                           CHKP-ID-FLD.
00351      IF NOT IP-GOOD-STATUS
00352         MOVE 16  TO RETURN-CODE
00353         DISPLAY 'DL/I ERROR ON INITIAL CHECKPOINT CALL'
00354         DISPLAY '   STATUS CODE:  '  IP-STATUS-CODE
00355         DISPLAY '   IO PCB.....:  '  IO-PCB
00356      END-IF.
00357                          EJECT
00358  1000-MAINLINE-RTN.
00359      PERFORM 1050-READ-PRORATE-RTN
00360      IF PRORATE-GOOD-READ  AND  NO-SEVERE-ERROR
00361         IF PR-KEYPCL = KEYPCL-HOLD
00362            PERFORM 1100-CHECK-KEYPCL-RECORD-RTN
00363            IF GOOD-REC
00364               PERFORM 1200-COMPARE-PROP-RTN
00365               PERFORM 2000-KEYPCL-TABL-RTN
00366            END-IF
00367         ELSE
00368            PERFORM 3000-KEYPCL-BREAK-RTN
00369            PERFORM 1200-COMPARE-PROP-RTN
00370            PERFORM 2000-KEYPCL-TABL-RTN
00371         END-IF
00372      END-IF.
00373                          SKIP2
00374  1050-READ-PRORATE-RTN.
00375      READ PRORATION-FILE
00376         AT END
00377            SET EOF-PRORATE  TO TRUE
00378         NOT AT END
00379            ADD +1  TO REC-READ-CTR
00380            IF FIRST-REC
00381               MOVE PR-KEYPCL  TO KEYPCL-HOLD
00382            END-IF
00383            MOVE PR-KEYPCL  TO CUR-KEYPCL
00384            MOVE PR-PROP  TO CUR-PROP
00385            MOVE PR-RECCD  TO CUR-RECCD
00386            IF CUR-REC-KEY >= PREV-REC-KEY
00387               MOVE CUR-REC-KEY  TO PREV-REC-KEY
00388            ELSE
00389               MOVE 16  TO RETURN-CODE
00390               SET SEVERE-ERROR  TO TRUE
00391               MOVE REC-READ-CTR  TO CTR-DISPLAY
00392               DISPLAY SPACES
00393               DISPLAY 'SEQUENCE ERROR ON PRORATION FILE READ'
00394               DISPLAY '   RECORD NUMBER......: '  CTR-DISPLAY
00395               DISPLAY '   CURRENT RECORD KEY.: '  CUR-REC-KEY
00396               DISPLAY '   PREVIOUS RECORD KEY: '  PREV-REC-KEY
00397            END-IF
00398      END-READ.
00399                          SKIP2
00400  1100-CHECK-KEYPCL-RECORD-RTN.
00401      MOVE PR-KEYPCL  TO KEYPCL-CHECK
00402      IF CONDO-KEYPCL
00403         SET BYPASS-REC  TO TRUE
00404         ADD +1  TO REC-BYPASS-CTR
00405      ELSE
00406         SET GOOD-REC  TO TRUE
00407      END-IF.
00408                          SKIP2
00409  1200-COMPARE-PROP-RTN.
00410      IF PR-PROP NOT EQUAL PROP-HOLD
00411         SET ACCUM-PRORATE TO TRUE
00412         MOVE PR-PROP TO PROP-HOLD
00413      END-IF.
00414                          EJECT
00415  2000-KEYPCL-TABL-RTN.
00416      IF MAX-TABL-ENTRIES
00417         ADD +1  TO REC-BYPASS-CTR
00418         SET TABLFULL-PROCESSED  TO TRUE
00419         MOVE PRORATION-REC  TO KEYPCL-ENTRY (TABL-SUB)
00420         MOVE 15  TO RETURN-CODE
00421         MOVE TABL-SUB  TO PRINT-SUB  MTM-AMT
00422         MOVE ZEROS  TO TOTRATE-HOLD
00423         MOVE MAX-TABL-MSG  TO MSG-HOLD
00424         PERFORM 5000-PRINT-TABL-RTN
00425         DISPLAY 'RECORD EXCEEDS LIMIT OF '  TABL-SUB
00426                 ' FOR INCLUDING IN THE KEYPCL TABLE'
00427         DISPLAY '   KEY PARCEL NUMBER: '  PR-KEYPCL
00428         DISPLAY '   PROPERTY NUMBER..: '  PR-PROP
00429      ELSE
00430         IF ACCUM-PRORATE
00431            ADD PR-PRORATE  TO TOTAL-PRORATE
00432            SET NO-ACCUM-PRORATE TO TRUE
00433         END-IF
00434         ADD +1  TO TABL-SUB
00435         MOVE PRORATION-REC  TO KEYPCL-ENTRY (TABL-SUB)
00436         EVALUATE  PR-SF-STAT
00437            WHEN 'Q'
00438               SET Q-SET  TO TRUE
00439            WHEN 'R'
00440               SET R-SET  TO TRUE
00441            WHEN 'D'
00442               SET D-SET  TO TRUE
00443            WHEN OTHER
00444               SET X-SET  TO TRUE
00445         END-EVALUATE
00446         MOVE PR-SPLTCD  TO SPLTCD-CHECK
00447         IF SPLTCD-2-VALUE
00448            SET SPLTCD-2  TO TRUE
00449         END-IF
00450      END-IF.
00451                          SKIP2
00452  3000-KEYPCL-BREAK-RTN.
00453      MOVE TABL-SUB  TO TABL-ENTRIES
00454      EVALUATE  TRUE
00455         WHEN TABLFULL-PROCESSED
00456            SET FIRST-TABLFULL  TO TRUE
00457            ADD TABL-ENTRIES  TO REC-BYPASS-CTR
00458         WHEN SPLTCD-2
00459            SET SPLTCD-2-RESET  TO TRUE
00460            MOVE ZEROS  TO TOTRATE-HOLD
00461            MOVE SPLTCD-2-MSG  TO MSG-HOLD
00462            PERFORM 5000-PRINT-TABL-RTN  VARYING
00463                    PRINT-SUB FROM +1 BY +1  UNTIL
00464                    PRINT-SUB > TABL-ENTRIES
00465            ADD TABL-ENTRIES  TO REC-BYPASS-CTR
00466         WHEN OTHER
00467 *          PERFORM 3100-CHECK-PRORATE-RTN
00468            PERFORM 3200-EVALUATE-SWITCHES-RTN
00469            IF STOP-KEYPCL
00470               SET STOP-KEYPCL-RESET  TO TRUE
00471               ADD TABL-ENTRIES  TO REC-BYPASS-CTR
00472            ELSE
00473               PERFORM 3300-KEYPCL-PROCESS-RTN
00474                 VARYING TABL-SUB FROM +1 BY +1
00475                 UNTIL TABL-SUB > TABL-ENTRIES
00476            END-IF
00477      END-EVALUATE
00478      PERFORM 3900-CLEAR-IT-RTN.
00479                          EJECT
00480  3100-CHECK-PRORATE-RTN.
00481      MOVE TOTAL-PRORATE  TO PRORATE-CHECK
00482      IF NOT GOOD-PRORATE
00483         MOVE TOTAL-PRORATE  TO TOTRATE-HOLD
00484         MOVE TOTAL-PRORATE-MSG  TO MSG-HOLD
00485         PERFORM 5000-PRINT-TABL-RTN  VARYING
00486                 PRINT-SUB FROM +1 BY +1  UNTIL
00487                 PRINT-SUB > TABL-ENTRIES
00488      END-IF.
00489                          SKIP2
00490  3200-EVALUATE-SWITCHES-RTN.
00491      EVALUATE  TRUE
00492         WHEN (X-SET AND (Q-SET OR R-SET OR D-SET))
00493            CONTINUE
00494         WHEN OTHER
00495            SET STOP-KEYPCL  TO TRUE
00496            MOVE ZEROS  TO TOTRATE-HOLD
00497      END-EVALUATE.
00498                          SKIP2
00499  3300-KEYPCL-PROCESS-RTN.
00500      IF KT-SF-STAT (TABL-SUB) = 'Q' OR 'R' OR 'D'
00501         PERFORM 3400-IO-2-RTN
00502         IF NO-SEVERE-ERROR
00503            IF STOP-KEYPCL
00504               SET STOP-KEYPCL-RESET  TO TRUE
00505               ADD TABL-ENTRIES  TO REC-BYPASS-CTR
00506            ELSE
00507               PERFORM 3500-UPDATE-DB-RTN  VARYING
00508                  UPDT-SUB FROM +1 BY +1  UNTIL
00509                  UPDT-SUB > TABL-ENTRIES  OR SEVERE-ERROR
00510            END-IF
00511         END-IF
00512      END-IF.
00513                          SKIP2
00514  3400-IO-2-RTN.
00515      MOVE KT-PROP (TABL-SUB)     TO PROP-ARG
00516      MOVE KT-BRTHDTE (TABL-SUB)  TO DETL-ARG
00517      PERFORM 8200-GU-DETL-SEGMENT-IO2-RTN
00518      IF NO-SEVERE-ERROR
00519         IF PI2-SEG-FOUND
00520            SET EOF-SOSC-RESET  TO TRUE
00521            MOVE ZEROS  TO SOSC-SUB
00522            PERFORM 3410-GET-ALL-SOSC-RTN  UNTIL
00523                    EOF-SOSC  OR SEVERE-ERROR
00524            MOVE SOSC-SUB  TO SOSC-ENTRIES
00525         ELSE
00526            SET STOP-KEYPCL  TO TRUE
00527            MOVE TABL-SUB TO PRINT-SUB
00528            MOVE PI2-SEGMENT-LVL  TO SEG-LVL-HOLD
00529            PERFORM 3910-NOTFND-MSG-RTN
00530         END-IF
00531      END-IF.
00532                          SKIP2
00533  3410-GET-ALL-SOSC-RTN.
00534      PERFORM 8210-GNP-SOSC-SEGMENT-RTN
00535      IF NO-SEVERE-ERROR
00536         IF PI2-SEG-FOUND
00537            IF MAX-SOSC-ENTRIES
00538               SET EOF-SOSC  TO TRUE
00539            ELSE
00540               ADD +1  TO SOSC-SUB
00541               MOVE C155-SENFRZSOSEC  TO SOSC-ENTRY (SOSC-SUB)
00542            END-IF
00543         ELSE
00544            SET EOF-SOSC  TO TRUE
00545         END-IF
00546      END-IF.
00547                          SKIP2
00548  3500-UPDATE-DB-RTN.
00549      MOVE KT-PROP (UPDT-SUB)  TO PROP-ARG
00550      PERFORM 8100-GHU-DETL-SEGMENT-RTN
00551      IF NO-SEVERE-ERROR
00552         IF SEG-FOUND
00553            PERFORM 8110-DLET-DETL-SEGMENT-RTN
00554            IF NO-SEVERE-ERROR
00555               PERFORM 3550-DETL-SEG-FIELDS-RTN
00556               PERFORM 8120-ISRT-DETL-SEGMENT-RTN
00557               ADD +1  TO DETL-UPDT-CTR
00558               IF NO-SEVERE-ERROR
00559                  SET EOF-SOSC-RESET  TO TRUE
00560                  PERFORM 3600-PROCESS-SOSC-RTN  VARYING
00561                          SOSC-SUB FROM +1 BY +1  UNTIL
00562                          SOSC-SUB > SOSC-ENTRIES
00563                  PERFORM 3950-CHECKPOINT-RTN
00564               END-IF
00565            END-IF
00566         ELSE
00567            IF DB-3RD-SEG-LEVEL
00568               PERFORM 3550-DETL-SEG-FIELDS-RTN
00569               PERFORM 8120-ISRT-DETL-SEGMENT-RTN
00570               ADD +1  TO DETL-ISRT-CTR
00571               PERFORM 3600-PROCESS-SOSC-RTN
00572                  VARYING SOSC-SUB FROM +1 BY +1
00573                  UNTIL SOSC-SUB > SOSC-ENTRIES
00574            ELSE
00575               ADD +1  TO REC-BYPASS-CTR
00576               MOVE UPDT-SUB  TO PRINT-SUB
00577               MOVE SEGMENT-LVL  TO SEG-LVL-HOLD
00578               PERFORM 3910-NOTFND-MSG-RTN
00579            END-IF
00580         END-IF
00581      END-IF.
00582                          EJECT
00583  3550-DETL-SEG-FIELDS-RTN.
00584      MOVE C150-BIRTHDTE    OF IO-2  TO
00585           C150-BIRTHDTE    OF IO-AREAS
00586      MOVE C150-APPLSTNAM   OF IO-2  TO
00587           C150-APPLSTNAM   OF IO-AREAS
00588      MOVE C150-APPFSTNAM   OF IO-2  TO
00589           C150-APPFSTNAM   OF IO-AREAS
00590      MOVE C150-APPMI       OF IO-2  TO
00591           C150-APPMI       OF IO-AREAS
00592      MOVE C150-APPTITLE    OF IO-2  TO
00593           C150-APPTITLE    OF IO-AREAS
00594      MOVE C150-SSN         OF IO-2  TO
00595           C150-SSN         OF IO-AREAS
00596      MOVE C150-AGE         OF IO-2  TO
00597           C150-AGE         OF IO-AREAS
00598      MOVE C150-PHONE       OF IO-2  TO
00599           C150-PHONE       OF IO-AREAS
00600      MOVE C150-MARSTAT     OF IO-2  TO
00601           C150-MARSTAT     OF IO-AREAS
00602      MOVE C150-SPSREDIND   OF IO-2  TO
00603           C150-SPSREDIND   OF IO-AREAS
00604      MOVE C150-SPSLSTNAM   OF IO-2  TO
00605           C150-SPSLSTNAM   OF IO-AREAS
00606      MOVE C150-SPSFSTNAM   OF IO-2  TO
00607           C150-SPSFSTNAM   OF IO-AREAS
00608      MOVE C150-SPSMI       OF IO-2  TO
00609           C150-SPSMI       OF IO-AREAS
00610      MOVE C150-SPSTITLE    OF IO-2  TO
00611           C150-SPSTITLE    OF IO-AREAS
00612      MOVE C150-SPSMAIL     OF IO-2  TO
00613           C150-SPSMAIL     OF IO-AREAS
00614      MOVE C150-SFSTAT      OF IO-2  TO
00615           C150-SFSTAT      OF IO-AREAS
00616      MOVE C150-DENIALDTE   OF IO-2  TO
00617           C150-DENIALDTE   OF IO-AREAS
00618      MOVE C150-FSTAPPDTE   OF IO-2  TO
00619           C150-FSTAPPDTE   OF IO-AREAS
00620      MOVE C150-LSTAPPDTE   OF IO-2  TO
00621           C150-LSTAPPDTE   OF IO-AREAS
00622      MOVE C150-QUALDTE     OF IO-2  TO
00623           C150-QUALDTE     OF IO-AREAS
00624      MOVE C150-RTNDDTE     OF IO-2  TO
00625           C150-RTNDDTE     OF IO-AREAS
00626      MOVE C150-MANRTNCODES OF IO-2  TO
00627           C150-MANRTNCODES OF IO-AREAS
00628      MOVE C150-RMRKS       OF IO-2  TO
00629           C150-RMRKS       OF IO-AREAS
00630      MOVE C150-MISRTNCODES OF IO-2  TO
00631           C150-MISRTNCODES OF IO-AREAS
00632      MOVE C150-COOPSENSHRS OF IO-2  TO
00633           C150-COOPSENSHRS OF IO-AREAS
00634      MOVE C150-PCTSENSHRS  OF IO-2  TO
00635           C150-PCTSENSHRS  OF IO-AREAS
00636                          EJECT
00637      MOVE C150-LIFECARE    OF IO-2  TO
00638           C150-LIFECARE    OF IO-AREAS
00639      MOVE C150-NOINCOME    OF IO-2  TO
00640           C150-NOINCOME    OF IO-AREAS
00641      MOVE C150-SSINC       OF IO-2  TO
00642           C150-SSINC       OF IO-AREAS
00643      MOVE C150-RRBEN       OF IO-2  TO
00644           C150-RRBEN       OF IO-AREAS
00645      MOVE C150-CSBEN       OF IO-2  TO
00646           C150-CSBEN       OF IO-AREAS
00647      MOVE C150-OTHBEN      OF IO-2  TO
00648           C150-OTHBEN      OF IO-AREAS
00649      MOVE C150-VETBEN      OF IO-2  TO
00650           C150-VETBEN      OF IO-AREAS
00651      MOVE C150-PUBAID      OF IO-2  TO
00652           C150-PUBAID      OF IO-AREAS
00653      MOVE C150-WAGES       OF IO-2  TO
00654           C150-WAGES       OF IO-AREAS
00655      MOVE C150-INT         OF IO-2  TO
00656           C150-INT         OF IO-AREAS
00657      MOVE C150-NETRENT     OF IO-2  TO
00658           C150-NETRENT     OF IO-AREAS
00659      MOVE C150-NETCAPGAIN  OF IO-2  TO
00660           C150-NETCAPGAIN  OF IO-AREAS
00661      MOVE C150-OTHINC      OF IO-2  TO
00662           C150-OTHINC      OF IO-AREAS
00663      MOVE C150-TOTINC      OF IO-2  TO
00664           C150-TOTINC      OF IO-AREAS
00665      MOVE C150-SIGNED      OF IO-2  TO
00666           C150-SIGNED      OF IO-AREAS
00667      MOVE C150-NOTARIZED   OF IO-2  TO
00668           C150-NOTARIZED   OF IO-AREAS
00669      MOVE C150-BATCH       OF IO-2  TO
00670           C150-BATCH       OF IO-AREAS
00671      MOVE C150-ORIGDTE     OF IO-2  TO
00672           C150-ORIGDTE     OF IO-AREAS
00673      MOVE C150-KEYTIME     OF IO-2  TO
00674           C150-KEYTIME     OF IO-AREAS
00675      MOVE C150-ORIGEMPNO   OF IO-2  TO
00676           C150-ORIGEMPNO   OF IO-AREAS
00677      MOVE C150-TERMID      OF IO-2  TO
00678           C150-TERMID      OF IO-AREAS
00679      MOVE 'ASHMA832'  TO C150-LSTUPID OF IO-AREAS
00680      MOVE UPDT-DATE  TO C150-LSTUPDTE OF IO-AREAS
00681      MOVE SYSTEM-TIME  TO C150-LSTUPTIM OF IO-AREAS.
00682                          EJECT
00683  3600-PROCESS-SOSC-RTN.
00684      MOVE SOSC-ENTRY (SOSC-SUB)  TO C155-SENFRZSOSEC
00685      PERFORM 8130-ISRT-SOSC-SEGMENT-RTN
00686      ADD +1  TO SOSC-UPDT-CTR.
00687                          SKIP2
00688  3900-CLEAR-IT-RTN.
00689      MOVE PR-KEYPCL  TO KEYPCL-HOLD
00690      MOVE SPACES  TO KEYPCL-TABLE  SOSC-TABLE
00691      MOVE ZEROS  TO TOTAL-PRORATE  TABL-SUB  PROP-HOLD
00692      SET Q-RESET  TO TRUE
00693      SET R-RESET  TO TRUE
00694      SET D-RESET  TO TRUE
00695      SET X-RESET  TO TRUE.
00696                          SKIP2
00697  3910-NOTFND-MSG-RTN.
00698      MOVE ZEROS  TO TOTRATE-HOLD
00699      EVALUATE  SEG-LVL-HOLD
00700         WHEN '03'
00701            MOVE DETL-NOTFND-MSG  TO MSG-HOLD
00702         WHEN '02'
00703            MOVE MAST-NOTFND-MSG  TO MSG-HOLD
00704         WHEN '01'
00705            MOVE ASMT-NOTFND-MSG  TO MSG-HOLD
00706         WHEN OTHER
00707            MOVE ROOT-NOTFND-MSG  TO MSG-HOLD
00708      END-EVALUATE
00709      PERFORM 5000-PRINT-TABL-RTN.
00710                          SKIP2
00711  3950-CHECKPOINT-RTN.
00712      ADD +2  TO CHKP-CTR
00713      IF CHKP-CTR > CICV-HOLD  AND  NO-SEVERE-ERROR
00714         ADD 1  TO CHKP-ID-CTR
00715         MOVE ZEROS  TO CHKP-CTR
00716         PERFORM 8900-CHKP-RTN
00717      END-IF.
00718                          EJECT
00719  5000-PRINT-TABL-RTN.
00720      PERFORM 5100-TABL-FIELDS-RTN
00721      IF END-PAGE
00722         PERFORM 5200-HEADER-RTN
00723      END-IF
00724      ADD +2  TO LINE-CTR
00725      WRITE PRINT-REC  FROM DETAIL-LINE
00726            AFTER ADVANCING 2 LINES.
00727                          SKIP2
00728  5100-TABL-FIELDS-RTN.
00729      MOVE KT-KEYPCL (PRINT-SUB)  TO DL-KEYPCL
00730      INSPECT DL-KEYPCL  REPLACING ALL ',' BY '-'
00731      MOVE KT-PROP (PRINT-SUB)  TO DL-PROP
00732      INSPECT DL-PROP  REPLACING ALL ',' BY '-'
00733      MOVE KT-PRORATE (PRINT-SUB)  TO DL-PRORATE
00734      MOVE KT-SF-STAT (PRINT-SUB)  TO DL-SF-STAT
00735      MOVE KT-HM-STAT (PRINT-SUB)  TO DL-HM-STAT
00736      MOVE KT-HO-STAT (PRINT-SUB)  TO DL-HO-STAT
00737      MOVE TOTRATE-HOLD  TO DL-TOTRATE
00738      MOVE MSG-HOLD  TO DL-MSG.
00739                          SKIP2
00740  5200-HEADER-RTN.
00741      ADD +1  TO PAGE-CTR
00742      MOVE PAGE-CTR  TO H1-PAGE
00743      WRITE PRINT-REC  FROM HDR-1
00744            AFTER ADVANCING PAGE
00745      WRITE PRINT-REC  FROM HDR-2
00746            AFTER ADVANCING 2 LINES
00747      WRITE PRINT-REC  FROM HDR-3
00748            AFTER ADVANCING 2 LINES
00749      WRITE PRINT-REC  FROM HDR-4
00750            AFTER ADVANCING 1 LINES
00751      MOVE +6  TO LINE-CTR.
00752                          EJECT
00753  7000-FINAL-RTN.
00754      IF END-PAGE
00755         PERFORM 5200-HEADER-RTN
00756      END-IF
00757      DISPLAY SPACES
00758                          SKIP1
00759      MOVE REC-READ-CTR  TO CTR-DISPLAY
00760      MOVE CTR-DISPLAY  TO FL-TOTAL
00761      MOVE FINAL-MSG-1  TO FL-MSG
00762      WRITE PRINT-REC  FROM FINAL-LINE
00763            AFTER ADVANCING 3 LINES
00764      DISPLAY 'TOTAL KEY PARCEL RECORDS READ....: '  CTR-DISPLAY
00765                          SKIP1
00766      MOVE DETL-UPDT-CTR  TO CTR-DISPLAY
00767      MOVE CTR-DISPLAY  TO FL-TOTAL
00768      MOVE FINAL-MSG-2  TO FL-MSG
00769      WRITE PRINT-REC  FROM FINAL-LINE
00770            AFTER ADVANCING 1 LINES
00771      DISPLAY 'TOTAL DETAIL SEGMENTS UPDATED....: '  CTR-DISPLAY
00772                          SKIP1
00773      MOVE DETL-ISRT-CTR  TO CTR-DISPLAY
00774      MOVE CTR-DISPLAY  TO FL-TOTAL
00775      MOVE FINAL-MSG-2A TO FL-MSG
00776      WRITE PRINT-REC  FROM FINAL-LINE
00777            AFTER ADVANCING 1 LINES
00778      DISPLAY 'TOTAL DETAIL SEGMENTS INSERTED ..: '  CTR-DISPLAY
00779                          SKIP1
00780      MOVE SOSC-UPDT-CTR  TO CTR-DISPLAY
00781      MOVE CTR-DISPLAY  TO FL-TOTAL
00782      MOVE FINAL-MSG-3  TO FL-MSG
00783      WRITE PRINT-REC  FROM FINAL-LINE
00784            AFTER ADVANCING 1 LINES
00785      DISPLAY 'TOTAL SOC. SEC. SEGMENTS ADDED...: '  CTR-DISPLAY
00786                          SKIP1
00787      MOVE REC-BYPASS-CTR  TO CTR-DISPLAY
00788      MOVE CTR-DISPLAY  TO FL-TOTAL
00789      MOVE FINAL-MSG-4  TO FL-MSG
00790      WRITE PRINT-REC  FROM FINAL-LINE
00791            AFTER ADVANCING 1 LINES
00792      DISPLAY 'TOTAL KEY PARCEL RECORDS BYPASSED: '  CTR-DISPLAY.
00793                          EJECT
00794  8100-GHU-DETL-SEGMENT-RTN.
00795      CALL 'CBLTDLI'  USING GHU
00796           PCB-1
00797           C150-SENFRZDETAIL OF IO-AREAS
00798           PROP-QUAL-SSA  ASMT-QUAL-SSA
00799           MAST-UNQUAL-SSA  DETL-QUAL-SSA.
00800      IF SEG-FOUND  OR SEG-NOTFND
00801         CONTINUE
00802      ELSE
00803         MOVE '8100-GHU-DETL-SEGMENT-RTN'  TO PARA-NAME
00804         DISPLAY 'DL/I ERROR ON PROPERTY INFORMATION DB -- '
00805                 'GHU DETAIL SEGMENT CALL'
00806         DISPLAY ' PROPERTY QUAL SSA...........: ' PROP-QUAL-SSA
00807         DISPLAY ' ASSESSMENT QUAL SSA.........: ' ASMT-QUAL-SSA
00808         DISPLAY ' MASTER UNQUAL SSA...........: ' MAST-UNQUAL-SSA
00809         DISPLAY ' DETAIL QUAL SSA.............: ' DETL-QUAL-SSA
00810         PERFORM 8991-DISPLAY-PCB1-RTN
00811      END-IF.
00812                          SKIP2
00813  8110-DLET-DETL-SEGMENT-RTN.
00814      CALL 'CBLTDLI'  USING DLET
00815           PCB-1
00816           C150-SENFRZDETAIL OF IO-AREAS.
00817      IF NOT GOOD-STATUS
00818         MOVE '8110-DLET-DETL-SEGMENT-RTN'  TO PARA-NAME
00819         DISPLAY 'DL/I ERROR ON PROPERTY INFORMATION DB -- '
00820                 'DLET DETAIL SEGMENT CALL'
00821         DISPLAY '   NO SSAS USED IN THIS CALL'
00822         PERFORM 8991-DISPLAY-PCB1-RTN
00823      END-IF.
00824                          SKIP2
00825  8120-ISRT-DETL-SEGMENT-RTN.
00826      CALL 'CBLTDLI'  USING ISRT
00827           PCB-1
00828           C150-SENFRZDETAIL OF IO-AREAS
00829           PROP-QUAL-SSA  ASMT-QUAL-SSA
00830           MAST-UNQUAL-SSA  DETL-UNQUAL-SSA.
00831      IF NOT GOOD-STATUS
00832         MOVE '8120-ISRT-DETL-SEGMENT-RTN'  TO PARA-NAME
00833         DISPLAY 'DL/I ERROR ON PROPERTY INFORMATION DB -- '
00834                 'ISRT DETAIL SEGMENT CALL'
00835         DISPLAY ' PROPERTY QUAL SSA...........: ' PROP-QUAL-SSA
00836         DISPLAY ' ASSESSMENT QUAL SSA.........: ' ASMT-QUAL-SSA
00837         DISPLAY ' MASTER UNQUAL SSA...........: ' MAST-UNQUAL-SSA
00838         DISPLAY ' DETAIL UNQUAL SSA...........: ' DETL-UNQUAL-SSA
00839         PERFORM 8991-DISPLAY-PCB1-RTN
00840      END-IF.
00841                          EJECT
00842  8130-ISRT-SOSC-SEGMENT-RTN.
00843      CALL 'CBLTDLI'  USING ISRT
00844           PCB-1
00845           C155-SENFRZSOSEC
00846           PROP-QUAL-SSA  ASMT-QUAL-SSA
00847           MAST-UNQUAL-SSA  DETL-QUAL-SSA  SOSC-UNQUAL-SSA.
00848      IF NOT GOOD-STATUS
00849         MOVE '8130-ISRT-SOSC-SEGMENT-RTN'  TO PARA-NAME
00850         DISPLAY 'DL/I ERROR ON PROPERTY INFORMATION DB -- '
00851                 'ISRT SOCIAL SECURITY SEGMENT CALL'
00852         DISPLAY ' PROPERTY QUAL SSA...........: ' PROP-QUAL-SSA
00853         DISPLAY ' ASSESSMENT QUAL SSA.........: ' ASMT-QUAL-SSA
00854         DISPLAY ' MASTER UNQUAL SSA...........: ' MAST-UNQUAL-SSA
00855         DISPLAY ' DETAIL QUAL SSA.............: ' DETL-QUAL-SSA
00856         DISPLAY ' SOCIAL SECURITY UNQUAL SSA..: ' SOSC-UNQUAL-SSA
00857         PERFORM 8991-DISPLAY-PCB1-RTN
00858      END-IF.
00859                          SKIP2
00860  8200-GU-DETL-SEGMENT-IO2-RTN.
00861      CALL 'CBLTDLI'  USING GU
00862           PCB-2
00863           IO-2
00864           PROP-QUAL-SSA  ASMT-QUAL-SSA
00865           MAST-UNQUAL-SSA  DETL-QUAL-SSA.
00866      IF PI2-SEG-FOUND  OR PI2-SEG-NOTFND
00867         CONTINUE
00868      ELSE
00869         MOVE '8200-GU-DETL-SEGMENT-IO2-RTN'  TO PARA-NAME
00870         DISPLAY 'DL/I ERROR ON PROPERTY INFORMATION DB -- '
00871                 'GU DETAIL SEGMENT CALL'
00872         DISPLAY '   PROPERTY QUAL SSA...........: ' PROP-QUAL-SSA
00873         DISPLAY '   ASSESSMENT QUAL SSA.........: ' ASMT-QUAL-SSA
00874         DISPLAY '   MASTER QUAL SSA.............: ' MAST-QUAL-SSA
00875         DISPLAY '   DETAIL QUAL SSA.............: ' DETL-QUAL-SSA
00876         PERFORM 8992-DISPLAY-PCB2-RTN
00877      END-IF.
00878                          SKIP2
00879  8210-GNP-SOSC-SEGMENT-RTN.
00880      CALL 'CBLTDLI'  USING GNP
00881           PCB-2
00882           C155-SENFRZSOSEC
00883           SOSC-UNQUAL-SSA.
00884      IF PI2-SEG-FOUND  OR PI2-SEG-NOTFND
00885         CONTINUE
00886      ELSE
00887         MOVE '8210-GNP-SOSC-SEGMENT-RTN'  TO PARA-NAME
00888         DISPLAY 'DL/I ERROR ON PROPERTY INFORMATION DB -- '
00889                 'GNP SOCIAL SECURITY SEGMENT CALL'
00890         DISPLAY '   SOCIAL SECURITY UNQUAL SSA..: '
00891                 SOSC-UNQUAL-SSA
00892         PERFORM 8992-DISPLAY-PCB2-RTN
00893      END-IF.
00894                          EJECT
00895  8900-CHKP-RTN.
00896      CALL 'CBLTDLI' USING CHKP
00897           IO-PCB
00898           CHKP-ID-FLD.
00899      IF NOT IP-GOOD-STATUS
00900         MOVE 16  TO RETURN-CODE
00901         SET SEVERE-ERROR  TO TRUE
00902         DISPLAY 'DATA BASE ON CHECKPOINT CALL NO:  '
00903                 CHKP-ID-CTR
00904         DISPLAY '   STATUS CODE:  '  IP-STATUS-CODE
00905         DISPLAY '   IO PCB.....:  '  IO-PCB
00906 *       DISPLAY SPACES
00907         DISPLAY 'ROLL ISSUED TO TERMINATE PROGRAM'
00908         PERFORM 8905-ROLL-RTN
00909      END-IF.
00910                          SKIP2
00911  8905-ROLL-RTN.
00912      CALL 'CBLTDLI' USING ROLL.
00913                          SKIP2
00914  8991-DISPLAY-PCB1-RTN.
00915      SET SEVERE-ERROR  TO TRUE
00916      MOVE 16 TO RETURN-CODE
00917      DISPLAY '   PARAGRAPH...................: ' PARA-NAME
00918      DISPLAY 'PCB1 FIELDS'
00919      DISPLAY '   DBD NAME....................: ' DBD-NAME
00920      DISPLAY '   SEGMENT LEVEL...............: ' SEGMENT-LVL
00921      DISPLAY '   STATUS CODE.................: ' PCB-STATUS-CODE
00922      DISPLAY '   PROCESSING OPTIONS..........: ' PROC-OPT
00923      DISPLAY '   SEGMENT NAME................: ' NAME-FDBK
00924      DISPLAY '   LENGTH OF KEY FEED-BACK AREA: ' KEY-FDBK-LNG
00925      DISPLAY '   NUMBER OF SENSITIVE SEGMENTS: ' SEN-SEG
00926      DISPLAY '   KEY-FEEDBACK AREA...........: ' KEY-FDBK.
00927                          SKIP2
00928  8992-DISPLAY-PCB2-RTN.
00929      SET SEVERE-ERROR  TO TRUE
00930      MOVE 16 TO RETURN-CODE
00931      DISPLAY '   PARAGRAPH...................: ' PARA-NAME
00932      DISPLAY 'PCB2 FIELDS'
00933      DISPLAY '   DBD NAME....................: ' PI2-DBD-NAME
00934      DISPLAY '   SEGMENT LEVEL...............: ' PI2-SEGMENT-LVL
00935      DISPLAY '   STATUS CODE.................: ' PI2-STATUS-CODE
00936      DISPLAY '   PROCESSING OPTIONS..........: ' PI2-PROC-OPT
00937      DISPLAY '   SEGMENT NAME................: ' PI2-NAME-FDBK
00938      DISPLAY '   LENGTH OF KEY FEED-BACK AREA: ' PI2-KEY-FDBK-LNG
00939      DISPLAY '   NUMBER OF SENSITIVE SEGMENTS: ' PI2-SEN-SEG
00940      DISPLAY '   KEY-FEEDBACK AREA...........: ' PI2-KEY-FDBK.
00941                          EJECT
00942  9000-CONTROL-EDIT-RTN.
00943      READ CONTROL-FILE
00944         AT END
00945            MOVE 16  TO RETURN-CODE
00946            DISPLAY 'CONTROL CARD ERROR -- CONTROL '
00947                    'CARD NOT FOUND'
00948         NOT AT END
00949            PERFORM 9010-DO-CONTROL-EDIT-RTN
00950      END-READ.
00951                          SKIP2
00952  9010-DO-CONTROL-EDIT-RTN.
00953      IF CICV-VALUEX NUMERIC
00954         MOVE CICV-VALUE  TO CICV-HOLD
00955      ELSE
00956         MOVE 16  TO RETURN-CODE
00957         DISPLAY 'CONTROL CARD ERROR -- CICV VALUE IS NOT '
00958                 'NUMERIC: '  CICV-VALUEX
00959      END-IF
00960      IF PROCYR-VALUEX NUMERIC
00961         MOVE PROCYR-VALUE  TO PROCYR-HOLD
00962      ELSE
00963         MOVE 16  TO RETURN-CODE
00964         DISPLAY 'CONTROL CARD ERROR -- PROC YEAR VALUE IS NOT '
00965                 'NUMERIC: '  PROCYR-VALUEX
00966      END-IF.
00967                          SKIP2
00968  9100-INITIALIZE-RTN.
00969      ACCEPT SYSTEM-DATE  FROM DATE
00970      ACCEPT SYSTEM-TIME  FROM TIME
00971      COMPUTE CURRENT-DATE = SYSTEM-DATE * YMD-TO-MDY
00972      MOVE CURRENT-DATE  TO H1-DATE
00973      MOVE SYSTEM-DATE  TO UPDT-YMD
00974      IF UPDT-YY > 80
00975         MOVE 19  TO UPDT-CC
00976      ELSE
00977         MOVE 20  TO UPDT-CC
00978      END-IF
00979      MOVE PROCYR-HOLD  TO DL-TAXYR  ASMT-PROCYR  ASMT-TAXYR
00980      MOVE ASMT-ARG-HOLD  TO ASMT-ARG
00981      WRITE PRINT-REC  FROM BLANK-LINE
00982            AFTER ADVANCING PAGE.
