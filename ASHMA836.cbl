00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASHMA836.
00003 *AUTHOR. SQUILLACE - KALEMBA
00004 *DATE-WRITTEN.   AUG,1995.
00005 *****************************************************************
00006 *REMARKS.
00007 *       THIS PROGRAM UPDATES QUALIFIED SENIOR FREEZE EXEMPTION
00008 *       DATA TO THE TAX BILLING ASSESSMENT MASTER AND CREATES
00009 *       THE HOMESTEAD AND HOMEOWNER FILES FROM THE ASSESSORS
00010 *       DATA BASE.
00011 *****************************************************************
00012
00013  ENVIRONMENT DIVISION.
00014  CONFIGURATION SECTION.
00015  SOURCE-COMPUTER. IBM-370.
00016  OBJECT-COMPUTER. IBM-370.
00017  INPUT-OUTPUT SECTION.
00018  FILE-CONTROL.
00019      SELECT CNTRL-FILE          ASSIGN TO UT-S-CNTRLFLE.
00020      SELECT MASTER-IN           ASSIGN TO UT-S-MSTRIN.
00021      SELECT MASTER-OUT-2        ASSIGN TO UT-S-MSTROUT.
00022      SELECT SR-HOMESTEAD-FILE   ASSIGN TO UT-S-HOMESTD.
00023      SELECT HOMEOWNER-EXEMPT    ASSIGN TO UT-S-HOMEXMPT.
00024      SELECT COOP-FILE           ASSIGN TO UT-S-COOP.
00025      SELECT PRINT-FILE          ASSIGN TO UT-S-RPT.
00026        SKIP1
00027  DATA DIVISION.
00028  FILE SECTION.
00029  FD  CNTRL-FILE
00030      RECORDING MODE IS F
00031      BLOCK CONTAINS 0 RECORDS
00032      RECORD CONTAINS 80 CHARACTERS
00033      LABEL RECORDS ARE STANDARD
00034      DATA RECORD IS CNTRL-CARD.
00035  01  CNTRL-CARD.
00036      03 CC-YR.
00037         05 CC-YR-CENT       PIC 99.
00038         05 CC-YR-N          PIC 99.
00039      03 FILLER              PIC X(76).
00040        SKIP3
00041  COPY ASREASFD01.
00042  EJECT
00043  COPY ASREASFD03.
00044  EJECT
00045  COPY ASHMSTFD01.
00046  EJECT
00047  COPY ASHMOWFD01.
00048  SKIP3
00049  COPY COOPFLFD01.
00050  SKIP3
00051  COPY PRINTFILE.
00052  EJECT
00053  WORKING-STORAGE SECTION.
00054  77  SUB                    PIC S9(4) VALUE +0 BINARY.
00055  77  ACPT-DATE              PIC S9(6) VALUE +0.
00056  77  MSTR-RECS-RD           PIC S9(7) VALUE +0 PACKED-DECIMAL.
00057  77  MSTR-RECS-WR           PIC S9(7) VALUE +0 PACKED-DECIMAL.
00058  77  HOWNR-RECS-WR          PIC S9(7) VALUE +0 PACKED-DECIMAL.
00059  77  HMSTD-RECS-WR          PIC S9(7) VALUE +0 PACKED-DECIMAL.
00060  77  COOP-RECS-WR           PIC S9(7) VALUE +0 PACKED-DECIMAL.
00061  77  MSTR-EX-SEGS-RD        PIC S9(7) VALUE +0 PACKED-DECIMAL.
00062  77  DETL-EX-SEGS-RD        PIC S9(7) VALUE +0 PACKED-DECIMAL.
00063  77  MSTR-STAT2-3           PIC S9(7) VALUE +0 PACKED-DECIMAL.
00064  77  MSTR-STAT2-5           PIC S9(7) VALUE +0 PACKED-DECIMAL.
00065  77  LINE-CNT               PIC S999  VALUE +60 PACKED-DECIMAL.
00066  77  PAGE-CNT               PIC S9(5) VALUE +0 PACKED-DECIMAL.
00067  77  BLNK                   PIC X     VALUE SPACE.
00068  77  HO-SWITCH              PIC X     VALUE 'N'.
00069  77  END-MSTR-SW            PIC X     VALUE 'N'.
00070      88  MSTR-EOF                     VALUE 'Y'.
00071  77  STOP-PROCESSING-SW     PIC X     VALUE 'N'.
00072      88  STOP-PROCESSING              VALUE 'Y'.
00073          EJECT
00074  01  WORK-AREA.
00075      05  WS-BIRTHDTE.
00076          10  WS-BDMMDD      PIC X(4).
00077          10  FILLER         PIC XX.
00078          10  WS-BDYY        PIC XX.
00079      05  WS-BIRTHDTE-REF.
00080          10  WS-BDYY        PIC XX.
00081          10  WS-BDMMDD      PIC X(4).
00082      05  WS-VALUE           PIC 9(3)V9(6).
00083      05  WS-VALUE-R REDEFINES WS-VALUE PIC 9(9).
00084      05  WS-VALUE5          PIC 9(8)V9.
00085      05  WS-VALUE5-R REDEFINES WS-VALUE5 PIC 9(9).
00086      05  WS-YEAR.
00087          10  WS-CC              PIC 9(2) VALUE 19.
00088          10  WS-YY              PIC 9(2).
00089      05  WS-YR-N    REDEFINES  WS-YEAR PIC 9(4).
00090
00091      05  WS-TXCD                PIC 9(05).
00092      05  WS-TXCD-X REDEFINES  WS-TXCD.
00093          10  WS-TXCD-TOWN       PIC 99.
00094          10  FILLER             PIC X(3).
00095
00096      05  CURR-KEY.
00097          10 CURR-TOWN           PIC 99.
00098          10 CURR-VOL            PIC 999   PACKED-DECIMAL.
00099          10 CURR-PROP           PIC 9(15) PACKED-DECIMAL.
00100          10 CURR-TXTYP          PIC 9.
00101          10 CURR-RECCD          PIC 9.
00102
00103      05  PREV-KEY               PIC X(14).
00104
00105      05  WS-DOB.
00106          10   WS-DOB-YY         PIC 99.
00107          10   WS-DOB-MM         PIC 99.
00108          10   WS-DOB-DD         PIC 99.
00109
00110      05  WS-MDCY.
00111          10   WS-MO             PIC 99.
00112          10   WS-DA             PIC 99.
00113          10   WS-CE             PIC 99.
00114          10   WS-YR             PIC 99.
00115      05  WS-MMDDCCYY  REDEFINES WS-MDCY    PIC 9(8).
00116
00117      05  CURR-DATE.
00118          10  CENT                   PIC  99       VALUE 19.
00119          10  YR-MO-DAY              PIC  9(6)     VALUE  0.
00120      05  CURR-DATE-N   REDEFINES    CURR-DATE PIC 9(8).
00121
00122      05  CURR-TIME-X.
00123          10  CURR-TIME              PIC  9(6).
00124          10  CURR-TIME-2            PIC  9(2).
00125      05  CURR-TIME-N   REDEFINES    CURR-TIME-X PIC 9(8).
00126
00127      05  MO-DAY-YR                  PIC  9(6)     VALUE  0.
00128      EJECT
00129  01  PRINT-LINES.
00130      05  HL-1.
00131          10  FILLER         PIC XXX   VALUE SPACE.
00132          10  H1-DATE        PIC 99B99B99.
00133          10  FILLER         PIC X(39) VALUE SPACE.
00134          10  FILLER         PIC X(69) VALUE
00135          'OFFICE OF THE COOK COUNTY ASSESSOR'.
00136          10  FILLER         PIC X(5)  VALUE 'PAGE'.
00137          10  H1-PAGE        PIC ZZ,ZZ9.
00138          SKIP1
00139      05  HL-2.
00140          10  FILLER         PIC XXX   VALUE SPACE.
00141          10  FILLER         PIC X(40) VALUE 'ASHMA836'.
00142          10  FILLER         PIC X(47) VALUE
00143      'SENIOR FREEZE - ASSESSMENT MASTER UPDATE REPORT'.
00144          SKIP1
00145      05  HL-3.
00146          10  FILLER         PIC X(10) VALUE SPACE.
00147          10  FILLER         PIC X(44) VALUE
00148          'PERMANENT INDEX    TAX'.
00149          SKIP1
00150      05  HL-4.
00151          10  FILLER         PIC XXX   VALUE SPACE.
00152          10  FILLER         PIC X(52) VALUE
00153          'VOL        NUMBER         TYPE    STATUS-2'.
00154          10  FILLER         PIC X(07) VALUE
00155          'MESSAGE'.
00156          SKIP1
00157      05  DET-LN.
00158          10  FILLER         PIC XXX   VALUE SPACE.
00159          10  DL-VOL         PIC 999.
00160          10  FILLER         PIC XXX   VALUE SPACE.
00161          10  DL-PROP        PIC 99,99,999,999,9999.
00162          10  FILLER         PIC XXX   VALUE SPACE.
00163          10  DL-TXTYP       PIC 9.
00164          10  FILLER         PIC X(10) VALUE SPACE.
00165          10  DL-STAT-2      PIC X.
00166          10  FILLER         PIC X(13) VALUE SPACE.
00167          10  DL-MSG         PIC X(36).
00168          SKIP1
00169      05  TOT-LN.
00170          10  FILLER     PIC XX    VALUE SPACE.
00171          10  TL-TOT     PIC Z,ZZZ,ZZ9.
00172          10  FILLER     PIC XX    VALUE SPACE.
00173          10  TL-MSG     PIC X(45).
00174          SKIP3
00175  01  MSGS.
00176      05  MSG-1                  PIC X(50) VALUE
00177          'NO MATCHING ROOT SEGMENT'.
00178      05  MSG-2                  PIC X(50) VALUE
00179          'NO MATCHING ASSESSMENT DATA SEGMENT'.
00180      05  MSG-3                  PIC X(50) VALUE
00181          'NO MATCHING MASTER EXEMPTION SEGMENT'.
00182      05  MSG-4                  PIC X(50) VALUE
00183          'NO MATCHING EXEMPTION DETAIL SEGMENT'.
00184       EJECT
00185  01  IO-AREA.
00186  COPY PIROOTSEGM.
00187  COPY PIASSESSMT.
00188  COPY ASAIS145SG.
00189  COPY ASAIS150SG.
00190     EJECT
00191  COPY IMSCALLS.
00192     EJECT
00193  COPY ASAISSSA29.
00194     EJECT
00195  LINKAGE SECTION.
00196  COPY IOPCBDESC.
00197  01  PI-PCB1.
00198  COPY PIPCB1DESC.
00199          07  PI-SEG-KEY      PIC X(17).
00200         SKIP3
00201  PROCEDURE DIVISION.
00202  100-HOMEUSEKEEPING.
00203      ENTRY 'DLITCBL' USING IO-PCB  PI-PCB1
00204         SKIP1
00205      OPEN INPUT CNTRL-FILE
00206                 MASTER-IN
00207          OUTPUT SR-HOMESTEAD-FILE MASTER-OUT-2
00208                 HOMEOWNER-EXEMPT COOP-FILE
00209                 PRINT-FILE.
00210          SKIP1
00211      READ CNTRL-FILE AT END
00212         DISPLAY 'PARM CARD IS MISSING'
00213         MOVE 16 TO RETURN-CODE.
00214      IF CC-YR        NUMERIC
00215         CONTINUE
00216      ELSE
00217         MOVE   16   TO RETURN-CODE
00218         DISPLAY 'PROCESS YEAR MUST BE NUMERIC'
00219      END-IF.
00220      IF RETURN-CODE     EQUAL  16
00221         STOP RUN
00222      END-IF
00223      SKIP1
00224      ACCEPT     ACPT-DATE    FROM  DATE
00225      MULTIPLY   ACPT-DATE    BY    +100.0001  GIVING ACPT-DATE
00226      MOVE       ACPT-DATE    TO    H1-DATE
00227      INSPECT    H1-DATE     REPLACING ALL SPACES  BY  '/'
00228      SKIP1
00229      WRITE PRINT-REC  FROM BLNK AFTER ADVANCING PAGE
00230      SKIP1
00231      PERFORM 200-MAINLINE
00232          UNTIL  STOP-PROCESSING
00233           OR MSTR-EOF
00234           OR RETURN-CODE EQUAL 16.
00235                SKIP1
00236      IF LINE-CNT
00237         GREATER THAN +55
00238         PERFORM 950-MSG-HEADING-RTN.
00239        SKIP1
00240      DISPLAY
00241      'TOTAL MASTER RECORDS READ            = ' MSTR-RECS-RD
00242      MOVE MSTR-RECS-RD TO TL-TOT
00243      MOVE
00244      'TOTAL MASTER RECORDS READ'  TO  TL-MSG
00245      WRITE PRINT-REC FROM TOT-LN AFTER ADVANCING 3.
00246        SKIP1
00247      DISPLAY
00248      'TOTAL MASTER RECORDS WRITTEN         = ' MSTR-RECS-WR
00249      MOVE MSTR-RECS-WR TO TL-TOT
00250      MOVE
00251      'TOTAL MASTER RECORDS WRITTEN'  TO  TL-MSG
00252      WRITE PRINT-REC FROM TOT-LN AFTER ADVANCING 1.
00253        SKIP1
00254      DISPLAY
00255      'TOTAL HOMEOWNER RECORDS WRITTEN      = ' HOWNR-RECS-WR
00256      MOVE HOWNR-RECS-WR TO TL-TOT
00257      MOVE
00258      'TOTAL HOMEOWNER RECORDS WRITTEN'  TO  TL-MSG
00259      WRITE PRINT-REC FROM TOT-LN AFTER ADVANCING 1.
00260        SKIP1
00261      DISPLAY
00262      'TOTAL COOP RECORDS WRITTEN           = ' COOP-RECS-WR
00263      MOVE COOP-RECS-WR TO TL-TOT
00264      MOVE
00265      'TOTAL COOP RECORDS WRITTEN'  TO  TL-MSG
00266      WRITE PRINT-REC FROM TOT-LN AFTER ADVANCING 1.
00267        SKIP1
00268      DISPLAY
00269      'TOTAL HOMESTEAD RECORDS WRITTEN      = ' HMSTD-RECS-WR
00270      MOVE HMSTD-RECS-WR TO TL-TOT
00271      MOVE
00272      'TOTAL HOMESTEAD RECORDS WRITTEN'  TO  TL-MSG
00273      WRITE PRINT-REC FROM TOT-LN AFTER ADVANCING 1.
00274        SKIP1
00275      DISPLAY
00276      'TOTAL MASTER EXEMPTION SEGMENTS READ = ' MSTR-EX-SEGS-RD
00277      MOVE MSTR-EX-SEGS-RD TO TL-TOT
00278      MOVE
00279      'TOTAL MASTER EXEMPTION SEGMENTS READ'  TO  TL-MSG
00280      WRITE PRINT-REC FROM TOT-LN AFTER ADVANCING 1.
00281        SKIP1
00282      DISPLAY
00283      'TOTAL DETAIL EXEMPTION SEGMENTS READ = ' DETL-EX-SEGS-RD
00284      MOVE DETL-EX-SEGS-RD TO TL-TOT
00285      MOVE
00286      'TOTAL DETAIL EXEMPTION SEGMENTS READ'  TO  TL-MSG
00287      WRITE PRINT-REC FROM TOT-LN AFTER ADVANCING 1.
00288        SKIP1
00289      DISPLAY
00290      'TOTAL MASTER RECORDS WITH STATUS2 EQUAL TO 3 = '
00291      MSTR-STAT2-3
00292      MOVE MSTR-STAT2-3 TO TL-TOT
00293      MOVE
00294      'TOTAL MASTER RECORDS WITH STATUS2 EQUAL TO 3 = '
00295                                              TO  TL-MSG
00296      WRITE PRINT-REC FROM TOT-LN AFTER ADVANCING 1.
00297        SKIP1
00298      DISPLAY
00299      'TOTAL MASTER RECORDS WITH STATUS2 EQUAL TO 5 = '
00300      MSTR-STAT2-5
00301      MOVE MSTR-STAT2-5 TO TL-TOT
00302      MOVE
00303      'TOTAL MASTER RECORDS WITH STATUS2 EQUAL TO 5 = '
00304                                              TO  TL-MSG
00305      WRITE PRINT-REC FROM TOT-LN AFTER ADVANCING 1.
00306        SKIP1
00307      CLOSE      MASTER-IN
00308                 MASTER-OUT-2
00309                 CNTRL-FILE
00310                 SR-HOMESTEAD-FILE
00311                 HOMEOWNER-EXEMPT COOP-FILE
00312                 PRINT-FILE
00313      GOBACK.
00314       SKIP3
00315  200-MAINLINE.
00316      MOVE 'N' TO HO-SWITCH
00317      PERFORM  300-RD-MSTR-FILE
00318      IF NOT MSTR-EOF
00319         IF (M-EXEMPT OR M-RAILROAD)
00320             PERFORM 450-MOVE-TO-MSTR-OUT
00321         ELSE
00322           PERFORM 410-GU-ASMT-SEG
00323           IF PI-SEG-FOUND
00324              PERFORM 420-GHN-MSTR
00325              ADD +1 TO MSTR-EX-SEGS-RD
00326              IF PI-SEG-FOUND
00327                 ADD +1 TO DETL-EX-SEGS-RD
00328                 PERFORM 430-GNP-DETL-SEG
00329                 IF PI-SEG-FOUND
00330                    IF C145-RECCODE = 1
00331                       PERFORM 500-MOVE-TO-MSTR1
00332                       IF C150-HOSTAT = '1'
00333                          MOVE C150-HOSTAT TO MA-HO-RESPONSE
00334                          PERFORM 600-MOVE-WR-HOMEOWNER
00335                       END-IF
00336                       IF C150-HSSTAT = '1'
00337                          PERFORM 700-MOVE-WR-HOMESTEAD
00338                          IF C145-HSUNITS EQUAL 00001
00339 *                           MOVE '3' TO MA-STAT2
00340                             ADD +1 TO MSTR-STAT2-3
00341                          END-IF
00342                          IF C145-HSUNITS > 00001
00343                             PERFORM 800-MOVE-WR-COOP
00344 *                           MOVE '5' TO MA-STAT2
00345                             ADD +1 TO MSTR-STAT2-5
00346                          END-IF
00347                       END-IF
00348                       IF C150-HSSTAT NOT EQUAL '1'
00349                          IF C145-HSUNITS GREATER THAN 00001
00350                             PERFORM 800-MOVE-WR-COOP
00351 *                           MOVE '5' TO MA-STAT2
00352                             ADD +1 TO MSTR-STAT2-5
00353                          END-IF
00354                       END-IF
00355                       PERFORM 590-GNP-DETL-SEG UNTIL
00356                               PI-SEG-NOTFND
00357                       PERFORM 810-WR-MSTR
00358                    ELSE
00359                       PERFORM 550-MOVE-TO-MSTR2
00360                       IF C150-HOSTAT = '1'
00361                          MOVE C150-HOSTAT TO MA-HO-RESPONSE
00362                          PERFORM 600-MOVE-WR-HOMEOWNER
00363                       END-IF
00364                       IF C145-HSUNITS GREATER THAN 00001
00365                          PERFORM 700-MOVE-WR-HOMESTEAD
00366                          PERFORM 800-MOVE-WR-COOP
00367 *                        MOVE '5' TO MA-STAT2
00368                          ADD +1 TO MSTR-STAT2-5
00369                       ELSE
00370                          IF C150-HSSTAT = '1'
00371                             PERFORM 700-MOVE-WR-HOMESTEAD
00372 *                           MOVE '3' TO MA-STAT2
00373                             ADD +1 TO MSTR-STAT2-3
00374                          END-IF
00375                       END-IF
00376                       PERFORM 810-WR-MSTR
00377                       IF C150-HOSTAT = '1'
00378                          MOVE 'Y' TO HO-SWITCH
00379                       ELSE
00380                          MOVE 'N' TO HO-SWITCH
00381                       END-IF
00382                       PERFORM 580-GNP-DETL-SEG UNTIL
00383                               PI-SEG-NOTFND
00384                    END-IF
00385                 END-IF
00386              END-IF
00387           END-IF
00388         END-IF
00389      END-IF.
00390      SKIP3
00391  300-RD-MSTR-FILE.
00392      READ MASTER-IN
00393          AT END  MOVE 'Y' TO END-MSTR-SW.
00394      IF MSTR-EOF
00395          CONTINUE
00396      ELSE
00397          ADD  +1 TO MSTR-RECS-RD
00398      END-IF.
00399      SKIP3
00400  410-GU-ASMT-SEG.
00401      MOVE  M-PROP       TO   LVL1-ARG
00402      MOVE  CC-YR-N      TO   LVL2-PROCYR
00403                              LVL2-TXYR
00404      MOVE  M-TXTYP      TO   LVL2-TXTYP
00405      CALL 'CBLTDLI' USING GU
00406                           PI-PCB1
00407                           PY-ASMTDATA
00408                           LVL1-QUAL-SSA
00409                           LVL2-QUAL-SSA
00410      IF PI-DB-GOOD-STATUS
00411         NEXT SENTENCE
00412      ELSE
00413         IF PI-SEG-NOTFND
00414            IF PI-SEGMENT-LVL = '00'
00415               PERFORM 450-MOVE-TO-MSTR-OUT
00416               MOVE SPACES TO DET-LN
00417               MOVE MSG-1          TO DL-MSG
00418               PERFORM 900-MSG-LINE-LISTING
00419               WRITE PRINT-REC FROM DET-LN
00420                     AFTER ADVANCING 2
00421               ADD +2 TO LINE-CNT
00422            ELSE
00423               IF PI-DB-1ST-SEG-LEVEL
00424                  PERFORM 450-MOVE-TO-MSTR-OUT
00425                  MOVE SPACES TO DET-LN
00426                  MOVE MSG-2          TO DL-MSG
00427                  PERFORM 900-MSG-LINE-LISTING
00428                  WRITE PRINT-REC FROM DET-LN
00429                        AFTER ADVANCING 2
00430                  ADD +2 TO LINE-CNT
00431               ELSE
00432                  NEXT SENTENCE
00433         ELSE
00434            DISPLAY 'DL/1 ERROR 410-GU-ASMT-SEG'
00435            DISPLAY 'PCB  AREA: ' PI-PCB1
00436            MOVE 15 TO RETURN-CODE
00437            MOVE 'Y' TO STOP-PROCESSING-SW
00438      END-IF.
00439      SKIP3
00440  420-GHN-MSTR.
00441      CALL 'CBLTDLI' USING GHN
00442                           PI-PCB1
00443                           C145-SENFRZMASTER
00444                           LVL1-QUAL-SSA
00445                           LVL2-QUAL-SSA
00446                           LVL3-UNQUAL-SSA
00447      IF PI-DB-GOOD-STATUS
00448         NEXT SENTENCE
00449      ELSE
00450         IF PI-DB-END   OR
00451            PI-SEG-NOTFND
00452            IF PI-DB-2ND-SEG-LEVEL
00453               PERFORM 450-MOVE-TO-MSTR-OUT
00454               MOVE SPACES TO DET-LN
00455               MOVE MSG-3 TO DL-MSG
00456               PERFORM 900-MSG-LINE-LISTING
00457               WRITE PRINT-REC FROM DET-LN
00458                     AFTER ADVANCING 2
00459               ADD +2 TO LINE-CNT
00460            ELSE
00461               NEXT SENTENCE
00462         ELSE
00463            DISPLAY 'DL/1 ERROR  420-GHN-MSTR'
00464            DISPLAY 'PCB  AREA: ' PI-PCB1
00465            MOVE 15 TO RETURN-CODE
00466            MOVE 'Y' TO STOP-PROCESSING-SW
00467         END-IF
00468      END-IF.
00469      SKIP3
00470  430-GNP-DETL-SEG.
00471      CALL 'CBLTDLI' USING GNP
00472                           PI-PCB1
00473                           C150-SENFRZDETAIL
00474                           LVL4-UNQUAL-SSA
00475      IF PI-DB-GOOD-STATUS
00476         NEXT SENTENCE
00477      ELSE
00478         IF PI-DB-END   OR
00479            PI-SEG-NOTFND
00480            IF PI-DB-3RD-SEG-LEVEL
00481               PERFORM 450-MOVE-TO-MSTR-OUT
00482               MOVE SPACES TO DET-LN
00483               MOVE MSG-4 TO DL-MSG
00484               PERFORM 900-MSG-LINE-LISTING
00485               WRITE PRINT-REC FROM DET-LN
00486                     AFTER ADVANCING 2
00487               ADD +2 TO LINE-CNT
00488            ELSE
00489               NEXT SENTENCE
00490         ELSE
00491            DISPLAY 'DL/1 ERROR  430-GNP-DETL-SEG'
00492            DISPLAY 'PCB  AREA: ' PI-PCB1
00493            MOVE 15 TO RETURN-CODE
00494            MOVE 'Y' TO STOP-PROCESSING-SW
00495         END-IF
00496      END-IF.
00497      SKIP3
00498  450-MOVE-TO-MSTR-OUT.
00499      MOVE 0 TO M-VALUE (1)
00500      MOVE 1000000 TO M-VALUE (2)
00501      MOVE 1000 TO M-VALUE (5)
00502      MOVE  MSTR-IN-REC TO MSTR-OUT-REC-2
00503      PERFORM 810-WR-MSTR.
00504      SKIP3
00505  500-MOVE-TO-MSTR1.
00506      MOVE  MSTR-IN-REC TO MSTR-OUT-REC-2
00507      MOVE ZEROS TO MA-VALUE-1 (1)
00508      MOVE C145-BVYNOCALCIND TO MA-NO-CALC-SF
00509      IF C145-PROPCT = 0
00510          MOVE 1000000 TO MA-VALUE-1 (2)
00511      ELSE
00512          MOVE C145-PROPCT TO WS-VALUE
00513          MOVE WS-VALUE-R TO MA-VALUE-1 (2).
00514      IF C145-OCCFAC = 0
00515          MOVE 1000 TO MA-VALUE-1 (5)
00516      ELSE
00517          MOVE C145-OCCFAC TO WS-VALUE5
00518          MOVE WS-VALUE5-R TO MA-VALUE-1 (5).
00519      IF C150-SFSTAT = 'Q'
00520         MOVE C145-CYFNLEVDIFF TO MA-VALUE-1 (1)
00521         MOVE 'Q' TO MA-SF-IND.
00522      SKIP3
00523  550-MOVE-TO-MSTR2.
00524      MOVE  MSTR-IN-REC TO MSTR-OUT-REC-2
00525      MOVE ZEROS TO MA-VALUE-1 (1)
00526      MOVE C145-BVYNOCALCIND TO MA-NO-CALC-SF
00527      IF C145-PROPCT = 0
00528          MOVE 1000000 TO MA-VALUE-1 (2)
00529      ELSE
00530          MOVE C145-PROPCT TO WS-VALUE
00531          MOVE WS-VALUE-R TO MA-VALUE-1 (2).
00532      IF C145-OCCFAC = 0
00533          MOVE 1000 TO MA-VALUE-1 (5)
00534      ELSE
00535          MOVE C145-OCCFAC TO WS-VALUE5
00536          MOVE WS-VALUE5-R TO MA-VALUE-1 (5).
00537      IF C145-SFSHARES = ZEROS
00538         CONTINUE
00539      ELSE
00540         MOVE C145-CYFNLEVDIFF TO MA-VALUE-1 (1)
00541         MOVE 'Q' TO MA-SF-IND.
00542      SKIP3
00543  580-GNP-DETL-SEG.
00544      CALL 'CBLTDLI' USING GNP
00545                           PI-PCB1
00546                           C150-SENFRZDETAIL
00547                           LVL4-UNQUAL-SSA
00548      IF PI-DB-GOOD-STATUS
00549         IF HO-SWITCH = 'Y'
00550            IF C150-HSSTAT = '1'
00551               PERFORM 700-MOVE-WR-HOMESTEAD
00552            END-IF
00553         ELSE
00554            IF C150-HOSTAT = '1'
00555               MOVE C150-HOSTAT TO MA-HO-RESPONSE
00556               PERFORM 600-MOVE-WR-HOMEOWNER
00557               MOVE 'Y' TO HO-SWITCH
00558            ELSE
00559               MOVE 'N' TO HO-SWITCH
00560            END-IF
00561            IF C150-HSSTAT = '1'
00562               PERFORM 700-MOVE-WR-HOMESTEAD
00563            END-IF
00564         END-IF
00565      ELSE
00566         IF PI-SEG-NOTFND
00567            NEXT SENTENCE
00568         ELSE
00569            DISPLAY 'DL/1 ERROR  580-GNP-DETL-SEG'
00570            DISPLAY 'PCB  AREA: ' PI-PCB1
00571            MOVE 15 TO RETURN-CODE
00572            SET STOP-PROCESSING    TO  TRUE
00573         END-IF
00574      END-IF.
00575      SKIP3
00576  590-GNP-DETL-SEG.
00577      CALL 'CBLTDLI' USING GNP
00578                           PI-PCB1
00579                           C150-SENFRZDETAIL
00580                           LVL4-UNQUAL-SSA
00581      IF PI-DB-GOOD-STATUS
00582         IF C150-SFSTAT = 'Q'
00583            MOVE C145-CYFNLEVDIFF TO MA-VALUE-1 (1)
00584            MOVE 'Q' TO MA-SF-IND
00585         END-IF
00586      ELSE
00587         IF PI-SEG-NOTFND
00588            NEXT SENTENCE
00589         ELSE
00590            DISPLAY 'DL/1 ERROR  580-GNP-DETL-SEG'
00591            DISPLAY 'PCB  AREA: ' PI-PCB1
00592            MOVE 15 TO RETURN-CODE
00593            SET STOP-PROCESSING    TO  TRUE
00594         END-IF
00595      END-IF.
00596      SKIP3
00597  600-MOVE-WR-HOMEOWNER.
00598 *    IF C150-HOSTAT = '1'
00599         MOVE SPACES   TO HE-REC
00600         MOVE M-VOL    TO HE-VOL
00601         MOVE M-PROP   TO HE-PROP
00602         MOVE M-TXCD   TO HE-TXCD
00603         MOVE M-CLS    TO HE-CLASS
00604         MOVE M-TXTYP  TO HE-TXTYP
00605         MOVE C145-PROPCT TO HE-PRORATE
00606         MOVE C145-KEYPCL TO HE-KEYPCL
00607         MOVE C145-OCCFAC TO HE-OCCFAC
00608         MOVE C145-HOUNITS TO HE-COOPQTY
00609         MOVE C145-SPLTCD TO HE-SPLIT-CD
00610         MOVE C150-HOSTAT TO HE-STAT1
00611         MOVE ZERO     TO  HE-STAT2
00612         MOVE ZERO     TO  HE-CLERKS-CLS
00613         MOVE C150-HOBASYR TO HE-YRAPPL
00614         MOVE C150-ELGIND TO HE-IND
00615         MOVE C150-HOBYEQFAC TO HE-EQFCTR
00616         MOVE ZEROS       TO HE-H-V
00617         MOVE C150-HOBYAV TO HE-ASSDVAL
00618         MOVE C150-HOBYEV TO HE-EQVAL
00619         MOVE C150-APPOLDNAM TO HE-NAME
00620         MOVE C150-APPADDR TO HE-ADDR
00621         MOVE C150-APPCITY TO HE-CITY
00622         MOVE C150-APPSTATE TO HE-STATE
00623         MOVE C150-APPZIP TO HE-ZIP
00624         MOVE ZEROS       TO HE-C-OF-E
00625                                HE-STAT3
00626         PERFORM 610-WR-HE-REC.
00627      SKIP3
00628  610-WR-HE-REC.
00629      WRITE HE-REC
00630      ADD +1 TO HOWNR-RECS-WR.
00631      SKIP3
00632  700-MOVE-WR-HOMESTEAD.
00633 *    IF C150-HSSTAT = '1'
00634         MOVE SPACES   TO SF-REC
00635         MOVE M-VOL    TO SF-VOL
00636         MOVE M-PROP   TO SF-PROP
00637         MOVE C150-APPOLDNAM TO SF-NAME
00638         MOVE C150-APPADDR TO SF-ADDR
00639         MOVE C150-APPCITY TO SF-CITY
00640         MOVE C150-APPSTATE TO SF-STATE
00641         MOVE C150-APPZIP TO SF-ZIP
00642         MOVE M-TXCD   TO SF-TXCD
00643         MOVE M-TXTYP  TO SF-TXTYP
00644         MOVE C150-HSYRAPPLD TO SF-YEAR-APPLD
00645         MOVE C145-RECCODE TO SF-REC-CD
00646         MOVE C150-BIRTHDTE TO WS-BIRTHDTE
00647         MOVE CORRESPONDING WS-BIRTHDTE TO WS-BIRTHDTE-REF
00648         MOVE WS-BIRTHDTE-REF TO SF-BIRTSF-DATE
00649         MOVE ZEROS    TO  SF-LBL-IND SF-SEQNO
00650         MOVE C150-HSBATCH TO SF-BATCSF-NO
00651         PERFORM 710-WR-SF-REC.
00652 *    END-IF.
00653      SKIP3
00654  710-WR-SF-REC.
00655      WRITE SF-REC
00656      ADD +1 TO HMSTD-RECS-WR.
00657      SKIP3
00658  800-MOVE-WR-COOP.
00659      IF C145-HSUNITS GREATER THAN ZEROS
00660         MOVE SPACES   TO CF-REC
00661         MOVE M-PROP   TO CF-PROP-NO
00662         MOVE C145-HSUNITS TO CF-COOP-QUAN
00663         PERFORM 820-WR-CF-REC
00664      END-IF.
00665      SKIP3
00666  810-WR-MSTR.
00667      ADD +1 TO MSTR-RECS-WR
00668      WRITE MSTR-OUT-REC-2.
00669      SKIP3
00670  820-WR-CF-REC.
00671      ADD +1 TO COOP-RECS-WR
00672      WRITE CF-REC.
00673      SKIP3
00674  900-MSG-LINE-LISTING.
00675      IF LINE-CNT
00676         GREATER THAN +55
00677         PERFORM 950-MSG-HEADING-RTN.
00678      MOVE M-VOL         TO DL-VOL
00679      MOVE M-PROP        TO DL-PROP
00680      INSPECT DL-PROP REPLACING ALL ',' BY '-'
00681      MOVE M-TXTYP      TO DL-TXTYP
00682      MOVE M-STAT2      TO DL-STAT-2.
00683      SKIP3
00684  950-MSG-HEADING-RTN.
00685      ADD +1 TO PAGE-CNT
00686      MOVE PAGE-CNT TO H1-PAGE
00687      WRITE PRINT-REC FROM HL-1 AFTER ADVANCING PAGE
00688      WRITE PRINT-REC FROM HL-2 AFTER ADVANCING 2
00689      WRITE PRINT-REC FROM HL-3 AFTER ADVANCING 2
00690      WRITE PRINT-REC FROM HL-4 AFTER ADVANCING 1
00691      MOVE +6 TO LINE-CNT.
