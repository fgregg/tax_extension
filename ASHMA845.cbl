00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASHMA845.
00003 *AUTHOR. SQUILLACE - KALEMBA
00004 *DATE-WRITTEN.   MAY 1996.
00005 *****************************************************************
00006 *REMARKS.
00007 *       THIS PROGRAM UPDATES THE SENIOR
00008 *       FREEZE MASTER SEGMENT WITH THE TOTAL NUMBER OF SENIOR
00009 *       FREEZE SHARES.
00010 *
00011 *****************************************************************
00012     SKIP1
00013  ENVIRONMENT DIVISION.
00014  CONFIGURATION SECTION.
00015  SOURCE-COMPUTER. IBM-370.
00016  OBJECT-COMPUTER. IBM-370.
00017  INPUT-OUTPUT SECTION.
00018  FILE-CONTROL.
00019      SELECT    ASSFMSDT     ASSIGN TO UT-S-ASSFMSDT.
00020      SELECT    PRINT-FILE   ASSIGN TO UT-S-RPT.
00021      SELECT    PRT-ERR-FILE ASSIGN TO UT-S-ERRRPT.
00022      SELECT    CNTRL-CARD   ASSIGN TO UT-S-CARD.
00023          SKIP1
00024  DATA DIVISION.
00025  FILE SECTION.
00026  COPY ASSFMSDTF1.
00027    SKIP3
00028  COPY PRINTFILE.
00029    SKIP3
00030  COPY PRINTERRFL.
00031    EJECT
00032  FD  CNTRL-CARD
00033      RECORDING MODE IS F
00034      BLOCK CONTAINS 0 RECORDS
00035      RECORD CONTAINS 80 CHARACTERS
00036      LABEL RECORDS ARE STANDARD
00037      DATA RECORD IS CNTRL-RECORD.
00038  01  CNTRL-RECORD.
00039      03 CNTRL-CICV          PIC X(4).
00040      03 CNTRL-VAL.
00041         05 CNTRL-CICV-VAL   PIC 9(4).
00042      03 CNTRL-YR-PROC       PIC X(8).
00043      03 CNTRL-YR.
00044         05 CNTRL-CENT       PIC 99.
00045         05 CNTRL-YR-N       PIC 99.
00046      03 FILLER              PIC X(60).
00047     EJECT
00048  WORKING-STORAGE SECTION.
00049  77  SUB                    PIC S9(4)     VALUE +0 BINARY.
00050  77  BLNK                   PIC X         VALUE SPACE.
00051  77  CIC-CTR                PIC S9(5)     VALUE +0 PACKED-DECIMAL
00052  77  EXMT-READ-CTR          PIC S9(7)     VALUE +0 PACKED-DECIMAL
00053      88  FIRST-REC                      VALUE +1.
00054  77  FIRST-PROP-REC         PIC S9(7)     VALUE +0 PACKED-DECIMAL
00055      88  FIRST-PROP                     VALUE +1.
00056  77  EXMT-SEL-CTR           PIC S9(7)     VALUE +0 PACKED-DECIMAL
00057  77  MAST-SEGS-UPD-CTR      PIC S9(7)     VALUE +0 PACKED-DECIMAL
00058  77  DET-SEGS-ADD-CTR       PIC S9(7)     VALUE +0 PACKED-DECIMAL
00059  77  DET-SEGS-UPD-CTR       PIC S9(7)     VALUE +0 PACKED-DECIMAL
00060  77  LINE-CTR               PIC S9(3)   VALUE +999 PACKED-DECIMAL
00061  77  LINE-CTRE              PIC S9(3)   VALUE +999 PACKED-DECIMAL
00062  77  PAGE-CTR               PIC S9(5)     VALUE +0 PACKED-DECIMAL
00063  77  PAGE-CTRE              PIC S9(5)    VALUE +0 PACKED-DECIMAL.
00064  77  END-EXMT-SW                   PIC X VALUE 'N'.
00065      88  EXMT-EOF                        VALUE 'Y'.
00066  77  STOP-PROCCESSING-SW           PIC X VALUE 'N'.
00067      88  STOP-PROCCESSING                VALUE 'S'.
00068  77  MESG-SW                       PIC X VALUE '0'.
00069      88  RESET-MSG-SW                    VALUE '0'.
00070      88  MESG-1                          VALUE '1'.
00071      88  MESG-2                          VALUE '2'.
00072      88  MESG-3                          VALUE '3'.
00073      88  MESG-4                          VALUE '4'.
00074      88  MESG-5                          VALUE '5'.
00075       EJECT
00076  01  WORK-AREA.
00077 *------------- CHECKPOINT IDENTIFICATION NUMBER ---------------*
00078      05  CHECK-PT-ID-NO.
00079         10  FILLER              PIC XX     VALUE 'AS'.
00080         10  CP-TRAN-NO          PIC 9(6)   VALUE 0.
00081 *--------------------------------------------------------------*
00082          SKIP1
00083      05  WS-YEAR.
00084          10  WS-CC              PIC 9(2) VALUE 19.
00085          10  WS-YY              PIC 9(2).
00086      05  WS-YEAR-N  REDEFINES  WS-YEAR PIC 9(4).
00087          SKIP1
00088      05  WS-TXCD                PIC 9(05).
00089      05  WS-TXCD-X REDEFINES  WS-TXCD.
00090          10  WS-TXCD-TOWN       PIC 99.
00091          10  FILLER             PIC X(3).
00092               SKIP1
00093      05  CURR-KEY.
00094          10 CURR-PROP           PIC 9(14).
00095                SKIP1
00096      05  PREV-KEY               PIC X(14).
00097                SKIP1
00098      05  SAVE-PROP              PIC 9(15)         VALUE ZERO.
00099                SKIP1
00100      05  WS-DOB.
00101          10   WS-DOB-YY         PIC 99.
00102          10   WS-DOB-MM         PIC 99.
00103          10   WS-DOB-DD         PIC 99.
00104                SKIP1
00105      05  WS-MDCY.
00106          10   WS-MO             PIC 99.
00107          10   WS-DA             PIC 99.
00108          10   WS-CE             PIC 99.
00109          10   WS-YR             PIC 99.
00110      05  WS-MMDDCCYY  REDEFINES WS-MDCY    PIC 9(8).
00111               SKIP1
00112      05  CURR-DATE.
00113          10  CENT                   PIC  99       VALUE 19.
00114          10  YR-MO-DAY              PIC  9(6)     VALUE  0.
00115      05  CURR-DATE-N   REDEFINES    CURR-DATE PIC 9(8).
00116               SKIP1
00117      05  CURR-TIME-X.
00118          10  CURR-TIME              PIC  9(6).
00119          10  CURR-TIME-2            PIC  9(2).
00120      05  CURR-TIME-N   REDEFINES    CURR-TIME-X PIC 9(8).
00121               SKIP1
00122      05  MO-DAY-YR                  PIC  9(6)     VALUE  0.
00123               EJECT
00124  01  PRINT-LINES.
00125      05  HEAD1.
00126          10  FILLER             PIC X     VALUE SPACE.
00127          10  FILLER             PIC X(08) VALUE
00128          'ASHMA845'.
00129          10  FILLER             PIC X(29) VALUE SPACE.
00130          10  FILLER             PIC X(34) VALUE
00131          'OFFICE OF THE COOK COUNTY ASSESSOR'.
00132          10  FILLER             PIC X(33) VALUE SPACE.
00133          10  FILLER             PIC X(07) VALUE 'PAGE'.
00134          10  H1-PAGE            PIC Z,ZZ9.
00135                     SKIP1
00136      05  HEAD2.
00137          10  FILLER             PIC X     VALUE SPACE.
00138          10  H2-DATE            PIC 99B99B99.
00139          10  FILLER             PIC X(21) VALUE SPACE.
00140          10  FILLER             PIC X(42) VALUE
00141          'SENIOR FREEZE MASTER SEGMENT UPDATE REPORT'.
00142                     SKIP1
00143      05  HEAD3.
00144          10  FILLER             PIC X     VALUE SPACE.
00145          10  FILLER             PIC X(49) VALUE
00146          'TOWN   VOL   PROPERTY INDEX NUMBER   REC. CODE'.
00147          10  FILLER             PIC X(44) VALUE
00148          'BIRTH DATE     COOP SHARES    SEN FRZ SHARES'.
00149            SKIP1
00150      05  DET-LN.
00151          10  FILLER             PIC XX    VALUE SPACE.
00152          10  DL-TOWN            PIC 99.
00153          10  FILLER             PIC X(4)  VALUE SPACES.
00154          10  DL-VOL             PIC 999.
00155          10  FILLER             PIC X(4)  VALUE SPACES.
00156          10  DL-PIN             PIC 99B99B999B999B9999.
00157          10  FILLER             PIC X(9)  VALUE SPACES.
00158          10  DL-RECCD           PIC 9.
00159          10  FILLER             PIC X(7)  VALUE SPACES.
00160          10  DL-BIRTH-DTE       PIC 99B99B9999.
00161          10  FILLER             PIC X(6)  VALUE SPACES.
00162          10  DL-COOP-SHARES     PIC ZZZ,ZZ9.
00163          10  FILLER             PIC X(10) VALUE SPACES.
00164          10  DL-SENFRZ-SHARES   PIC ZZZ,ZZ9.
00165            SKIP1
00166      05  TOT-LN.
00167          10  FILLER     PIC XX    VALUE SPACE.
00168          10  TL-MSG     PIC X(32).
00169          10  FILLER     PIC XXXX  VALUE SPACE.
00170          10  TL-TOT     PIC ZZ,ZZ9.
00171            SKIP2
00172      05  HEAD1E.
00173          10  FILLER             PIC X     VALUE SPACE.
00174          10  FILLER             PIC X(08) VALUE
00175          'ASHMA845'.
00176          10  FILLER             PIC X(29) VALUE SPACE.
00177          10  FILLER             PIC X(34) VALUE
00178          'OFFICE OF THE COOK COUNTY ASSESSOR'.
00179          10  FILLER             PIC X(33) VALUE SPACE.
00180          10  FILLER             PIC X(07) VALUE 'PAGE'.
00181          10  H1-PAGEE           PIC Z,ZZ9.
00182                     SKIP1
00183      05  HEAD2E.
00184          10  FILLER             PIC X     VALUE SPACE.
00185          10  H2-DATEE           PIC 99B99B99.
00186          10  FILLER             PIC X(21) VALUE SPACE.
00187          10  FILLER             PIC X(47) VALUE
00188          'SENIOR FREEZE MASTER RECORD UPDATE ERROR REPORT'.
00189                     SKIP1
00190      05  HEAD3E.
00191          10  FILLER             PIC X     VALUE SPACE.
00192          10  FILLER             PIC X(49) VALUE
00193          'TOWN   VOL   PROPERTY INDEX NUMBER   REC. CODE'.
00194          10  FILLER             PIC X(24) VALUE
00195          'BIRTH DATE      MESSAGE:'.
00196            SKIP1
00197      05  DET-LNE.
00198          10  FILLER             PIC XX    VALUE SPACE.
00199          10  DL-TOWNE           PIC 99.
00200          10  FILLER             PIC X(4)  VALUE SPACES.
00201          10  DL-VOLE            PIC 999.
00202          10  FILLER             PIC X(4)  VALUE SPACES.
00203          10  DL-PINE            PIC 99B99B999B999B9999.
00204          10  FILLER             PIC X(9)  VALUE SPACES.
00205          10  DL-RECCDE          PIC 9.
00206          10  FILLER             PIC X(7)  VALUE SPACES.
00207          10  DL-BIRTH-DTEE      PIC 99B99B9999.
00208          10  FILLER             PIC X(6)  VALUE SPACES.
00209          10  DL-MSGE            PIC X(40).
00210            SKIP1
00211      05  TOT-LNE.
00212          10  FILLER     PIC XX    VALUE SPACE.
00213          10  TL-MSGE    PIC X(32).
00214          10  FILLER     PIC XXXX  VALUE SPACE.
00215          10  TL-TOTE    PIC ZZ,ZZ9.
00216            EJECT
00217  01  MESSAGES.
00218      05  MSG-1                  PIC X(40) VALUE
00219          'NO MATCHING ROOT SEGMENT'.
00220      05  MSG-2                  PIC X(40) VALUE
00221          'NO MATCHING ASSESSMENT DATA SEGMENT'.
00222      05  MSG-3                  PIC X(40) VALUE
00223          'NO MATCHING SENIOR FREEZE MASTER SEGMENT'.
00224      05  MSG-4                  PIC X(40) VALUE
00225          'MASTER SEGMENT UPDATED'.
00226      05  MSG-5                  PIC X(40) VALUE
00227          'SFSHARES GREATER THAN BUILDING SHARES'.
00228       EJECT
00229  01  IO-AREA.
00230  COPY PIROOTSEGM.
00231  SKIP3
00232  COPY PIASSESSMT.
00233  SKIP3
00234  COPY ASAIS145SG.
00235  SKIP3
00236  COPY ASAIS150SG.
00237       SKIP3
00238  COPY IMSCALLS.
00239       EJECT
00240  COPY ASAISSSA29.
00241       EJECT
00242  LINKAGE SECTION.
00243  COPY IOPCBDESC.
00244  01  PI-PCB.
00245  COPY PIPCB1DESC.
00246          10  PI-SEG-KEY            PIC X(09).
00247      EJECT
00248  PROCEDURE DIVISION.
00249      SKIP1
00250  100-HOUSEKEEPING.
00251      ENTRY 'DLITCBL' USING IO-PCB  PI-PCB
00252            SKIP1
00253      OPEN INPUT ASSFMSDT
00254                 CNTRL-CARD
00255          OUTPUT PRINT-FILE PRT-ERR-FILE.
00256            SKIP1
00257      READ CNTRL-CARD AT END
00258         DISPLAY 'CONTROL CARD IS MISSING'
00259         MOVE 16 TO RETURN-CODE.
00260      IF RETURN-CODE NOT  EQUAL 16
00261         PERFORM 995-CHECK-CNTRL-RTN
00262         IF RETURN-CODE  EQUAL  16
00263            STOP RUN
00264         END-IF
00265      END-IF.
00266      SKIP1
00267      ACCEPT   CURR-TIME-N   FROM  TIME
00268      ACCEPT    YR-MO-DAY    FROM  DATE
00269      MULTIPLY  YR-MO-DAY    BY    100.0001
00270        GIVING  MO-DAY-YR
00271      MOVE      MO-DAY-YR    TO  H2-DATE H2-DATEE
00272      INSPECT   H2-DATE   REPLACING  ALL  ' '  BY '/'
00273      INSPECT   H2-DATEE  REPLACING  ALL  ' '  BY '/'.
00274                 SKIP1
00275      WRITE PRINT-REC   FROM BLNK AFTER ADVANCING PAGE
00276      WRITE PRT-ERR-REC FROM BLNK AFTER ADVANCING PAGE.
00277                 SKIP1
00278      PERFORM 200-MAINLINE
00279          UNTIL  STOP-PROCCESSING
00280           OR EXMT-EOF
00281           OR RETURN-CODE EQUAL 16.
00282                  SKIP1
00283      IF  LINE-CTR  GREATER  +55
00284          PERFORM   830-HEADERS
00285      END-IF
00286                  SKIP1
00287      IF  LINE-CTRE GREATER  +55
00288          PERFORM   830-HEADERSE
00289      END-IF
00290                  SKIP1
00291      MOVE
00292      'TOTAL INPUT RECORDS READ ' TO TL-MSG TL-MSGE
00293      MOVE EXMT-READ-CTR TO TL-TOT TL-TOTE
00294      WRITE PRINT-REC   FROM TOT-LN  AFTER ADVANCING 3
00295      WRITE PRT-ERR-REC FROM TOT-LNE AFTER ADVANCING 3.
00296      DISPLAY
00297      'TOTAL INPUT RECORDS READ         ' EXMT-READ-CTR
00298      SKIP1
00299      MOVE
00300      'TOTAL INPUT RECORDS SELECTED '
00301      TO TL-MSG TL-MSGE
00302      MOVE EXMT-SEL-CTR TO TL-TOT TL-TOTE
00303      WRITE PRINT-REC   FROM TOT-LN  AFTER ADVANCING 2
00304      WRITE PRT-ERR-REC FROM TOT-LNE AFTER ADVANCING 2.
00305      DISPLAY
00306      'TOTAL INPUT RECORDS SELECTED '
00307                                       EXMT-SEL-CTR
00308      SKIP1
00309      MOVE
00310      'TOTAL SF MASTER SEGMENTS UPDATED ' TO TL-MSG TL-MSGE
00311      MOVE MAST-SEGS-UPD-CTR TO TL-TOT TL-TOTE
00312      WRITE PRINT-REC   FROM TOT-LN  AFTER ADVANCING 2
00313      WRITE PRT-ERR-REC FROM TOT-LNE AFTER ADVANCING 2.
00314      DISPLAY
00315      'TOTAL SF MASTER SEGMENTS UPDATED ' MAST-SEGS-UPD-CTR.
00316                  SKIP1
00317      CLOSE      ASSFMSDT
00318                 CNTRL-CARD
00319                 PRINT-FILE PRT-ERR-FILE.
00320                       SKIP1
00321      GOBACK.
00322       SKIP2
00323  200-MAINLINE.
00324      SET  RESET-MSG-SW  TO  TRUE
00325      PERFORM  300-READ-EXMT-FILE
00326      IF NOT EXMT-EOF
00327         IF OF-C150-SGSFSTAT EQUAL 'Q'
00328            ADD +1 TO EXMT-SEL-CTR
00329            PERFORM 900-DETERMINE-CHECKPOINT
00330            IF RETURN-CODE NOT EQUAL 16
00331               PERFORM 620-GHU-MASTER-SEG
00332               IF PI-SEG-FOUND
00333                  IF CURR-PROP = SAVE-PROP
00334                     PERFORM 500-UPD-FLDS-MASTER-SEG
00335                     PERFORM 550-COMPARE-SHARES
00336                     PERFORM 650-REPL-MASTER-SEG
00337                  ELSE
00338                     MOVE ZEROS TO C145-SFSHARES
00339                     PERFORM 500-UPD-FLDS-MASTER-SEG
00340                     PERFORM 650-REPL-MASTER-SEG
00341                     MOVE CURR-PROP TO SAVE-PROP
00342                  END-IF
00343               END-IF
00344            END-IF
00345         END-IF
00346      END-IF.
00347      SKIP3
00348  300-READ-EXMT-FILE.
00349      READ ASSFMSDT
00350          AT END  SET EXMT-EOF TO TRUE.
00351      IF EXMT-EOF
00352          CONTINUE
00353      ELSE
00354          ADD  +1 TO EXMT-READ-CTR
00355          MOVE  OF-PROP-NO   TO   CURR-PROP
00356          IF PREV-KEY        GREATER  CURR-KEY
00357              MOVE 16 TO RETURN-CODE
00358              SET STOP-PROCCESSING   TO  TRUE
00359              DISPLAY 'SEQUENCE ERROR IN DIVISION MASTER FILE'
00360              DISPLAY 'PREVIOUS KEY = ' PREV-KEY
00361              DISPLAY 'CURRENT KEY  = ' CURR-KEY
00362          ELSE
00363              MOVE CURR-KEY        TO PREV-KEY
00364          END-IF
00365      END-IF.
00366       SKIP3
00367  500-UPD-FLDS-MASTER-SEG.
00368      ADD  OF-C150-SGCOOPSENSHRS TO C145-SFSHARES
00369      IF OF-C150-SGCOOPSENSHRS GREATER ZERO
00370         PERFORM 800-WRITE-RPT.
00371      SKIP3
00372  550-COMPARE-SHARES.
00373      IF C145-SFSHARES GREATER C145-BLDGSHARES
00374         SET MESG-5  TO  TRUE
00375         PERFORM 850-WRITE-ERR-RPT.
00376      SKIP3
00377  620-GHU-MASTER-SEG.
00378      MOVE  OF-PROP-NO   TO   LVL1-ARG
00379      MOVE  CNTRL-YR-N   TO   LVL2-PROCYR
00380                              LVL2-TXYR
00381      MOVE  0            TO   LVL2-TXTYP
00382      MOVE OF-C145-RECCODE TO LVL3-ARG
00383      CALL 'CBLTDLI' USING GHU
00384                           PI-PCB
00385                           C145-SENFRZMASTER
00386                           LVL1-QUAL-SSA
00387                           LVL2-QUAL-SSA
00388                           LVL3-QUAL-SSA
00389      IF PI-DB-GOOD-STATUS
00390         NEXT SENTENCE
00391      ELSE
00392         IF PI-SEG-NOTFND
00393            IF PI-SEGMENT-LVL = '00'
00394               SET MESG-1 TO TRUE
00395               PERFORM 850-WRITE-ERR-RPT
00396            ELSE
00397               IF PI-DB-1ST-SEG-LEVEL
00398                  SET MESG-2 TO TRUE
00399                  PERFORM 850-WRITE-ERR-RPT
00400               ELSE
00401                  SET MESG-3 TO TRUE
00402                  PERFORM 850-WRITE-ERR-RPT
00403               END-IF
00404            END-IF
00405         ELSE
00406            DISPLAY 'DL/1 ERROR 620-GHU-MASTER-SEG'
00407            DISPLAY 'PCB  AREA: ' PI-PCB
00408            MOVE 16 TO RETURN-CODE
00409            SET STOP-PROCCESSING   TO  TRUE
00410         END-IF
00411      END-IF.
00412       SKIP3
00413  650-REPL-MASTER-SEG.
00414      CALL 'CBLTDLI' USING REPL
00415                           PI-PCB
00416                           C145-SENFRZMASTER
00417      IF PI-DB-GOOD-STATUS
00418         ADD  +2  TO  CIC-CTR
00419         ADD  +1  TO  MAST-SEGS-UPD-CTR
00420         SET   MESG-4    TO   TRUE
00421      ELSE
00422         DISPLAY 'DL/1 ERROR 650-REPL-MASTER-SEG'
00423         DISPLAY 'PCB  AREA: ' PI-PCB
00424         MOVE 16 TO RETURN-CODE
00425         SET STOP-PROCCESSING   TO  TRUE
00426      END-IF.
00427      SKIP3
00428      SKIP3
00429  800-WRITE-RPT.
00430      IF  LINE-CTR  GREATER  +55
00431          PERFORM   830-HEADERS
00432      END-IF
00433      MOVE  SPACES   TO   DET-LN
00434      MOVE OF-TOWN-NO    TO   DL-TOWN
00435      MOVE OF-VOLUME     TO   DL-VOL
00436      MOVE OF-PROP-NO    TO   DL-PIN
00437      INSPECT  DL-PIN REPLACING ALL ' ' BY  '-'
00438      MOVE OF-C145-RECCODE TO DL-RECCD
00439      MOVE OF-C150-SGBIRTHDTE TO DL-BIRTH-DTE
00440      MOVE OF-C150-SGCOOPSENSHRS TO DL-COOP-SHARES
00441      MOVE C145-SFSHARES      TO DL-SENFRZ-SHARES
00442      WRITE PRINT-REC FROM DET-LN
00443      AFTER ADVANCING 2
00444      ADD  +2   TO  LINE-CTR.
00445       SKIP3
00446  850-WRITE-ERR-RPT.
00447      IF  LINE-CTRE GREATER  +55
00448          PERFORM   830-HEADERSE
00449      END-IF
00450      MOVE  SPACES   TO   DET-LNE
00451      MOVE OF-TOWN-NO    TO   DL-TOWNE
00452      MOVE OF-VOLUME     TO   DL-VOLE
00453      MOVE OF-PROP-NO    TO   DL-PINE
00454      INSPECT  DL-PINE REPLACING ALL ' ' BY  '-'
00455      MOVE OF-C145-RECCODE TO DL-RECCDE
00456      MOVE OF-C150-SGBIRTHDTE TO DL-BIRTH-DTEE
00457      EVALUATE TRUE
00458         WHEN  MESG-1
00459             MOVE  MSG-1  TO  DL-MSGE
00460         WHEN  MESG-2
00461             MOVE  MSG-2  TO  DL-MSGE
00462         WHEN  MESG-3
00463             MOVE  MSG-3  TO  DL-MSGE
00464         WHEN  MESG-4
00465             MOVE  MSG-4  TO  DL-MSGE
00466         WHEN  MESG-5
00467             MOVE  MSG-5  TO  DL-MSGE
00468      END-EVALUATE
00469      WRITE PRT-ERR-REC FROM DET-LNE
00470      AFTER ADVANCING 2
00471      ADD  +2   TO  LINE-CTRE.
00472      SKIP3
00473  830-HEADERS.
00474      ADD     +1        TO    PAGE-CTR
00475      MOVE  PAGE-CTR    TO    H1-PAGE
00476      WRITE PRINT-REC   FROM HEAD1
00477            AFTER ADVANCING PAGE
00478      WRITE PRINT-REC   FROM HEAD2
00479            AFTER ADVANCING 2
00480      WRITE PRINT-REC   FROM HEAD3
00481            AFTER ADVANCING 3
00482      MOVE +6   TO    LINE-CTR.
00483      SKIP3
00484  830-HEADERSE.
00485      ADD     +1        TO    PAGE-CTRE
00486      MOVE  PAGE-CTRE   TO    H1-PAGEE
00487      WRITE PRT-ERR-REC FROM HEAD1E
00488            AFTER ADVANCING PAGE
00489      WRITE PRT-ERR-REC FROM HEAD2E
00490            AFTER ADVANCING 2
00491      WRITE PRT-ERR-REC FROM HEAD3E
00492            AFTER ADVANCING 3
00493      MOVE +6   TO    LINE-CTRE.
00494       SKIP3
00495  900-DETERMINE-CHECKPOINT.
00496      IF CIC-CTR GREATER THAN CNTRL-CICV-VAL
00497         ADD +1                    TO CP-TRAN-NO
00498         PERFORM 910-ISSUE-CHECKPOINT
00499         MOVE ZEROES               TO CIC-CTR.
00500        SKIP3
00501  910-ISSUE-CHECKPOINT.
00502      CALL 'CBLTDLI' USING  CHKP, IO-PCB, CHECK-PT-ID-NO.
00503      IF IP-GOOD-STATUS
00504         NEXT SENTENCE
00505      ELSE
00506         PERFORM 920-ISSUE-CHKPT-ERR.
00507       SKIP3
00508  920-ISSUE-CHKPT-ERR.
00509      CALL 'CBLTDLI' USING ROLL.
00510      DISPLAY 'CHECK POINT ISSUE ERROR'.
00511      MOVE 0016 TO RETURN-CODE.
00512      SET STOP-PROCCESSING   TO  TRUE
00513      DISPLAY 'LOGICAL TERMINAL NAME:  '
00514          IP-TERM-NAME.
00515      DISPLAY 'STATUS CODE:  '
00516          IP-STATUS-CODE.
00517      SKIP3
00518  995-CHECK-CNTRL-RTN.
00519      IF CNTRL-CICV   EQUAL  'CICV'
00520         CONTINUE
00521      ELSE
00522         MOVE   16   TO RETURN-CODE
00523         DISPLAY 'POS 1-4 MUST BE EQUAL TO ''CICV'''
00524      END-IF.
00525           SKIP1
00526      IF CNTRL-VAL NUMERIC
00527         CONTINUE
00528      ELSE
00529         MOVE   16   TO RETURN-CODE
00530         DISPLAY 'POS 5-8 MUST BE NUMERIC'
00531      END-IF.
00532           SKIP1
00533      IF CNTRL-YR-PROC EQUAL 'PROCYEAR'
00534         CONTINUE
00535      ELSE
00536         MOVE   16   TO RETURN-CODE
00537         DISPLAY 'POS 9-16 MUST BE EQUAL TO ''PROCYEAR'''
00538      END-IF.
00539
00540      IF CNTRL-YR     NUMERIC
00541         CONTINUE
00542      ELSE
00543         MOVE   16   TO RETURN-CODE
00544         DISPLAY 'POS 17-20 MUST BE A NUMERIC VALUE'
00545      END-IF.
