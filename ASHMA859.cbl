00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASHMA859.
00003 *AUTHOR. R. CRUZ/K. LEE.
00004 *DATE-WRITTEN. 06/2000.
00005 *REMARKS. COMPARE THE COE/SENIOR FREEZE SQUENTIAL FILE WITH THE
00006 *         DATABASE. IF THE SEQUENTIAL FILE VALUE IS LOWER THAN
00007 *         THE VALUE IN THE DATABASE, CALCULATIONS MUST BE
00008 *         PERFORMED AND THE DATABASE UPDATED.
00009
00010  ENVIRONMENT DIVISION.
00011  CONFIGURATION SECTION.
00012  SOURCE-COMPUTER. IBM-370.
00013  OBJECT-COMPUTER. IBM-370.
00014  INPUT-OUTPUT SECTION.
00015  FILE-CONTROL.
00016      SELECT PARAM-LINE      ASSIGN TO UT-S-PARAM.
00017      SELECT INPUT-FILE      ASSIGN TO UT-S-INFILE.
00018      SELECT PRINT-FILE-1    ASSIGN TO UT-S-PRINT1.
00019      SELECT PRINT-FILE-2    ASSIGN TO UT-S-PRINT2.
00020
00021  DATA DIVISION.
00022  FILE SECTION.
00023
00024  FD  PARAM-LINE
00025      RECORDING MODE IS F
00026      BLOCK CONTAINS 0 RECORDS
00027      RECORD CONTAINS 80 CHARACTERS
00028      LABEL RECORDS ARE STANDARD
00029      DATA RECORD IS PARAM-REC.
00030  01  PARAM-REC.
00031      03 PR-CICV             PIC X(4).
00032      03 PR-CICV-VAL         PIC X(4).
00033      03 PR-CICV-VAL-N       REDEFINES PR-CICV-VAL  PIC 9(4).
00034      03 PR-PROC-YR.
00035         05  PR-PROC-YR-N    PIC 9(4).
00036      03 PR-EQ-FCTR          PIC X(5).
00037      03 PR-EQ-FCTR-N        REDEFINES PR-EQ-FCTR   PIC 9V9(4).
00038      03 PR-TYPE             PIC X.
00039      03 FILLER              PIC X(62).
00040
00041  FD  INPUT-FILE
00042      BLOCK CONTAINS 0 CHARACTERS
00043      RECORD CONTAINS 400 CHARACTERS
00044      LABEL RECORDS ARE STANDARD
00045      DATA RECORD IS INPUT-REC.
00046  01  INPUT-REC               PIC X(400).
00047
00048  FD  PRINT-FILE-1
00049      RECORDING MODE IS F
00050      RECORD CONTAINS 133 CHARACTERS
00051      BLOCK CONTAINS 0 RECORDS
00052      LABEL RECORDS ARE STANDARD
00053      DATA RECORD IS PRINT-REC-1.
00054  01  PRINT-REC-1            PIC X(133).
00055
00056  FD  PRINT-FILE-2
00057      RECORDING MODE IS F
00058      RECORD CONTAINS 133 CHARACTERS
00059      BLOCK CONTAINS 0 RECORDS
00060      LABEL RECORDS ARE STANDARD
00061      DATA RECORD IS PRINT-REC-2.
00062  01  PRINT-REC-2            PIC X(133).
00063
00064  WORKING-STORAGE SECTION.
00065  77  IN-REC-CTR             PIC S9(7) COMP-3  VALUE +0.
00066  77  INFILE-EOF-SW          PIC X     VALUE 'N'.
00067  77  END-PROCESSING-SW      PIC X     VALUE 'N'.
00068  77  ERROR-SW               PIC X     VALUE 'N'.
00069  77  SFSTAT-IS-Q            PIC X     VALUE 'N'.
00070  77  VALID-REC              PIC X     VALUE 'N'.
00071  77  EXEMPT-DET-EOF-SW      PIC 9     VALUE ZERO.
00072  77  MATCH-REC-SW           PIC X     VALUE 'N'.
00073  77  PCT-SHARES             PIC V9(6) VALUE ZERO   COMP-3.
00074  77  BASE-DIFF              PIC S9(9) VALUE ZERO   COMP-3.
00075  77  CURR-DIFF              PIC S9(9) VALUE ZERO   COMP-3.
00076  77  CURR-TAX-DIFF          PIC S9(9) VALUE ZERO   COMP-3.
00077  77  FIRST-TIME             PIC X     VALUE 'N'.
00078  77  MISSING-SEGMENT        PIC X     VALUE 'N'.
00079  77  QUALIFY-SW             PIC X     VALUE 'N'.
00080  77  EXEMP-MAST-REPL-CTR    PIC S9(8) VALUE +0     BINARY.
00081  77  EXEMP-DET-SEG-CTR      PIC S9(8) VALUE +0     BINARY.
00082  77  CICV-CTR               PIC S9(8) VALUE +0     BINARY.
00083  77  SUB1                   PIC S9(4) VALUE +0     BINARY.
00084  77  SUB2                   PIC S9(4) VALUE +0     BINARY.
00085  77  LN-CTR-1               PIC S9(4) VALUE +99    BINARY.
00086  77  LN-CTR-2               PIC S9(4) VALUE +99    BINARY.
00087  77  PG-CTR-1               PIC S9(4) VALUE +0     BINARY.
00088  77  PG-CTR-2               PIC S9(4) VALUE +0     BINARY.
00089  77  BLNK                   PIC X     VALUE SPACE.
00090  77  XRST-LGTH1             PIC S9(5) COMP        VALUE +30.
00091  77  XRST-LGTH2             PIC S9(5) COMP        VALUE +58.
00092  77  XRST-AREA              PIC X(30) VALUE SPACE.
00093  77  NO-CALC-SW             PIC X     VALUE SPACES.
00094  77  WS-PROP                PIC 9(15) VALUE ZEROS.
00095  01  WORK-SF-REC.
00096  COPY ASHMASFR01.
00097  01  WORK-AREAS.
00098      03 CURR-IN-KEY.
00099         05 CU-TOWN          PIC 99.
00100         05 CU-VOL           PIC 999.
00101         05 CU-PROP          PIC 9(15) COMP-3.
00102      03 PREV-IN-KEY.
00103         05 PV-TOWN          PIC 99.
00104         05 PV-VOL           PIC 999.
00105         05 PV-PROP          PIC 9(15) COMP-3.
00106      03 COMP-CURR-PROP.
00107         05 CMP-CU-PROP      PIC 9(15) COMP-3.
00108      03 COMP-PREV-PROP.
00109         05 CMP-PV-PROP      PIC 9(15) COMP-3.
00110      03 CHECK-POINT-ID.
00111         05 FILLER           PIC XX    VALUE 'AS'.
00112         05 CPI-TRANS-NO     PIC 9(6)  VALUE ZEROES.
00113      03 WS-CLS              PIC X(6).
00114      03 FILLER REDEFINES WS-CLS.
00115         05 FILLER           PIC X(3).
00116         05 WS-CLS-N         PIC 9(3).
00117      03 MDY-DATE.
00118         05 MO               PIC XX.
00119         05 FILLER           PIC X     VALUE '/'.
00120         05 DA               PIC XX.
00121         05 FILLER           PIC X     VALUE '/'.
00122         05 CCYY             PIC X(4).
00123      03 WRK-SYS-DATE.
00124         05 CCYY             PIC X(4).
00125         05 MO               PIC XX.
00126         05 DA               PIC XX.
00127         05 FILLER           PIC X(13).
00128
00129      03 WS-HLD-AREAS.
00130         05 WS-HLD-REC                 PIC X(400)  VALUE SPACES.
00131         05 WS-HLD-UPD-FLDS.
00132          10  WS-HLD-BVYNOCALCIND      PIC X       VALUE SPACE.
00133          10  WS-HLD-BASVALYR          PIC 9(4)    VALUE ZEROES.
00134          10  WS-HLD-BVYFULLAV         PIC 9(9)    VALUE ZEROES.
00135          10  WS-HLD-CYFULLEV          PIC 9(9)    VALUE ZEROES.
00136          10  WS-HLD-CYELCOMPEV        PIC 9(9)    VALUE ZEROES.
00137          10  WS-HLD-288EXPEV          PIC 9(9)    VALUE ZEROES.
00138          10  WS-HLD-288OVRLIMEV       PIC 9(9)    VALUE ZEROES.
00139          10  WS-HLD-BVYEV             PIC 9(9)    VALUE ZEROES.
00140          10  WS-HLD-BVYELCOMPFULLAV   PIC 9(9)    VALUE ZEROES.
00141          10  WS-HLD-CYFNLEVDIFF       PIC 9(9)    VALUE ZEROES.
00142          10  WS-HLD-BASVALYR-X        PIC 9(4)    VALUE ZEROES.
00143          10  WS-HLD-BVYTOTELCOMPEV    PIC 9(9)    VALUE ZEROES.
00144          10  WS-HLD-BVYTOTELCOMPEV-X  PIC 9(9)    VALUE ZEROES.
00145          10  WS-HLD-CYNOTELEV         PIC S9(9)   COMP-3
00146                 VALUE ZEROES.
00147          10  TOT-BASE-COMP-EAV        PIC 9(9)    COMP-3
00148                 VALUE ZEROES.
00149          10  FINAL-DIFF-TX-COMP       PIC 9(9)    COMP-3
00150                 VALUE ZEROES.
00151          10  CURR-BASE-VAL-EAV        PIC 9(9)    COMP-3
00152                 VALUE ZEROES.
00153      03 WS-NEW-AREAS.
00154         05 WS-NEW-REC                 PIC X(400)  VALUE SPACES.
00155         05 WS-NEW-UPD-FLDS.
00156          10  WS-NEW-BVYNOCALCIND      PIC X       VALUE SPACE.
00157          10  WS-NEW-CALCIND           PIC X       VALUE SPACE.
00158          10  WS-NEW-BASVALYR          PIC 9(4)    VALUE ZEROES.
00159          10  WS-NEW-BVYFULLAV         PIC 9(9)    VALUE ZEROES.
00160          10  WS-NEW-BVYEV             PIC 9(9)    VALUE ZEROES.
00161          10  WS-NEW-BVYELCOMPFULLAV   PIC 9(9)    VALUE ZEROES.
00162          10  WS-NEW-CYFNLEVDIFF       PIC 9(9)    VALUE ZEROES.
00163          10  WS-NEW-BASVALYR-X        PIC 9(4)    VALUE ZEROES.
00164          10  WS-NEW-BVYTOTELCOMPEV    PIC 9(9)    VALUE ZEROES.
00165          10  WS-NEW-CYNOTELEV         PIC 9(9)    COMP-3
00166                 VALUE ZEROES.
00167
00168      03 HEAD1.
00169         05 FILLER           PIC XX    VALUE SPACES.
00170         05 H1-CURR-DTE      PIC X(10).
00171         05 FILLER           PIC X(37) VALUE SPACES.
00172         05 FILLER           PIC X(19) VALUE 'OFFICE OF THE COOK '.
00173         05 FILLER           PIC X(15) VALUE 'COUNTY ASSESSOR'.
00174         05 FILLER           PIC X(31) VALUE SPACES.
00175         05 FILLER           PIC X(5)  VALUE 'PAGE '.
00176         05 H1-PAGE          PIC ZZZ,ZZ9.
00177
00178      03 HEAD2.
00179         05 FILLER           PIC X(11) VALUE '  ASHMA859-'.
00180         05 H2-REPORT-TYP    PIC X.
00181         05 FILLER           PIC X(20) VALUE SPACES.
00182         05 H2-REPORT-TITLE  PIC X(70).
00183
00184      03 HEAD3.
00185         05 FILLER           PIC X(19) VALUE '  VOL.   PROPERTY I'.
00186         05 FILLER           PIC X(19) VALUE 'NDEX NO.   TAX TYPE'.
00187         05 FILLER           PIC X(8)  VALUE '   CLASS'.
00188
00189      03 HEAD3-4.
00190         05 FILLER           PIC X(22) VALUE SPACES.
00191         05 FILLER           PIC X(11) VALUE 'CURRENT'.
00192         05 FILLER           PIC X(8)  VALUE 'BASE'.
00193         05 FILLER           PIC X(10) VALUE 'NO CALC'.
00194         05 FILLER           PIC X(13) VALUE 'TOT BASE'.
00195         05 FILLER           PIC X(13) VALUE 'CURR-ELIG'.
00196         05 FILLER           PIC X(12) VALUE 'FINAL DIFF'.
00197         05 FILLER           PIC X(12) VALUE 'BASE VAL'.
00198         05 FILLER           PIC X(12) VALUE 'CURR VAL'.
00199         05 FILLER           PIC X(8)  VALUE 'CURR NOT'.
00200
00201      03 HEAD4.
00202         05 FILLER           PIC X(22) VALUE '  PROPERTY NUMBER'.
00203         05 FILLER           PIC X(11) VALUE 'YR CLASS'.
00204         05 FILLER           PIC X(10) VALUE 'YEAR'.
00205         05 FILLER           PIC X(8)  VALUE 'SW'.
00206         05 FILLER           PIC X(14) VALUE 'COMP EAV'.
00207         05 FILLER           PIC X(14) VALUE 'COMP EAV'.
00208         05 FILLER           PIC X(13) VALUE 'TAX COMP'.
00209         05 FILLER           PIC X(12) VALUE 'AV'.
00210         05 FILLER           PIC X(12) VALUE 'AV'.
00211         05 FILLER           PIC X(8)  VALUE 'ELIG EAV'.
00212
00213      03 DETAIL1.
00214         05 FILLER           PIC XX    VALUE SPACES.
00215         05 DL1-VOL          PIC XXX.
00216         05 FILLER           PIC X(4)  VALUE SPACES.
00217         05 DL1-PROP         PIC 99B99B999B999B9(4).
00218         05 FILLER           PIC X(7)  VALUE SPACES.
00219         05 DL1-TXTYP        PIC X.
00220         05 FILLER           PIC X(6)  VALUE SPACES.
00221         05 DL1-CLS          PIC 9B99.
00222         05 FILLER           PIC X(5)  VALUE SPACES.
00223         05 DL1-BIRTHDTE     PIC X(8).
00224         05 FILLER           PIC X(5)  VALUE SPACES.
00225         05 DL1-MESSG        PIC X(63).
00226
00227      03 DETAIL4               VALUE SPACES.
00228         05 FILLER             PIC XX.
00229         05 DL4-PROP           PIC Z99B99B999B999B9(4).
00230         05 FILLER             PIC X(3).
00231         05 DL4-CURR-YR-CLS    PIC 9B99.
00232         05 FILLER             PIC X(5).
00233         05 DL4-BASE-YR        PIC X(4).
00234         05 FILLER             PIC X(7).
00235         05 DL4-NO-CALC-SW     PIC X.
00236         05 FILLER             PIC X(3).
00237         05 DL4-TOT-BASE-EAV   PIC ZZZ,ZZZ,ZZ9.
00238         05 FILLER             PIC X(3).
00239         05 DL4-CURR-ELIG-EAV  PIC ZZZ,ZZZ,ZZ9.
00240         05 FILLER             PIC X.
00241         05 DL4-FINAL-DIFF-TX  PIC ZZZ,ZZZ,ZZ9.
00242         05 FILLER             PIC X.
00243         05 DL4-BASE-VAL-AV    PIC ZZZ,ZZZ,ZZ9.
00244         05 FILLER             PIC X.
00245         05 DL4-CURR-VAL-AV    PIC ZZZ,ZZZ,ZZ9.
00246         05 FILLER             PIC X.
00247         05 DL4-CURR-NOT-ELIG-EAV  PIC ZZZ,ZZZ,ZZ9.
00248
00249      03 TOTAL1.
00250         05 FILLER           PIC XXX   VALUE SPACES.
00251         05 T1-TOTAL         PIC Z,ZZZ,ZZ9.
00252         05 FILLER           PIC XX    VALUE SPACES.
00253         05 T1-MESSG         PIC X(54).
00254
00255  01  IO-AREA.
00256  COPY PIASSESSMT.
00257
00258  COPY ASAIS145SG.
00259
00260  COPY ASAIS150SG.
00261
00262  COPY IMSCALLS.
00263
00264  COPY ASAISSSA29.
00265
00266      EJECT
00267  LINKAGE SECTION.
00268  COPY IOPCBDESC.
00269
00270  01  PI-PCB1.
00271  COPY PIPCB1DESC.
00272          07  PI-KEY-FB-ITEM PIC X(22).
00273  01  PI-PCB2.
00274  COPY PIPCB2DESC.
00275          07  PI-KEY-FB-ITEM2 PIC X(22).
00276      EJECT
00277  PROCEDURE DIVISION.
00278  A010-BEGIN.
00279      ENTRY 'DLITCBL' USING IO-PCB, PI-PCB1, PI-PCB2.
00280      DISPLAY 'PROGRAM ASHMA859'
00281      DISPLAY SPACES
00282      OPEN INPUT PARAM-LINE
00283      READ PARAM-LINE AT END
00284         DISPLAY 'PARAM LINE IS MISSING'
00285         MOVE 'Y' TO ERROR-SW
00286         GOBACK.
00287      IF PR-CICV NOT EQUAL 'CICV'
00288         DISPLAY 'PARAM CHECK POINT CONTROL VALUE NOT EQUAL "CICV"
00289 -               ' ' PR-CICV
00290         MOVE 'Y' TO ERROR-SW.
00291      IF PR-CICV-VAL NOT NUMERIC OR PR-CICV-VAL EQUAL ZEROES
00292         DISPLAY 'PARAM CICV VALUE NOT VALID  ' PR-CICV-VAL
00293         MOVE 'Y' TO ERROR-SW.
00294      IF PR-PROC-YR NOT NUMERIC OR PR-PROC-YR EQUAL ZEROES
00295         DISPLAY 'PARAM PROCESS YEAR NOT VALID  ' PR-PROC-YR
00296         MOVE 'Y' TO ERROR-SW.
00297      IF PR-EQ-FCTR NOT NUMERIC OR PR-EQ-FCTR EQUAL ZEROES
00298         DISPLAY 'PARAM EQUALIZATION FACTOR NOT VALID  ' PR-EQ-FCTR
00299         MOVE 'Y' TO ERROR-SW.
00300      IF PR-TYPE = ( 'C' OR 'L' )
00301         NEXT SENTENCE
00302      ELSE
00303         DISPLAY 'PARAM TYPE NOT VALID       ' PR-TYPE
00304         MOVE 'Y' TO ERROR-SW.
00305      IF ERROR-SW EQUAL 'Y'
00306         MOVE 16 TO RETURN-CODE
00307         CLOSE PARAM-LINE
00308         GOBACK.
00309      MOVE FUNCTION CURRENT-DATE TO WRK-SYS-DATE.
00310      MOVE CORRESPONDING WRK-SYS-DATE TO MDY-DATE.
00311      MOVE MDY-DATE               TO H1-CURR-DTE
00312      OPEN INPUT  INPUT-FILE,
00313           OUTPUT PRINT-FILE-1, PRINT-FILE-2.
00314      WRITE PRINT-REC-1 FROM BLNK AFTER ADVANCING PAGE
00315      WRITE PRINT-REC-2 FROM BLNK AFTER ADVANCING PAGE
00316      PERFORM N010-XRST.
00317      PERFORM E020-ISSUE-CHECKPOINT.
00318      PERFORM B010-MAINLINE
00319          UNTIL END-PROCESSING-SW = 'Y' OR
00320          ERROR-SW EQUAL 'Y'.
00321
00322  A070-END.
00323      IF ERROR-SW EQUAL 'Y'
00324         MOVE 16 TO RETURN-CODE
00325      ELSE
00326         PERFORM M050-FORMAT-TOTALS.
00327      DISPLAY 'TOTAL NO. OF CHECKPOINTS TAKEN                    '
00328              CPI-TRANS-NO
00329      DISPLAY 'NUMBER OF INPUT RECORDS READ                      '
00330              IN-REC-CTR
00331      DISPLAY 'TOTAL EXEMPTION MASTER SEGMENTS UPDATED           '
00332              EXEMP-MAST-REPL-CTR
00333      DISPLAY SPACES
00334      DISPLAY ' PARAM-CICV      '    PR-CICV
00335      DISPLAY ' PARAM-PROC-YR   '    PR-PROC-YR
00336      DISPLAY ' PARAM-EQ-FCTR   '    PR-EQ-FCTR
00337      DISPLAY ' PARAM-PR-TYPE   '    PR-TYPE
00338      DISPLAY SPACES
00339      CLOSE PARAM-LINE, INPUT-FILE,
00340            PRINT-FILE-1, PRINT-FILE-2.
00341      GOBACK.
00342
00343  B010-MAINLINE.
00344      PERFORM C010-READ-INPUT-FILE
00345      IF INFILE-EOF-SW NOT EQUAL 'Y'
00346         IF COMP-CURR-PROP EQUAL COMP-PREV-PROP
00347            PERFORM B050-PROCESS-INREC
00348         ELSE
00349            IF MATCH-REC-SW = 'Y'
00350               PERFORM D020-1150-STEP17
00351               MOVE SPACES TO MATCH-REC-SW
00352            END-IF
00353            PERFORM C020-GET-DLI-RECORD
00354            IF MISSING-SEGMENT = 'N'
00355               PERFORM D010-MOVE-CURR-DETL-TO-WS
00356               PERFORM B050-PROCESS-INREC
00357            END-IF
00358            MOVE COMP-CURR-PROP TO COMP-PREV-PROP
00359         END-IF
00360      ELSE
00361         IF MATCH-REC-SW = 'Y'
00362            PERFORM D020-1150-STEP17
00363            MOVE SPACES TO MATCH-REC-SW
00364         END-IF
00365         MOVE 'Y' TO END-PROCESSING-SW
00366      END-IF.
00367  B050-PROCESS-INREC.
00368      IF PR-TYPE = ( 'L' OR 'C' )
00369        MOVE 'Y' TO VALID-REC
00370        IF SFSTAT-IS-Q = 'Y'
00371           NEXT SENTENCE
00372        ELSE
00373           MOVE 'N' TO VALID-REC
00374           MOVE 'RECORD NOT QUALIFIED FOR SENIOR FREEZE' TO
00375                            DL1-MESSG
00376           PERFORM F010-FORMAT-REPORT-1.
00377      IF PR-TYPE = ( 'L' OR 'C' )
00378       IF VALID-REC = 'Y'
00379        IF SF-BVYNOCALCIND = 'Y'
00380           MOVE 'INPUT RECORD COULD NOT BE CALCULATED ' TO
00381                               DL1-MESSG
00382           PERFORM F010-FORMAT-REPORT-1
00383        ELSE
00384          IF PR-TYPE = 'C'
00385 *           IF SF-BASVALYR NOT LESS ( WS-HLD-BASVALYR-X - 1 )
00386 *           IF SF-BASVALYR GREATER ( WS-HLD-BASVALYR-X - 1 )
00387 *              IF SF-BVYTOTELCOMPEV NOT GREATER
00388                IF SF-BVYTOTELCOMPEV LESS
00389                                          WS-HLD-BVYTOTELCOMPEV-X
00390                   MOVE 'Y' TO MATCH-REC-SW
00391                   PERFORM L010-1070-STEP12
00392                END-IF
00393 *           END-IF
00394          ELSE
00395             IF PR-TYPE = 'L'
00396 *              IF SF-BASVALYR NOT LESS WS-HLD-BASVALYR-X
00397 *              IF SF-BASVALYR GREATER WS-HLD-BASVALYR-X
00398 *                IF SF-BVYTOTELCOMPEV NOT GREATER
00399                  IF SF-BVYTOTELCOMPEV LESS
00400                                         WS-HLD-BVYTOTELCOMPEV-X
00401                  MOVE 'Y' TO MATCH-REC-SW
00402                  MOVE SF-BVYNOCALCIND TO WS-HLD-BVYNOCALCIND
00403                  MOVE SF-BASVALYR     TO WS-HLD-BASVALYR
00404                                          WS-HLD-BASVALYR-X
00405                  MOVE SF-BVYFULLAV    TO WS-HLD-BVYFULLAV
00406                  MOVE SF-BVYEV        TO WS-HLD-BVYEV
00407                  MOVE SF-BVYELCOMPFULLAV TO WS-HLD-BVYELCOMPFULLAV
00408                  MOVE SF-BVYTOTELCOMPEV TO WS-HLD-BVYTOTELCOMPEV
00409                                            WS-HLD-BVYTOTELCOMPEV-X
00410                  MOVE SF-CYFNLEVDIFF  TO WS-HLD-CYFNLEVDIFF.
00411
00412 *        IF SF-BASVALYR NOT LESS ( WS-HLD-BASVALYR-X - 1 )
00413 *          IF SF-BVYTOTELCOMPEV LESS WS-HLD-BVYTOTELCOMPEV-X
00414 *           MOVE 'Y' TO MATCH-REC-SW
00415 *           IF PR-TYPE = 'L'
00416 *            MOVE SF-BVYNOCALCIND TO WS-HLD-BVYNOCALCIND
00417 *            MOVE SF-BASVALYR     TO WS-HLD-BASVALYR
00418 *                                    WS-HLD-BASVALYR-X
00419 *            MOVE SF-BVYFULLAV    TO WS-HLD-BVYFULLAV
00420 *            MOVE SF-BVYEV        TO WS-HLD-BVYEV
00421 *            MOVE SF-BVYELCOMPFULLAV TO WS-HLD-BVYELCOMPFULLAV
00422 *            MOVE SF-BVYTOTELCOMPEV TO WS-HLD-BVYTOTELCOMPEV
00423 *                                       WS-HLD-BVYTOTELCOMPEV-X
00424 *            MOVE SF-CYFNLEVDIFF  TO WS-HLD-CYFNLEVDIFF
00425 *           ELSE
00426 *            PERFORM L010-1070-STEP12.
00427     SKIP3
00428  C010-READ-INPUT-FILE.
00429      READ INPUT-FILE INTO WORK-SF-REC
00430       AT END
00431         MOVE 'Y'            TO INFILE-EOF-SW.
00432      IF INFILE-EOF-SW NOT EQUAL 'Y'
00433         ADD +1              TO IN-REC-CTR
00434         MOVE SF-VOL         TO CU-VOL
00435         MOVE SF-PROP        TO CU-PROP, CMP-CU-PROP
00436         MOVE SF-TOWN        TO CU-TOWN
00437         IF CURR-IN-KEY LESS        PREV-IN-KEY
00438            DISPLAY 'INPUT FILE IS OUT OF SEQUENCE'
00439            DISPLAY 'CURRENT RECORD IS   ' CU-TOWN  ' ' CU-VOL  ' '
00440                                           CU-PROP
00441            DISPLAY 'PREVIOUS RECORD IS  ' PV-TOWN  ' ' PV-VOL  ' '
00442                                           PV-PROP
00443            MOVE CURR-IN-KEY TO PREV-IN-KEY
00444            MOVE 'Y'         TO ERROR-SW
00445         ELSE
00446            MOVE CURR-IN-KEY TO PREV-IN-KEY.
00447
00448  C020-GET-DLI-RECORD.
00449      MOVE 'N' TO MISSING-SEGMENT
00450      PERFORM C030-GU-ROOT-ASSESS-SEG
00451      IF PI-DB-GOOD-STATUS
00452         PERFORM C040-GNP-EXEMP-MAST-SEG
00453         IF PI-DB-GOOD-STATUS
00454            MOVE 'N' TO SFSTAT-IS-Q
00455            MOVE ZEROES TO EXEMPT-DET-EOF-SW,
00456                           EXEMP-DET-SEG-CTR
00457            PERFORM C045-GET-DET-SEG
00458                     UNTIL EXEMPT-DET-EOF-SW EQUAL 1
00459                     OR ERROR-SW EQUAL 'Y'
00460            IF EXEMP-DET-SEG-CTR = ZERO
00461               MOVE 'Y' TO MISSING-SEGMENT
00462               MOVE 'NO MATCHING MASTER EXEMPTION SEGMENT'
00463                                    TO DL1-MESSG
00464               PERFORM F010-FORMAT-REPORT-1
00465            END-IF
00466         ELSE
00467            IF PI-SEG-NOTFND
00468               MOVE 'Y' TO MISSING-SEGMENT
00469               MOVE 'NO MATCHING MASTER EXEMPTION SEGMENT'
00470                                       TO DL1-MESSG
00471               PERFORM F010-FORMAT-REPORT-1
00472            END-IF
00473         END-IF
00474      ELSE
00475         IF PI-SEG-NOTFND
00476            MOVE 'Y' TO MISSING-SEGMENT
00477            MOVE 'NO MATCHING ROOT, NOMATCHING ASSSESSMENT DATA SE
00478 -               'MENT' TO DL1-MESSG
00479            PERFORM F010-FORMAT-REPORT-1
00480         END-IF
00481      END-IF.
00482     SKIP2
00483  C030-GU-ROOT-ASSESS-SEG.
00484      MOVE SF-PROP          TO LVL1-ARG
00485      MOVE PR-PROC-YR (3:2) TO LVL2-PROCYR, LVL2-TXYR
00486      MOVE '0'              TO LVL2-TXTYP
00487      CALL 'CBLTDLI' USING GU
00488                           PI-PCB1
00489                           PY-ASMTDATA
00490                           LVL1-QUAL-SSA
00491                           LVL2-QUAL-SSA.
00492      IF PI-DB-GOOD-STATUS
00493         NEXT SENTENCE
00494      ELSE
00495         IF PI-SEG-NOTFND AND PI-SEGMENT-LVL EQUAL ZEROES
00496            NEXT SENTENCE
00497 *          DISPLAY 'ROOT SEGMENT NOT FOUND USING A GU  ' LVL1-ARG
00498         ELSE
00499            IF PI-SEG-NOTFND AND PI-SEGMENT-LVL EQUAL '01'
00500               DISPLAY 'ASSESSMENT SEGMENT NOT FOUND USING A GU  '
00501                       LVL1-ARG ' ' LVL2-ARG
00502            ELSE
00503               DISPLAY 'ROOT/ASSESSMENT SEGMENT GU ERROR  '
00504                       LVL1-ARG ' ' LVL2-ARG
00505               PERFORM X020-DISPLAY-PI-PCB.
00506
00507  C040-GNP-EXEMP-MAST-SEG.
00508      MOVE SF-PROP TO LVL1-ARG.
00509      MOVE PR-PROC-YR (3:2) TO LVL2-PROCYR, LVL2-TXYR
00510      MOVE '0'              TO LVL2-TXTYP
00511      CALL 'CBLTDLI' USING GN
00512                           PI-PCB1
00513                           C145-SENFRZMASTER
00514                           LVL1-QUAL-SSA
00515                           LVL2-QUAL-SSA
00516                           LVL3-UNQUAL-SSA.
00517      IF PI-DB-GOOD-STATUS
00518          NEXT SENTENCE
00519      ELSE
00520          IF PI-SEG-NOTFND
00521             NEXT SENTENCE
00522          ELSE
00523             DISPLAY 'EXEMPTION MASTER SEGMENT GNP ERROR  '
00524                     LVL1-ARG ' ' LVL2-ARG
00525             PERFORM X020-DISPLAY-PI-PCB.
00526
00527  C045-GET-DET-SEG.
00528      PERFORM C050-GNP-EXEMP-DET-SEG.
00529      IF PI-DB-GOOD-STATUS
00530         IF C150-SFSTAT = 'Q'
00531            MOVE 'Y' TO SFSTAT-IS-Q.
00532
00533  C050-GNP-EXEMP-DET-SEG.
00534      MOVE SF-PROP TO LVL1-ARG.
00535      MOVE PR-PROC-YR (3:2) TO LVL2-PROCYR, LVL2-TXYR
00536      MOVE '0'              TO LVL2-TXTYP
00537      MOVE C145-RECCODE TO LVL3-ARG.
00538 **                        LVL1-QUAL-SSA
00539 **                        LVL2-QUAL-SSA
00540 **                        LVL3-QUAL-SSA
00541      CALL 'CBLTDLI' USING GNP
00542                           PI-PCB1
00543                           C150-SENFRZDETAIL
00544                           LVL4-UNQUAL-SSA.
00545      IF PI-DB-GOOD-STATUS
00546         ADD 1 TO EXEMP-DET-SEG-CTR
00547      ELSE
00548         IF PI-SEG-NOTFND
00549            MOVE 1 TO EXEMPT-DET-EOF-SW
00550         ELSE
00551            DISPLAY 'EXEMPTION DETAIL SEGMENT GNP ERROR  '
00552                    LVL1-ARG ' ' LVL2-ARG ' ' C145-RECCODE
00553            PERFORM X020-DISPLAY-PI-PCB.
00554
00555  D010-MOVE-CURR-DETL-TO-WS.
00556      MOVE C145-SENFRZMASTER TO WS-HLD-REC.
00557      MOVE C145-BVYNOCALCIND TO WS-NEW-CALCIND.
00558      MOVE C145-BASVALYR     TO WS-NEW-BASVALYR.
00559      MOVE C145-BVYFULLAV    TO WS-NEW-BVYFULLAV.
00560      MOVE C145-BVYEV        TO WS-NEW-BVYEV.
00561      MOVE C145-BVYELCOMPFULLAV TO WS-NEW-BVYELCOMPFULLAV.
00562      MOVE C145-BVYTOTELCOMPEV  TO WS-NEW-BVYTOTELCOMPEV,
00563      MOVE C145-CYFNLEVDIFF     TO WS-NEW-CYFNLEVDIFF.
00564      MOVE C145-BASVALYR  TO WS-HLD-BASVALYR-X
00565      MOVE C145-BVYTOTELCOMPEV TO WS-HLD-BVYTOTELCOMPEV-X.
00566
00567  D020-1150-STEP17.
00568      PERFORM E010-DETERMINE-CHECKPOINT
00569      IF ERROR-SW NOT EQUAL 'Y'
00570         PERFORM E030-GHU-ROOT-ASS-EX-MAST-SEG
00571         IF PI2-DB-GOOD-STATUS
00572            PERFORM E040-UPDATE-EXEMP-MAST-SEG
00573            PERFORM E050-REPL-EXEMP-MAST-SEG.
00574      PERFORM E060-INITIALIZE-FIELDS.
00575
00576  E010-DETERMINE-CHECKPOINT.
00577      IF CICV-CTR GREATER PR-CICV-VAL-N
00578         ADD 1       TO CPI-TRANS-NO
00579         PERFORM E020-ISSUE-CHECKPOINT
00580         MOVE ZEROES TO CICV-CTR.
00581
00582  E020-ISSUE-CHECKPOINT.
00583      CALL 'CBLTDLI' USING CHKP, IO-PCB, CHECK-POINT-ID
00584      IF IP-GOOD-STATUS
00585         NEXT SENTENCE
00586      ELSE
00587         PERFORM X010-ISSUE-CHKPT-ERR.
00588
00589  E030-GHU-ROOT-ASS-EX-MAST-SEG.
00590      MOVE CMP-PV-PROP      TO LVL1-ARG
00591      MOVE PR-PROC-YR (3:2) TO LVL2-PROCYR, LVL2-TXYR
00592      MOVE '0'              TO LVL2-TXTYP
00593      MOVE C145-RECCODE     TO LVL3-ARG
00594      CALL 'CBLTDLI' USING GHU
00595                           PI-PCB2
00596                           C145-SENFRZMASTER
00597                           LVL1-QUAL-SSA
00598                           LVL2-QUAL-SSA
00599                           LVL3-QUAL-SSA.
00600      IF PI2-DB-GOOD-STATUS
00601         NEXT SENTENCE
00602      ELSE
00603         IF PI2-SEG-NOTFND AND PI2-SEGMENT-LVL EQUAL ZEROES
00604            DISPLAY 'ROOT SEGMENT NOT FOUND USING GHU  ' LVL1-ARG
00605         ELSE
00606            IF PI2-SEG-NOTFND AND PI2-SEGMENT-LVL EQUAL '01'
00607               DISPLAY 'ASSESSMENT SEGMENT NOT FOUND USING GHU  '
00608                       LVL1-ARG ' ' LVL2-ARG
00609            ELSE
00610               IF PI2-SEG-NOTFND AND PI2-SEGMENT-LVL EQUAL '02'
00611                  DISPLAY 'EXEMPTION MASTER SEGMENT NOT FOUND USIN
00612 -                        ' GHU  ' LVL1-ARG ' ' LVL2-ARG ' '
00613                          LVL3-ARG
00614               ELSE
00615                  DISPLAY 'ROOT/ASSESSMENT/EXEMPTION MASTER SEGMEN
00616 -                        ' GHU ERROR  ' LVL1-ARG ' ' LVL2-ARG ' '
00617                          LVL3-ARG
00618                  PERFORM X030-DISPLAY-PI-PCB2.
00619
00620  E040-UPDATE-EXEMP-MAST-SEG.
00621       MOVE  C145-BVYMANCALCIND   TO  C145-BVYMANCALCIND-OR.
00622       MOVE  C145-BASVALYR        TO  C145-BASVALYR-OR.
00623       MOVE  C145-BVYFULLAV       TO  C145-BVYFULLAV-OR.
00624       MOVE  C145-BVYEV           TO  C145-BVYEV-OR.
00625       MOVE  C145-BVYELCOMPFULLAV TO  C145-BVYELCOMPFULLAV-OR.
00626       MOVE  C145-BVYTOTELCOMPEV  TO  C145-BVYTOTELCOMPEV-OR.
00627       MOVE  C145-CYFNLEVDIFF    TO   C145-CYFNLEVDIFF-OR.
00628       MOVE SPACES  TO  C145-BVYNOCALCIND, C145-BVYMANCALCIND.
00629       MOVE WS-HLD-BASVALYR       TO C145-BASVALYR
00630       MOVE WS-HLD-BVYFULLAV      TO C145-BVYFULLAV
00631       MOVE WS-HLD-BVYEV          TO C145-BVYEV
00632       MOVE WS-HLD-BVYELCOMPFULLAV TO
00633                               C145-BVYELCOMPFULLAV
00634       MOVE WS-HLD-BVYTOTELCOMPEV  TO
00635                               C145-BVYTOTELCOMPEV
00636       MOVE WS-HLD-CYFNLEVDIFF    TO
00637                               C145-CYFNLEVDIFF.
00638       PERFORM M010-FORMAT-REPORT-2.
00639 * REMOVE THIS AFTER TESTING
00640 *     DISPLAY 'UPDATE RECORD ' IN-REC-CTR SPACE CMP-PV-PROP.
00641 * REMOVE THIS AFTER TESTING
00642
00643  E050-REPL-EXEMP-MAST-SEG.
00644      CALL 'CBLTDLI' USING REPL
00645                           PI-PCB2
00646                           C145-SENFRZMASTER.
00647      IF PI2-DB-GOOD-STATUS
00648         ADD 1 TO EXEMP-MAST-REPL-CTR
00649         ADD 1 TO CICV-CTR
00650      ELSE
00651         DISPLAY 'EXEMPTION MASTER SEGMENT REPL ERROR  '
00652                 LVL1-ARG ' ' LVL2-ARG ' ' LVL3-ARG
00653         PERFORM X030-DISPLAY-PI-PCB2.
00654
00655  E060-INITIALIZE-FIELDS.
00656      MOVE SPACES TO  WS-HLD-REC, WS-HLD-BVYNOCALCIND.
00657      MOVE ZEROES  TO      WS-HLD-BASVALYR
00658                           WS-HLD-BVYFULLAV
00659                           WS-HLD-BVYEV
00660                           WS-HLD-BVYELCOMPFULLAV
00661                           WS-HLD-CYFNLEVDIFF.
00662      MOVE ZEROES  TO      TOT-BASE-COMP-EAV
00663                           WS-HLD-CYNOTELEV
00664                           FINAL-DIFF-TX-COMP.
00665
00666  F010-FORMAT-REPORT-1.
00667      MOVE SF-VOL      TO DL1-VOL
00668      MOVE SF-PROP     TO DL1-PROP
00669      INSPECT DL1-PROP REPLACING ALL SPACES BY '-'
00670      MOVE SF-TAX-TYPE TO DL1-TXTYP
00671      MOVE SF-CYCLS    TO WS-CLS.
00672      MOVE WS-CLS-N    TO DL1-CLS
00673      INSPECT DL1-CLS  REPLACING ALL SPACES BY '-'
00674      MOVE SF-BIRTHDTE TO DL1-BIRTHDTE.
00675      IF LN-CTR-1 GREATER 57
00676         PERFORM F020-HDR-RTN-1.
00677      WRITE PRINT-REC-1 FROM DETAIL1 AFTER ADVANCING 2 LINES
00678      ADD 2            TO LN-CTR-1.
00679
00680  F020-HDR-RTN-1.
00681      ADD 1         TO PG-CTR-1
00682      MOVE PG-CTR-1 TO H1-PAGE
00683      MOVE '1'      TO H2-REPORT-TYP
00684      MOVE '                          ERROR REPORT #1'
00685                    TO H2-REPORT-TITLE
00686      WRITE PRINT-REC-1 FROM HEAD1 AFTER ADVANCING PAGE
00687      WRITE PRINT-REC-1 FROM HEAD2 AFTER ADVANCING 2
00688      WRITE PRINT-REC-1 FROM HEAD3 AFTER ADVANCING 2
00689      MOVE 5        TO LN-CTR-1.
00690
00691  L010-1070-STEP12.
00692      ADD SF-BVYTOTELCOMPEV C145-288EXPEV
00693          C145-288OVRLIMEV GIVING TOT-BASE-COMP-EAV.
00694      COMPUTE
00695        WS-HLD-CYFULLEV ROUNDED = C145-CYFULLAV * PR-EQ-FCTR-N
00696      SUBTRACT TOT-BASE-COMP-EAV FROM WS-HLD-CYFULLEV
00697        GIVING WS-HLD-CYNOTELEV.
00698      IF SF-BVYNOCALCIND = 'Y'
00699        MOVE 'Y' TO QUALIFY-SW.
00700      IF C145-SFSHARES = ZERO
00701        IF WS-HLD-CYNOTELEV LESS THAN +0
00702           MOVE ZEROES TO FINAL-DIFF-TX-COMP
00703        ELSE
00704           MOVE WS-HLD-CYNOTELEV TO FINAL-DIFF-TX-COMP
00705        END-IF
00706        IF QUALIFY-SW = 'Y'
00707           PERFORM L020-1100-STEP16
00708        ELSE
00709           PERFORM L030-1150-STEP17X
00710        END-IF
00711      ELSE
00712        PERFORM M030-CHECK-BASEYR-GN-DET-SEG
00713        PERFORM L030-1150-STEP17X.
00714
00715  L020-1100-STEP16.
00716      IF TOT-BASE-COMP-EAV EQUAL ZEROES OR
00717         WS-HLD-CYFULLEV   EQUAL ZEROES
00718         IF TOT-BASE-COMP-EAV EQUAL ZEROES
00719            MOVE 'TOTAL ELIG COMPARISON EAV IS EQUAL TO ZERO' TO
00720                 DL1-MESSG
00721            PERFORM F010-FORMAT-REPORT-1
00722         END-IF
00723         IF WS-HLD-CYFULLEV   EQUAL ZEROES
00724            MOVE 'TOTAL CURRENT BASE VALUE EAV IS EQUAL TO ZERO' TO
00725                 DL1-MESSG
00726            PERFORM F010-FORMAT-REPORT-1
00727         END-IF
00728      ELSE
00729         IF WS-HLD-288EXPEV    GREATER ZEROES OR
00730            WS-HLD-288OVRLIMEV GREATER ZEROES
00731            IF WS-HLD-288OVRLIMEV GREATER ZEROES
00732               MOVE '288 EXPIRE VALUE GREATER THAN ZERO' TO
00733                    DL1-MESSG
00734               PERFORM F010-FORMAT-REPORT-1
00735            END-IF
00736            IF WS-HLD-288OVRLIMEV GREATER ZEROES
00737               MOVE '288 OVER 30,000 GREATER THAN ZERO' TO
00738                    DL1-MESSG
00739               PERFORM F010-FORMAT-REPORT-1
00740            END-IF
00741         ELSE
00742            IF FINAL-DIFF-TX-COMP EQUAL ZEROES
00743               MOVE 'FINAL EAV TAX COMPUTATION EQUAL TO ZERO' TO
00744                    DL1-MESSG
00745               PERFORM F010-FORMAT-REPORT-1
00746            END-IF
00747         END-IF
00748      END-IF.
00749
00750  L030-1150-STEP17X.
00751      IF C145-BVYMANCALCIND = 'X'
00752         MOVE 'MANUAL CALCULATION INDICATOR ' TO DL1-MESSG
00753         PERFORM F010-FORMAT-REPORT-1
00754         MOVE 'N'    TO MATCH-REC-SW.
00755      MOVE SF-BASVALYR          TO WS-HLD-BASVALYR.
00756      MOVE SF-BVYFULLAV         TO WS-HLD-BVYFULLAV.
00757      MOVE SF-BVYELCOMPFULLAV   TO WS-HLD-BVYELCOMPFULLAV.
00758      MOVE FINAL-DIFF-TX-COMP   TO WS-HLD-CYFNLEVDIFF.
00759      MOVE SF-BVYEV             TO WS-HLD-BVYEV.
00760      MOVE TOT-BASE-COMP-EAV    TO WS-HLD-BVYTOTELCOMPEV.
00761      MOVE SF-BASVALYR          TO WS-HLD-BASVALYR.
00762      MOVE SF-CYELCOMPEV       TO WS-HLD-CYELCOMPEV.
00763
00764  M010-FORMAT-REPORT-2.
00765      MOVE CMP-PV-PROP TO DL4-PROP
00766      INSPECT DL4-PROP REPLACING ALL SPACES BY '-'
00767      MOVE C145-CYCLS  TO WS-CLS.
00768      MOVE WS-CLS-N    TO DL4-CURR-YR-CLS.
00769      INSPECT DL4-CURR-YR-CLS REPLACING ALL SPACES BY '-'
00770      MOVE PR-PROC-YR-N TO DL4-BASE-YR.
00771      MOVE C145-BVYTOTELCOMPEV TO DL4-TOT-BASE-EAV.
00772      MOVE C145-CYELCOMPEV     TO DL4-CURR-ELIG-EAV.
00773      MOVE C145-CYFNLEVDIFF    TO DL4-FINAL-DIFF-TX.
00774      MOVE C145-BVYFULLAV      TO DL4-BASE-VAL-AV.
00775      MOVE C145-CYFULLAV       TO DL4-CURR-VAL-AV.
00776      MOVE C145-CYNOTELEV      TO DL4-CURR-NOT-ELIG-EAV.
00777      IF LN-CTR-2 GREATER 57
00778         PERFORM M020-HDR-RTN-2.
00779      WRITE PRINT-REC-2 FROM DETAIL4 AFTER ADVANCING 2 LINES
00780      ADD 2            TO LN-CTR-2.
00781
00782  M020-HDR-RTN-2.
00783      ADD 1                TO PG-CTR-2
00784      MOVE PG-CTR-2        TO H1-PAGE
00785      MOVE '2'             TO H2-REPORT-TYP
00786      MOVE '            SENIOR FREEZE BASE VALUE UPDATE REPORT'
00787                           TO H2-REPORT-TITLE
00788      WRITE PRINT-REC-2 FROM HEAD1 AFTER ADVANCING PAGE
00789      WRITE PRINT-REC-2 FROM HEAD2 AFTER ADVANCING 2
00790      WRITE PRINT-REC-2 FROM HEAD3 AFTER ADVANCING 2
00791      WRITE PRINT-REC-2 FROM HEAD4 AFTER ADVANCING 1.
00792      MOVE 6               TO LN-CTR-2.
00793
00794  M030-CHECK-BASEYR-GN-DET-SEG.
00795      MOVE ZEROES TO EXEMPT-DET-EOF-SW  EXEMP-DET-SEG-CTR
00796      PERFORM
00797      UNTIL EXEMPT-DET-EOF-SW EQUAL 1
00798      OR ERROR-SW EQUAL 'Y'
00799         PERFORM C050-GNP-EXEMP-DET-SEG
00800         IF PI-DB-GOOD-STATUS
00801            IF C150-SFSTAT EQUAL 'Q'
00802               IF C145-BLDGSHARES GREATER ZERO
00803                  COMPUTE PCT-SHARES =
00804                    C150-COOPSENSHRS / C145-BLDGSHARES
00805               ELSE
00806                  MOVE 'Y' TO NO-CALC-SW
00807               END-IF
00808               COMPUTE BASE-DIFF =
00809                 C150-BSYR-EQVAL * PCT-SHARES
00810               COMPUTE CURR-DIFF =
00811                 WS-HLD-CYELCOMPEV  * PCT-SHARES
00812               COMPUTE CURR-TAX-DIFF =
00813                 CURR-DIFF - BASE-DIFF
00814               IF CURR-TAX-DIFF POSITIVE
00815                  ADD CURR-TAX-DIFF TO FINAL-DIFF-TX-COMP
00816               END-IF
00817            END-IF
00818         END-IF
00819      END-PERFORM.
00820      IF EXEMP-DET-SEG-CTR EQUAL ZEROES
00821         MOVE 'N' TO MATCH-REC-SW
00822         MOVE 'NO-MATCHING DETAIL SEGMENT' TO DL1-MESSG
00823         PERFORM F010-FORMAT-REPORT-1.
00824
00825  M050-FORMAT-TOTALS.
00826      IF LN-CTR-1 GREATER 45
00827         PERFORM M020-HDR-RTN-2.
00828      MOVE 'TOTAL CURRENT YEAR RECORDS READ'         TO T1-MESSG
00829      MOVE IN-REC-CTR                                TO T1-TOTAL
00830      MOVE 'TOTAL MASTER SEGMENTS UPDATED'           TO T1-MESSG
00831      MOVE EXEMP-MAST-REPL-CTR                       TO T1-TOTAL
00832      WRITE PRINT-REC-1 FROM TOTAL1 AFTER ADVANCING 1.
00833
00834  N010-XRST.
00835      CALL 'CBLTDLI' USING XRST
00836                           IO-PCB
00837                           XRST-LGTH1
00838                           XRST-AREA
00839                           XRST-LGTH2
00840                           PI-PCB2.
00841
00842  X010-ISSUE-CHKPT-ERR.
00843      CALL 'CBLTDLI' USING ROLL
00844      DISPLAY 'CHECK POINT ISSUE ERROR'
00845      DISPLAY 'STATUS CODE            ' IP-STATUS-CODE
00846      MOVE 'Y' TO ERROR-SW.
00847
00848  X020-DISPLAY-PI-PCB.
00849      DISPLAY 'THE DBD NAME IS                         '
00850              PI-DBD-NAME
00851      DISPLAY 'THE SEGMENT LEVEL IS                    '
00852              PI-SEGMENT-LVL
00853      DISPLAY 'THE STATUS CODE IS                      '
00854              PI-STATUS-CODE
00855      DISPLAY 'THE PROCESSING OPTIONS ARE              '
00856              PI-PROC-OPT
00857      DISPLAY 'THE SEGMENT NAME IS                     '
00858              PI-NAME-FDBK
00859      DISPLAY 'THE LENGTH OF THE KEY FEEDBACK AREA IS  '
00860              PI-KEY-FDBK-LNG
00861      DISPLAY 'THE NUMBER OF SENSITIVE SEGMENTS ARE    ' PI-SEN-SEG
00862      DISPLAY 'THE KEY FEEDBACK AREA IS                '
00863              PI-KEY-FB-ITEM
00864      MOVE 'Y' TO ERROR-SW.
00865
00866  X030-DISPLAY-PI-PCB2.
00867      DISPLAY 'THE DBD NAME IS                         '
00868              PI2-DBD-NAME
00869      DISPLAY 'THE SEGMENT LEVEL IS                    '
00870              PI2-SEGMENT-LVL
00871      DISPLAY 'THE STATUS CODE IS                      '
00872              PI2-STATUS-CODE
00873      DISPLAY 'THE PROCESSING OPTIONS ARE              '
00874              PI2-PROC-OPT
00875      DISPLAY 'THE SEGMENT NAME IS                     '
00876              PI2-NAME-FDBK
00877      DISPLAY 'THE LENGTH OF THE KEY FEEDBACK AREA IS  '
00878              PI2-KEY-FDBK-LNG
00879      DISPLAY 'THE NUMBER OF SENSITIVE SEGMENTS ARE    '
00880              PI2-SEN-SEG.
00881      DISPLAY 'THE KEY FEEDBACK AREA IS                '
00882              PI-KEY-FB-ITEM2.
00883      MOVE 'Y' TO ERROR-SW.
00884
