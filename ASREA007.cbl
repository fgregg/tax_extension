00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID.    ASREA007.
00003  AUTHOR.        GOLD-MOLIS.
00004  DATE-WRITTEN.  DEC. 27, 1983.
00005      SKIP1
00006 *REMARKS.       THIS PROGRAM DELETES THE 1980
00007 *               TYPE 1 COMPLAINT SEGMENTS FROM
00008 *               THE PROPERTY INFORMATION DATA
00009 *               BASE.
00010      SKIP2
00011  ENVIRONMENT DIVISION.
00012  CONFIGURATION SECTION.
00013  SOURCE-COMPUTER.  IBM-370.
00014  OBJECT-COMPUTER.  IBM-370.
00015      SKIP2
00016  DATA DIVISION.
00017      SKIP1
00018  WORKING-STORAGE SECTION.
00019      SKIP1
00020  77  CMPLT-SEG-DELETE     PIC S9(7) COMP-3  VALUE +0.
00021  77  ERR-MESG             PIC X(22).
00022      SKIP1
00023  77  DB-EOF-SW            PIC X             VALUE 'N'.
00024      88  DB-EOF                             VALUE 'Y'.
00025      SKIP1
00026  77  PROCESSING-SW        PIC X             VALUE 'Y'.
00027      88  OK-PROCESSING                      VALUE 'Y'.
00028      88  STOP-PROCESSING                    VALUE 'N'.
00029      SKIP1
00030  01  CMPLT-SEG-IO.
00031      SKIP1
00032  COPY PICOMPLAIN.
00033      SKIP1
00034    03  ASSMT-SEG-SSA.
00035      05  FILLER            PIC X(8)  VALUE 'ASSMTSEG'.
00036      05  FILLER            PIC X     VALUE '('.
00037      05  FILLER            PIC X(8)  VALUE 'PYKEY   '.
00038      05  FILLER            PIC XX    VALUE 'EQ'.
00039      05  ASSMT-SSA-KEY.
00040          10  AKEY-YEAR     PIC 9(2)  VALUE 80.
00041          10  AKEY-TXYR     PIC 9(2)  VALUE 80.
00042          10  AKEY-TXTYPE   PIC 9     VALUE  0.
00043      05  FILLER            PIC X     VALUE ')'.
00044      SKIP1
00045    03  CMPLT-SEG-SSA.
00046      05  FILLER            PIC X(8)  VALUE 'CMPLTSEG'.
00047      05  FILLER            PIC X     VALUE '('.
00048      05  FILLER            PIC X(8)  VALUE 'PGKEY   '.
00049      05  FILLER            PIC XX    VALUE 'EQ'.
00050      05  CMPLT-SSA-KEY.
00051          10  CKEY-PGTYPE   PIC 9     VALUE  1.
00052      05  FILLER            PIC X     VALUE ')'.
00053      SKIP1
00054  COPY IMSCALLS.
00055      SKIP1
00056  01  DB-ERR-MESGS.
00057      05  GHN-ERR           PIC X(20) VALUE
00058                                'DATA BASE GHN ERROR'.
00059      05  DLET-ERR          PIC X(22) VALUE
00060                                'DATA BASE DELETE ERROR'.
00061  EJECT
00062  LINKAGE SECTION.
00063  SKIP1
00064  01  PI-PCB.
00065      05  PI-DBD-NAME       PIC X(8).
00066      05  PI-SEGMENT-LVL    PIC X(2).
00067      05  PI-STATUS-CODE    PIC X(2).
00068          88  PI-DB-GOOD-STATUS     VALUE SPACE.
00069          88  PI-SEG-NOTFND         VALUE 'GE'.
00070          88  PI-DB-END             VALUE 'GB'.
00071      05  PI-PROC-OPT       PIC X(4).
00072      05  FILLER            PIC S9(5)  COMP.
00073      05  PI-NAME-FDBK      PIC X(8).
00074      05  PI-KEY-FDBK-LNG   PIC S9(5)  COMP.
00075      05  PI-SEN-SEG        PIC S9(5)  COMP.
00076      05  PI-KEY-FDBK.
00077          10  PI-ROOT-KEY   PIC X(12).
00078          10  PI-ASSMT-KEY  PIC X(5).
00079          10  PI-COMPLT-KEY PIC X.
00080      SKIP3
00081  PROCEDURE DIVISION.
00082      SKIP1
00083  100-HOUSEKEEPING.
00084      ENTRY 'DLITCBL' USING PI-PCB.
00085      SKIP1
00086      PERFORM 200-MAINLINE UNTIL
00087          DB-EOF OR STOP-PROCESSING.
00088      SKIP1
00089      DISPLAY 'TOTAL COMPLAINT SEGMENTS DELETED  = '
00090                                            CMPLT-SEG-DELETE.
00091      SKIP1
00092      IF STOP-PROCESSING
00093          MOVE 16 TO RETURN-CODE.
00094      SKIP1
00095      GOBACK.
00096          SKIP2
00097  200-MAINLINE.
00098      CALL 'CBLTDLI' USING GHN
00099                           PI-PCB
00100                           CMPLT-SEG-IO
00101                           ASSMT-SEG-SSA
00102                           CMPLT-SEG-SSA.
00103      SKIP1
00104      IF PI-DB-GOOD-STATUS
00105          PERFORM 300-DELETE-SEGMENT
00106      ELSE
00107          IF PI-DB-END
00108             MOVE 'Y' TO DB-EOF-SW
00109          ELSE
00110              MOVE GHN-ERR TO ERR-MESG
00111              PERFORM 400-DATA-BASE-ERROR.
00112      SKIP2
00113  300-DELETE-SEGMENT.
00114      CALL 'CBLTDLI' USING DLET
00115                           PI-PCB
00116                           CMPLT-SEG-IO.
00117      SKIP1
00118      IF PI-DB-GOOD-STATUS
00119              ADD +1 TO CMPLT-SEG-DELETE
00120      ELSE
00121          MOVE DLET-ERR TO ERR-MESG
00122          PERFORM 400-DATA-BASE-ERROR.
00123      SKIP2
00124  400-DATA-BASE-ERROR.
00125      DISPLAY ERR-MESG.
00126      DISPLAY 'STATUS CODE  = ' PI-STATUS-CODE.
00127      DISPLAY 'KEY LENGTH   = ' PI-KEY-FDBK-LNG.
00128      DISPLAY 'KEY FEEDBACK = ' PI-KEY-FDBK.
00129      MOVE 'N' TO PROCESSING-SW.
