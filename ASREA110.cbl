00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASREA110.
00003  AUTHOR. DAVE MILLER.
00004 *REMARKS.
00005 *    THE PURPOSE OF THIS PROGRAM IS TO
00006 *        CREATE A REAL ESTATE SUMMARY
00007 *        FILE AND A RAILROAD SUMMARY
00008 *        FILE USING THE ASSESSMENT
00009 *        MASTER FILE AS INPUT.
00010 *MODIFIED 7-20-87. GOLD-MOLIS. THIS PROGRAM IS COPIED FROM
00011 *        ASREA111 WITH CHANGE IN OUTPUT FILE.
00012  ENVIRONMENT DIVISION.
00013  CONFIGURATION SECTION.
00014  SOURCE-COMPUTER. IBM-370.
00015  OBJECT-COMPUTER. IBM-370.
00016  INPUT-OUTPUT SECTION.
00017  FILE-CONTROL.
00018      SELECT MASTER-FILE    ASSIGN TO UT-S-MASTER.
00019      SELECT RR-SUM-FILE   ASSIGN TO UT-S-RRSUM.
00020      SELECT RE-SUM-FILE   ASSIGN TO UT-S-RESUM.
00021  DATA DIVISION.
00022  FILE SECTION.
00023  FD  MASTER-FILE
00024      LABEL RECORDS ARE STANDARD
00025      BLOCK CONTAINS 0 CHARACTERS
00026      RECORDING MODE IS S.
00027  01  M-IN.
00028  COPY ASREASRD01.
00029  COPY RRSUMMRYF2.
00030  COPY RESUMMFD01.
00031  WORKING-STORAGE SECTION.
00032  77  R-IN                        PIC S9(7) COMP-3 VALUE +0.
00033  77  RE-OUT                      PIC S9(7) COMP-3 VALUE +0.
00034  77  RR-OUT                      PIC S9(7) COMP-3 VALUE +0.
00035  01  CONTROL-SECTION.
00036      05 UNPK-CLASS               PIC 999.
00037      05 FILLER REDEFINES UNPK-CLASS.
00038         10 MA-CLS                PIC 9.
00039         10 MI-CLS                PIC 99.
00040      05 EDIT-VOL                 PIC 999.
00041         88 RE                    VALUES 001 THRU 601.
00042         88 RR                    VALUE 605.
00043      05 MASTER-EOF               PIC X     VALUE SPACE.
00044         88 MST-EOF               VALUE 'E'.
00045  PROCEDURE DIVISION.
00046      OPEN INPUT MASTER-FILE
00047      OPEN OUTPUT RR-SUM-FILE
00048      OPEN OUTPUT RE-SUM-FILE
00049      PERFORM A010-MAINLINE
00050         UNTIL MST-EOF
00051      DISPLAY 'MASTER RECORDS READ ' R-IN
00052      DISPLAY 'RR RECORDS WRITTEN  ' RR-OUT
00053      DISPLAY 'RE RECORDS WRITTEN  ' RE-OUT
00054      CLOSE MASTER-FILE
00055            RE-SUM-FILE
00056            RR-SUM-FILE
00057      STOP RUN.
00058  A010-MAINLINE.
00059      READ MASTER-FILE
00060         AT END
00061            MOVE 'E' TO MASTER-EOF.
00062      IF MST-EOF
00063         NEXT SENTENCE
00064      ELSE
00065         ADD +1 TO R-IN
00066         MOVE M-VOL TO EDIT-VOL
00067         IF RE
00068            PERFORM B010-RE
00069         ELSE
00070            IF RR
00071               PERFORM C010-RR
00072            ELSE
00073               DISPLAY 'VOL NOT RR OR RE ' M-VOL ' ' M-PROP
00074               PERFORM B010-RE.
00075  B010-RE.
00076      MOVE M-CLS TO UNPK-CLASS
00077      MOVE M-VOL TO RE-VOL
00078      MOVE M-PROP TO RE-PROP
00079      MOVE M-TXCD TO RE-TXCD
00080      MOVE MA-CLS TO RE-MA
00081      MOVE MI-CLS TO RE-MI
00082      MOVE M-NBHD TO RE-NBHD
00083      MOVE M-STRT TO RE-STRT
00084      MOVE M-HSENO TO RE-HSENO
00085      IF M-STAT2 = ('0' OR '1' OR '2')
00086         MOVE M-STAT2 TO RE-EX-RR
00087      ELSE
00088         MOVE '0' TO RE-EX-RR.
00089      WRITE RE-REC
00090      ADD +1 TO RE-OUT.
00091  C010-RR.
00092      MOVE M-CLS TO UNPK-CLASS
00093      MOVE M-VOL TO RS-VOL
00094      MOVE M-PROP TO RS-PROP
00095      MOVE M-TXCD TO RS-TXCD
00096      MOVE MA-CLS TO RS-MA-CLS
00097      MOVE MI-CLS TO RS-MI-CLS
00098      MOVE M-NBHD TO RS-NBHD
00099      MOVE M-STRT TO RS-STRT
00100      MOVE M-HSENO TO RS-HSENO
00101      IF M-STAT2 = ('0' OR '1' OR '2')
00102         MOVE M-STAT2 TO RS-EX-RR
00103      ELSE
00104         MOVE '0' TO RS-EX-RR.
00105      MOVE ZEROES TO RS-RRNO.
00106      MOVE M-VALUE (9) TO RS-ASSD-VAL.
00107      WRITE RR-SUM-REC.
00108      ADD +1 TO RR-OUT.
