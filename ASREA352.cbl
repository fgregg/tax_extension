00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASREA352.
00003  AUTHOR. SCHOLL-MOLIS.
00004  DATE-WRITTEN. MAY 15,1980.
00005  DATE-COMPILED.
00006 *REMARKS. THIS PROGRAM COMPARES AND UPDATES THE TAXCODE FIELD
00007 *         OF THE BOARD OF APPEALS & ASSESSMENT MASTER RECORD.
00008      SKIP2
00009  ENVIRONMENT DIVISION.
00010  CONFIGURATION SECTION.
00011  SOURCE-COMPUTER. IBM-370.
00012  OBJECT-COMPUTER. IBM-370.
00013      SKIP2
00014  INPUT-OUTPUT SECTION.
00015  FILE-CONTROL.
00016      SELECT MASTER-IN
00017           ASSIGN TO UT-S-MSTRIN.
00018      SELECT BA-MASTER
00019           ASSIGN TO UT-S-BAMSTR.
00020      SELECT MASTER-OUT
00021           ASSIGN TO UT-S-MSTROUT.
00022      SKIP2
00023  DATA DIVISION.
00024  FILE SECTION.
00025  FD  MASTER-IN
00026      BLOCK CONTAINS 0 CHARACTERS
00027      RECORD CONTAINS 122 TO 18706 CHARACTERS
00028      LABEL RECORDS ARE STANDARD
00029      RECORDING MODE IS S.
00030  01  MSTR-IN-REC.
00031  COPY ASREASRD01.
00032      SKIP2
00033  FD  BA-MASTER
00034      BLOCK CONTAINS 0 CHARACTERS
00035      RECORD CONTAINS 122 TO 18706 CHARACTERS
00036      LABEL RECORDS ARE STANDARD
00037      RECORDING MODE IS S
00038      DATA RECORD IS BA-MSTR-REC.
00039  01  BA-MSTR-REC.
00040  COPY ASREASRD02.
00041      SKIP2
00042  FD  MASTER-OUT
00043      BLOCK CONTAINS 0 CHARACTERS
00044      RECORD CONTAINS 122 TO 18706 CHARACTERS
00045      LABEL RECORDS ARE STANDARD
00046      RECORDING MODE IS S
00047      DATA RECORD IS MSTR-OUT-REC.
00048  01  MSTR-OUT-REC.
00049      05 MO-BASE                 PIC X(118).
00050      05 MO-REC-CTRS.
00051         10 MO-SLS-CTR           PIC S9.
00052         10 MO-DTL-QST-CTR       PIC S9(3).
00053      05 MO-SALES OCCURS 0 TO 1 TIMES DEPENDING ON MO-SLS-CTR.
00054         10 MO-SALES-SEG         PIC X(34).
00055      05 MO-DTL-QST
00056               OCCURS 0 TO 350 TIMES DEPENDING ON MO-DTL-QST-CTR.
00057         10 MO-DETAILS           PIC X(53).
00058      EJECT
00059  WORKING-STORAGE SECTION.
00060  77  TOTAL-BA-REC-READ          PIC S9(7)   COMP-3  VALUE +0.
00061  77  TOTAL-AS-REC-READ          PIC S9(7)   COMP-3  VALUE +0.
00062  77  TOTAL-BA-WRITTEN           PIC S9(7)   COMP-3  VALUE +0.
00063  77  WS-TXCD-CNT                PIC S9(7)   COMP-3  VALUE +0.
00064  77  AS-EOF                     PIC X               VALUE 'N'.
00065      88 EOF-AS                                      VALUE 'Y'.
00066  77  BA-EOF                     PIC X               VALUE 'N'.
00067      88 EOF-BA                                      VALUE 'Y'.
00068  77  ERR-SW                     PIC X               VALUE 'N'.
00069      88 ERR                                         VALUE 'Y'.
00070      SKIP2
00071  01  WORK-AREAS.
00072      05 AS-PREVIOUS-KEY1           PIC X(18)         VALUE SPACES.
00073      05 AS-CURRENT-KEY1.
00074         10 AS-CK1-VOL              PIC 9(3).
00075         10 AS-CK1-PROP             PIC 9(15).
00076      05 BA-PREVIOUS-KEY2           PIC X(18)         VALUE SPACES.
00077      05 BA-CURRENT-KEY2.
00078         10 BA-CK2-VOL              PIC 9(3).
00079         10 BA-CK2-PROP             PIC 9(15).
00080      EJECT
00081  PROCEDURE DIVISION.
00082      OPEN INPUT MASTER-IN, BA-MASTER
00083           OUTPUT MASTER-OUT
00084      PERFORM 030-READ-MAST2 THRU 030-EXIT
00085         PERFORM 040-READ-MAST THRU 040-EXIT
00086         PERFORM 010-MAIN-ROUTINE THRU 010-EXIT
00087         UNTIL EOF-BA OR ERR.
00088      IF ERR
00089         MOVE 16 TO RETURN-CODE.
00090      DISPLAY ' TOTAL BA RECORDS READ '    TOTAL-BA-REC-READ
00091      DISPLAY ' TOTAL AS RECORDS READ '    TOTAL-AS-REC-READ
00092      DISPLAY ' TOTAL BA RECORDS WRITTEN ' TOTAL-BA-WRITTEN
00093      DISPLAY ' TOTAL TXCDS UPDATED      ' WS-TXCD-CNT
00094      CLOSE MASTER-IN, BA-MASTER, MASTER-OUT
00095      STOP RUN.
00096      SKIP2
00097  010-MAIN-ROUTINE.
00098      IF AS-CURRENT-KEY1 EQUAL TO BA-CURRENT-KEY2
00099         IF M-TXCD OF MSTR-IN-REC = M-TXCD OF BA-MSTR-REC
00100            CONTINUE
00101         ELSE
00102            MOVE M-TXCD OF MSTR-IN-REC TO M-TXCD OF BA-MSTR-REC
00103            ADD +1 TO WS-TXCD-CNT
00104         END-IF
00105         PERFORM 060-WRITE-MSTR-OUT-REC THRU 060-EXIT
00106         PERFORM 030-READ-MAST2 THRU 030-EXIT
00107         PERFORM 040-READ-MAST THRU 040-EXIT
00108      ELSE
00109         IF AS-CURRENT-KEY1 LESS THAN BA-CURRENT-KEY2
00110            PERFORM 040-READ-MAST THRU 040-EXIT
00111         ELSE
00112              PERFORM 060-WRITE-MSTR-OUT-REC THRU 060-EXIT
00113              PERFORM 030-READ-MAST2 THRU 030-EXIT.
00114  010-EXIT. EXIT.
00115      SKIP2
00116  030-READ-MAST2.
00117      READ BA-MASTER AT END
00118         MOVE 'Y' TO BA-EOF
00119         MOVE HIGH-VALUES TO BA-CURRENT-KEY2.
00120      IF NOT EOF-BA
00121         ADD 1 TO TOTAL-BA-REC-READ
00122         MOVE M-VOL OF BA-MSTR-REC TO BA-CK2-VOL
00123         MOVE M-PROP OF BA-MSTR-REC TO BA-CK2-PROP
00124         IF BA-CURRENT-KEY2 GREATER THAN BA-PREVIOUS-KEY2
00125            MOVE BA-CURRENT-KEY2 TO BA-PREVIOUS-KEY2
00126         ELSE
00127            DISPLAY ' BA MASTER FILE IS OUT OF SEQUENCE '
00128            DISPLAY ' CURRENT KEY IS ' BA-CURRENT-KEY2
00129            DISPLAY ' PREVIOUS KEY IS ' BA-PREVIOUS-KEY2
00130            MOVE 'Y' TO ERR-SW.
00131  030-EXIT. EXIT.
00132      SKIP2
00133  040-READ-MAST.
00134      READ MASTER-IN AT END
00135         MOVE 'Y' TO AS-EOF
00136         MOVE HIGH-VALUES TO AS-CURRENT-KEY1.
00137      IF NOT EOF-AS
00138         ADD 1 TO TOTAL-AS-REC-READ
00139         MOVE M-VOL OF MSTR-IN-REC TO AS-CK1-VOL
00140         MOVE M-PROP OF MSTR-IN-REC TO AS-CK1-PROP.
00141         IF AS-CURRENT-KEY1 GREATER THAN AS-PREVIOUS-KEY1
00142            MOVE AS-CURRENT-KEY1 TO AS-PREVIOUS-KEY1
00143         ELSE
00144            DISPLAY ' AS-MASTER FILE IS OUT OF SEQUENCE '
00145            DISPLAY ' CURRENT KEY IS ' AS-CURRENT-KEY1
00146            DISPLAY ' PREVIOUS KEY IS ' AS-PREVIOUS-KEY1
00147            MOVE 'Y' TO ERR-SW.
00148  040-EXIT. EXIT.
00149      SKIP2
00150  060-WRITE-MSTR-OUT-REC.
00151      MOVE M-REC-CTRS-1 TO MO-REC-CTRS
00152      MOVE BA-MSTR-REC TO MSTR-OUT-REC
00153      WRITE MSTR-OUT-REC
00154      ADD +1 TO TOTAL-BA-WRITTEN.
00155  060-EXIT. EXIT.
