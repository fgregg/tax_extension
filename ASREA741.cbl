00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID.    ASREA741.
00003  AUTHOR.        GOLD - MOLIS.
00004  DATE-WRITTEN.  JULY 23, 1992.
00005  SKIP1
00006 *REMARKS. THE PURPOSE OF THIS PROGRAM IS TO CREATE THE FROZEN
00007 *         VALUE FILE FOR NON-QUAD TOWNS.
00008  SKIP2
00009  ENVIRONMENT DIVISION.
00010  CONFIGURATION SECTION.
00011  SOURCE-COMPUTER. IBM-370.
00012  OBJECT-COMPUTER. IBM-370.
00013  SKIP1
00014  INPUT-OUTPUT SECTION.
00015  FILE-CONTROL.
00016      SELECT MASTER-IN     ASSIGN TO UT-S-MASTNEW.
00017      SELECT MASTER-IN-BA  ASSIGN TO UT-S-MASTOLD.
00018      SELECT FRZVALFL      ASSIGN TO DA-FRZVALFL
00019             ORGANIZATION IS INDEXED
00020             ACCESS IS RANDOM
00021             RECORD KEY IS FV-KEY
00022             FILE STATUS IS FILE-STATUS
00023                            FILE-STATUS-2.
00024      SELECT PRINT-FILE    ASSIGN TO UT-S-PRINTFLE.
00025      SELECT PRT-ERR-FILE  ASSIGN TO UT-S-PRINTERR.
00026      SELECT PRT-DTL-FILE  ASSIGN TO UT-S-PRINTDTL.
00027      SELECT OUTPUT-FILE   ASSIGN TO UT-S-OUTPUTF.
00028  SKIP1
00029  DATA DIVISION.
00030  FILE SECTION.
00031  SKIP1
00032  COPY ASREASFD06.
00033  SKIP1
00034  FD  MASTER-IN-BA
00035      BLOCK CONTAINS 0 CHARACTERS
00036      RECORD CONTAINS 122 TO 18706 CHARACTERS
00037      LABEL RECORDS ARE STANDARD
00038      RECORDING MODE IS S
00039      DATA RECORD IS MSTR-BA-REC.
00040  01  MSTR-BA-REC.
00041  COPY ASREASRD05.
00042  SKIP1
00043  COPY ASFRZVALF1.
00044  SKIP1
00045  COPY PRINTFILE.
00046  SKIP1
00047  COPY PRINTERRFL.
00048  SKIP1
00049  COPY PRINTDTLFL.
00050  FD  OUTPUT-FILE
00051      BLOCK CONTAINS 0 CHARACTERS
00052      RECORD CONTAINS 130 CHARACTERS
00053      LABEL RECORDS ARE STANDARD
00054      RECORDING MODE IS F
00055      DATA RECORD IS OUTPUT-REC.
00056
00057  01  OUTPUT-REC.
00058      05  OUT-PROP        PIC 9(15).
00059      05  OUT-PROP-IMPV   PIC 9(13).
00060      05  OUT-PROP-CURR-288  PIC 9(13).
00061      05  FILLER          PIC X(89).
00062  WORKING-STORAGE SECTION.
00063      SKIP1
00064  77  NEW-EXEMPT-SW      PIC X         VALUE '1'.
00065      88  NEW-EXEMPT                   VALUE '1'.
00066      88  NOT-NEW-EXEMPT               VALUE '0'.
00067  77  OLD-EXEMPT-SW      PIC X         VALUE '1'.
00068      88  OLD-EXEMPT                   VALUE '1'.
00069      88  NOT-OLD-EXEMPT               VALUE '0'.
00070  77  OLD-MAST-RECS      PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00071      88  FIRST-REC-OLD                VALUE +1.
00072  77  NEW-MAST-RECS      PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00073      88  FIRST-REC-NEW                VALUE +1.
00074  77  WS-OUT-CNT         PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00075  77  RECS-UPD           PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00076  77  RECS-ADD           PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00077  77  RECS-WRIT-OLD-1    PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00078  77  RECS-WRIT-OLD-2    PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00079  77  RECS-WRIT-NEW-1    PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00080  77  RECS-WRIT-NEW-2    PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00081  77  RECS-WRIT-3        PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00082  77  WS-P1              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00083  77  WS-P2              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00084  77  WS-P3              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00085  77  WS-P4              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00086  77  WS-P5              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00087  77  WS-P6              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00088  77  WS-P7              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00089  77  WS-P8              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00090  77  WS-C1              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00091  77  WS-C2              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00092  77  WS-C3              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00093  77  WS-C4              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00094  77  WS-C5              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00095  77  WS-C6              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00096  77  WS-C7              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00097  77  WS-C8              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00098  77  WS-C9              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00099  77  WS-C10             PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00100  77  WS-P1P2            PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00101  77  WS-C1C2C5          PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00102  77  WS-C7-C5-C10       PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00103  77  WS-P3-P2           PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00104  77  WS-C2-P2           PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00105  77  WS-C3-C2           PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00106 *NOTE:  LINE-CNTR IS ACTUALLY NUMBER OF RECORDS PRINTED ON
00107 *       REPORT.  ONLY 2 PER REPORT 1 OR 2 (ONE FOR 3).
00108  77  LINE-CNTR          PIC S9(3)     VALUE +2  PACKED-DECIMAL.
00109  77  PAGE-CNTR          PIC S9(5)     VALUE +0  PACKED-DECIMAL.
00110  77  LINE-CNTR-2        PIC S9(3)     VALUE +2  PACKED-DECIMAL.
00111  77  PAGE-CNTR-2        PIC S9(5)     VALUE +0  PACKED-DECIMAL.
00112  77  PAGE-CNTR-3        PIC S9(5)     VALUE +0  PACKED-DECIMAL.
00113  77  SUB                PIC S9(4)     VALUE +0  BINARY.
00114  77  SUBO               PIC S9(4)     VALUE +0  BINARY.
00115  77  SUBD               PIC S9(4)     VALUE +0  BINARY.
00116  77  SUBV               PIC S9(4)     VALUE +0  BINARY.
00117  77  SUBT               PIC S9(4)     VALUE +0  BINARY.
00118  77  TYPE4-SUM          PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00119  77  TYPE4-SUM2         PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00120  77  TYPE5-SUM          PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00121  77  BLANK-LINE         PIC X         VALUE SPACE.
00122      SKIP1
00123  77  NEW-EOF-SW         PIC X         VALUE 'N'.
00124      88  NEW-EOF                      VALUE 'Y'.
00125  77  OLD-EOF-SW         PIC X         VALUE 'N'.
00126      88  OLD-EOF                      VALUE 'Y'.
00127  77  ERR-SW             PIC X         VALUE 'N'.
00128      88  ERR                          VALUE 'Y'.
00129  77  FRZVAL-FILE-FND-SW PIC X         VALUE 'N'.
00130      88  FRZVAL-FILE-FND              VALUE 'F'.
00131  77  PRINT-ERR-MSG-SW   PIC X         VALUE 'N'.
00132      88  PRINT-ERR-MSG                VALUE 'Y'.
00133  77  PRINT-REP-2-SW     PIC X         VALUE 'N'.
00134      88  PRINT-REP-2                  VALUE 'Y'.
00135  77  WS-288-IND-SW      PIC X         VALUE '0'.
00136      88  WS-288-IND-1                 VALUE '1'.
00137  77  WS-CHG-ACT-SW      PIC X         VALUE 'N'.
00138      88  WS-CHG-ACT-Y                 VALUE 'Y'.
00139  77  WS-AGE-CHECK       PIC 999.
00140      88  WS-AGE-1                     VALUE 001.
00141      88  WS-AGE-2-999                 VALUE 002 THRU 999.
00142  77  WS-CLS-CHECK       PIC 999.
00143      88  WS-CLS-288                   VALUE 288.
00144      SKIP1
00145  01  WORK-AREA.
00146      SKIP1
00147      05  FILE-STATUS        PIC 99.
00148          88  VALID-STATUS                 VALUE 00.
00149          88  STREPT-NOTFND                VALUE 23.
00150      05  FILE-STATUS-2      BINARY.
00151          10  FILE-RETURN    PIC 99        VALUE 0.
00152          10  FILE-FUNCTION  PIC 9         VALUE 0.
00153          10  FILE-FEEDBACK  PIC 999       VALUE 0.
00154           SKIP1
00155      05  CURR-NEW-KEY.
00156          10  CNEW-DIV    PIC 9(14)    VALUE ZEROES.
00157          10  CNEW-PROP   PIC 9(15).
00158      05  PREV-NEW-KEY    PIC X(29)    VALUE LOW-VALUES.
00159      SKIP1
00160      05  SAVE-NEW-DIV    PIC 9(14)    VALUE ZEROES.
00161      05  SAVE-NEW-PROP   PIC 9(15)    VALUE ZEROES.
00162      SKIP1
00163      05  CURR-OLD-KEY.
00164          10  COLD-DIV    PIC 9(14)    VALUE ZEROES.
00165          10  COLD-PROP   PIC 9(15).
00166      05  PREV-OLD-KEY    PIC X(29)    VALUE LOW-VALUES.
00167      SKIP1
00168      05  SAVE-OLD-DIV    PIC 9(14)    VALUE ZEROES.
00169      05  SAVE-OLD-PROP   PIC 9(15)    VALUE ZEROES.
00170      SKIP1
00171      05  UNPK-DIVNO      PIC 9(14).
00172      05  FILLER REDEFINES UNPK-DIVNO.
00173          10  UNPK-DIV4   PIC 9999.
00174          10  FILLER      PIC 9(10).
00175      SKIP1
00176      05  DSP-DATE.
00177          10  DSP-MO                 PIC 99.
00178          10  FILLER                 PIC X         VALUE '/'.
00179          10  DSP-DA                 PIC 99.
00180          10  FILLER                 PIC X         VALUE '/'.
00181          10  DSP-YR                 PIC 99.
00182      05  ACPT-DATE                  PIC 9(6).
00183      05  ACPT-DATE-X REDEFINES ACPT-DATE.
00184          10  ACPT-YR                PIC 99.
00185          10  ACPT-MO                PIC 99.
00186          10  ACPT-DA                PIC 99.
00187      SKIP1
00188      05  HEADING1.
00189          10  FILLER          PIC X(45)   VALUE '  ASREA741-1'.
00190          10  FILLER          PIC X(70)   VALUE
00191              'OFFICE OF THE COOK COUNTY ASSESSOR'.
00192          10  FILLER          PIC X(5)    VALUE 'PAGE'.
00193          10  HD1-PAGE        PIC ZZZ,ZZ9.
00194      SKIP1
00195      05  HEADING1-2.
00196          10  FILLER          PIC X(45)   VALUE '  ASREA741-2'.
00197          10  FILLER          PIC X(70)   VALUE
00198              'OFFICE OF THE COOK COUNTY ASSESSOR'.
00199          10  FILLER          PIC X(5)    VALUE 'PAGE'.
00200          10  HD1-PAGE-2      PIC ZZZ,ZZ9.
00201      SKIP1
00202      05  HEADING1-3.
00203          10  FILLER          PIC X(45)   VALUE '  ASREA741-3'.
00204          10  FILLER          PIC X(70)   VALUE
00205              'OFFICE OF THE COOK COUNTY ASSESSOR'.
00206          10  FILLER          PIC X(5)    VALUE 'PAGE'.
00207          10  HD1-PAGE-3      PIC ZZZ,ZZ9.
00208      SKIP1
00209      05  HEADING2.
00210          10  FILLER          PIC X(7)    VALUE '  DATE'.
00211          10  HD2-DATE        PIC X(8).
00212          10  FILLER          PIC X(18)   VALUE SPACES.
00213          10  FILLER          PIC X(28)   VALUE
00214              'CREATE FROZEN VALUE FILE -'.
00215          10  FILLER          PIC X(35)   VALUE
00216              'DETAIL OF NON-REASSESSMENT TOWNSHIP'.
00217      SKIP1
00218      05  HEADING2-2.
00219          10  FILLER          PIC X(7)    VALUE '  DATE'.
00220          10  HD2-DATE-2      PIC X(8).
00221          10  FILLER          PIC X(15)   VALUE SPACES.
00222          10  FILLER          PIC X(28)   VALUE
00223              'CREATE FROZEN VALUE FILE -'.
00224          10  FILLER          PIC X(41)   VALUE
00225              'ERROR REPORT OF NON-REASSESSMENT TOWNSHIP'.
00226      SKIP1
00227      05  HEADING2-3.
00228          10  FILLER          PIC X(7)    VALUE '  DATE'.
00229          10  HD2-DATE-3      PIC X(8).
00230          10  FILLER          PIC X(11)   VALUE SPACES.
00231          10  FILLER          PIC X(28)   VALUE
00232              'CREATE FROZEN VALUE FILE -'.
00233          10  FILLER          PIC X(51)   VALUE
00234          'DIVISION NO. SUMMARY - NON-REASSESSMENT TOWNSHIP'.
00235      SKIP1
00236      05  HEADING3.
00237          10  FILLER          PIC XX.
00238          10  HD3-MSG         PIC X(30)   VALUE SPACES.
00239      SKIP1
00240      05  HEADING3-3                      VALUE SPACES.
00241          10  FILLER          PIC XX.
00242          10  H3-MSG          PIC X(22).
00243          10  H3-1            PIC X(22).
00244          10  H3-2            PIC X(22).
00245          10  H3-3            PIC X(22).
00246          10  H3-4            PIC X(22).
00247          10  H3-5            PIC X(22).
00248      SKIP1
00249      05  HEADING4.
00250          10  FILLER          PIC X(39)   VALUE '  DIVISION'.
00251          10  FILLER          PIC X(46)   VALUE
00252              'P1 - OVER 1 YR OLD        P2 - 1 YR OLD'.
00253          10  FILLER          PIC X(46)   VALUE
00254              'P3 - 1 YR OLD             P4 - DIFF'.
00255      SKIP1
00256      05  HEADING5.
00257          10  FILLER          PIC X(43)   VALUE
00258              '    NUMBER        PROPERTY NUMBER'.
00259          10  FILLER          PIC X(42)   VALUE
00260              'A/V ACTUAL             A/V ACTUAL'.
00261          10  FILLER          PIC X(35)   VALUE
00262              'FULL-TYPE 2-5              1 YR OLD'.
00263      SKIP1
00264      05  HEADING6.
00265          10  FILLER          PIC X(39)   VALUE SPACES.
00266          10  FILLER          PIC X(46)   VALUE
00267              'P5 - TOTAL LAND           P6 - TOTAL IMPV'.
00268          10  FILLER          PIC X(46)   VALUE
00269              'P7 - TOTAL                P8 - PARCEL'.
00270      SKIP1
00271      05  HEADING7.
00272          10  FILLER          PIC X(44)   VALUE SPACES.
00273          10  FILLER          PIC X(46)   VALUE
00274              'VALUATION                 VALUATION'.
00275          10  FILLER          PIC X(46)   VALUE
00276              'VALUATION                 COUNT'.
00277      SKIP1
00278      05  HEADING8.
00279          10  FILLER          PIC X(39)   VALUE '  DIVISION'.
00280          10  FILLER          PIC X(46)   VALUE
00281              'C1 - OVER 1 YR OLD        C2 - 1 YR OLD'.
00282          10  FILLER          PIC X(46)   VALUE
00283              'C3 - 1 YR OLD             C4 - DIFF'.
00284      SKIP1
00285      05  HEADING10.
00286          10  FILLER          PIC X(39)   VALUE SPACES.
00287          10  FILLER          PIC X(46)   VALUE
00288              'C5 - CURR YR 288 WITH     C6 - TOTAL LAND'.
00289          10  FILLER          PIC X(46)   VALUE
00290              'C7 - TOTAL IMPV           C8 - TOTAL'.
00291      SKIP1
00292      05  HEADING11.
00293          10  FILLER          PIC X(44).
00294          10  FILLER          PIC X(47)   VALUE
00295              'PRMT OVER 30,000          VALUATION'.
00296          10  FILLER          PIC X(46)   VALUE
00297              'VALUATION               VALUATION'.
00298      SKIP1
00299      05  HEADING12.
00300          10  FILLER          PIC X(39).
00301          10  FILLER          PIC X(46)   VALUE
00302              'C9 - PARCEL           C10 - DIFF OF 1 YEAR'.
00303      SKIP1
00304      05  HEADING13.
00305          10  FILLER          PIC X(44).
00306          10  FILLER          PIC X(46)   VALUE
00307              'COUNT                  PRIOR/CURRENT'.
00308      SKIP1
00309      05  DETAIL-LINE-1                   VALUE SPACES.
00310          10  FILLER          PIC X.
00311          10  D-DIVNO-1       PIC 9(14).
00312          10  FILLER          PIC X.
00313          10  D-PROP-1        PIC 99,99,999,999,9999.
00314          10  FILLER          PIC XXX.
00315          10  D-P1VAL-1       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
00316          10  FILLER          PIC X(6).
00317          10  D-P2VAL-1       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
00318          10  FILLER          PIC XXXX.
00319          10  D-P3VAL-1       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
00320          10  FILLER          PIC X(6).
00321          10  D-P4VAL-1       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
00322          SKIP1
00323      05  DETAIL-LINE-2                   VALUE SPACES.
00324          10  FILLER          PIC X(37).
00325          10  D-P5VAL-2       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
00326          10  FILLER          PIC X(6).
00327          10  D-P6VAL-2       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
00328          10  FILLER          PIC XXXX.
00329          10  D-P7VAL-2       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
00330          10  FILLER          PIC X(6).
00331          10  D-P8VAL-2       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
00332          SKIP1
00333      05  DETAIL-LINE-3                   VALUE SPACES.
00334          10  FILLER          PIC X(37).
00335          10  D-C9VAL-3       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
00336          10  FILLER          PIC X(6).
00337          10  D-C10VAL-3      PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
00338          SKIP1
00339      05  DETAIL-LINE-1-3                 VALUE SPACES.
00340          10  FILLER          PIC XXX.
00341          10  D1-DIV          PIC 9(14).
00342          10  FILLER          PIC X(6).
00343          10  D1-VAL3-TABLE OCCURS 5 TIMES.
00344              15 D1-FILLER    PIC X(4).
00345              15 D1-VAL-3     PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
00346          SKIP1
00347      05  ERR-MSG-LINE.
00348          10  FILLER          PIC X(28).
00349          10  E-MSG           PIC X(63) VALUE
00350          '******  ACCUMULATED A/V NOT EQUAL TO TOTAL A/V  ******'.
00351         EJECT
00352  LINKAGE SECTION.
00353      SKIP1
00354  01  LINK-INFO.
00355      05  LINK-LENGTH         PIC S9(4)   BINARY.
00356          88  VAL-L-LENGTH                VALUE +2.
00357      05  LINK-PARM.
00358          10 LINK-CURRYR-N    PIC 99.
00359      SKIP2
00360  PROCEDURE DIVISION USING LINK-INFO.
00361      SKIP1
00362  010-HOUSEKEEPING.
00363      IF NOT VAL-L-LENGTH
00364         DISPLAY 'INVALID LINKAGE LENGTH'
00365         MOVE 16 TO RETURN-CODE
00366         STOP RUN.
00367      SKIP1
00368      IF LINK-PARM NOT NUMERIC
00369         DISPLAY 'CURRENT YEAR 288 LINKAGE VALUE MUST BE A 2 POSITI
00370 -               'ON NUMERIC VALUE ' LINK-PARM
00371         MOVE 16 TO RETURN-CODE
00372         STOP RUN.
00373      SKIP1
00374      OPEN I-O FRZVALFL.
00375      IF NOT VALID-STATUS
00376         DISPLAY 'VSAM OPEN FROZEN VALUE FILE INVALID'
00377         DISPLAY 'FILE-STATUS = ' FILE-STATUS
00378         DISPLAY 'FILE-STATUS FEEDBACK = ' FILE-RETURN ' '
00379                  FILE-FUNCTION ' ' FILE-FEEDBACK
00380         MOVE 16 TO RETURN-CODE
00381         STOP RUN.
00382      SKIP1
00383      OPEN INPUT  MASTER-IN
00384                  MASTER-IN-BA
00385           OUTPUT PRINT-FILE
00386                  PRT-ERR-FILE
00387                  OUTPUT-FILE
00388                  PRT-DTL-FILE.
00389      SKIP1
00390      ACCEPT ACPT-DATE FROM DATE.
00391      MOVE ACPT-MO TO DSP-MO.
00392      MOVE ACPT-DA TO DSP-DA.
00393      MOVE ACPT-YR TO DSP-YR.
00394      MOVE DSP-DATE  TO HD2-DATE HD2-DATE-2 HD2-DATE-3.
00395      SKIP1
00396      WRITE PRINT-REC   FROM BLANK-LINE AFTER ADVANCING PAGE.
00397      WRITE PRT-DTL-REC FROM BLANK-LINE AFTER ADVANCING PAGE.
00398      WRITE PRT-ERR-REC FROM BLANK-LINE AFTER ADVANCING PAGE.
00399      SKIP1
00400      PERFORM 030-READ-ACCUM-NEW-MAST UNTIL SAVE-NEW-DIV NOT EQUAL
00401              TO CNEW-DIV.
00402      SKIP1
00403      PERFORM 040-READ-ACCUM-OLD-MAST UNTIL SAVE-OLD-DIV NOT EQUAL
00404              TO COLD-DIV.
00405      SKIP1
00406      PERFORM 020-MAINLINE UNTIL ERR OR (NEW-EOF AND OLD-EOF).
00407      SKIP1
00408      IF NOT ERR
00409         IF OLD-MAST-RECS > NEW-MAST-RECS
00410            NEXT SENTENCE
00411         ELSE
00412            IF SAVE-NEW-DIV = SAVE-OLD-DIV
00413               PERFORM 050-ACCUM-WRITE-CHECK
00414               PERFORM 060-ACCESS-FROZVAL-REPORT-RTN
00415            ELSE
00416               PERFORM 055-ACCUM-WRITE-CHECK-NEW
00417               PERFORM 060-ACCESS-FROZVAL-REPORT-RTN.
00418      SKIP1
00419      DISPLAY 'TOTAL MASTER OLD RECORDS READ
00420 -            ' ' OLD-MAST-RECS.
00421      DISPLAY 'TOTAL MASTER NEW RECORDS READ
00422 -            ' ' NEW-MAST-RECS.
00423      DISPLAY 'TOTAL MASTER OLD RECORDS WRITTEN TO OUTPUT REPORT 1
00424 -            ' ' RECS-WRIT-OLD-1.
00425      DISPLAY 'TOTAL MASTER NEW RECORDS WRITTEN TO OUTPUT REPORT 1
00426 -            ' ' RECS-WRIT-NEW-1.
00427      DISPLAY 'TOTAL MASTER OLD RECORDS WRITTEN TO OUTPUT REPORT 2
00428 -            ' ' RECS-WRIT-OLD-2.
00429      DISPLAY 'TOTAL MASTER NEW RECORDS WRITTEN TO OUTPUT REPORT 2
00430 -            ' ' RECS-WRIT-NEW-2.
00431      DISPLAY 'TOTAL MASTER RECORDS WRITTEN TO OUTPUT REPORT 3
00432 -            ' ' RECS-WRIT-3.
00433      DISPLAY 'TOTAL FROZEN VALUE RECORDS UPDATED
00434 -            ' ' RECS-UPD.
00435      DISPLAY 'TOTAL FROZEN VALUE RECORDS WRITTEN
00436 -            ' ' RECS-ADD.
00437      DISPLAY 'TOTAL OUTPUT FILE RECORDS WRITTEN
00438 -            ' ' WS-OUT-CNT.
00439      SKIP1
00440      CLOSE MASTER-IN
00441            MASTER-IN-BA
00442            PRINT-FILE
00443            PRT-ERR-FILE
00444            PRT-DTL-FILE
00445            OUTPUT-FILE
00446            FRZVALFL.
00447      IF NOT VALID-STATUS
00448         DISPLAY 'VSAM CLOSE FROZEN VALUE FILE INVALID'
00449         DISPLAY 'FILE-STATUS = ' FILE-STATUS
00450         DISPLAY 'FILE-STATUS FEEDBACK = ' FILE-RETURN ' '
00451                  FILE-FUNCTION ' ' FILE-FEEDBACK
00452         MOVE 16 TO RETURN-CODE.
00453      SKIP1
00454      STOP RUN.
00455      SKIP2
00456  020-MAINLINE.
00457      SKIP1
00458      IF NOT (ERR OR (NEW-EOF AND OLD-EOF))
00459         IF SAVE-NEW-DIV = SAVE-OLD-DIV
00460            PERFORM 050-ACCUM-WRITE-CHECK
00461            PERFORM 060-ACCESS-FROZVAL-REPORT-RTN
00462            PERFORM 023-ZERO-NEW
00463            PERFORM 031-ACCUM-NEW-MAST
00464            MOVE CNEW-DIV TO SAVE-NEW-DIV
00465            MOVE M-PROP   TO SAVE-NEW-PROP
00466            PERFORM 030-READ-ACCUM-NEW-MAST UNTIL SAVE-NEW-DIV
00467                    NOT EQUAL TO CNEW-DIV OR NEW-EOF
00468            PERFORM 025-ZERO-OLD
00469            PERFORM 041-ACCUM-OLD-MAST
00470            MOVE COLD-DIV TO SAVE-OLD-DIV
00471            MOVE MA-PROP  TO SAVE-OLD-PROP
00472            PERFORM 040-READ-ACCUM-OLD-MAST UNTIL SAVE-OLD-DIV
00473                    NOT EQUAL TO COLD-DIV OR OLD-EOF
00474         ELSE
00475            IF SAVE-NEW-DIV > SAVE-OLD-DIV
00476               PERFORM 025-ZERO-OLD
00477               PERFORM 041-ACCUM-OLD-MAST
00478               MOVE COLD-DIV TO SAVE-OLD-DIV
00479               MOVE MA-PROP  TO SAVE-OLD-PROP
00480               PERFORM 040-READ-ACCUM-OLD-MAST UNTIL SAVE-OLD-DIV
00481                       NOT EQUAL TO COLD-DIV OR OLD-EOF
00482            ELSE
00483               PERFORM 055-ACCUM-WRITE-CHECK-NEW
00484               PERFORM 060-ACCESS-FROZVAL-REPORT-RTN
00485               PERFORM 023-ZERO-NEW
00486               PERFORM 031-ACCUM-NEW-MAST
00487               MOVE CNEW-DIV TO SAVE-NEW-DIV
00488               MOVE M-PROP   TO SAVE-NEW-PROP
00489               PERFORM 030-READ-ACCUM-NEW-MAST UNTIL SAVE-NEW-DIV
00490                       NOT EQUAL TO CNEW-DIV OR NEW-EOF.
00491      SKIP2
00492  023-ZERO-NEW.
00493      MOVE '1' TO NEW-EXEMPT-SW
00494      MOVE '0' TO WS-288-IND-SW.
00495      MOVE 'N' TO WS-CHG-ACT-SW.
00496      MOVE ZEROES TO WS-C1 WS-C2 WS-C3 WS-C4 WS-C5 WS-C6 WS-C7
00497                     WS-C8 WS-C9 WS-C10.
00498      SKIP2
00499  025-ZERO-OLD.
00500      MOVE '1' TO OLD-EXEMPT-SW
00501      MOVE ZEROES TO WS-P1 WS-P2 WS-P3 WS-P4 WS-P5 WS-P6 WS-P7
00502                     WS-P8.
00503      SKIP2
00504  030-READ-ACCUM-NEW-MAST.
00505      PERFORM 035-READ-NEW-MAST.
00506      SKIP1
00507      IF NOT (ERR OR NEW-EOF)
00508         IF SAVE-NEW-DIV EQUAL CNEW-DIV
00509            PERFORM 031-ACCUM-NEW-MAST.
00510      SKIP2
00511  031-ACCUM-NEW-MAST.
00512      ADD +1 TO WS-C9.
00513      ADD M-VALUE (5) TO WS-C6.
00514      ADD M-VALUE (6) TO WS-C7.
00515      ADD M-VALUE (7) TO WS-C8.
00516      SKIP1
00517      IF M-PERMIND = 2 OR M-PERMIND = 3
00518         MOVE 'Y' TO WS-CHG-ACT-SW.
00519      IF NOT M-EXEMPT
00520          MOVE '0' TO NEW-EXEMPT-SW
00521      END-IF
00522      SKIP1
00523      IF M-DETAIL-PRESENT
00524         PERFORM 037-READ-NEW-DETAIL VARYING SUB FROM 1 BY 1
00525                 UNTIL SUB > M-DTL-QST-CTR.
00526      SKIP2
00527  035-READ-NEW-MAST.
00528      READ MASTER-IN
00529          AT END
00530             MOVE ALL '9' TO CURR-NEW-KEY
00531             MOVE 'Y'     TO NEW-EOF-SW.
00532              SKIP1
00533      IF NOT NEW-EOF
00534         ADD +1              TO NEW-MAST-RECS
00535         MOVE M-DIVNO-PROP   TO CNEW-DIV
00536         MOVE M-PROP         TO CNEW-PROP
00537         IF CURR-NEW-KEY NOT GREATER PREV-NEW-KEY
00538            DISPLAY 'NEW BA MAST FILE OUT OF SEQUENCE'
00539            DISPLAY 'CURRENT  KEY IS ' CURR-NEW-KEY
00540            DISPLAY 'PREVIOUS KEY IS ' PREV-NEW-KEY
00541            MOVE 16  TO RETURN-CODE
00542            MOVE 'Y' TO ERR-SW
00543         ELSE
00544            MOVE CURR-NEW-KEY TO PREV-NEW-KEY
00545            IF FIRST-REC-NEW
00546               MOVE M-DIVNO-PROP TO SAVE-NEW-DIV
00547               MOVE M-PROP       TO SAVE-NEW-PROP.
00548      SKIP2
00549  037-READ-NEW-DETAIL.
00550      IF D2-TYPE2-5 (SUB)
00551         MOVE D2-AGE (SUB) TO WS-AGE-CHECK
00552         MOVE D2-CLS (SUB) TO WS-CLS-CHECK
00553         IF WS-AGE-2-999
00554            ADD D2-VAL (SUB) TO WS-C1
00555         ELSE
00556            IF WS-AGE-1 AND NOT WS-CLS-288
00557               ADD D2-VAL (SUB) TO WS-C2.
00558      SKIP1
00559      IF D2-TYPE2-5 (SUB)
00560         IF (D2-TYPE2 (SUB) OR D3-TYPE3 (SUB)) AND
00561            (WS-AGE-1 AND NOT WS-CLS-288)
00562            ADD D2-VAL (SUB) TO WS-C3.
00563      SKIP1
00564      IF D2-TYPE2-5 (SUB)
00565         IF D3-TYPE3 (SUB)
00566            IF WS-AGE-1 AND WS-CLS-288 AND
00567               (D3-YR (SUB) NOT EQUAL LINK-CURRYR-N)
00568               ADD D3-VAL (SUB) TO WS-C1
00569            ELSE
00570               IF WS-CLS-288 AND (D3-YR (SUB) = LINK-CURRYR-N)
00571                  AND (M-PERMIND = '2')
00572                  ADD D3-VAL (SUB) TO WS-C5
00573                  MOVE '1' TO WS-288-IND-SW
00574               ELSE
00575                  IF WS-CLS-288 AND (D3-YR (SUB) = LINK-CURRYR-N)
00576                     AND (M-PERMIND NOT EQUAL '2')
00577                     ADD D3-VAL (SUB) TO WS-C1
00578                     IF M-PERMIND = '3'
00579                        MOVE '1' TO WS-288-IND-SW
00580                     ELSE
00581                        NEXT SENTENCE
00582                  ELSE
00583                     NEXT SENTENCE
00584         ELSE
00585            IF D4-TYPE4 (SUB) AND WS-AGE-1
00586               COMPUTE TYPE4-SUM =
00587                       (D4-TOTVAL (SUB) * D4-COND (SUB)) / 100
00588               IF D4-PCASSD (SUB) > 0
00589                  COMPUTE TYPE4-SUM2 =
00590                          (TYPE4-SUM * D4-PCASSD (SUB)) / 100
00591               ELSE
00592                  MOVE TYPE4-SUM TO TYPE4-SUM2
00593               END-IF
00594               ADD TYPE4-SUM2 TO WS-C3
00595            ELSE
00596               IF D5-TYPE5 (SUB) AND WS-AGE-1
00597                  IF D5-OCCFAC (SUB) > 0
00598                     COMPUTE TYPE4-SUM = (D5-REPCST (SUB) * 100) /
00599                                          D5-OCCFAC (SUB)
00600                  ELSE
00601                     MOVE D5-REPCST (SUB) TO TYPE4-SUM
00602                  END-IF
00603                  COMPUTE TYPE4-SUM2 =
00604                          (TYPE4-SUM * D5-COND (SUB)) / 100
00605                  IF D5-PCASSD (SUB) > 0
00606                     COMPUTE TYPE5-SUM =
00607                             (TYPE4-SUM2 * D5-PCASSD (SUB)) / 100
00608                  ELSE
00609                     MOVE TYPE4-SUM2 TO TYPE5-SUM
00610                  END-IF
00611                  ADD TYPE5-SUM TO WS-C3.
00612      SKIP1
00613      IF D2-QUES-CLASS (SUB)
00614         ADD +1 TO SUB.
00615      SKIP2
00616  040-READ-ACCUM-OLD-MAST.
00617      PERFORM 045-READ-OLD-MAST.
00618      SKIP1
00619      IF NOT (ERR OR OLD-EOF)
00620         IF SAVE-OLD-DIV EQUAL COLD-DIV
00621            PERFORM 041-ACCUM-OLD-MAST.
00622      SKIP2
00623  041-ACCUM-OLD-MAST.
00624      ADD +1 TO WS-P8.
00625      ADD MA-VALUE-1 (5) TO WS-P5.
00626      ADD MA-VALUE-1 (6) TO WS-P6.
00627      ADD MA-VALUE-1 (7) TO WS-P7.
00628      IF NOT MA-EXEMPT
00629          MOVE '0' TO OLD-EXEMPT-SW
00630      END-IF
00631      IF MA-DETAIL-PRESENT
00632         PERFORM 047-READ-OLD-DETAIL VARYING SUBO FROM 1 BY 1
00633                 UNTIL SUBO > MA-DTL-QST-CTR-1.
00634      SKIP2
00635  045-READ-OLD-MAST.
00636      READ MASTER-IN-BA
00637          AT END
00638             MOVE ALL '9' TO CURR-OLD-KEY
00639             MOVE 'Y'     TO OLD-EOF-SW.
00640              SKIP1
00641      IF NOT OLD-EOF
00642         ADD +1               TO OLD-MAST-RECS
00643         MOVE MA-DIVNO-PROP   TO COLD-DIV
00644         MOVE MA-PROP         TO COLD-PROP
00645         IF CURR-OLD-KEY NOT GREATER PREV-OLD-KEY
00646            DISPLAY 'OLD BA MAST FILE OUT OF SEQUENCE'
00647            DISPLAY 'CURRENT  KEY IS ' CURR-OLD-KEY
00648            DISPLAY 'PREVIOUS KEY IS ' PREV-OLD-KEY
00649            MOVE 16  TO RETURN-CODE
00650            MOVE 'Y' TO ERR-SW
00651         ELSE
00652            MOVE CURR-OLD-KEY TO PREV-OLD-KEY
00653            IF FIRST-REC-OLD
00654               MOVE MA-DIVNO-PROP TO SAVE-OLD-DIV
00655               MOVE MA-PROP       TO SAVE-OLD-PROP.
00656      SKIP2
00657  047-READ-OLD-DETAIL.
00658      IF D2M-TYPE2-5 (SUBO)
00659         MOVE D2M-AGE (SUBO) TO WS-AGE-CHECK
00660         MOVE D2M-CLS (SUBO) TO WS-CLS-CHECK
00661         IF WS-AGE-2-999
00662            ADD D2M-VAL (SUBO) TO WS-P1
00663         ELSE
00664            IF WS-AGE-1 AND NOT WS-CLS-288
00665               ADD D2M-VAL (SUBO) TO WS-P2
00666            ELSE
00667               IF WS-AGE-1 AND WS-CLS-288
00668                  ADD D2M-VAL (SUBO) TO WS-P1.
00669      SKIP1
00670      IF D2M-TYPE2-5 (SUBO)
00671         IF D2M-TYPE2 (SUBO) OR D3M-TYPE3 (SUBO) AND WS-AGE-1 AND
00672            NOT WS-CLS-288
00673            ADD D2M-VAL (SUBO) TO WS-P3
00674         ELSE
00675            IF D4M-TYPE4 (SUBO) AND WS-AGE-1
00676               COMPUTE TYPE4-SUM =
00677                       (D4M-TOTVAL (SUBO) * D4M-COND (SUBO)) / 100
00678               IF D4M-PCASSD (SUBO) > 0
00679                 COMPUTE TYPE4-SUM2 =
00680                         (TYPE4-SUM * D4M-PCASSD (SUBO)) / 100
00681               ELSE
00682                  MOVE TYPE4-SUM TO TYPE4-SUM2
00683               END-IF
00684               ADD TYPE4-SUM2 TO WS-P3
00685            ELSE
00686               IF D5M-TYPE5 (SUBO) AND WS-AGE-1
00687                  IF D5M-OCCFAC (SUBO) > 0
00688                    COMPUTE TYPE4-SUM = (D5M-REPCST (SUBO) * 100) /
00689                                          D5M-OCCFAC (SUBO)
00690                  ELSE
00691                     MOVE D5M-REPCST (SUBO) TO TYPE4-SUM
00692                  END-IF
00693                  COMPUTE TYPE4-SUM2 =
00694                          (TYPE4-SUM * D5M-COND (SUBO)) / 100
00695                  IF D5M-PCASSD (SUBO) > 0
00696                     COMPUTE TYPE5-SUM =
00697                             (TYPE4-SUM2 * D5M-PCASSD (SUBO)) / 100
00698                  ELSE
00699                     MOVE TYPE4-SUM2 TO TYPE5-SUM
00700                  END-IF
00701                  ADD TYPE5-SUM TO WS-P3.
00702      SKIP1
00703      IF D2M-QUES-CLASS (SUBO)
00704         ADD +1 TO SUBO.
00705      SKIP2
00706  050-ACCUM-WRITE-CHECK.
00707      MOVE 'N' TO PRINT-ERR-MSG-SW PRINT-REP-2-SW.
00708      SKIP1
00709      COMPUTE WS-P3-P2 = WS-P3 - WS-P2.
00710      IF WS-P3-P2 > 0
00711         ADD WS-P3-P2 TO WS-P4
00712      ELSE
00713         CONTINUE
00714      END-IF
00715      COMPUTE WS-C3-C2 = WS-C3 - WS-C2
00716      IF WS-C3-C2 > 0
00717         ADD WS-C3-C2 TO WS-C4
00718      ELSE
00719         CONTINUE
00720      END-IF
00721      COMPUTE WS-C2-P2 = WS-C2 - WS-P2
00722      IF WS-C2-P2 > 0
00723         ADD WS-C2-P2 TO WS-C10
00724      ELSE
00725         CONTINUE
00726      END-IF
00727      COMPUTE WS-P1P2 = WS-P1 + WS-P2
00728      IF WS-P1P2 = WS-P6
00729         CONTINUE
00730      ELSE
00731         MOVE 'Y' TO PRINT-ERR-MSG-SW
00732         MOVE 'Y' TO PRINT-REP-2-SW
00733         PERFORM 120-MOVE-DETAIL-REPORT-1-OLD
00734      END-IF
00735      MOVE 'N' TO PRINT-REP-2-SW
00736      PERFORM 120-MOVE-DETAIL-REPORT-1-OLD
00737      MOVE 'N' TO PRINT-ERR-MSG-SW
00738      COMPUTE WS-C1C2C5 = WS-C1 + WS-C2 + WS-C5
00739      IF WS-C1C2C5 = WS-C7
00740         CONTINUE
00741      ELSE
00742         MOVE 'Y' TO PRINT-ERR-MSG-SW
00743         MOVE 'Y' TO PRINT-REP-2-SW
00744         PERFORM 110-MOVE-DETAIL-REPORT-1-NEW
00745      END-IF
00746      MOVE 'N' TO PRINT-REP-2-SW
00747      PERFORM 110-MOVE-DETAIL-REPORT-1-NEW.
00748      SKIP2
00749  055-ACCUM-WRITE-CHECK-NEW.
00750      MOVE 'N' TO PRINT-ERR-MSG-SW PRINT-REP-2-SW.
00751      SKIP1
00752      MOVE 'NO PRIOR RECORD (NO OLD)' TO HD3-MSG.
00753      PERFORM 140-PRT-DET-HEAD-OLD.
00754      SKIP1
00755      MOVE WS-C2 TO WS-C10.
00756      COMPUTE WS-C3-C2 = WS-C3 - WS-C2.
00757      IF WS-C3-C2 > 0
00758         ADD WS-C3-C2 TO WS-C4
00759      ELSE
00760         CONTINUE
00761      END-IF
00762      COMPUTE WS-C1C2C5 = WS-C1 + WS-C2 + WS-C5
00763      IF WS-C1C2C5 = WS-C7
00764         CONTINUE
00765      ELSE
00766         MOVE 'Y' TO PRINT-ERR-MSG-SW
00767         MOVE 'Y' TO PRINT-REP-2-SW
00768         PERFORM 110-MOVE-DETAIL-REPORT-1-NEW
00769      END-IF
00770      MOVE 'N' TO PRINT-REP-2-SW
00771      PERFORM 110-MOVE-DETAIL-REPORT-1-NEW.
00772      SKIP2
00773  060-ACCESS-FROZVAL-REPORT-RTN.
00774      PERFORM 070-RAND-READ-FROZEN-VAL-FILE.
00775      IF NOT ERR
00776         IF FRZVAL-FILE-FND
00777            PERFORM 080-PREPARE-FROZEN-VAL
00778            PERFORM 130-WRITE-DETAIL-REPORT-3
00779            PERFORM 090-REWRITE-FRZVAL-FILE
00780         ELSE
00781            PERFORM 075-PREPARE-WRITE-FROZEN-VAL
00782            PERFORM 080-PREPARE-FROZEN-VAL
00783            IF FV-PROP-IMP-VAL = 0 AND FV-PROP-EXPR-288-VAL = 0 AND
00784               FV-PROP-CURR-288-VAL = 0 AND FV-PROP-TOT-VAL = 0 AND
00785               FV-PROP-ACT-VAL = 0
00786               NEXT SENTENCE
00787            ELSE
00788               PERFORM 130-WRITE-DETAIL-REPORT-3
00789               PERFORM 100-WRITE-FRZVAL-FILE.
00790      SKIP2
00791  070-RAND-READ-FROZEN-VAL-FILE.
00792      MOVE SAVE-NEW-DIV TO FV-DIVNO.
00793      SKIP1
00794      READ FRZVALFL.
00795      IF VALID-STATUS
00796         MOVE 'F' TO FRZVAL-FILE-FND-SW
00797      ELSE
00798         IF STREPT-NOTFND
00799            MOVE 'N' TO FRZVAL-FILE-FND-SW
00800         ELSE
00801            DISPLAY 'VSAM READ FOR FROZEN VALUE FILE INVALID'
00802            DISPLAY 'FILE-STATUS = ' FILE-STATUS
00803            DISPLAY 'FILE-STATUS FEEDBACK = ' FILE-RETURN ' '
00804                     FILE-FUNCTION ' ' FILE-FEEDBACK
00805            MOVE 'Y' TO ERR-SW
00806            MOVE 16 TO RETURN-CODE.
00807      SKIP2
00808  075-PREPARE-WRITE-FROZEN-VAL.
00809      MOVE SAVE-NEW-DIV TO FV-DIVNO.
00810      SKIP1
00811      PERFORM 200-INITIAL-VALUES VARYING SUBT FROM 1 BY 1 UNTIL
00812              SUBT > 29.
00813         SKIP2
00814  080-PREPARE-FROZEN-VAL.
00815      ADD WS-P5   TO FV-PRIOR-LND-VAL.
00816      ADD WS-P6   TO FV-PRIOR-IMP-VAL.
00817      ADD WS-P7   TO FV-PRIOR-TOT-VAL.
00818      ADD WS-P8   TO FV-PRIOR-PCL.
00819      ADD WS-C6   TO FV-CURR-LND-VAL.
00820      ADD WS-C7   TO FV-CURR-IMP-VAL.
00821      ADD WS-C8   TO FV-CURR-TOT-VAL
00822      ADD WS-C9   TO FV-CURR-PCL.
00823      IF OLD-EXEMPT AND NOT-NEW-EXEMPT
00824          ADD WS-C8 TO FV-PROP-IMP-VAL
00825          ADD WS-C8 TO FV-PROP-TOT-VAL
00826      ELSE
00827          ADD WS-C5   TO FV-PROP-CURR-288-VAL FV-PROP-TOT-VAL
00828          ADD WS-C10  TO FV-PROP-IMP-VAL FV-PROP-TOT-VAL
00829          IF (WS-C5 GREATER ZERO) OR (WS-C10 GREATER ZERO)
00830             ADD WS-P5  TO FV-CA-PR-LND-VAL FV-CA-PR-TOT-VAL
00831             ADD WS-C6  TO FV-CA-CR-LND-VAL FV-CA-CR-TOT-VAL
00832             COMPUTE WS-C7-C5-C10 = WS-C7 - WS-C5 - WS-C10
00833             ADD  WS-C7-C5-C10 TO FV-CA-CR-IMP-VAL
00834                 FV-CA-CR-TOT-VAL
00835             ADD WS-P6  TO FV-CA-PR-IMP-VAL FV-CA-PR-TOT-VAL
00836          ELSE
00837             IF WS-288-IND-1 OR WS-CHG-ACT-Y
00838                PERFORM 085-ADD-RTN
00839             ELSE
00840                IF UNPK-DIV4 = ZEROES
00841                   PERFORM 085-ADD-RTN
00842               ELSE
00843                  ADD WS-P5  TO FV-WOCA-PR-LND-VAL
00844                  ADD WS-P6  TO FV-WOCA-PR-IMP-VAL
00845                  ADD WS-P7  TO FV-WOCA-PR-TOT-VAL
00846                  ADD WS-P8  TO FV-WOCA-PR-PCL
00847                  ADD WS-C6  TO FV-WOCA-CR-LND-VAL
00848                  ADD WS-C7  TO FV-WOCA-CR-IMP-VAL
00849                  ADD WS-C8  TO FV-WOCA-CR-TOT-VAL
00850                  ADD WS-C9  TO FV-WOCA-CR-PCL.
00851      SKIP1
00852  085-ADD-RTN.
00853      ADD WS-P5  TO FV-CA-PR-LND-VAL.
00854      ADD WS-P6  TO FV-CA-PR-IMP-VAL.
00855      ADD WS-P7  TO FV-CA-PR-TOT-VAL.
00856      ADD WS-P8  TO FV-CA-PR-PCL.
00857      ADD WS-C6  TO FV-CA-CR-LND-VAL.
00858      ADD WS-C7  TO FV-CA-CR-IMP-VAL.
00859      ADD WS-C8  TO FV-CA-CR-TOT-VAL.
00860      ADD WS-C9  TO FV-CA-CR-PCL.
00861         SKIP2
00862  090-REWRITE-FRZVAL-FILE.
00863      REWRITE FRZVALREC.
00864      IF VALID-STATUS
00865         ADD +1 TO RECS-UPD
00866      ELSE
00867         DISPLAY 'VSAM REWRITE FOR FROZEN VALUE FILE INVALID'
00868         DISPLAY 'FILE-STATUS = ' FILE-STATUS
00869         DISPLAY 'FILE-STATUS FEEDBACK = ' FILE-RETURN ' '
00870                  FILE-FUNCTION ' ' FILE-FEEDBACK
00871         MOVE 'Y' TO ERR-SW
00872         MOVE 16 TO RETURN-CODE.
00873      SKIP2
00874  100-WRITE-FRZVAL-FILE.
00875      WRITE FRZVALREC.
00876      IF VALID-STATUS
00877         ADD +1 TO RECS-ADD
00878      ELSE
00879         DISPLAY 'VSAM WRITE FOR FROZEN VALUE FILE INVALID'
00880         DISPLAY 'FILE-STATUS = ' FILE-STATUS
00881         DISPLAY 'FILE-STATUS FEEDBACK = ' FILE-RETURN ' '
00882                  FILE-FUNCTION ' ' FILE-FEEDBACK
00883         MOVE 'Y' TO ERR-SW
00884         MOVE 16 TO RETURN-CODE.
00885      SKIP2
00886  110-MOVE-DETAIL-REPORT-1-NEW.
00887      MOVE SAVE-NEW-DIV   TO D-DIVNO-1.
00888      MOVE SAVE-NEW-PROP  TO D-PROP-1.
00889      INSPECT D-PROP-1 REPLACING ALL ',' BY '-'.
00890      MOVE WS-C1          TO D-P1VAL-1.
00891      MOVE WS-C2          TO D-P2VAL-1.
00892      MOVE WS-C3          TO D-P3VAL-1.
00893      MOVE WS-C4          TO D-P4VAL-1.
00894      MOVE WS-C5          TO D-P5VAL-2.
00895      MOVE WS-C6          TO D-P6VAL-2.
00896      MOVE WS-C7          TO D-P7VAL-2.
00897      MOVE WS-C8          TO D-P8VAL-2.
00898      MOVE WS-C9          TO D-C9VAL-3.
00899      MOVE WS-C10         TO D-C10VAL-3.
00900      MOVE 'CURRENT RECORD (MASTER NEW)' TO HD3-MSG.
00901      IF WS-C10 > 0
00902         INITIALIZE OUTPUT-REC
00903         MOVE SAVE-NEW-PROP  TO  OUT-PROP
00904         MOVE WS-C10         TO  OUT-PROP-IMPV
00905         MOVE WS-C5          TO  OUT-PROP-CURR-288
00906         ADD +1 TO WS-OUT-CNT
00907         WRITE OUTPUT-REC
00908      END-IF
00909      SKIP1
00910      IF PRINT-REP-2
00911         PERFORM 160-PRT-DET-HEAD-2-NEW
00912      ELSE
00913         PERFORM 155-PRT-DET-HEAD-NEW.
00914      SKIP2
00915  120-MOVE-DETAIL-REPORT-1-OLD.
00916      MOVE SAVE-OLD-DIV    TO D-DIVNO-1.
00917      MOVE SAVE-OLD-PROP   TO D-PROP-1.
00918      INSPECT D-PROP-1 REPLACING ALL ',' BY '-'.
00919      MOVE WS-P1          TO D-P1VAL-1.
00920      MOVE WS-P2          TO D-P2VAL-1.
00921      MOVE WS-P3          TO D-P3VAL-1.
00922      MOVE WS-P4          TO D-P4VAL-1.
00923      MOVE WS-P5          TO D-P5VAL-2.
00924      MOVE WS-P6          TO D-P6VAL-2.
00925      MOVE WS-P7          TO D-P7VAL-2.
00926      MOVE WS-P8          TO D-P8VAL-2.
00927      MOVE 'PRIOR  RECORD  (MASTER OLD)' TO HD3-MSG.
00928      SKIP1
00929      IF PRINT-REP-2
00930         PERFORM 150-PRT-DET-HEAD-2-OLD
00931      ELSE
00932         PERFORM 140-PRT-DET-HEAD-OLD.
00933      SKIP2
00934  130-WRITE-DETAIL-REPORT-3.
00935      PERFORM 190-HEADING-RTN-3.
00936      SKIP1
00937      MOVE +0 TO SUBD.
00938      MOVE SPACES TO DETAIL-LINE-1-3.
00939      MOVE FV-DIVNO TO D1-DIV.
00940      SKIP1
00941      PERFORM 300-INITIAL-VALUES-DET VARYING SUBT FROM 1 BY 1
00942              UNTIL SUBT > 5.
00943      PERFORM 135-MOVE-VALUES VARYING SUBV FROM 1 BY 1 UNTIL
00944              SUBV > 5.
00945      WRITE PRT-DTL-REC FROM DETAIL-LINE-1-3 AFTER ADVANCING 2.
00946      SKIP1
00947      MOVE ' CURR IMPV VALUE' TO H3-1.
00948      MOVE 'CURR TOTAL VALUE' TO H3-2.
00949      MOVE ' CURR PCL COUNT ' TO H3-3.
00950      MOVE 'PROP ASSD 1ST IMP' TO H3-4.
00951      MOVE ' AS 1ST EXPR 288' TO H3-5.
00952      WRITE PRT-DTL-REC FROM HEADING3-3 AFTER ADVANCING 3.
00953      WRITE PRT-DTL-REC FROM BLANK-LINE AFTER ADVANCING 1.
00954      MOVE SPACES TO HEADING3-3.
00955      SKIP1
00956      MOVE +0 TO SUBD.
00957      MOVE SPACES TO DETAIL-LINE-1-3.
00958      PERFORM 300-INITIAL-VALUES-DET VARYING SUBT FROM 1 BY 1
00959              UNTIL SUBT > 5.
00960      PERFORM 135-MOVE-VALUES VARYING SUBV FROM 6 BY 1 UNTIL
00961              SUBV > 10.
00962      WRITE PRT-DTL-REC FROM DETAIL-LINE-1-3 AFTER ADVANCING 2.
00963      SKIP1
00964      MOVE ' AS 1ST CURR 288' TO H3-1.
00965      MOVE 'PROP ASSD 1ST TOT' TO H3-2.
00966      MOVE 'ASSD 1ST ACT TOT' TO H3-3.
00967      MOVE 'W/CHG ACT PR LAND' TO H3-4.
00968      MOVE 'W/CHG ACT PR IMPV' TO H3-5.
00969      WRITE PRT-DTL-REC FROM HEADING3-3 AFTER ADVANCING 3.
00970      WRITE PRT-DTL-REC FROM BLANK-LINE AFTER ADVANCING 1.
00971      MOVE SPACES TO HEADING3-3.
00972      SKIP1
00973      MOVE +0 TO SUBD.
00974      MOVE SPACES TO DETAIL-LINE-1-3.
00975      PERFORM 300-INITIAL-VALUES-DET VARYING SUBT FROM 1 BY 1
00976              UNTIL SUBT > 5.
00977      PERFORM 135-MOVE-VALUES VARYING SUBV FROM 11 BY 1 UNTIL
00978              SUBV > 15.
00979      WRITE PRT-DTL-REC FROM DETAIL-LINE-1-3 AFTER ADVANCING 2.
00980      SKIP1
00981      MOVE 'W/CHG ACT PR TOT' TO H3-1.
00982      MOVE 'W/CHG ACT PR CNT' TO H3-2.
00983      MOVE 'W/CHG ACT CR LAND' TO H3-3.
00984      MOVE 'W/CHG ACT CR IMPV' TO H3-4.
00985      MOVE 'W/CHG ACT CR TOT ' TO H3-5.
00986      WRITE PRT-DTL-REC FROM HEADING3-3 AFTER ADVANCING 3.
00987      WRITE PRT-DTL-REC FROM BLANK-LINE AFTER ADVANCING 1.
00988      MOVE SPACES TO HEADING3-3.
00989      SKIP1
00990      MOVE +0 TO SUBD.
00991      MOVE SPACES TO DETAIL-LINE-1-3.
00992      PERFORM 300-INITIAL-VALUES-DET VARYING SUBT FROM 1 BY 1
00993              UNTIL SUBT > 5.
00994      PERFORM 135-MOVE-VALUES VARYING SUBV FROM 16 BY 1 UNTIL
00995              SUBV > 20.
00996      WRITE PRT-DTL-REC FROM DETAIL-LINE-1-3 AFTER ADVANCING 2.
00997      SKIP1
00998      MOVE 'W/CHG ACT CR CNT' TO H3-1.
00999      MOVE 'W/O ACT PR LAND' TO H3-2.
01000      MOVE 'W/O ACT PR IMPV' TO H3-3.
01001      MOVE 'W/O ACT PR TOTAL' TO H3-4.
01002      MOVE 'W/O ACT PR CNT' TO H3-5.
01003      WRITE PRT-DTL-REC FROM HEADING3-3 AFTER ADVANCING 3.
01004      WRITE PRT-DTL-REC FROM BLANK-LINE AFTER ADVANCING 1.
01005      MOVE SPACES TO HEADING3-3.
01006      SKIP1
01007      MOVE +0 TO SUBD.
01008      MOVE SPACES TO DETAIL-LINE-1-3.
01009      PERFORM 300-INITIAL-VALUES-DET VARYING SUBT FROM 1 BY 1
01010              UNTIL SUBT > 5.
01011      PERFORM 135-MOVE-VALUES VARYING SUBV FROM 21 BY 1 UNTIL
01012              SUBV > 25.
01013      WRITE PRT-DTL-REC FROM DETAIL-LINE-1-3 AFTER ADVANCING 2.
01014      SKIP1
01015      MOVE 'W/O ACT CR LAND' TO H3-1.
01016      MOVE 'W/O ACT CR IMPV' TO H3-2.
01017      MOVE 'W/O ACT CR TOT ' TO H3-3.
01018      MOVE 'W/O ACT CR CNT  ' TO H3-4.
01019      WRITE PRT-DTL-REC FROM HEADING3-3 AFTER ADVANCING 3.
01020      WRITE PRT-DTL-REC FROM BLANK-LINE AFTER ADVANCING 1.
01021      MOVE SPACES TO HEADING3-3.
01022      SKIP1
01023      MOVE +0 TO SUBD.
01024      MOVE SPACES TO DETAIL-LINE-1-3.
01025      PERFORM 300-INITIAL-VALUES-DET VARYING SUBT FROM 1 BY 1
01026              UNTIL SUBT > 4.
01027      PERFORM 135-MOVE-VALUES VARYING SUBV FROM 26 BY 1 UNTIL
01028              SUBV > 29.
01029      WRITE PRT-DTL-REC FROM DETAIL-LINE-1-3 AFTER ADVANCING 2.
01030      SKIP1
01031      ADD +1  TO RECS-WRIT-3.
01032      SKIP2
01033  135-MOVE-VALUES.
01034      ADD +1 TO SUBD.
01035      MOVE FV-VAL (SUBV) TO D1-VAL-3 (SUBD).
01036      SKIP2
01037  140-PRT-DET-HEAD-OLD.
01038      IF LINE-CNTR = +2
01039         PERFORM 170-HEADING-RTN.
01040         SKIP1
01041      WRITE PRINT-REC FROM HEADING3      AFTER ADVANCING 3.
01042      WRITE PRINT-REC FROM HEADING4      AFTER ADVANCING 2.
01043      WRITE PRINT-REC FROM HEADING5      AFTER ADVANCING 1.
01044      WRITE PRINT-REC FROM DETAIL-LINE-1 AFTER ADVANCING 2.
01045      WRITE PRINT-REC FROM HEADING6      AFTER ADVANCING 3.
01046      WRITE PRINT-REC FROM HEADING7      AFTER ADVANCING 1.
01047      WRITE PRINT-REC FROM DETAIL-LINE-2 AFTER ADVANCING 2.
01048      MOVE SPACES TO DETAIL-LINE-1 DETAIL-LINE-2.
01049      IF PRINT-ERR-MSG
01050         WRITE PRINT-REC FROM ERR-MSG-LINE AFTER ADVANCING 3.
01051         SKIP1
01052      ADD +1  TO RECS-WRIT-OLD-1.
01053      ADD +1  TO LINE-CNTR.
01054      SKIP2
01055  150-PRT-DET-HEAD-2-OLD.
01056      IF LINE-CNTR-2 = +2
01057         PERFORM 180-HEADING-RTN-2.
01058         SKIP1
01059      WRITE PRT-ERR-REC FROM HEADING3      AFTER ADVANCING 3.
01060      WRITE PRT-ERR-REC FROM HEADING4      AFTER ADVANCING 2.
01061      WRITE PRT-ERR-REC FROM HEADING5      AFTER ADVANCING 1.
01062      WRITE PRT-ERR-REC FROM DETAIL-LINE-1 AFTER ADVANCING 2.
01063      WRITE PRT-ERR-REC FROM HEADING6      AFTER ADVANCING 3.
01064      WRITE PRT-ERR-REC FROM HEADING7      AFTER ADVANCING 1.
01065      WRITE PRT-ERR-REC FROM DETAIL-LINE-2 AFTER ADVANCING 2.
01066      MOVE SPACES TO DETAIL-LINE-1 DETAIL-LINE-2.
01067      IF PRINT-ERR-MSG
01068         WRITE PRT-ERR-REC FROM ERR-MSG-LINE AFTER ADVANCING 3.
01069         SKIP1
01070      ADD +1  TO RECS-WRIT-OLD-2.
01071      ADD +1  TO LINE-CNTR-2.
01072      SKIP2
01073  155-PRT-DET-HEAD-NEW.
01074      IF LINE-CNTR = +2
01075         PERFORM 170-HEADING-RTN.
01076         SKIP1
01077      WRITE PRINT-REC FROM HEADING3      AFTER ADVANCING 3.
01078      WRITE PRINT-REC FROM HEADING8      AFTER ADVANCING 2.
01079      WRITE PRINT-REC FROM HEADING5      AFTER ADVANCING 1.
01080      WRITE PRINT-REC FROM DETAIL-LINE-1 AFTER ADVANCING 2.
01081      WRITE PRINT-REC FROM HEADING10     AFTER ADVANCING 3.
01082      WRITE PRINT-REC FROM HEADING11     AFTER ADVANCING 1.
01083      WRITE PRINT-REC FROM DETAIL-LINE-2 AFTER ADVANCING 2.
01084      WRITE PRINT-REC FROM HEADING12     AFTER ADVANCING 3.
01085      WRITE PRINT-REC FROM HEADING13     AFTER ADVANCING 1.
01086      WRITE PRINT-REC FROM DETAIL-LINE-3 AFTER ADVANCING 2.
01087      MOVE SPACES TO DETAIL-LINE-1 DETAIL-LINE-2 DETAIL-LINE-3.
01088      IF PRINT-ERR-MSG
01089         WRITE PRINT-REC FROM ERR-MSG-LINE AFTER ADVANCING 3.
01090         SKIP1
01091      ADD +1  TO RECS-WRIT-NEW-1.
01092      ADD +1  TO LINE-CNTR.
01093      SKIP2
01094  160-PRT-DET-HEAD-2-NEW.
01095      IF LINE-CNTR-2 = +2
01096         PERFORM 180-HEADING-RTN-2.
01097         SKIP1
01098      WRITE PRT-ERR-REC FROM HEADING3      AFTER ADVANCING 3.
01099      WRITE PRT-ERR-REC FROM HEADING8      AFTER ADVANCING 2.
01100      WRITE PRT-ERR-REC FROM HEADING5      AFTER ADVANCING 1.
01101      WRITE PRT-ERR-REC FROM DETAIL-LINE-1 AFTER ADVANCING 2.
01102      WRITE PRT-ERR-REC FROM HEADING10     AFTER ADVANCING 3.
01103      WRITE PRT-ERR-REC FROM HEADING11     AFTER ADVANCING 1.
01104      WRITE PRT-ERR-REC FROM DETAIL-LINE-2 AFTER ADVANCING 2.
01105      WRITE PRT-ERR-REC FROM HEADING12     AFTER ADVANCING 3.
01106      WRITE PRT-ERR-REC FROM HEADING13     AFTER ADVANCING 1.
01107      WRITE PRT-ERR-REC FROM DETAIL-LINE-3 AFTER ADVANCING 2.
01108      MOVE SPACES TO DETAIL-LINE-1 DETAIL-LINE-2 DETAIL-LINE-3.
01109      IF PRINT-ERR-MSG
01110         WRITE PRT-ERR-REC FROM ERR-MSG-LINE AFTER ADVANCING 3.
01111         SKIP1
01112      ADD +1  TO RECS-WRIT-NEW-2.
01113      ADD +1  TO LINE-CNTR-2.
01114      SKIP2
01115  170-HEADING-RTN.
01116      ADD +1 TO PAGE-CNTR.
01117      MOVE PAGE-CNTR TO HD1-PAGE.
01118      WRITE PRINT-REC FROM HEADING1 AFTER ADVANCING PAGE.
01119      WRITE PRINT-REC FROM HEADING2 AFTER ADVANCING 2.
01120      MOVE +0 TO LINE-CNTR.
01121      SKIP2
01122  180-HEADING-RTN-2.
01123      ADD +1 TO PAGE-CNTR-2.
01124      MOVE PAGE-CNTR-2 TO HD1-PAGE-2.
01125      WRITE PRT-ERR-REC FROM HEADING1-2 AFTER ADVANCING PAGE.
01126      WRITE PRT-ERR-REC FROM HEADING2-2 AFTER ADVANCING 2.
01127      MOVE +0 TO LINE-CNTR-2.
01128      SKIP2
01129  190-HEADING-RTN-3.
01130      ADD +1 TO PAGE-CNTR-3.
01131      MOVE PAGE-CNTR-3 TO HD1-PAGE-3.
01132      WRITE PRT-DTL-REC FROM HEADING1-3 AFTER ADVANCING PAGE.
01133      WRITE PRT-DTL-REC FROM HEADING2-3 AFTER ADVANCING 2.
01134      MOVE 'DIVISION NUMBER'  TO H3-MSG.
01135      MOVE 'PRIOR LAND VALUE' TO H3-1.
01136      MOVE 'PRIOR IMPV VALUE' TO H3-2.
01137      MOVE ' PRIOR TOT VALUE' TO H3-3.
01138      MOVE ' PRIOR PCL COUNT' TO H3-4.
01139      MOVE ' CURR LAND COUNT' TO H3-5.
01140      WRITE PRT-DTL-REC FROM BLANK-LINE AFTER ADVANCING 1.
01141      WRITE PRT-DTL-REC FROM HEADING3-3 AFTER ADVANCING 3.
01142      WRITE PRT-DTL-REC FROM BLANK-LINE AFTER ADVANCING 1.
01143      MOVE SPACES TO HEADING3-3.
01144      SKIP2
01145  200-INITIAL-VALUES.
01146      MOVE ZEROES TO FV-VAL (SUBT).
01147      SKIP2
01148  300-INITIAL-VALUES-DET.
01149      MOVE ZEROES TO D1-VAL-3 (SUBT).
01150      MOVE SPACES TO D1-FILLER (SUBT).
