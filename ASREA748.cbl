00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASREA748.
00003 *AUTHOR. RUGIS - MOLIS.
00004 *DATE-WRITTEN. AUGUST 12, 1992.
00005 *REMARKS. THE PURPOSE OF THIS PROGRAM IS TO COMPUTE THE
00006 *         FROZEN EQUALIZED VALUES AND TAX AMOUNTS.
00007  SKIP2
00008  ENVIRONMENT DIVISION.
00009  CONFIGURATION SECTION.
00010  SOURCE-COMPUTER. IBM-370.
00011  OBJECT-COMPUTER. IBM-370.
00012  SKIP1
00013  INPUT-OUTPUT SECTION.
00014  FILE-CONTROL.
00015      SELECT FRZDIV-PRCNT-FILE ASSIGN UT-S-FRZVALIN.
00016      SELECT TAXCD-MSTR-FILE ASSIGN DA-TXCDMSTR
00017          ORGANIZATION IS INDEXED
00018          ACCESS  MODE IS RANDOM
00019          RECORD  KEY  IS TXCD-TAX-CODE
00020          FILE  STATUS IS FILE-STATUS.
00021      SELECT FRZAGCY-FILE  ASSIGN UT-S-FRZAGCY.
00022  SKIP1
00023  DATA DIVISION.
00024  FILE SECTION.
00025  SKIP1
00026  COPY CLFZDVPCF1.
00027  SKIP1
00028  COPY TXCDMSTRF2.
00029  SKIP1
00030  COPY CLFRZAGYF1.
00031  SKIP3
00032  WORKING-STORAGE SECTION.
00033      SKIP1
00034  77  RECS-IN-CNTR       PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00035      88  FIRST-REC      VALUE +1.
00036  77  RECS-OUT-CNTR      PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00037  77  CURR-288-VAL       PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00038  77  EXPR-288-VAL       PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00039  77  1ST-TIME-VAL       PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00040  77  EXP-INC-VAL        PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00041  77  TOT-ASSD-VAL       PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00042  77  SUB                PIC S9(4)     VALUE +0  BINARY.
00043  77  SV-TXCD            PIC 9(5).
00044      SKIP1
00045  77  FRZ-EOF-SW         PIC X         VALUE 'N'.
00046      88  FRZ-EOF                      VALUE 'Y'.
00047  77  ERR-SW             PIC X         VALUE 'N'.
00048      88  ERR                          VALUE 'Y'.
00049      SKIP1
00050  77  FILE-STATUS        PIC 99.
00051      88  GOOD-STATUS                  VALUE 00.
00052      88  REC-NOTFND                   VALUE 23.
00053      SKIP3
00054  01  WORK-AREA.
00055      SKIP1
00056      05  FILE-STATUS2                 BINARY.
00057          10  FS-RETURN   PIC 99       VALUE 0.
00058          10  FS-FUNCTION PIC 9        VALUE 0.
00059          10  FS-FEEDBACK PIC 999      VALUE 0.
00060      SKIP1
00061      05  CURR-FRZ-KEY.
00062          10  CF-DIVNO    PIC 9(15).
00063          10  CF-TXCD     PIC 9(5).
00064      SKIP1
00065      05  PREV-FRZ-KEY    PIC X(20)    VALUE LOW-VALUES.
00066      SKIP3
00067  LINKAGE SECTION.
00068      SKIP1
00069  01  LINK-INFO.
00070      05  LINK-LENGTH         PIC S9(4)   COMP.
00071          88  VALID-LK-LENGTH VALUE +5.
00072      SKIP1
00073      05  LINK-EQFCTR.
00074          10  LINK-EQFCTR-N   PIC 9V9(4).
00075      EJECT
00076  PROCEDURE DIVISION USING LINK-INFO.
00077      SKIP1
00078  010-HOUSEKEEPING.
00079      SKIP1
00080      IF NOT VALID-LK-LENGTH
00081          DISPLAY 'LINKAGE LENGTH INVALID OR MISSING'
00082          MOVE 16 TO RETURN-CODE
00083          STOP RUN.
00084      SKIP1
00085      IF LINK-EQFCTR NUMERIC
00086              AND
00087         LINK-EQFCTR GREATER ZERO
00088           NEXT SENTENCE
00089      ELSE
00090          DISPLAY 'LINKAGE EQFACTOR INVALID ' LINK-EQFCTR
00091          MOVE 16 TO RETURN-CODE
00092          STOP RUN.
00093      SKIP1
00094      OPEN INPUT TAXCD-MSTR-FILE.
00095      IF NOT GOOD-STATUS
00096          DISPLAY 'INVALID OPEN ON TXCD MSTR FILE'
00097          DISPLAY 'FILE STATUS IS ' FILE-STATUS
00098          DISPLAY 'FEEDBACK STATUS IS ' FS-RETURN ' '
00099                    FS-FUNCTION ' ' FS-FEEDBACK
00100          MOVE 16 TO RETURN-CODE
00101          STOP RUN.
00102      SKIP1
00103      OPEN INPUT FRZDIV-PRCNT-FILE
00104          OUTPUT FRZAGCY-FILE.
00105      SKIP1
00106      PERFORM 020-MAINLINE
00107         UNTIL FRZ-EOF OR ERR.
00108      SKIP1
00109      IF NOT ERR
00110          PERFORM 040-ACCESS-TAXCD-MSTR-FILE
00111          IF NOT ERR
00112              PERFORM 050-BUILD-AGCY-OUT-REC
00113                VARYING SUB FROM 1 BY 1
00114                UNTIL SUB GREATER 40.
00115      SKIP1
00116      DISPLAY SPACES
00117      DISPLAY 'TOTAL RECORDS READ...: ' RECS-IN-CNTR.
00118      DISPLAY 'TOTAL RECORDS WRITTEN: ' RECS-OUT-CNTR.
00119      SKIP1
00120      CLOSE TAXCD-MSTR-FILE.
00121      IF NOT GOOD-STATUS
00122          DISPLAY 'INVALID CLOSE ON TXCD MSTR FILE'
00123          DISPLAY 'FILE STATUS IS ' FILE-STATUS
00124          DISPLAY 'FEEDBACK STATUS IS ' FS-RETURN ' '
00125                    FS-FUNCTION ' ' FS-FEEDBACK
00126          MOVE 16 TO RETURN-CODE.
00127      SKIP1
00128      CLOSE FRZDIV-PRCNT-FILE  FRZAGCY-FILE.
00129      STOP RUN.
00130      SKIP3
00131  020-MAINLINE.
00132      PERFORM 030-READ-FROZEN.
00133      IF NOT (FRZ-EOF OR ERR)
00134          IF CF-TXCD EQUAL SV-TXCD
00135              ADD FDP-CURR-288-VAL TO CURR-288-VAL
00136                                      TOT-ASSD-VAL
00137              ADD FDP-EXPR-288-VAL TO EXPR-288-VAL
00138                                      TOT-ASSD-VAL
00139              ADD FDP-FRST-VAL TO 1ST-TIME-VAL
00140                                  TOT-ASSD-VAL
00141              ADD FDP-EXP-INC  TO EXP-INC-VAL
00142           ELSE
00143              PERFORM 040-ACCESS-TAXCD-MSTR-FILE
00144              IF NOT ERR
00145                  PERFORM 050-BUILD-AGCY-OUT-REC
00146                    VARYING SUB FROM 1 BY 1
00147                    UNTIL SUB GREATER 40
00148                  MOVE CF-TXCD TO SV-TXCD
00149                  MOVE ZEROES TO CURR-288-VAL EXPR-288-VAL
00150                      1ST-TIME-VAL EXP-INC-VAL TOT-ASSD-VAL
00151                  ADD FDP-CURR-288-VAL TO CURR-288-VAL
00152                                          TOT-ASSD-VAL
00153                  ADD FDP-EXPR-288-VAL TO EXPR-288-VAL
00154                                          TOT-ASSD-VAL
00155                  ADD FDP-FRST-VAL TO 1ST-TIME-VAL
00156                                      TOT-ASSD-VAL
00157                  ADD FDP-EXP-INC  TO EXP-INC-VAL.
00158      SKIP2
00159  030-READ-FROZEN.
00160      READ FRZDIV-PRCNT-FILE
00161        AT END
00162          MOVE 'Y' TO FRZ-EOF-SW.
00163      IF NOT FRZ-EOF
00164          ADD +1 TO RECS-IN-CNTR
00165          MOVE FDP-DIVNO TO CF-DIVNO
00166          MOVE FDP-TXCD  TO CF-TXCD
00167          IF CURR-FRZ-KEY LESS PREV-FRZ-KEY
00168              DISPLAY 'FROZEN DIVISION FILE OUT OF SEQUENCE'
00169              DISPLAY 'CURRENT  KEY IS ' CURR-FRZ-KEY
00170              DISPLAY 'PREVIOUS KEY IS ' PREV-FRZ-KEY
00171              MOVE 16  TO RETURN-CODE
00172              MOVE 'Y' TO ERR-SW
00173          ELSE
00174              MOVE CURR-FRZ-KEY TO PREV-FRZ-KEY
00175              IF FIRST-REC
00176                 MOVE CF-TXCD  TO SV-TXCD.
00177      SKIP3
00178  040-ACCESS-TAXCD-MSTR-FILE.
00179      MOVE SV-TXCD TO TXCD-TAX-CODE.
00180      READ TAXCD-MSTR-FILE.
00181      IF NOT GOOD-STATUS AND NOT REC-NOTFND
00182          DISPLAY 'INVALID READ ON TXCD MSTR FILE'
00183          DISPLAY 'FILE STATUS IS ' FILE-STATUS
00184          DISPLAY 'FEEDBACK STATUS IS ' FS-RETURN ' '
00185                    FS-FUNCTION ' ' FS-FEEDBACK
00186          MOVE 16 TO RETURN-CODE
00187          MOVE 'Y' TO ERR-SW
00188      ELSE
00189          IF REC-NOTFND
00190              DISPLAY 'NO MATCHING TAX CODE MASTER RECORD'
00191              DISPLAY 'TAX CODE IS ' SV-TXCD
00192              MOVE 16 TO RETURN-CODE
00193              MOVE 'Y' TO ERR-SW.
00194      SKIP3
00195  050-BUILD-AGCY-OUT-REC.
00196      IF TXCD-AGCY-NO (SUB) GREATER ZEROES
00197          MOVE SPACES TO FRZAGCY-REC
00198          INITIALIZE FRZAGCY-REC
00199          MOVE TXCD-TAX-CODE TO FA-TXCD
00200          MOVE TXCD-AGCY-NO (SUB) TO FA-AGCY
00201          MOVE TXCD-TXCDE-RATE TO FA-TXRTE
00202          MOVE CURR-288-VAL TO FA-CURR-288
00203          MOVE EXPR-288-VAL TO FA-EXPR-288
00204          MOVE 1ST-TIME-VAL TO FA-FRST-TIME
00205          MOVE EXP-INC-VAL  TO FA-EXPINC
00206          MOVE TOT-ASSD-VAL TO FA-TOT-FROZ
00207          COMPUTE FA-FROZ-EQLZD ROUNDED =
00208                 TOT-ASSD-VAL * LINK-EQFCTR-N
00209          COMPUTE FA-FROZ-TXAMT ROUNDED =
00210                (FA-TOT-FROZ * TXCD-TXCDE-RATE) / 100
00211          COMPUTE FA-EXPINCEV ROUNDED =
00212                 EXP-INC-VAL * LINK-EQFCTR-N
00213          COMPUTE FA-EXPINCTX ROUNDED =
00214                (FA-EXPINCEV * TXCD-TXCDE-RATE) / 100
00215          WRITE FRZAGCY-REC
00216          ADD +1 TO RECS-OUT-CNTR.
