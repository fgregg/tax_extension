00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASREA840.
00003 *AUTHOR. FURMANIAK-MOLIS.
00004 *DATE-WRITTEN. MARCH 2,1981.
00005 *REMARKS.    THIS PROGRAM UPDATES THE HOMEOWNER FILE
00006 *            WITH SCANNED HOMEOWNER RENEWAL RECORDS.
00007      SKIP2
00008  ENVIRONMENT DIVISION.
00009  CONFIGURATION SECTION.
00010  SOURCE-COMPUTER. IBM-370.
00011  OBJECT-COMPUTER. IBM-370.
00012      SKIP1
00013  INPUT-OUTPUT SECTION.
00014  FILE-CONTROL.
00015      SELECT HOMEOWNER-MAST ASSIGN TO UT-S-HOMEMSTR.
00016      SELECT RENEWAL  ASSIGN TO UT-S-RENEWAL.
00017      SELECT HOMEOWNER-MAST2 ASSIGN TO UT-S-HOMEOUT.
00018      SELECT PRINTOUT ASSIGN TO UT-S-PRINTOUT.
00019      SELECT ERRPRINT ASSIGN TO UT-S-ERRPRINT.
00020      EJECT
00021  DATA DIVISION.
00022  FILE SECTION.
00023      SKIP1
00024  COPY HOMOWNFD01.
00025  COPY HOMOWNFD02.
00026      SKIP2
00027  FD  RENEWAL
00028      LABEL RECORDS ARE STANDARD
00029      BLOCK CONTAINS 0 RECORDS
00030      RECORD CONTAINS 80 CHARACTERS
00031      RECORDING MODE IS F
00032      DATA RECORD IS RENEWAL-REC.
00033  01  RENEWAL-REC.
00034      05  REN-PROP        PIC X(14).
00035      05  FILLER          PIC X(6).
00036      05  REN-BATCH       PIC X(5).
00037      05  FILLER          PIC X(55).
00038      SKIP2
00039  FD  PRINTOUT
00040      LABEL RECORDS ARE STANDARD
00041      BLOCK CONTAINS 0 RECORDS
00042      RECORD CONTAINS 133 CHARACTERS
00043      RECORDING MODE IS F
00044      DATA RECORD IS PRINT-REC.
00045  01  PRINT-REC           PIC X(133).
00046      SKIP2
00047  FD  ERRPRINT
00048      LABEL RECORDS ARE STANDARD
00049      BLOCK CONTAINS 0 RECORDS
00050      RECORD CONTAINS 133 CHARACTERS
00051      RECORDING MODE IS F
00052      DATA RECORD IS PRINT-ERR.
00053  01  PRINT-ERR           PIC X(133).
00054      EJECT
00055  WORKING-STORAGE SECTION.
00056  77  HOME-RECS-READ      PIC S9(9)   VALUE +0 PACKED-DECIMAL.
00057  77  HOME-RECS-WRITTEN   PIC S9(9)   VALUE +0 PACKED-DECIMAL.
00058  77  HOME-RECS-UPDATED   PIC S9(9)   VALUE +0 PACKED-DECIMAL.
00059  77  RENW-RECS-READ      PIC S9(7)   VALUE +0 PACKED-DECIMAL.
00060  77  RENW-RECS-REJECT    PIC S9(7)   VALUE +0 PACKED-DECIMAL.
00061  77  RECS-UPDATED        PIC S9(7)   VALUE +0 PACKED-DECIMAL.
00062  77  PAGE-CNT            PIC S9(5)   VALUE +0 PACKED-DECIMAL.
00063  77  ERR-PAGE-CNT        PIC S9(5)   VALUE +0 PACKED-DECIMAL.
00064  77  LINE-CNT            PIC S9(3)   VALUE +53 PACKED-DECIMAL.
00065  77  ERR-LINE-CNT        PIC S9(3)   VALUE +53 PACKED-DECIMAL.
00066  77  BLNK                PIC X       VALUE SPACE.
00067  77  HOME-SW             PIC X       VALUE 'N'.
00068      88  HOME-EOF                    VALUE 'Y'.
00069  77  RENEWAL-SW          PIC X       VALUE 'N'.
00070      88  RENEWAL-EOF                 VALUE 'Y'.
00071  77  CHECK-PROCESSING    PIC X       VALUE 'Y'.
00072      88  PROCESSING-OK               VALUE 'Y'.
00073      88  STOP-PROCESSING             VALUE 'N'.
00074  77  VALID-REC-SW        PIC X       VALUE 'N'.
00075      88  VALID-REC                   VALUE 'Y'.
00076      SKIP2
00077  01  WORK-AREA.
00078      05  CURR-HOM-PROP   PIC 9(15).
00079      05  PREV-HOM-PROP   PIC 9(15)   VALUE 0.
00080      05  CURR-REN-PROP   PIC 9(15).
00081      05  PREV-REN-PROP   PIC 9(15)   VALUE 0.
00082      SKIP2
00083      05  PRINT-HEAD-1.
00084          10  FILLER      PIC X(3)    VALUE SPACES.
00085          10  PRT-HD1-DTE PIC X(8).
00086          10  FILLER      PIC X(44)   VALUE SPACES.
00087          10  FILLER      PIC X(65)   VALUE
00088          'OFFICE OF THE ASSESSOR'.
00089          10  FILLER      PIC X(5)    VALUE 'PAGE '.
00090          10  PRT-HD1-PGE PIC ZZ,ZZ9.
00091      SKIP2
00092      05  PRINT-HEAD-2.
00093          10  FILLER      PIC X(48)   VALUE
00094          '   ASREA840-1'.
00095          10  FILLER      PIC X(85)   VALUE
00096          'HOMEOWNER SCAN-RENEWAL UPDATE REPORT'.
00097      SKIP2
00098      05  PRINT-HEAD-3.
00099          10  FILLER      PIC X(49)   VALUE
00100          ' VOL    PROPERTY NUMBER      TAX  CLASS    YEAR  '.
00101          10  FILLER      PIC X(84)   VALUE
00102          'PRO-RATION   COOP    ASSESSED     BATCH'.
00103      SKIP2
00104      05  PRINT-HEAD-4.
00105          10  FILLER      PIC X(28)   VALUE SPACES.
00106          10  FILLER      PIC X(105)  VALUE
00107          'CODE  MA-MI    APPL               QNTY    VALUATION   NU
00108 -        'NBER'.
00109      SKIP2
00110      05  PRINT-DETAIL.
00111          10  FILLER      PIC X.
00112          10  PD-VOL      PIC 999.
00113          10  FILLER      PIC XX      VALUE SPACES.
00114          10  PD-PROP     PIC 99,99,999,999,9999.
00115          10  FILLER      PIC XXX     VALUE SPACES.
00116          10  PD-TXCODE   PIC 99999.
00117          10  FILLER      PIC XXX     VALUE SPACES.
00118          10  PD-CLASS    PIC 9,99.
00119          10  FILLER      PIC X(6)    VALUE '    19'.
00120          10  PD-YR-APPL  PIC 99.
00121          10  FILLER      PIC XXX     VALUE SPACES.
00122          10  PD-PRO-RAT  PIC 9.999999.
00123          10  FILLER      PIC XXX     VALUE SPACES.
00124          10  PD-COOP-QTY PIC 99999.
00125          10  FILLER      PIC XXX     VALUE SPACES.
00126          10  PD-ASSESVAL PIC ZZZ,ZZZ,ZZ9.
00127          10  FILLER      PIC XXX.
00128          10  PD-BATCH-NO PIC X(5).
00129      SKIP2
00130      05  PRINT-ERR-1.
00131          10  FILLER      PIC XXX     VALUE SPACES.
00132          10  PRT-ER1-DTE PIC X(8).
00133          10  FILLER      PIC X(44)   VALUE SPACES.
00134          10  FILLER      PIC X(65)   VALUE
00135          'OFFICE OF THE ASSESSOR'.
00136          10  FILLER      PIC X(5)    VALUE 'PAGE '.
00137          10  PRT-ER1-PGE PIC ZZ,ZZ9.
00138      SKIP2
00139      05  PRINT-ERR-2.
00140          10  FILLER      PIC X(48)   VALUE
00141          '   ASREA840-2'.
00142          10  FILLER      PIC X(85)   VALUE
00143          'HOMEOWNER SCAN-RENEWAL NO-MATCH REPORT'.
00144      SKIP2
00145      05  PRINT-ERR-3.
00146          10  FILLER      PIC X(28)   VALUE
00147          '     PROPERTY NUMBER'.
00148          10  FILLER      PIC X(105)  VALUE
00149          '       BATCH NO'.
00150      SKIP2
00151      05  PRINT-ERROR.
00152          10  FILLER      PIC XXX     VALUE SPACES.
00153          10  PE-PROP.
00154              15 PE-PROP-R PIC 99,99,999,999,9999.
00155          10  FILLER      PIC X(8)    VALUE SPACES.
00156          10 PE-BATCH.
00157              15 PE-BATCH-R PIC 9(5).
00158      SKIP2
00159      05  TOTAL-RENEWAL-LINE.
00160          10  FILLER      PIC X(23)   VALUE
00161          ' TOTAL RENEWAL RECORDS '.
00162          10  TOT-REN-MSG PIC X(11).
00163          10  TOT-REN-CNT PIC Z,ZZZ,ZZ9.
00164      SKIP2
00165      05  TOTAL-HOMEOWNER-LINE.
00166          10  FILLER      PIC X(25)   VALUE
00167          ' TOTAL HOMEOWNER RECORDS '.
00168          10  TOT-HOM-MSG PIC X(9).
00169          10  TOT-HOM-CNT PIC Z,ZZZ,ZZ9.
00170      05  DSP-TIME                   PIC 99,99.
00171      05  DSP-DATE.
00172          10  DSP-MO                 PIC 99.
00173          10  FILLER                 PIC X         VALUE '/'.
00174          10  DSP-DA                 PIC 99.
00175          10  FILLER                 PIC X         VALUE '/'.
00176          10  DSP-YR                 PIC 99.
00177      05  ACPT-DATE                  PIC 9(6).
00178      05  ACPT-DATE-X REDEFINES ACPT-DATE.
00179          10  ACPT-YR                PIC 99.
00180          10  ACPT-MO                PIC 99.
00181          10  ACPT-DA                PIC 99.
00182      05  ACPT-TIME-HOLD             PIC 9(8).
00183      05  ACPT-TIME-HOLD-X REDEFINES ACPT-TIME-HOLD.
00184          10  ACPT-TIME              PIC 9(4).
00185          10  FILLER                 PIC 9(4).
00186      EJECT
00187  PROCEDURE DIVISION.
00188      ACCEPT ACPT-TIME-HOLD FROM TIME.
00189      MOVE ACPT-TIME TO DSP-TIME.
00190      INSPECT DSP-TIME REPLACING ALL ',' BY ':'.
00191      ACCEPT ACPT-DATE FROM DATE.
00192      MOVE ACPT-MO TO DSP-MO.
00193      MOVE ACPT-DA TO DSP-DA.
00194      MOVE ACPT-YR TO DSP-YR.
00195      DISPLAY 'PROGRAM ASREA841 DATE AND TIME OF RUN = '
00196        DSP-DATE ' ' DSP-TIME.
00197      OPEN INPUT HOMEOWNER-MAST RENEWAL
00198      OPEN OUTPUT PRINTOUT ERRPRINT HOMEOWNER-MAST2.
00199      SKIP1
00200  050-INITIAL-READS.
00201      PERFORM 200-READ-HOMEOWNER THRU 200-EXIT
00202      IF HOME-EOF
00203          DISPLAY 'NO HOMEOWNER RECORDS ON FILE'
00204          MOVE 'N' TO CHECK-PROCESSING
00205      ELSE
00206          PERFORM 300-READ-EDIT-RENEWAL THRU 300-EXIT
00207             UNTIL  VALID-REC OR RENEWAL-EOF OR STOP-PROCESSING
00208          IF RENEWAL-EOF
00209              DISPLAY 'NO RENEWAL RECORDS'
00210              MOVE 'N' TO CHECK-PROCESSING
00211          END-IF
00212      END-IF.
00213      IF PROCESSING-OK
00214         MOVE DSP-DATE TO PRT-HD1-DTE PRT-ER1-DTE
00215      END-IF.
00216      SKIP1
00217  070-START.
00218      PERFORM 100-MAINLINE THRU 100-EXIT
00219        UNTIL (HOME-EOF AND RENEWAL-EOF) OR
00220            STOP-PROCESSING.
00221      SKIP1
00222  090-EOJ.
00223      IF STOP-PROCESSING
00224          MOVE 16 TO RETURN-CODE
00225      END-IF.
00226      PERFORM 900-TOTAL THRU 900-EXIT
00227      CLOSE HOMEOWNER-MAST RENEWAL
00228      CLOSE PRINTOUT ERRPRINT HOMEOWNER-MAST2
00229      STOP RUN.
00230      SKIP1
00231  100-MAINLINE.
00232      IF CURR-HOM-PROP LESS THAN CURR-REN-PROP
00233          PERFORM 400-MOVE-HOMEOWNER THRU 400-EXIT
00234          PERFORM 500-WRITE-NEW-MASTER THRU 500-EXIT
00235          PERFORM 200-READ-HOMEOWNER THRU 200-EXIT
00236      ELSE
00237          IF CURR-HOM-PROP = CURR-REN-PROP
00238              MOVE 2 TO HO-STAT1
00239              PERFORM 400-MOVE-HOMEOWNER THRU 400-EXIT
00240              PERFORM 500-WRITE-NEW-MASTER THRU 500-EXIT
00241              PERFORM 450-MOVE-TO-PRINT THRU 450-EXIT
00242              PERFORM 550-WRITE-PRINT THRU 550-EXIT
00243              PERFORM 200-READ-HOMEOWNER THRU 200-EXIT
00244              MOVE 'N'  TO  VALID-REC-SW
00245              PERFORM 300-READ-EDIT-RENEWAL THRU 300-EXIT
00246               UNTIL VALID-REC OR RENEWAL-EOF OR STOP-PROCESSING
00247          ELSE
00248              PERFORM 600-MOVE-NO-MATCH THRU 600-EXIT
00249              PERFORM 700-PRINT-NO-MATCH THRU 700-EXIT
00250              MOVE 'N'  TO  VALID-REC-SW
00251              PERFORM 300-READ-EDIT-RENEWAL THRU 300-EXIT
00252               UNTIL VALID-REC OR RENEWAL-EOF OR STOP-PROCESSING
00253          END-IF
00254      END-IF.
00255  100-EXIT.
00256      EXIT.
00257      SKIP1
00258  200-READ-HOMEOWNER.
00259      READ HOMEOWNER-MAST AT END
00260          MOVE 'Y' TO HOME-SW
00261          MOVE ALL '9' TO CURR-HOM-PROP.
00262      IF NOT HOME-EOF
00263          ADD +1 TO HOME-RECS-READ
00264          MOVE HO-PROP TO CURR-HOM-PROP
00265          IF CURR-HOM-PROP NOT GREATER THAN PREV-HOM-PROP
00266              MOVE 'N' TO CHECK-PROCESSING
00267              DISPLAY 'HOMEOWNER FILE OUT OF SEQUENCE'
00268              DISPLAY 'CURRENT PROP ' CURR-HOM-PROP
00269              DISPLAY 'PREVIOUS PROP ' PREV-HOM-PROP
00270          ELSE
00271              MOVE CURR-HOM-PROP TO PREV-HOM-PROP
00272          END-IF
00273      END-IF.
00274  200-EXIT.
00275      EXIT.
00276      SKIP1
00277  300-READ-EDIT-RENEWAL.
00278      PERFORM  310-READ-RENEWAL THRU 310-EXIT
00279      IF NOT (RENEWAL-EOF OR STOP-PROCESSING)
00280         IF REN-PROP NUMERIC AND
00281            REN-PROP GREATER ZEROS
00282            MOVE 'Y' TO VALID-REC-SW
00283         ELSE
00284              PERFORM 600-MOVE-NO-MATCH THRU 600-EXIT
00285              PERFORM 700-PRINT-NO-MATCH THRU 700-EXIT
00286         END-IF
00287      END-IF.
00288  300-EXIT.
00289      EXIT.
00290  310-READ-RENEWAL.
00291      READ RENEWAL AT END
00292          MOVE 'Y' TO RENEWAL-SW
00293          MOVE ALL '9' TO CURR-REN-PROP.
00294      IF NOT RENEWAL-EOF
00295          ADD +1 TO RENW-RECS-READ
00296          MOVE REN-PROP TO CURR-REN-PROP
00297          IF CURR-REN-PROP LESS THAN PREV-REN-PROP
00298              MOVE 'N' TO CHECK-PROCESSING
00299              DISPLAY 'RENEWAL FILE OUT OF SEQUENCE'
00300              DISPLAY 'CURRENT RENEWAL PROP ' CURR-REN-PROP
00301              DISPLAY 'PREVIOUS RENEWAL PROP ' PREV-REN-PROP
00302          ELSE
00303              MOVE CURR-REN-PROP TO PREV-REN-PROP
00304         END-IF
00305      END-IF.
00306  310-EXIT.
00307      EXIT.
00308      SKIP1
00309  400-MOVE-HOMEOWNER.
00310      MOVE HO-REC TO HO-REC2.
00311  400-EXIT.
00312      EXIT.
00313      SKIP1
00314  450-MOVE-TO-PRINT.
00315      MOVE HO-VOL TO PD-VOL
00316      MOVE HO-PROP TO PD-PROP
00317      INSPECT PD-PROP REPLACING ALL ',' BY '-'
00318      MOVE HO-TXCD TO PD-TXCODE
00319      MOVE HO-CLASS TO PD-CLASS
00320      INSPECT PD-CLASS REPLACING ALL ',' BY '-'
00321      MOVE HO-YRAPPL TO PD-YR-APPL
00322      MOVE HO-PRORATE TO PD-PRO-RAT
00323      MOVE HO-COOPQTY TO PD-COOP-QTY
00324      MOVE HO-ASSDVAL TO PD-ASSESVAL.
00325      MOVE REN-BATCH TO PD-BATCH-NO.
00326  450-EXIT.
00327      EXIT.
00328      SKIP1
00329  500-WRITE-NEW-MASTER.
00330      WRITE HO-REC2
00331      ADD +1 TO HOME-RECS-WRITTEN.
00332  500-EXIT.
00333      EXIT.
00334      SKIP1
00335  550-WRITE-PRINT.
00336      IF LINE-CNT GREATER THAN +52
00337          PERFORM 800-HEADING THRU 800-EXIT
00338      END-IF.
00339      WRITE PRINT-REC FROM PRINT-DETAIL AFTER ADVANCING 1
00340      ADD +1 TO LINE-CNT HOME-RECS-UPDATED RECS-UPDATED.
00341  550-EXIT.
00342      EXIT.
00343      SKIP1
00344  600-MOVE-NO-MATCH.
00345      IF  VALID-REC
00346          MOVE REN-PROP TO PE-PROP-R
00347          INSPECT PE-PROP REPLACING ALL ',' BY '-'
00348          MOVE REN-BATCH TO PE-BATCH-R
00349      ELSE
00350          MOVE REN-PROP TO PE-PROP
00351          MOVE REN-BATCH TO PE-BATCH
00352      END-IF.
00353  600-EXIT.
00354      EXIT.
00355      SKIP1
00356  700-PRINT-NO-MATCH.
00357      IF ERR-LINE-CNT GREATER THAN +52
00358          PERFORM 850-ERR-HEAD THRU 850-EXIT
00359      END-IF.
00360      WRITE PRINT-ERR FROM PRINT-ERROR AFTER ADVANCING 2
00361      ADD +2 TO ERR-LINE-CNT
00362      ADD +1 TO RENW-RECS-REJECT.
00363  700-EXIT.
00364      EXIT.
00365      SKIP1
00366  800-HEADING.
00367      ADD +1 TO PAGE-CNT
00368      MOVE PAGE-CNT TO PRT-HD1-PGE
00369      WRITE PRINT-REC FROM PRINT-HEAD-1 AFTER ADVANCING PAGE
00370      WRITE PRINT-REC FROM PRINT-HEAD-2 AFTER ADVANCING 2
00371      WRITE PRINT-REC FROM PRINT-HEAD-3 AFTER ADVANCING 2
00372      WRITE PRINT-REC FROM PRINT-HEAD-4 AFTER ADVANCING 1
00373      WRITE PRINT-REC FROM BLNK AFTER ADVANCING 1
00374      MOVE +7 TO LINE-CNT.
00375  800-EXIT.
00376      EXIT.
00377      SKIP1
00378  850-ERR-HEAD.
00379      ADD +1 TO ERR-PAGE-CNT
00380      MOVE ERR-PAGE-CNT TO PRT-ER1-PGE
00381      WRITE PRINT-ERR FROM PRINT-ERR-1 AFTER ADVANCING PAGE
00382      WRITE PRINT-ERR FROM PRINT-ERR-2 AFTER ADVANCING 2
00383      WRITE PRINT-ERR FROM PRINT-ERR-3 AFTER ADVANCING 2
00384      MOVE +5 TO ERR-LINE-CNT.
00385  850-EXIT.
00386      EXIT.
00387      SKIP2
00388  900-TOTAL.
00389      MOVE 'READ' TO TOT-REN-MSG
00390      MOVE RENW-RECS-READ TO TOT-REN-CNT
00391      WRITE PRINT-REC FROM TOTAL-RENEWAL-LINE AFTER ADVANCING 3
00392      WRITE PRINT-ERR FROM TOTAL-RENEWAL-LINE AFTER ADVANCING 3
00393      MOVE 'UPDATED' TO TOT-REN-MSG
00394      MOVE RECS-UPDATED TO TOT-REN-CNT
00395      WRITE PRINT-REC FROM TOTAL-RENEWAL-LINE AFTER ADVANCING 1
00396      WRITE PRINT-ERR FROM TOTAL-RENEWAL-LINE AFTER ADVANCING 1
00397      MOVE 'REJECTED' TO TOT-REN-MSG
00398      MOVE RENW-RECS-REJECT TO TOT-REN-CNT
00399      WRITE PRINT-REC FROM TOTAL-RENEWAL-LINE AFTER ADVANCING 1
00400      WRITE PRINT-ERR FROM TOTAL-RENEWAL-LINE AFTER ADVANCING 1
00401      MOVE 'READ' TO TOT-HOM-MSG
00402      MOVE HOME-RECS-READ TO TOT-HOM-CNT
00403      WRITE PRINT-REC FROM TOTAL-HOMEOWNER-LINE AFTER ADVANCING 1
00404      WRITE PRINT-ERR FROM TOTAL-HOMEOWNER-LINE AFTER ADVANCING 1
00405      MOVE 'WRITTEN' TO TOT-HOM-MSG
00406      MOVE HOME-RECS-WRITTEN TO TOT-HOM-CNT
00407      WRITE PRINT-REC FROM TOTAL-HOMEOWNER-LINE AFTER ADVANCING 1
00408      WRITE PRINT-ERR FROM TOTAL-HOMEOWNER-LINE AFTER ADVANCING 1
00409      MOVE 'UPDATED' TO TOT-HOM-MSG
00410      MOVE HOME-RECS-UPDATED TO TOT-HOM-CNT
00411      WRITE PRINT-REC FROM TOTAL-HOMEOWNER-LINE
00412              AFTER ADVANCING 1
00413      WRITE PRINT-ERR FROM TOTAL-HOMEOWNER-LINE
00414              AFTER ADVANCING 1.
00415  900-EXIT.
00416      EXIT.
00417      SKIP1
