00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASREA847.
00003  AUTHOR. BEATA - MOLIS.
00004  DATE-WRITTEN. MAY 29 1981.
00005 *REMARKS.  THE PURPOSE OF THIS PROGRAM IS TO UPDATE THE
00006 *          NAME/ADDRESS FIELDS OF THE HOMEOWNER FILE WITH
00007 *          CORRESPONDING FIELDS FROM THE PROPERTY MASTER.
00008      SKIP2
00009  ENVIRONMENT DIVISION.
00010  CONFIGURATION SECTION.
00011  SOURCE-COMPUTER. IBM-370.
00012  OBJECT-COMPUTER. IBM-370.
00013  INPUT-OUTPUT SECTION.
00014  FILE-CONTROL.
00015      SELECT PROPMAST        ASSIGN TO DA-PROPMAST
00016                             ACCESS IS SEQUENTIAL
00017                             ORGANIZATION IS INDEXED
00018                             RECORD KEY IS PM-KEY
00019                             FILE STATUS IS FILE-STATUS.
00020      SELECT HOMEOWNER-MAST  ASSIGN TO UT-S-HOMEIN.
00021      SELECT HOMEOWNER-MAST2 ASSIGN TO UT-S-HOMEOUT.
00022      SKIP2
00023  DATA DIVISION.
00024  FILE SECTION.
00025  COPY PROPMSFD01.
00026  COPY HOMOWNFD01.
00027  COPY HOMOWNFD02.
00028  WORKING-STORAGE SECTION.
00029  77  SUB               PIC S9(4)     VALUE +0   COMP.
00030  77  FILE-STATUS       PIC 99.
00031      88 VALID-STATUS                 VALUE 00.
00032      88 PROP-EOF                     VALUE 10.
00033  77  HOME-EOF-SW       PIC X         VALUE 'N'.
00034      88 HOME-EOF                     VALUE 'Y'.
00035  77  NAME-SW           PIC X         VALUE 'N'.
00036      88 NAME-FOUND                   VALUE 'Y'.
00037  77  ERR-SW            PIC X         VALUE 'N'.
00038      88 ERR                          VALUE 'Y'.
00039  01  WORK-AREA.
00040      05 CURR-HOME-KEY  PIC 9(14)     VALUE ZERO.
00041      05 PREV-HOME-KEY  PIC 9(14)     VALUE ZERO.
00042      05 CURR-PROP-KEY  PIC 9(14)     VALUE ZERO.
00043      EJECT
00044  PROCEDURE DIVISION.
00045      OPEN INPUT PROPMAST
00046      IF NOT VALID-STATUS
00047          DISPLAY 'INVALID OPEN PROPMAST'
00048          DISPLAY 'FILE STATUS IS ' FILE-STATUS
00049          MOVE 16 TO RETURN-CODE
00050          STOP RUN.
00051      OPEN INPUT HOMEOWNER-MAST
00052      OPEN OUTPUT HOMEOWNER-MAST2
00053      PERFORM 300-READ-PROPMAST THRU 300-EXIT
00054      PERFORM 400-READ-HOMEOWNER THRU 400-EXIT
00055      SKIP1
00056      PERFORM 100-MAINLINE THRU 100-EXIT
00057          UNTIL HOME-EOF OR ERR
00058      SKIP1
00059      CLOSE HOMEOWNER-MAST
00060      CLOSE HOMEOWNER-MAST2
00061      CLOSE PROPMAST
00062      IF NOT VALID-STATUS
00063          DISPLAY 'INVALID CLOSE PROPMAST'
00064          DISPLAY 'FILE STATUS IS ' FILE-STATUS
00065          MOVE 16 TO RETURN-CODE.
00066      STOP RUN.
00067      SKIP1
00068  100-MAINLINE.
00069      IF CURR-HOME-KEY GREATER THAN CURR-PROP-KEY
00070          PERFORM 300-READ-PROPMAST THRU 300-EXIT
00071      ELSE
00072          IF CURR-HOME-KEY EQUAL CURR-PROP-KEY
00073              MOVE HO-REC TO HO-REC2
00074              MOVE 'N' TO NAME-SW
00075              PERFORM 150-FIND-NAME THRU 150-EXIT
00076                  VARYING SUB FROM 1 BY 1
00077                  UNTIL SUB GREATER THAN PM-SEGCTR
00078                  OR NAME-FOUND
00079              PERFORM 200-WRITE-HOMEOWNER THRU 200-EXIT
00080              PERFORM 300-READ-PROPMAST THRU 300-EXIT
00081              PERFORM 400-READ-HOMEOWNER THRU 400-EXIT
00082          ELSE
00083              MOVE HO-REC TO HO-REC2
00084              PERFORM 200-WRITE-HOMEOWNER THRU 200-EXIT
00085              PERFORM 400-READ-HOMEOWNER THRU 400-EXIT.
00086  100-EXIT.
00087      EXIT.
00088      SKIP1
00089  150-FIND-NAME.
00090      IF NAME-TYPE-CODE (SUB)
00091          MOVE PM-NAME (SUB) TO HO-NAME2
00092          ADD +1 TO SUB
00093          MOVE PM-ADDR (SUB) TO HO-ADDR2
00094          ADD +1 TO SUB
00095          MOVE PM-CITY (SUB) TO HO-CITY2
00096          MOVE PM-STATE (SUB) TO HO-STATE2
00097          MOVE PM-ZIP (SUB) TO HO-ZIP2
00098          MOVE 'Y' TO NAME-SW.
00099  150-EXIT.
00100      EXIT.
00101      SKIP1
00102  200-WRITE-HOMEOWNER.
00103      WRITE HO-REC2.
00104  200-EXIT.
00105      EXIT.
00106      SKIP1
00107  300-READ-PROPMAST.
00108      READ PROPMAST
00109      IF PROP-EOF
00110          MOVE ALL '9' TO CURR-PROP-KEY
00111      ELSE
00112          IF VALID-STATUS
00113              MOVE PM-PROP TO CURR-PROP-KEY
00114          ELSE
00115              DISPLAY 'INVALID READ PROPMAST'
00116              DISPLAY 'FILE STATUS IS ' FILE-STATUS
00117              DISPLAY 'RECORD KEY IS ' PM-PROP PM-TXTYP PM-TXYR
00118              MOVE 16 TO RETURN-CODE
00119              MOVE 'Y' TO ERR-SW.
00120  300-EXIT.
00121      EXIT.
00122      SKIP1
00123  400-READ-HOMEOWNER.
00124      READ HOMEOWNER-MAST AT END
00125          MOVE 'Y' TO HOME-EOF-SW.
00126      IF NOT HOME-EOF
00127          MOVE HO-PROP TO CURR-HOME-KEY
00128          IF CURR-HOME-KEY < PREV-HOME-KEY
00129              DISPLAY 'HOMEOWNER-MAST OUT OF SEQUENCE'
00130              DISPLAY 'CURRENT KEY IS ' CURR-HOME-KEY
00131              DISPLAY 'PREVIOUS KEY IS ' PREV-HOME-KEY
00132              MOVE 16 TO RETURN-CODE
00133              MOVE 'Y' TO ERR-SW
00134          ELSE
00135              MOVE CURR-HOME-KEY TO PREV-HOME-KEY.
00136  400-EXIT.
00137      EXIT.
