00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASREA863.
00003 *AUTHOR. PROGRAMMMER - ORAHOOD
00004 *        ANALYST     - MOLIS.
00005 *DATE-WRITTEN. MAY 25, 1979.
00006 *REMARKS. THIS PROGRAM EDITS HOMESTEAD EQUALIZED
00007 *         VALUATION MAINTENANCE RECORDS.
00008 *MODIFICATION ON 02/29/88 - SQUILLACE.
00009 *         'OCCUPANCY FACTOR FIELD' ADDED TO THE INPUT RECORD
00010 *         AND TO THE OUTPUT.
00011 *         'OCCUPANCY FACTOR FIELD' MUST BE EQUAL TO SPACES
00012 *         OR NUMERIC VALUE OF 000.1 THRU 100.0.
00013 *MODIFICATION ON 12/27/91 - HERING
00014 *         'ADD LOGIC FOR CLASS 9'S
00015 *         CONVERT TO COBOL II
00016 *MODIFICATION ON 08/27/04 - KLF
00017 *         EQUALIZED VALUATION & NPHE EXEMPTION AMOUNT ADDED TO
00018 *         THE INPUT RECORD AND OUTPUT RECORD.
00019 *MODIFICATION ON 08/31/04 - TJM
00020 *         EDIT IN-EQVAL AND IN-NPHE-AMT F NUMERICS.
00021 *MODIFICATION ON 04/13/05 - KLF
00022 *         EDIT IN-YR  NUMERICS.
00023 *COPIED ASREA860 TO ASREA863 TO ACCOMODATE INFILE LENGTH OF 140
00024 *   8/05 KLF
00025 *MODIFICATION ON 4/5/13 - KLF EDIT TOWN AND NPHE-STATUS
00026 *  TAX YEAR 2012 ONLY CITY TOWNS ARE ELIGIBLE FOR SUNSET
00027 *  EXEMPTION: VALID STATUS HS, LS, SS
00028 *  TAX YEAR 2013 ONLY NWWS TOWNS ARE ELIGIBLE FOR SUNSET
00029 *  EXEMPTION: VALID STATUS HS, LS, SS
00030 *  TAX YEAR 2014 ONLY SWWS TOWNS ARE ELIGIBLE FOR SUNSET
00031 *  EXEMPTION: VALID STATUS HS, LS, SS
00032  ENVIRONMENT DIVISION.
00033  CONFIGURATION SECTION.
00034  SOURCE-COMPUTER. IBM-370.
00035  OBJECT-COMPUTER. IBM-370.
00036  INPUT-OUTPUT SECTION.
00037  FILE-CONTROL.
00038      SELECT INFILE   ASSIGN TO UT-S-INFILE.
00039      SELECT VOLFILE  ASSIGN TO DA-VOLFILE
00040         ACCESS MODE  IS SEQUENTIAL
00041         ORGANIZATION IS INDEXED
00042         FILE STATUS  IS FILE-STATUS
00043         RECORD KEY   IS VOL-KEY.
00044      SELECT HOMEXEMP ASSIGN TO UT-S-HOMEXEMP.
00045      SELECT DETFILE  ASSIGN TO UT-S-DETFILE.
00046      SELECT ERRFILE  ASSIGN TO UT-S-ERRFILE.
00047  DATA DIVISION.
00048  FILE SECTION.
00049      SKIP1
00050  FD  INFILE
00051      BLOCK CONTAINS  0   RECORDS
00052      RECORD CONTAINS 140 CHARACTERS
00053      LABEL RECORDS   ARE STANDARD
00054      RECORDING MODE  IS  F.
00055  01  IN-REC.
00056      05 FILLER         PIC X.
00057      05 IN-KEY.
00058         10 IN-TOWN     PIC 9(2).
00059         10 IN-VOL      PIC 9(3).
00060         10 IN-PROP     PIC 9(14).
00061      05 FILLER         PIC X(15).
00062      05 IN-ASVAL       PIC 9(9).
00063      05 IN-EQVAL       PIC 9(9).
00064      05 IN-YR          PIC X(02).
00065      05 IN-OCCFAC      PIC 999V9.
00066      05 FILLER         PIC X(63).
00067      05 IN-NPHE-EXAMT  PIC 9(7).
00068      05 IN-BSYR        PIC 9(4).
00069      05 IN-STATUS      PIC X(2).
00070      05 FILLER         PIC X(5).
00071      SKIP3
00072  FD  VOLFILE
00073      BLOCK  CONTAINS 0   RECORDS
00074      RECORD CONTAINS 10  CHARACTERS
00075      LABEL  RECORDS  ARE STANDARD.
00076  01  VOL-REC.
00077      05 VOL-KEY.
00078         10 V-TWN       PIC 9(3)       PACKED-DECIMAL.
00079         10 V-VOL       PIC 9(3)       PACKED-DECIMAL.
00080      05 V-PROP-1       PIC 9(5)       PACKED-DECIMAL.
00081      05 V-PROP-2       PIC 9(5)       PACKED-DECIMAL.
00082      EJECT
00083  COPY HEXMNTFD02.
00084      EJECT
00085  FD  DETFILE
00086      BLOCK  CONTAINS 0   RECORDS
00087      RECORD CONTAINS 133 CHARACTERS
00088      LABEL  RECORDS  ARE STANDARD
00089      RECORDING MODE  IS  F.
00090  01  DET-REC           PIC X(133).
00091      SKIP3
00092  FD  ERRFILE
00093      BLOCK  CONTAINS 0   RECORDS
00094      RECORD CONTAINS 133 CHARACTERS
00095      LABEL  RECORDS  ARE STANDARD
00096      RECORDING MODE  IS  F.
00097  01  ERR-REC           PIC X(133).
00098      EJECT
00099  WORKING-STORAGE SECTION.
00100  77  LINE-CT           PIC S9(3) PACKED-DECIMAL VALUE +60.
00101  77  ERR-LINE-CT       PIC S9(3) PACKED-DECIMAL VALUE +60.
00102  77  PAGE-CT           PIC S9(3) PACKED-DECIMAL VALUE +0.
00103  77  ERR-PAGE-CT       PIC S9(3) PACKED-DECIMAL VALUE +0.
00104  77  READ-CT           PIC S9(5) PACKED-DECIMAL VALUE +0.
00105  77  REJ-CT            PIC S9(5) PACKED-DECIMAL VALUE +0.
00106  77  WRIT-CT           PIC S9(5) PACKED-DECIMAL VALUE +0.
00107  77  ERR-CT            PIC S9(3) PACKED-DECIMAL VALUE +0.
00108  77  REC-SW            PIC S9         VALUE +3.
00109      88 GOOD-REC       VALUE +0.
00110      88 DUP            VALUE +1 +2.
00111      88 NEW-KEY        VALUE +3.
00112  77  EDIT-ERR-SW       PIC X          VALUE '0'.
00113      88 EDIT-ERR       VALUE '1'.
00114  77  ERR-SW            PIC X          VALUE '0'.
00115      88 ERR            VALUE '1'.
00116  77  VSAM-ERR-SW       PIC X          VALUE '0'.
00117      88 VSAM-ERR       VALUE '1'.
00118  77  VOL-EOF-SW        PIC X          VALUE '0'.
00119      88 VOL-EOF        VALUE '1'.
00120  77  EOF-SW            PIC X          VALUE '0'.
00121      88 EOF            VALUE '1'.
00122  77  EOD-SW            PIC X          VALUE '0'.
00123      88 EOD            VALUE '1'.
00124  77  VERIFY-ACTION-CODE PIC X.
00125      88 VALID-ACTION-CODE   VALUE 'A' 'C' 'D'.
00126      88 ADD-REC             VALUE 'A'.
00127      88 DEL-REC             VALUE 'D'.
00128  77  VERIFY-TWN        PIC 9(2).
00129      88 VALID-TWN      VALUE 10 THRU 39   70 THRU 77.
00130  77  VERIFY-VOL        PIC 9(3).
00131      88 VALID-VOL      VALUE 001 THRU 601.
00132
00133  77  WK-TOWN             PIC 9(2).
00134      88  CITY-TOWN   VALUE 70 71 72 73 74 75 76 77.
00135      88  NORTHWEST   VALUE 10 16 17 18 20 22 23 24 25 26 29 35 38.
00136      88  SOUTHWEST   VALUE 11 12 13 14 15 19 21 27 28 30 31 32 33
00137                            34 36 37 39.
00138
00139  77  WK-STATUS           PIC X(2).
00140      88  VALID-SS    VALUE 'HS' 'LS' 'SS'.
00141
00142  77  VERIFY-CLASS      PIC 9(3).
00143      88  VALID-CLASS   VALUE 190
00144          201 THRU 213  224 225 234 236 278
00145          288 290 294 295 297 299
00146          301  313 THRU 315  318 THRU 321
00147          390 391 396 397 399
00148          401 413 THRU 423 426 THRU 433
00149          435 480 481 483 487
00150          489 THRU 493 496 497
00151          499
00152          501  516  517  522  523
00153          526 THRU 533 535 580 581 583 587
00154          589 THRU 593
00155          597  599
00156          663 670 671 673 677 679 680 681
00157          683 687 689 693
00158          701 716 717 722 723 726 THRU 733
00159          735 790 791 792 797 799
00160          801 816 817 822 823 826 THRU 833
00161          835 880 881 883 887 889 THRU 893
00162          897 899
00163          900 901 913 THRU 915 918 THRU 921
00164          959 990 991 996 997.
00165  77  SAVE-MSG          PIC X(31).
00166      EJECT
00167  01  VERIFY-OCCFAC.
00168      05  FILLER        PIC 9(3)V9.
00169      88  VALID-OCCFAC  VALUE 000.1 THRU 100.0.
00170  01  WORK-KEY.
00171      05 W-TWN          PIC 9(3).
00172      05 W-VOL          PIC 9(3).
00173      SKIP1
00174  01  VOL-WORK-KEY.
00175      05 VW-TWN         PIC 9(3)       VALUE 0.
00176      05 VW-VOL         PIC 9(3)       VALUE 0.
00177      SKIP1
00178  01  WORK-PROP.
00179      05 W-PROP-5       PIC 9(5).
00180      05 FILLER         PIC 9(9).
00181      EJECT
00182  01  WORK-AREA.
00183      05  WS-DATE.
00184          10  YY                      PIC 9(2)    VALUE ZEROS.
00185          10  MM                      PIC 9(2)    VALUE ZEROS.
00186          10  DD                      PIC 9(2)    VALUE ZEROS.
00187      05  WS-DATE-MMDDYY.
00188          10  MM                      PIC 9(2)    VALUE ZEROS.
00189          10  FILLER                  PIC X       VALUE '/'.
00190          10  DD                      PIC 9(2)    VALUE ZEROS.
00191          10  FILLER                  PIC X       VALUE '/'.
00192          10  YY                      PIC 9(2)    VALUE ZEROS.
00193      05  FILE-STATUS       PIC 9(2)   VALUE 0.
00194          88 GOOD-STATUS    VALUE 00.
00195      05  FILE-STATUS-2                          BINARY.
00196          10 FILE2-RETURN   PIC 99     VALUE 0.
00197          10 FILE2-FUNCTION PIC 9      VALUE 0.
00198          10 FILE2-FEEDBACK PIC 999    VALUE 0.
00199  01  HDR-1.
00200      05 FILLER         PIC X.
00201      05 FILLER         PIC X(2)  VALUE SPACE.
00202      05 H1-DATE        PIC X(8).
00203      05 FILLER         PIC X(44) VALUE SPACE.
00204      05 FILLER         PIC X(22) VALUE 'OFFICE OF THE ASSESSOR'.
00205      05 FILLER         PIC X(46) VALUE SPACE.
00206      05 FILLER         PIC X(5)  VALUE 'PAGE '.
00207      05 H1-PAGE        PIC ZZ9.
00208      SKIP1
00209  01  HDR-2.
00210      05 FILLER         PIC X.
00211      05 FILLER         PIC X(2)  VALUE SPACE.
00212      05 FILLER         PIC X(8)  VALUE 'ASREA863'.
00213      05 FILLER         PIC X(35) VALUE SPACE.
00214      05 FILLER         PIC X(39) VALUE
00215         'HOMEOWNERS HOMESTEAD MAINTENANCE REPORT'.
00216      SKIP1
00217  01  HDR-3.
00218      05 FILLER PIC X.
00219      05 FILLER PIC X(23) VALUE   ' ACT TN VOL   PROPERTY '.
00220      05 FILLER PIC X(24) VALUE   'NUMBER  CLASS PRO-RATN  '.
00221      05 FILLER PIC X(16) VALUE   'COOP  YR   ASSES'.
00222      05 FILLER PIC X(25) VALUE   'SED  OCCP    NAME/ADDRESS'.
00223      SKIP1
00224  01  HDR-4.
00225      05 FILLER PIC X.
00226      05 FILLER PIC X(31) VALUE   SPACE.
00227      05 FILLER PIC X(20) VALUE   'MA-MI  FACTOR   QNTY'.
00228      05 FILLER PIC X(21) VALUE   '  AP  VALUATION  FACT'.
00229      05 FILLER PIC X(27) VALUE   SPACE.
00230      05 FILLER PIC X(4)  VALUE   'NPHE'.
00231      SKIP1
00232  01  DET.
00233      05 FILLER         PIC X.
00234      05 FILLER         PIC XX         VALUE SPACE.
00235      05 D-ACT          PIC X.
00236      05 FILLER         PIC XX         VALUE SPACE.
00237      05 D-TWN          PIC 9(2).
00238      05 FILLER         PIC X          VALUE SPACE.
00239      05 D-VOL          PIC 9(3).
00240      05 FILLER         PIC X          VALUE SPACE.
00241      05 D-PROP.
00242         10 D-PROP-1    PIC 9(2).
00243         10 FILLER      PIC X          VALUE '-'.
00244         10 D-PROP-2    PIC 9(2).
00245         10 FILLER      PIC X          VALUE '-'.
00246         10 D-PROP-3    PIC 9(3).
00247         10 FILLER      PIC X          VALUE '-'.
00248         10 D-PROP-4    PIC 9(3).
00249         10 FILLER      PIC X          VALUE '-'.
00250         10 D-PROP-5    PIC 9(4).
00251      05 FILLER         PIC XX         VALUE SPACE.
00252      05 D-CLS.
00253         10 D-MA        PIC 9.
00254         10 FILLER      PIC X          VALUE '-'.
00255         10 D-MI        PIC 9(2).
00256      05 FILLER         PIC X          VALUE SPACE.
00257      05 D-PRO          PIC 9.999999.
00258      05 D-PRO-X REDEFINES D-PRO PIC X(8).
00259      05 FILLER         PIC X          VALUE SPACE.
00260      05 D-COOP         PIC ZZ,ZZ9.
00261      05 D-COOP-X REDEFINES D-COOP PIC X(6).
00262      05 FILLER         PIC X          VALUE SPACE.
00263      05 D-YR           PIC 9(2).
00264      05 D-YR-X REDEFINES D-YR PIC X(2).
00265      05 FILLER         PIC X          VALUE SPACE.
00266      05 D-AVAL         PIC ZZZ,ZZZ,ZZ9.
00267      05 D-AVAL-X REDEFINES D-AVAL PIC X(11).
00268      05 FILLER         PIC X          VALUE SPACE.
00269      05 D-OCCFAC       PIC Z99.9.
00270      05 D-OCCFAC-X REDEFINES D-OCCFAC PIC X(5).
00271      05 FILLER         PIC X          VALUE SPACE.
00272      05 D-NAME         PIC X(22).
00273      SKIP1
00274  01  DET-2.
00275      05 FILLER PIC X(75) VALUE   SPACE.
00276      05 D2-ADR PIC X(22).
00277      SKIP1
00278  01  DET-3.
00279      05 FILLER PIC X(75) VALUE   SPACE.
00280      05 D3-CTY PIC X(12).
00281      05 FILLER PIC X     VALUE   SPACE.
00282      05 D3-ST  PIC XX.
00283      05 FILLER PIC X     VALUE   SPACE.
00284      05 D3-ZIP PIC 9(5).
00285      05 D3-ZIP-X REDEFINES D3-ZIP PIC X(5).
00286      05 FILLER PIC X     VALUE   SPACE.
00287      05 D3-NPHE-EXAMT PIC Z,ZZZ,ZZ9.
00288      05 D3-NPHE-EXAMT-X REDEFINES D3-NPHE-EXAMT PIC X(9).
00289      05 FILLER PIC X     VALUE   SPACE.
00290      05 D3-NPHE-BSYR PIC 9(4).
00291      05 D3-NPHE-BSYR-X REDEFINES D3-NPHE-BSYR PIC X(4).
00292      EJECT
00293  01  ERR-HDR-2.
00294      05 FILLER         PIC X.
00295      05 FILLER         PIC X(2)  VALUE SPACE.
00296      05 FILLER         PIC X(8)  VALUE 'ASREA863'.
00297      05 FILLER         PIC X(32) VALUE SPACE.
00298      05 FILLER         PIC X(44) VALUE
00299         'HOMEOWNER HOMESTEAD MAINTENANCE ERROR REPORT'.
00300         SKIP1
00301  01  ERR-HDR-3.
00302      05 FILLER PIC X.
00303      05 FILLER PIC X(23) VALUE   ' ACT TN VOL   PROPERTY '.
00304      05 FILLER PIC X(24) VALUE   'NUMBER  CLASS PRO-RATN  '.
00305      05 FILLER PIC X(21) VALUE   'COOP  YR  ASSESSED   '.
00306      05 FILLER PIC X(20) VALUE   'OCCP    NAME/ADDRESS'.
00307         SKIP1
00308  01  ERR-HDR-4.
00309      05 FILLER PIC X.
00310      05 FILLER PIC X(31) VALUE   SPACE.
00311      05 FILLER PIC X(22) VALUE   'MA-MI  FACTOR   QNTY  '.
00312      05 FILLER PIC X(19) VALUE   'AP  VALUATION  FACT'.
00313      05 FILLER PIC X(27) VALUE   SPACE.
00314      05 FILLER PIC X(13) VALUE   'ERROR MESSAGE'.
00315         SKIP1
00316  01  ERR-LINE.
00317      05 FILLER         PIC X.
00318      05 FILLER         PIC XX         VALUE SPACE.
00319      05 E-ACT          PIC X.
00320      05 FILLER         PIC XX         VALUE SPACE.
00321      05 E-TWN          PIC X(2).
00322      05 FILLER         PIC X          VALUE SPACE.
00323      05 E-VOL          PIC X(3).
00324      05 FILLER         PIC X          VALUE SPACE.
00325      05 E-PROP.
00326         10 E-PROP-1    PIC X(2).
00327         10 FILLER      PIC X          VALUE '-'.
00328         10 E-PROP-2    PIC X(2).
00329         10 FILLER      PIC X          VALUE '-'.
00330         10 E-PROP-3    PIC X(3).
00331         10 FILLER      PIC X          VALUE '-'.
00332         10 E-PROP-4    PIC X(3).
00333         10 FILLER      PIC X          VALUE '-'.
00334         10 E-PROP-5    PIC X(4).
00335      05 FILLER         PIC XX         VALUE SPACE.
00336      05 E-CLS.
00337         10 E-MA        PIC X.
00338         10 FILLER      PIC X          VALUE '-'.
00339         10 E-MI        PIC X(2).
00340      05 FILLER         PIC X          VALUE SPACE.
00341      05 E-PRO.
00342         10 E-PRO-1     PIC X.
00343         10 FILLER      PIC X          VALUE '.'.
00344         10 E-PRO-2     PIC X(6).
00345      05 FILLER         PIC X          VALUE SPACE.
00346      05 E-COOP.
00347         10 E-COOP-1    PIC X(2).
00348         10 FILLER      PIC X          VALUE ','.
00349         10 E-COOP-2    PIC X(3).
00350      05 FILLER         PIC X          VALUE SPACE.
00351      05 E-YR           PIC X(2).
00352      05 FILLER         PIC X          VALUE SPACE.
00353      05 E-AVAL.
00354         10 E-AVAL-1    PIC X(3).
00355         10 FILLER      PIC X          VALUE ','.
00356         10 E-AVAL-2    PIC X(3).
00357         10 FILLER      PIC X          VALUE ','.
00358         10 E-AVAL-3    PIC X(3).
00359      05 FILLER         PIC X          VALUE SPACE.
00360      05 E-OCCFAC.
00361         10 E-OCC-1     PIC XXX.
00362         10 FILLER      PIC X          VALUE '.'.
00363         10 E-OCC-2     PIC X.
00364      05 FILLER         PIC X          VALUE SPACE.
00365      05 E-NAME         PIC X(22).
00366      05 FILLER         PIC XXX        VALUE SPACE.
00367      05 E-MSG          PIC X(34).
00368         SKIP1
00369  01  ERR-LN2.
00370      05 FILLER PIC X(75) VALUE   SPACE.
00371      05 E2-ADR PIC X(22).
00372      05 FILLER PIC XXX   VALUE   SPACE.
00373      05 E2-MSG PIC X(34).
00374         SKIP1
00375  01  ERR-LN3.
00376      05 FILLER PIC X(62) VALUE   SPACE.
00377      05 E3-YR  PIC X(4)  VALUE   SPACE.
00378      05 FILLER PIC X(2)  VALUE   SPACE.
00379      05 E3-CTY PIC X(12).
00380      05 FILLER PIC X     VALUE   SPACE.
00381      05 E3-ST  PIC XX.
00382      05 FILLER PIC X     VALUE   SPACE.
00383      05 E3-ZIP PIC X(5).
00384      05 FILLER PIC X     VALUE   SPACE.
00385 *    05 E3-NPHE-EXAMT PIC X(5).
00386      05 E3-NPHE-EXAMT PIC X(7).
00387 *    05 FILLER PIC XXXX  VALUE   SPACE.
00388      05 FILLER PIC X     VALUE   SPACE.
00389      05 E3-NPHE-BSYR PIC X(4)  VALUE   SPACE.
00390      05 E3-MSG PIC X(31).
00391         SKIP1
00392  01  AUX.
00393      05 FILLER         PIC X.
00394      05 FILLER         PIC X(99) VALUE SPACE.
00395      05 A-MSG          PIC X(35).
00396         SKIP1
00397  01  TOT-1.
00398      05 FILLER         PIC X.
00399      05 FILLER         PIC X(2)  VALUE SPACE.
00400      05 FILLER         PIC X(31) VALUE
00401         'TOTAL UPDATE RECORDS READ'.
00402      05 T1-READ        PIC ZZ,ZZ9.
00403  01  TOT-2.
00404      05 FILLER         PIC X.
00405      05 FILLER         PIC X(2)  VALUE SPACE.
00406      05 FILLER         PIC X(31) VALUE
00407         'TOTAL UPDATE RECORDS REJECTED'.
00408      05 T2-REJ         PIC ZZ,ZZ9.
00409  01  TOT-3.
00410      05 FILLER         PIC X.
00411      05 FILLER         PIC X(2)  VALUE SPACE.
00412      05 FILLER         PIC X(31) VALUE
00413         'TOTAL UPDATE RECORDS WRITTEN'.
00414      05 T3-WRIT        PIC ZZ,ZZ9.
00415
00416
00417
00418
00419  01  FILLER.
00420  COPY STATETABLE.
00421      SKIP2
00422  PROCEDURE DIVISION.
00423      OPEN INPUT VOLFILE.
00424      IF NOT GOOD-STATUS
00425         DISPLAY 'VOLFILE OPEN ERR - FILE STATUS IS ' FILE-STATUS
00426         DISPLAY 'FILE STATUS FEEDBACK  ' FILE2-RETURN
00427           ' ' FILE2-FUNCTION  ' ' FILE2-FEEDBACK
00428         MOVE 16 TO RETURN-CODE
00429         STOP RUN.
00430      OPEN INPUT  INFILE
00431           OUTPUT HOMEXEMP
00432                  DETFILE
00433                  ERRFILE.
00434      ACCEPT WS-DATE FROM DATE.
00435      MOVE CORR WS-DATE TO WS-DATE-MMDDYY.
00436      MOVE WS-DATE-MMDDYY TO H1-DATE.
00437      PERFORM READ-INFILE THRU READ-INFILE-EXIT.
00438      IF EOF
00439          MOVE '1' TO EOD-SW.
00440      PERFORM MAIN THRU MAIN-EXIT
00441         UNTIL ERR OR EOD.
00442      IF ERR MOVE 16 TO RETURN-CODE.
00443      IF NOT VSAM-ERR
00444         CLOSE VOLFILE
00445            IF NOT GOOD-STATUS
00446               DISPLAY 'ASREA863'
00447               DISPLAY 'VOLFILE CLOSE ERR - FILE STATUS IS '
00448                  FILE-STATUS
00449               DISPLAY 'FILE STATUS FEEDBACK  ' FILE2-RETURN
00450                 ' ' FILE2-FUNCTION ' ' FILE2-FEEDBACK
00451               MOVE '1' TO ERR-SW
00452               MOVE 16 TO RETURN-CODE.
00453      IF NOT ERR
00454         PERFORM LIST-TOTALS THRU LIST-TOTALS-EXIT.
00455      CLOSE INFILE
00456            HOMEXEMP
00457            DETFILE
00458            ERRFILE.
00459      STOP RUN.
00460
00461  MAIN.
00462      PERFORM FETCH-INFILE THRU FETCH-INFILE-EXIT
00463         UNTIL GOOD-REC OR ERR OR EOD.
00464      IF GOOD-REC
00465         MOVE +3 TO REC-SW
00466         MOVE HE-ACT TO VERIFY-ACTION-CODE
00467         IF NOT VALID-ACTION-CODE
00468            MOVE 'ACTION FIELD INVALID' TO SAVE-MSG
00469            PERFORM  LIST-ERR THRU  LIST-ERR-EXIT
00470            MOVE '1' TO EDIT-ERR-SW
00471         ELSE
00472            PERFORM EDIT-TWN-VOL-PROP THRU EDIT-TWN-VOL-PROP-EXIT
00473            IF NOT EDIT-ERR
00474               IF DEL-REC
00475                  PERFORM LIST-AND-WRITE-DET
00476                     THRU LIST-AND-WRITE-DET-EXIT
00477               ELSE
00478                  MOVE HE-TWN TO W-TWN
00479                  MOVE HE-VOL TO W-VOL
00480                  PERFORM READ-VOLFILE THRU READ-VOLFILE-EXIT
00481                     UNTIL ERR
00482                        OR VOL-WORK-KEY NOT LESS WORK-KEY
00483                  PERFORM VERIFY-PROP THRU VERIFY-PROP-EXIT
00484                     IF ADD-REC
00485                        PERFORM EDIT-ADD THRU EDIT-ADD-EXIT
00486                        IF EDIT-ERR
00487                           NEXT SENTENCE
00488                        ELSE
00489                           PERFORM LIST-AND-WRITE-DET
00490                              THRU LIST-AND-WRITE-DET-EXIT
00491                     ELSE
00492                        PERFORM EDIT-CHANGE THRU EDIT-CHANGE-EXIT
00493                        IF NOT EDIT-ERR
00494                           PERFORM LIST-AND-WRITE-DET
00495                              THRU LIST-AND-WRITE-DET-EXIT.
00496      IF EOF
00497         MOVE '1' TO EOD-SW.
00498      IF EDIT-ERR
00499         ADD +1 TO REJ-CT
00500         IF ERR-CT LESS THAN 3
00501            PERFORM AUX-CHECK THRU AUX-CHECK-EXIT.
00502         MOVE 0 TO ERR-CT.
00503         MOVE '0' TO EDIT-ERR-SW.
00504  MAIN-EXIT.
00505      EXIT.
00506
00507
00508  FETCH-INFILE.
00509      IF NEW-KEY
00510         MOVE +0 TO REC-SW
00511      ELSE
00512         IF DUP
00513            MOVE 'DUPLICATE RECORD' TO SAVE-MSG
00514            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00515            ADD +1 TO REJ-CT
00516            IF NOT EOF
00517               SUBTRACT +1 FROM REC-SW
00518            ELSE
00519               MOVE '1' TO EOD-SW.
00520      IF NOT EOF
00521         MOVE IN-REC TO HOMEXEMP-REC
00522         PERFORM READ-INFILE THRU READ-INFILE-EXIT.
00523      IF NOT EOF
00524         IF IN-KEY EQUAL HE-KEY
00525            MOVE +2 TO REC-SW
00526         ELSE
00527            IF IN-KEY LESS HE-KEY
00528               DISPLAY 'ASREA863'
00529               DISPLAY 'SEQ ERR INFILE'
00530               DISPLAY 'TWN VOL PROP ' IN-KEY
00531               DISPLAY 'PREVIOUS     ' HE-KEY
00532               MOVE '1' TO ERR-SW.
00533  FETCH-INFILE-EXIT.
00534      EXIT.
00535
00536
00537  READ-INFILE.
00538      READ INFILE
00539         AT END
00540            MOVE '1' TO EOF-SW.
00541      IF NOT EOF
00542         ADD +1 TO READ-CT.
00543  READ-INFILE-EXIT.
00544      EXIT.
00545
00546
00547
00548
00549  READ-VOLFILE.
00550      READ VOLFILE
00551         AT END
00552            DISPLAY 'ASREA863'
00553            DISPLAY 'EOF ON VOLFILE'
00554            MOVE '1' TO VOL-EOF-SW ERR-SW EDIT-ERR-SW.
00555      IF NOT VOL-EOF
00556         IF NOT GOOD-STATUS
00557            DISPLAY 'ASREA863'
00558            DISPLAY 'VOLFILE READ ERR - FILE STATUS IS '
00559               FILE-STATUS
00560            DISPLAY 'FILE STATUS FEEDBACK  ' FILE2-RETURN
00561             ' ' FILE2-FUNCTION ' ' FILE2-FEEDBACK
00562            MOVE '1' TO ERR-SW EDIT-ERR-SW VSAM-ERR-SW
00563         ELSE
00564            MOVE V-TWN TO VW-TWN
00565            MOVE V-VOL TO VW-VOL.
00566  READ-VOLFILE-EXIT.
00567      EXIT.
00568
00569
00570
00571
00572  EDIT-TWN-VOL-PROP.
00573      MOVE HE-TWN TO VERIFY-TWN.
00574      IF HE-TWN NOT NUMERIC
00575         OR NOT VALID-TWN
00576            MOVE 'TOWN NO. INVALID' TO SAVE-MSG
00577            PERFORM  LIST-ERR THRU  LIST-ERR-EXIT
00578            MOVE '1' TO EDIT-ERR-SW.
00579 *4/5/13 BEGIN
00580      MOVE HE-TWN TO WK-TOWN.
00581      MOVE HE-STATUS TO WK-STATUS.
00582 *    IF CITY-TOWN AND VALID-SS
00583 *    IF NORTHWEST AND VALID-SS
00584      IF SOUTHWEST AND VALID-SS
00585         CONTINUE
00586      ELSE
00587         IF VALID-SS
00588            MOVE 'ONLY NWWS TOWNS ELIGIBLE FOR SS '  TO SAVE-MSG
00589            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00590            MOVE '1' TO EDIT-ERR-SW.
00591 *4/5/13 END
00592      MOVE HE-VOL TO VERIFY-VOL.
00593      IF HE-VOL NOT NUMERIC
00594         OR NOT VALID-VOL
00595            MOVE 'VOLUME INVALID' TO SAVE-MSG
00596            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00597            MOVE '1' TO EDIT-ERR-SW.
00598      IF HE-PROP NOT NUMERIC
00599         OR HE-PROP NOT GREATER ZERO
00600            MOVE 'PROPERTY NO. INVALID' TO SAVE-MSG
00601            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00602            MOVE '1' TO EDIT-ERR-SW.
00603  EDIT-TWN-VOL-PROP-EXIT.
00604      EXIT.
00605
00606
00607  VERIFY-PROP.
00608      IF NOT ERR
00609         IF VOL-WORK-KEY NOT EQUAL WORK-KEY
00610            MOVE 'TOWN VOL AND/OR PROP NO. INVALID' TO SAVE-MSG
00611            PERFORM  LIST-ERR THRU  LIST-ERR-EXIT
00612            MOVE '1' TO EDIT-ERR-SW
00613         ELSE
00614            MOVE HE-PROP TO WORK-PROP
00615            IF W-PROP-5 LESS V-PROP-1
00616               OR W-PROP-5 GREATER V-PROP-2
00617                  MOVE 'TOWN VOL AND/OR PROP NO. INVALID'
00618                     TO SAVE-MSG
00619                  PERFORM  LIST-ERR THRU  LIST-ERR-EXIT
00620                  MOVE '1' TO EDIT-ERR-SW.
00621  VERIFY-PROP-EXIT.
00622      EXIT.
00623
00624
00625  EDIT-ADD.
00626      MOVE HE-CLS TO VERIFY-CLASS.
00627      IF HE-CLS NOT NUMERIC
00628         OR NOT VALID-CLASS
00629            MOVE 'CLASS INVALID' TO SAVE-MSG
00630            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00631            MOVE '1' TO EDIT-ERR-SW.
00632 *    IF HE-PRO EQUAL SPACE
00633 *       NEXT SENTENCE
00634 *    ELSE
00635         IF HE-PRO NUMERIC
00636            AND HE-PRO NOT < .000001
00637               NEXT SENTENCE
00638         ELSE
00639            MOVE 'PRO-RATION INVALID' TO SAVE-MSG
00640            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00641            MOVE '1' TO EDIT-ERR-SW.
00642 *    IF HE-COOP EQUAL SPACE
00643 *       NEXT SENTENCE
00644 *    ELSE
00645         IF HE-COOP NUMERIC
00646            AND HE-COOP NOT LESS 1
00647               NEXT SENTENCE
00648         ELSE
00649            MOVE 'COOP QUANTITY INVALID' TO SAVE-MSG
00650            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00651            MOVE '1' TO EDIT-ERR-SW.
00652      IF HE-AVAL NUMERIC
00653         AND HE-AVAL GREATER ZERO
00654            NEXT SENTENCE
00655      ELSE
00656         MOVE 'ASSESSED VALUATION INVALID' TO SAVE-MSG
00657         PERFORM LIST-ERR THRU LIST-ERR-EXIT
00658         MOVE '1' TO EDIT-ERR-SW.
00659      IF HE-EQVAL NUMERIC
00660 *       AND HE-EQVAL GREATER ZERO
00661            NEXT SENTENCE
00662      ELSE
00663         MOVE 'EQUALIZED VALUE INVALID' TO SAVE-MSG
00664         PERFORM LIST-ERR THRU LIST-ERR-EXIT
00665         MOVE '1' TO EDIT-ERR-SW.
00666      IF HE-YR NUMERIC
00667         AND HE-YR GREATER ZERO
00668            NEXT SENTENCE
00669      ELSE
00670         MOVE 'YEAR APPLIED INVALID' TO SAVE-MSG
00671         PERFORM LIST-ERR THRU LIST-ERR-EXIT
00672         MOVE '1' TO EDIT-ERR-SW.
00673 *    IF HE-OCCFAC EQUAL SPACE
00674 *       NEXT SENTENCE
00675 *    ELSE
00676 *       MOVE HE-OCCFAC TO VERIFY-OCCFAC
00677 *       IF HE-OCCFAC NUMERIC
00678 *          AND VALID-OCCFAC
00679 *          NEXT SENTENCE
00680 *       ELSE
00681 *          MOVE 'OCCUPANCY FACTOR INVALID' TO SAVE-MSG
00682 *          PERFORM LIST-ERR THRU LIST-ERR-EXIT
00683 *          MOVE '1' TO EDIT-ERR-SW.
00684      IF HE-ZIP EQUAL SPACE
00685         NEXT SENTENCE
00686      ELSE
00687         IF HE-ZIP NUMERIC
00688            AND HE-ZIP GREATER THAN ZERO
00689               NEXT SENTENCE
00690         ELSE
00691            MOVE 'ZIP CODE INVALID' TO SAVE-MSG
00692            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00693            MOVE '1' TO EDIT-ERR-SW.
00694      IF HE-STATE EQUAL TO SPACE
00695         NEXT SENTENCE
00696      ELSE
00697         SEARCH ALL ST-ENTRY
00698            AT END
00699              MOVE 'STATE FIELD INVALID' TO SAVE-MSG
00700              PERFORM LIST-ERR THRU LIST-ERR-EXIT
00701              MOVE '1' TO EDIT-ERR-SW
00702          WHEN ST-ABBR (X) EQUAL TO HE-STATE NEXT SENTENCE.
00703      IF HE-NPHE-EXAMT-R = SPACES
00704         NEXT SENTENCE
00705      ELSE
00706         IF HE-NPHE-EXAMT NUMERIC
00707            NEXT SENTENCE
00708         ELSE
00709            MOVE 'NPHE EXEMPTION AMOUNT INVALID' TO SAVE-MSG
00710            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00711            MOVE '1' TO EDIT-ERR-SW.
00712 *
00713      IF HE-NPHE-BSYR-R = SPACES
00714         NEXT SENTENCE
00715      ELSE
00716         IF HE-NPHE-BSYR  NUMERIC
00717            NEXT SENTENCE
00718         ELSE
00719            MOVE 'NPHE BASE YEAR INVALID' TO SAVE-MSG
00720            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00721            MOVE '1' TO EDIT-ERR-SW.
00722 *
00723      IF HE-YR-R = SPACES
00724         NEXT SENTENCE
00725      ELSE
00726         IF HE-YR NUMERIC
00727            NEXT SENTENCE
00728         ELSE
00729            MOVE 'BASE YEAR INVALID' TO SAVE-MSG
00730            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00731            MOVE '1' TO EDIT-ERR-SW.
00732 *
00733      IF HE-AVAL-R = SPACES
00734         NEXT SENTENCE
00735      ELSE
00736         IF HE-AVAL-R NUMERIC
00737            NEXT SENTENCE
00738         ELSE
00739            MOVE 'ASSESSED VALUE INVALID' TO SAVE-MSG
00740            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00741            MOVE '1' TO EDIT-ERR-SW.
00742      IF HE-EQVAL-R = SPACES
00743         NEXT SENTENCE
00744      ELSE
00745         IF HE-EQVAL-R NUMERIC
00746            NEXT SENTENCE
00747         ELSE
00748            MOVE 'EQUALIZED VALUE INVALID' TO SAVE-MSG
00749            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00750            MOVE '1' TO EDIT-ERR-SW.
00751 *
00752  EDIT-ADD-EXIT.
00753      EXIT.
00754
00755
00756  EDIT-CHANGE.
00757      IF HE-CLS EQUAL SPACE
00758         NEXT SENTENCE
00759      ELSE
00760         MOVE HE-CLS TO VERIFY-CLASS
00761         IF HE-CLS NOT NUMERIC
00762            OR NOT VALID-CLASS
00763               MOVE 'CLASS INVALID' TO SAVE-MSG
00764               PERFORM LIST-ERR THRU LIST-ERR-EXIT
00765               MOVE '1' TO EDIT-ERR-SW.
00766      IF HE-PRO-R EQUAL SPACE
00767         NEXT SENTENCE
00768      ELSE
00769        IF HE-PRO NUMERIC
00770           AND HE-PRO NOT < .000001
00771               NEXT SENTENCE
00772         ELSE
00773            MOVE 'PRO-RATION INVALID' TO SAVE-MSG
00774            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00775            MOVE '1' TO EDIT-ERR-SW.
00776      IF HE-COOP EQUAL SPACE
00777         NEXT SENTENCE
00778      ELSE
00779         IF HE-COOP NUMERIC
00780            AND HE-COOP NOT LESS 1
00781              NEXT SENTENCE
00782        ELSE
00783           MOVE 'COOP QUANTITY INVALID' TO SAVE-MSG
00784           PERFORM LIST-ERR THRU LIST-ERR-EXIT
00785           MOVE '1' TO EDIT-ERR-SW.
00786      IF HE-AVAL EQUAL SPACE
00787         NEXT SENTENCE
00788      ELSE
00789         IF HE-AVAL NUMERIC
00790 * *        AND HE-AVAL GREATER ZERO
00791               NEXT SENTENCE
00792         ELSE
00793            MOVE 'ASSESSED VALUATION INVALID' TO SAVE-MSG
00794            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00795            MOVE '1' TO EDIT-ERR-SW.
00796      IF HE-EQVAL EQUAL SPACE
00797         NEXT SENTENCE
00798      ELSE
00799         IF HE-EQVAL NUMERIC
00800 *          AND HE-EQVAL GREATER ZERO
00801               NEXT SENTENCE
00802         ELSE
00803            MOVE 'EQUALIZED VALUE INVALID' TO SAVE-MSG
00804            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00805            MOVE '1' TO EDIT-ERR-SW.
00806      IF HE-YR EQUAL SPACE
00807         OR HE-YR NUMERIC
00808           NEXT SENTENCE
00809      ELSE
00810         MOVE 'YEAR APPLIED INVALID' TO SAVE-MSG
00811         PERFORM LIST-ERR THRU LIST-ERR-EXIT
00812         MOVE '1' TO EDIT-ERR-SW.
00813 *    IF HE-OCCFAC-R EQUAL SPACE
00814 *       NEXT SENTENCE
00815 *    ELSE
00816 *       MOVE HE-OCCFAC TO VERIFY-OCCFAC
00817 *       IF HE-OCCFAC NUMERIC
00818 *          AND VALID-OCCFAC
00819 *          NEXT SENTENCE
00820 *       ELSE
00821 *          MOVE 'OCCUPANCY FACTOR INVALID' TO SAVE-MSG
00822 *          PERFORM LIST-ERR THRU LIST-ERR-EXIT
00823 *          MOVE '1' TO EDIT-ERR-SW.
00824      IF HE-ZIP EQUAL SPACE
00825         NEXT SENTENCE
00826      ELSE
00827         IF HE-ZIP NUMERIC
00828            AND HE-ZIP GREATER THAN ZERO
00829               NEXT SENTENCE
00830         ELSE
00831            MOVE 'ZIP CODE INVALID' TO SAVE-MSG
00832            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00833            MOVE '1' TO EDIT-ERR-SW.
00834      IF HE-STATE EQUAL TO SPACE
00835         NEXT SENTENCE
00836      ELSE
00837         SEARCH ALL ST-ENTRY
00838            AT END
00839              MOVE 'STATE FIELD INVALID' TO SAVE-MSG
00840              PERFORM LIST-ERR THRU LIST-ERR-EXIT
00841              MOVE '1' TO EDIT-ERR-SW
00842            WHEN ST-ABBR (X) EQUAL TO HE-STATE NEXT SENTENCE.
00843      IF HE-NPHE-EXAMT-R = SPACE
00844         NEXT SENTENCE
00845      ELSE
00846         IF HE-NPHE-EXAMT NUMERIC
00847            NEXT SENTENCE
00848         ELSE
00849            MOVE 'NPHE EXEMPTION AMOUNT INVALID' TO SAVE-MSG
00850            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00851            MOVE '1' TO EDIT-ERR-SW.
00852 *
00853      IF HE-NPHE-BSYR-R = SPACE
00854         NEXT SENTENCE
00855      ELSE
00856         IF HE-NPHE-BSYR NUMERIC
00857            NEXT SENTENCE
00858         ELSE
00859            MOVE 'NPHE BASE YEAR INVALID' TO SAVE-MSG
00860            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00861            MOVE '1' TO EDIT-ERR-SW.
00862 *
00863      IF HE-YR-R = SPACE
00864         NEXT SENTENCE
00865      ELSE
00866         IF HE-YR NUMERIC
00867            NEXT SENTENCE
00868         ELSE
00869            MOVE 'BASE YEAR INVALID' TO SAVE-MSG
00870            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00871            MOVE '1' TO EDIT-ERR-SW.
00872 *
00873      IF HE-AVAL-R = SPACES
00874         NEXT SENTENCE
00875      ELSE
00876         IF HE-AVAL-R NUMERIC
00877            NEXT SENTENCE
00878         ELSE
00879            MOVE 'ASSESSED VALUE INVALID' TO SAVE-MSG
00880            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00881            MOVE '1' TO EDIT-ERR-SW.
00882      IF HE-EQVAL-R = SPACES
00883         NEXT SENTENCE
00884      ELSE
00885         IF HE-EQVAL-R NUMERIC
00886            NEXT SENTENCE
00887         ELSE
00888            MOVE 'EQUALIZED VALUE INVALID' TO SAVE-MSG
00889            PERFORM LIST-ERR THRU LIST-ERR-EXIT
00890            MOVE '1' TO EDIT-ERR-SW.
00891 *
00892  EDIT-CHANGE-EXIT.
00893      EXIT.
00894
00895
00896  LIST-AND-WRITE-DET.
00897      IF LINE-CT GREATER +55
00898         PERFORM HDG THRU HDG-EXIT.
00899      MOVE HE-ACT    TO D-ACT.
00900      MOVE HE-TWN    TO D-TWN.
00901      MOVE HE-VOL    TO D-VOL.
00902      MOVE HE-PROP-1 TO D-PROP-1.
00903      MOVE HE-PROP-2 TO D-PROP-2.
00904      MOVE HE-PROP-3 TO D-PROP-3.
00905      MOVE HE-PROP-4 TO D-PROP-4.
00906      MOVE HE-PROP-5 TO D-PROP-5.
00907      IF HE-CLS NOT EQUAL SPACE
00908         MOVE HE-MA TO D-MA
00909         MOVE HE-MI TO D-MI
00910      ELSE
00911         MOVE ' -  ' TO D-CLS.
00912      IF HE-PRO-R NOT EQUAL SPACE
00913         MOVE HE-PRO TO D-PRO
00914      ELSE
00915         MOVE ' .      ' TO D-PRO-X.
00916      IF HE-COOP NOT EQUAL SPACE
00917         MOVE HE-COOP TO D-COOP
00918      ELSE
00919         MOVE SPACE TO D-COOP-X.
00920      IF HE-YR NOT EQUAL SPACE
00921         MOVE HE-YR TO D-YR
00922      ELSE
00923         MOVE SPACE TO D-YR-X.
00924      IF HE-AVAL NOT EQUAL SPACE
00925         MOVE HE-AVAL TO D-AVAL
00926      ELSE
00927         MOVE SPACE TO D-AVAL-X.
00928 *    IF HE-EQVAL NOT EQUAL SPACE
00929 *       MOVE HE-EQVAL TO D-EQVAL
00930 *    ELSE
00931 *       MOVE SPACE TO D-EQVAL-X.
00932      IF HE-OCCFAC-R NOT EQUAL SPACE
00933         MOVE HE-OCCFAC TO D-OCCFAC
00934      ELSE
00935         MOVE '   . ' TO D-OCCFAC-X.
00936      MOVE HE-NAME TO D-NAME.
00937      WRITE DET-REC FROM DET
00938         AFTER ADVANCING   2.
00939      MOVE HE-ADDR TO D2-ADR
00940      WRITE DET-REC FROM DET-2
00941         AFTER ADVANCING   1.
00942      MOVE HE-CITY TO D3-CTY
00943      MOVE HE-STATE TO D3-ST
00944      IF HE-ZIP NOT EQUAL TO SPACE
00945         MOVE HE-ZIP TO D3-ZIP
00946      ELSE
00947         MOVE SPACE TO D3-ZIP-X.
00948      IF HE-NPHE-EXAMT-R NOT EQUAL SPACE
00949         MOVE HE-NPHE-EXAMT TO D3-NPHE-EXAMT
00950      ELSE
00951         MOVE SPACES  TO D3-NPHE-EXAMT-X.
00952      IF HE-NPHE-BSYR-R NOT EQUAL SPACE
00953         MOVE HE-NPHE-BSYR TO D3-NPHE-BSYR
00954         IF HE-NPHE-BSYR > ZEROS
00955            MOVE HE-NPHE-BSYR (3:2) TO HE-YR
00956         END-IF
00957      ELSE
00958         MOVE SPACES  TO D3-NPHE-BSYR-X.
00959      WRITE DET-REC FROM DET-3
00960         AFTER ADVANCING   1.
00961      ADD +4 TO LINE-CT.
00962      WRITE HOMEXEMP-REC.
00963      ADD +1 TO WRIT-CT.
00964  LIST-AND-WRITE-DET-EXIT.
00965      EXIT.
00966      SKIP3
00967  LIST-ERR.
00968      ADD +1 TO ERR-CT.
00969      IF EDIT-ERR-SW EQUAL '0'
00970         PERFORM PRINT-ERR THRU PRINT-ERR-EXIT
00971      ELSE
00972         PERFORM LIST-AUX THRU LIST-AUX-EXIT.
00973  LIST-ERR-EXIT.
00974      EXIT.
00975      SKIP3
00976  PRINT-ERR.
00977      IF ERR-LINE-CT GREATER +55
00978         PERFORM ERR-HDG THRU ERR-HDG-EXIT.
00979      MOVE HE-ACT    TO E-ACT.
00980      MOVE HE-TWN    TO E-TWN.
00981      MOVE HE-VOL    TO E-VOL.
00982      MOVE HE-PROP-1 TO E-PROP-1.
00983      MOVE HE-PROP-2 TO E-PROP-2.
00984      MOVE HE-PROP-3 TO E-PROP-3.
00985      MOVE HE-PROP-4 TO E-PROP-4.
00986      MOVE HE-PROP-5 TO E-PROP-5.
00987      MOVE HE-MA     TO E-MA.
00988      MOVE HE-MI     TO E-MI.
00989      MOVE HE-PRO-1  TO E-PRO-1.
00990      MOVE HE-PRO-2  TO E-PRO-2.
00991      MOVE HE-COOP-1 TO E-COOP-1.
00992      MOVE HE-COOP-2 TO E-COOP-2.
00993      MOVE HE-YR     TO E-YR.
00994      MOVE HE-AVAL-1 TO E-AVAL-1.
00995      MOVE HE-AVAL-2 TO E-AVAL-2.
00996      MOVE HE-AVAL-3 TO E-AVAL-3.
00997 *    MOVE HE-EQVAL-1 TO E-EQVAL-1.
00998 *    MOVE HE-EQVAL-2 TO E-EQVAL-2.
00999 *    MOVE HE-EQVAL-3 TO E-EQVAL-3.
01000      MOVE HE-OCC-1  TO E-OCC-1.
01001      MOVE HE-OCC-2  TO E-OCC-2.
01002      MOVE HE-NAME TO E-NAME.
01003      MOVE SAVE-MSG TO E-MSG.
01004      WRITE ERR-REC FROM ERR-LINE
01005         AFTER ADVANCING   2.
01006      ADD +2 TO ERR-LINE-CT.
01007      MOVE SPACES TO ERR-LN2 ERR-LN3
01008      MOVE HE-ADDR TO E2-ADR.
01009      MOVE HE-CITY TO E3-CTY.
01010      MOVE HE-STATE TO E3-ST.
01011      MOVE HE-ZIP TO E3-ZIP.
01012      MOVE HE-NPHE-EXAMT-R TO E3-NPHE-EXAMT.
01013      MOVE HE-NPHE-BSYR-R  TO E3-NPHE-BSYR.
01014      MOVE HE-YR-R TO E3-YR.
01015  PRINT-ERR-EXIT.
01016      EXIT.
01017
01018
01019  LIST-AUX.
01020      IF ERR-CT EQUAL 2 OR 3
01021          NEXT SENTENCE
01022      ELSE
01023          PERFORM LIST-MORE-ERRORS.
01024      IF ERR-CT EQUAL 2
01025          MOVE SAVE-MSG TO E2-MSG
01026          WRITE ERR-REC FROM ERR-LN2
01027             AFTER ADVANCING   1
01028          ADD +1 TO ERR-LINE-CT.
01029      IF ERR-CT EQUAL 3
01030          MOVE SAVE-MSG TO E3-MSG
01031          WRITE ERR-REC FROM ERR-LN3
01032             AFTER ADVANCING   1
01033          ADD +1 TO ERR-LINE-CT.
01034  LIST-AUX-EXIT.
01035      EXIT.
01036
01037
01038  LIST-MORE-ERRORS.
01039      IF ERR-LINE-CT GREATER +55
01040          PERFORM ERR-HDG THRU ERR-HDG-EXIT.
01041      MOVE SAVE-MSG TO A-MSG.
01042      WRITE ERR-REC FROM AUX
01043          AFTER ADVANCING   1.
01044      ADD +1 TO ERR-LINE-CT.
01045  LIST-MORE-EXIT.
01046      EXIT.
01047      SKIP2
01048  AUX-CHECK.
01049      IF ERR-CT LESS THAN 2
01050          WRITE ERR-REC FROM ERR-LN2
01051              AFTER ADVANCING   1
01052          ADD +1 TO ERR-LINE-CT.
01053      IF ERR-CT LESS THAN 3
01054          WRITE ERR-REC FROM ERR-LN3
01055              AFTER ADVANCING   1
01056          ADD +1 TO ERR-LINE-CT.
01057  AUX-CHECK-EXIT.
01058      EXIT.
01059      SKIP2
01060  LIST-TOTALS.
01061      IF LINE-CT GREATER +53
01062         PERFORM HDG THRU HDG-EXIT.
01063      IF ERR-LINE-CT GREATER +53
01064         PERFORM ERR-HDG THRU ERR-HDG-EXIT.
01065      MOVE READ-CT TO T1-READ.
01066      WRITE DET-REC FROM TOT-1
01067         AFTER ADVANCING   3.
01068      WRITE ERR-REC FROM TOT-1
01069         AFTER ADVANCING   3.
01070      MOVE REJ-CT TO T2-REJ.
01071      WRITE DET-REC FROM TOT-2
01072         AFTER ADVANCING   1.
01073      WRITE ERR-REC FROM TOT-2
01074         AFTER ADVANCING   1.
01075      MOVE WRIT-CT TO T3-WRIT.
01076      WRITE DET-REC FROM TOT-3
01077         AFTER ADVANCING   1.
01078      WRITE ERR-REC FROM TOT-3
01079         AFTER ADVANCING   1.
01080  LIST-TOTALS-EXIT.
01081      EXIT.
01082
01083
01084  HDG.
01085      ADD +1 TO PAGE-CT.
01086      MOVE PAGE-CT TO H1-PAGE.
01087      WRITE DET-REC FROM HDR-1
01088         AFTER ADVANCING  PAGE.
01089      WRITE DET-REC FROM HDR-2
01090         AFTER ADVANCING   2.
01091      WRITE DET-REC FROM HDR-3
01092         AFTER ADVANCING   2.
01093      WRITE DET-REC FROM HDR-4
01094         AFTER ADVANCING   1.
01095      MOVE +6 TO LINE-CT.
01096  HDG-EXIT.
01097      EXIT.
01098
01099
01100  ERR-HDG.
01101      ADD +1 TO ERR-PAGE-CT.
01102      MOVE ERR-PAGE-CT TO H1-PAGE.
01103      WRITE ERR-REC FROM HDR-1
01104         AFTER ADVANCING  PAGE.
01105      WRITE ERR-REC FROM ERR-HDR-2
01106         AFTER ADVANCING   2.
01107      WRITE ERR-REC FROM ERR-HDR-3
01108         AFTER ADVANCING   2.
01109      WRITE ERR-REC FROM ERR-HDR-4
01110         AFTER ADVANCING   1.
01111      MOVE +6 TO ERR-LINE-CT.
01112  ERR-HDG-EXIT.
01113      EXIT.
