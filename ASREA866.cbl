00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID.  ASREA866.
00003  AUTHOR.  LIAUBA-KISTINGER
00004 *DATE-WRITTEN. 11/24/92.
00005  SKIP1
00006 *REMARKS---------------------------------------------------------*
00007 *     THE PURPOSE OF THIS PROGRAM IS TO CREATE THE NEXT GENERATION
00008 *        OF THE ASSESSOR'S HOMEOWNER FILE AFTER THE STATUS-1 AND
00009 *        STATUS-2 FIELDS OF THE RECORD HAVE BEEN UPDATED.
00010 *----------------------------------------------------------------*
00011  SKIP1
00012  ENVIRONMENT DIVISION.
00013  CONFIGURATION SECTION.
00014  SOURCE-COMPUTER. IBM-370.
00015  OBJECT-COMPUTER. IBM-370.
00016  INPUT-OUTPUT SECTION.
00017  FILE-CONTROL.
00018  SKIP2
00019      SELECT  HOMEOWNER-MAST   ASSIGN TO UT-S-MAST.
00020      SELECT  HOMESTEAD        ASSIGN TO UT-S-HOMESTD.
00021      SELECT  TEMPOUT          ASSIGN TO UT-S-HOMEOUT.
00022  DATA DIVISION.
00023  FILE SECTION.
00024  FD  HOMEOWNER-MAST
00025      BLOCK CONTAINS 0 RECORDS
00026      RECORD CONTAINS 130 CHARACTERS
00027      LABEL RECORDS ARE STANDARD
00028      DATA RECORD IS HO-REC.
00029  01  HO-REC.
00030  COPY HOMEXERD01.
00031      SKIP2
00032  COPY FDHMSTDMS.
00033  FD  TEMPOUT
00034      BLOCK CONTAINS 0 RECORDS
00035      RECORD CONTAINS 130 CHARACTERS
00036      LABEL RECORDS ARE STANDARD
00037      DATA RECORD IS TEMPOUT-REC.
00038  01  TEMPOUT-REC       PIC X(130).
00039      EJECT
00040  SKIP2
00041      EJECT
00042  WORKING-STORAGE SECTION.
00043  SKIP2
00044  77  FILLER                  PIC X(58)           VALUE
00045      '***PROGRAM ASREA866 WORKING-STORAGE SECTION STARTS HERE***'.
00046  SKIP2
00047  77  MASTER-COUNT            PIC S9(9) COMP-3 VALUE +0.
00048  77  MST-WRITTEN-COUNT       PIC S9(9) COMP-3 VALUE +0.
00049  77  MST-UPDATE-COUNT        PIC S9(9) COMP-3 VALUE +0.
00050  77  TOT-UNMTCH-DTL          PIC S9(9) COMP-3 VALUE +0.
00051  77  RPT-WRITTEN             PIC S9(9) COMP-3 VALUE +0.
00052  77  DETAIL-COUNT                      PIC S9(9) COMP-3 VALUE +0.
00053      88  FIRST-REC                                      VALUE +1.
00054  77  ERR-SW                    PIC X VALUE 'N'.
00055      88  ERR                         VALUE 'Y'.
00056  77  RUN-DATE                  PIC X(8).
00057  77  BLNK                      PIC X         VALUE SPACE.
00058  77  DETAIL-NEEDED-SW              PIC X     VALUE 'Y'.
00059      88  DETAIL-NEEDED                       VALUE 'Y'.
00060  77  MASTER-NEEDED-SW              PIC X     VALUE 'Y'.
00061      88  MASTER-NEEDED                       VALUE 'Y'.
00062  77  WS-STAT2            PIC 9.
00063      88  VALID-STAT2            VALUE 1.
00064  01  WORK-AREA.
00065      05  CURR-MAST-KEY.
00066          10  CURR-MAST-TOWN           PIC 9(2).
00067          10  CURR-MAST-VOL            PIC 9(3).
00068          10  CURR-MAST-PROP           PIC 9(15).
00069          10  CURR-MAST-TXTYP          PIC 9.
00070      05  PREV-MAST-KEY                PIC X(21) VALUE LOW-VALUES.
00071      05  CURR-DTL-KEY.
00072          10  CURR-DTL-TOWN           PIC 9(2).
00073          10  CURR-DTL-VOL            PIC 9(3).
00074          10  CURR-DTL-PROP           PIC 9(15).
00075      05  PREV-DTL-KEY                PIC X(20) VALUE LOW-VALUES.
00076      05  SAVE-KEY.
00077          10  SAVE-TOWN           PIC 9(2)   VALUE ZEROS.
00078      SKIP1
00079      05  WS-TWN.
00080          10  WS-TOWN             PIC 99.
00081             88  SV-SUBURBAN                    VALUE 10 THRU 39.
00082             88  SV-CITY                        VALUE 70 THRU 77.
00083      05  WS-TOWN-N REDEFINES WS-TWN  PIC 9(2).
00084      05  W-UNPK-TXCDE  PIC 9(5).
00085      05  FILLER REDEFINES W-UNPK-TXCDE.
00086          10 W-TOWN     PIC 9(2).
00087          10 W-REST     PIC 9(3).
00088      05  FILE-KEYS                         VALUE LOW-VALUES.
00089          10  CURRENT-KEYS.
00090              15  CURRENT-MASTER-KEY.
00091                  20  CURRENT-MASTER-TOWN PIC 9(02).
00092                  20  CURRENT-MASTER-VOL  PIC 9(03).
00093                  20  CURRENT-MASTER-PROP PIC 9(14).
00094              15  FILLER REDEFINES CURRENT-MASTER-KEY PIC X(19).
00095                  88  EOF-MASTER-FILE       VALUE HIGH-VALUES.
00096              15  CURRENT-DETAIL-KEY.
00097                  20  CURRENT-DETAIL-TOWN PIC 9(02).
00098                  20  CURRENT-DETAIL-VOL  PIC 9(03).
00099                  20  CURRENT-DETAIL-PROP   PIC 9(14).
00100              15  FILLER REDEFINES CURRENT-DETAIL-KEY PIC X(19).
00101                  88  EOF-DETAIL-FILE       VALUE HIGH-VALUES.
00102          10  FILLER REDEFINES CURRENT-KEYS PIC X(38).
00103              88  EOF-MASTER-DETAIL-FILES   VALUE HIGH-VALUES.
00104          10  PREVIOUS-KEYS.
00105              15  PREVIOUS-MASTER-KEY       PIC X(38).
00106              15  PREVIOUS-DETAIL-KEY       PIC X(38).
00107  SKIP2
00108  01  WORK-AREA.
00109      05  UNPK-PROP           PIC 9(15).
00110      05  UNPK-PROPX REDEFINES UNPK-PROP.
00111          10 HIGH-PROP        PIC 9.
00112          10 REST-PROP        PIC 9(14).
00113  SKIP2
00114      05  DSP-TIME                   PIC 99,99.
00115      05  DSP-DATE.
00116          10  DSP-MO                 PIC 99.
00117          10  FILLER                 PIC X         VALUE '/'.
00118          10  DSP-DA                 PIC 99.
00119          10  FILLER                 PIC X         VALUE '/'.
00120          10  DSP-YR                 PIC 99.
00121      05  ACPT-DATE                  PIC 9(6).
00122      05  ACPT-DATE-X REDEFINES ACPT-DATE.
00123          10  ACPT-YR                PIC 99.
00124          10  ACPT-MO                PIC 99.
00125          10  ACPT-DA                PIC 99.
00126      05  ACPT-TIME-HOLD             PIC 9(8).
00127      05  ACPT-TIME-HOLD-X REDEFINES ACPT-TIME-HOLD.
00128          10  ACPT-TIME              PIC 9(4).
00129          10  FILLER                 PIC 9(4).
00130      05  WS-TIME                    PIC 99,99,99.
00131  PROCEDURE DIVISION.
00132  SKIP2
00133  000-HOUSEKEEPING.
00134      ACCEPT ACPT-TIME-HOLD FROM TIME.
00135      MOVE ACPT-TIME TO DSP-TIME.
00136      INSPECT DSP-TIME REPLACING ALL ',' BY ':'.
00137      ACCEPT ACPT-DATE FROM DATE.
00138      MOVE ACPT-MO TO DSP-MO.
00139      MOVE ACPT-DA TO DSP-DA.
00140      MOVE ACPT-YR TO DSP-YR.
00141      DISPLAY 'PROGRAM ASREA866 DATE AND TIME OF RUN = '
00142        DSP-DATE ' ' DSP-TIME.
00143      OPEN INPUT HOMEOWNER-MAST
00144                 HOMESTEAD
00145         OUTPUT  TEMPOUT.
00146  SKIP2
00147      PERFORM  010-MAINLINE  UNTIL  EOF-MASTER-DETAIL-FILES OR ERR
00148      DISPLAY 'MASTER RECORDS READ '     MASTER-COUNT
00149      DISPLAY 'MASTER RECORDS WRITTEN '  MST-WRITTEN-COUNT
00150      CLOSE  HOMEOWNER-MAST
00151             HOMESTEAD
00152             TEMPOUT.
00153      STOP RUN.
00154  SKIP2
00155  010-MAINLINE.
00156      IF DETAIL-NEEDED AND NOT EOF-DETAIL-FILE
00157         PERFORM 100-READ-DETAIL-FILE
00158         MOVE 'N' TO DETAIL-NEEDED-SW
00159         IF NOT (EOF-DETAIL-FILE OR ERR)
00160            PERFORM 300-PROCESS-MASTER
00161         ELSE
00162              CONTINUE
00163      ELSE
00164           PERFORM 300-PROCESS-MASTER.
00165  SKIP2
00166  100-READ-DETAIL-FILE.
00167      READ HOMESTEAD
00168      AT END
00169        MOVE HIGH-VALUES  TO  CURRENT-DETAIL-KEY.
00170  SKIP2
00171      IF  NOT  EOF-DETAIL-FILE
00172          MOVE H-PROP      TO     UNPK-PROP
00173          MOVE REST-PROP   TO     CURR-DTL-PROP
00174                                  CURRENT-DETAIL-PROP
00175          MOVE H-TXCD     TO      W-UNPK-TXCDE
00176          MOVE W-TOWN      TO     CURRENT-DETAIL-TOWN
00177                                  CURR-DTL-TOWN
00178          MOVE H-VOL       TO     CURRENT-DETAIL-VOL
00179                                  CURR-DTL-VOL
00180          IF  CURR-DTL-KEY LESS THAN PREV-DTL-KEY
00181              MOVE  16  TO  RETURN-CODE
00182              MOVE  'Y' TO  ERR-SW
00183              DISPLAY 'DETAIL SEQUENCE ERROR'
00184              DISPLAY 'CURRENT DETAIL KEY  ('
00185                       CURR-DTL-KEY ')'
00186              DISPLAY 'PREVIOUS DETAIL KEY ('
00187                       PREV-DTL-KEY ')'
00188          ELSE
00189              ADD   +1  TO  DETAIL-COUNT
00190              MOVE CURR-DTL-KEY TO PREV-DTL-KEY.
00191  SKIP1
00192  200-READ-MASTER-FILE.
00193      READ HOMEOWNER-MAST
00194      AT END
00195        MOVE HIGH-VALUES TO CURRENT-MASTER-KEY.
00196      IF NOT  EOF-MASTER-FILE
00197          MOVE HM-VOL      TO    CURRENT-MASTER-VOL
00198                                 CURR-MAST-VOL
00199          MOVE HM-PROP     TO    CURRENT-MASTER-PROP
00200                                 CURR-MAST-PROP
00201          MOVE  HM-TXCD    TO    W-UNPK-TXCDE
00202          MOVE  W-TOWN     TO    CURRENT-MASTER-TOWN
00203                                 CURR-MAST-TOWN
00204          MOVE HM-TXTYP    TO    CURR-MAST-TXTYP
00205          IF  CURR-MAST-KEY  LESS PREV-MAST-KEY
00206              MOVE  16  TO  RETURN-CODE
00207              MOVE  'Y' TO  ERR-SW
00208              DISPLAY 'MASTER SEQUENCE ERROR'
00209              DISPLAY 'CURRENT MASTER KEY  ('
00210                       CURR-MAST-KEY ')'
00211              DISPLAY 'PREVIOUS MASTER KEY ('
00212                       PREV-MAST-KEY ')'
00213           ELSE
00214                 ADD   +1  TO  MASTER-COUNT
00215                 MOVE  CURR-MAST-KEY TO PREV-MAST-KEY.
00216  SKIP1
00217  300-PROCESS-MASTER.
00218      IF  MASTER-NEEDED  AND  NOT  EOF-MASTER-FILE
00219          PERFORM 200-READ-MASTER-FILE
00220          MOVE  'N'  TO  MASTER-NEEDED-SW.
00221
00222      IF  NOT ERR AND  NOT  EOF-MASTER-DETAIL-FILES
00223          IF CURRENT-MASTER-KEY EQUAL CURRENT-DETAIL-KEY
00224             MOVE 1     TO HM-STAT1  HM-STAT2
00225             PERFORM  350-WRITE-OUTREC
00226             MOVE  'Y'  TO  DETAIL-NEEDED-SW
00227             MOVE  'Y'  TO  MASTER-NEEDED-SW
00228          ELSE
00229             IF CURRENT-MASTER-KEY LESS  CURRENT-DETAIL-KEY
00230                 MOVE HM-STAT2  TO  WS-STAT2
00231                 IF VALID-STAT2
00232                     MOVE ZERO  TO HM-STAT1  HM-STAT2
00233                     PERFORM  350-WRITE-OUTREC
00234                     MOVE  'Y'  TO  MASTER-NEEDED-SW
00235                     MOVE  'N'  TO  DETAIL-NEEDED-SW
00236                 ELSE
00237                     PERFORM  350-WRITE-OUTREC
00238                     MOVE  'Y'  TO  MASTER-NEEDED-SW
00239                     MOVE  'N'  TO  DETAIL-NEEDED-SW
00240             ELSE
00241                IF CURRENT-MASTER-KEY GREATER CURRENT-DETAIL-KEY
00242                      MOVE  'N'  TO  MASTER-NEEDED-SW
00243                      MOVE  'Y'  TO  DETAIL-NEEDED-SW.
00244  SKIP2
00245  350-WRITE-OUTREC.
00246      MOVE  HO-REC TO TEMPOUT-REC
00247      WRITE TEMPOUT-REC
00248      ADD +1         TO  MST-WRITTEN-COUNT.
00249  SKIP2
