00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID.    ASREA872.
00003  AUTHOR.        GOLD-MOLIS.
00004  DATE-WRITTEN.  MARCH 10, 1987.
00005      SKIP1
00006 *REMARKS.      THIS PROGRAM PRINTS THE HOMEOWNER TOWN
00007 *              RESPONSE REPORT.
00008      SKIP2
00009  ENVIRONMENT DIVISION.
00010  CONFIGURATION SECTION.
00011  SOURCE-COMPUTER. IBM-370.
00012  OBJECT-COMPUTER. IBM-370.
00013      SKIP1
00014  INPUT-OUTPUT SECTION.
00015  FILE-CONTROL.
00016      SKIP1
00017      SELECT  HOMEOWNER-MAST2 ASSIGN TO UT-S-HOMOWN.
00018      SKIP1
00019      SELECT  PRINT-FILE   ASSIGN TO UT-S-PRINTFLE.
00020      SKIP2
00021  DATA DIVISION.
00022  FILE SECTION.
00023      SKIP2
00024  COPY HOMOWNFD02.
00025      SKIP2
00026  COPY PRINTFILE.
00027      EJECT
00028  WORKING-STORAGE SECTION.
00029      SKIP1
00030  77  SUBT                   PIC S9(4)  VALUE +0  COMP.
00031  77  BLANK-LINE             PIC X      VALUE SPACE.
00032  77  SAVE-TOWN              PIC 99     VALUE 0.
00033  77  TOT-RECS-READ          PIC S9(9)  VALUE +0  COMP-3.
00034      88 FIRST-REC                      VALUE +1.
00035  77  TOT-RECS-TOWN          PIC S9(9)  VALUE +0  COMP-3.
00036  77  TOT-RECS-NR            PIC S9(9)  VALUE +0  COMP-3.
00037  77  TOT-RECS-YES           PIC S9(9)  VALUE +0  COMP-3.
00038  77  TOT-RECS-NO            PIC S9(9)  VALUE +0  COMP-3.
00039  77  TOT-RECS-INC           PIC S9(9)  VALUE +0  COMP-3.
00040  77  TOT-RECS-UND           PIC S9(9)  VALUE +0  COMP-3.
00041  77  TOT-RECS-OTH           PIC S9(9)  VALUE +0  COMP-3.
00042  77  RECS-TOWN              PIC S9(9)  VALUE +0  COMP-3.
00043  77  RECS-NR                PIC S9(9)  VALUE +0  COMP-3.
00044  77  RECS-YES               PIC S9(9)  VALUE +0  COMP-3.
00045  77  RECS-NO                PIC S9(9)  VALUE +0  COMP-3.
00046  77  RECS-INC               PIC S9(9)  VALUE +0  COMP-3.
00047  77  RECS-UND               PIC S9(9)  VALUE +0  COMP-3.
00048  77  RECS-OTH               PIC S9(9)  VALUE +0  COMP-3.
00049      SKIP1
00050  77  ERR-SW                 PIC X      VALUE 'N'.
00051      88  ERR                           VALUE 'Y'.
00052  77  HO-EOF-SW              PIC X      VALUE 'N'.
00053      88  HO-EOF                        VALUE 'Y'.
00054  77  CURRENT-DATE       PIC 99/99/9(4).
00055  77  MDCY-OR-CYMD       PIC S9(5)V9(4)  VALUE +10000.0001.
00056      SKIP1
00057  01  WORK-AREA.
00058      SKIP1
00059      05  CURRENT-DATE-RETURN.
00060          10  SYSTEM-DATE    PIC 9(8).
00061      05  HO-CURR-KEY.
00062          10  HOCURR-TOWN    PIC 99.
00063          10  HOCURR-STAT    PIC 9.
00064      05  HO-PREV-KEY        PIC X(06)  VALUE LOW-VALUES.
00065      SKIP1
00066      05  TOWN-UNPK          PIC 9(5).
00067      05  FILLER REDEFINES TOWN-UNPK.
00068          10  TOWN           PIC 99.
00069          10  FILLER         PIC X(3).
00070      SKIP1
00071      05 HEADING-1.
00072         10  FILLER          PIC X(3)   VALUE SPACES.
00073         10  H1-DATE         PIC X(10).
00074         10  FILLER          PIC X(38)   VALUE SPACES.
00075         10  FILLER          PIC X(70)  VALUE
00076             'OFFICE OF THE COOK COUNTY ASSESSOR'.
00077      SKIP1
00078      05 HEADING-2.
00079         10  FILLER          PIC X(3)   VALUE SPACES.
00080         10  FILLER          PIC X(50)  VALUE 'ASREA872'.
00081         10  FILLER          PIC X(30)  VALUE
00082             'HOMEOWNER TOWN RESPONSE REPORT'.
00083      SKIP1
00084      05 HEADING-3.
00085         10  FILLER          PIC X(4)   VALUE SPACES.
00086         10  FILLER          PIC X(38)  VALUE
00087             'TOWNSHIP       TOTAL     DID NOT'.
00088         10  FILLER          PIC X(54)  VALUE
00089         'YES        NO      INCOMPLETE   UNDELIVERABLE    OTHER'.
00090      SKIP1
00091      05 HEADING-4.
00092         10  FILLER          PIC X(6)   VALUE SPACES.
00093         10  FILLER          PIC X(44)  VALUE
00094         'NAME        RECORDS    RESPONDS  RESPONSES'.
00095         10  FILLER          PIC X(48)  VALUE
00096          'RESPONSES   RESPONSES     RESPONSES    RESPONSES'.
00097      SKIP1
00098      05  DETAIL-LINE                   VALUE SPACES.
00099          10  FILLER         PIC X(2).
00100          10  DL-TOWN        PIC X(13).
00101          10  FILLER         PIC X(2).
00102          10  DL-RECS        PIC Z,ZZZ,ZZ9.
00103          10  FILLER         PIC X(2).
00104          10  DL-NR          PIC Z,ZZZ,ZZ9.
00105          10  FILLER         PIC X(2).
00106          10  DL-YES         PIC Z,ZZZ,ZZ9.
00107          10  FILLER         PIC X(2).
00108          10  DL-NO          PIC Z,ZZZ,ZZ9.
00109          10  FILLER         PIC X(2).
00110          10  DL-INC         PIC Z,ZZZ,ZZ9.
00111          10  FILLER         PIC X(5).
00112          10  DL-UND         PIC Z,ZZZ,ZZ9.
00113          10  FILLER         PIC X(5).
00114          10  DL-OTH         PIC Z,ZZZ,ZZ9.
00115          SKIP1
00116      05  TOTAL-LINE.
00117          10  FILLER         PIC X(2)   VALUE SPACES.
00118          10  FILLER         PIC X(15)  VALUE 'TOTAL COUNTY'.
00119          10  TL-RECS        PIC Z,ZZZ,ZZ9.
00120          10  FILLER         PIC X(2)   VALUE SPACES.
00121          10  TL-NR          PIC Z,ZZZ,ZZ9.
00122          10  FILLER         PIC X(2)   VALUE SPACES.
00123          10  TL-YES         PIC Z,ZZZ,ZZ9.
00124          10  FILLER         PIC X(2)   VALUE SPACES.
00125          10  TL-NO          PIC Z,ZZZ,ZZ9.
00126          10  FILLER         PIC X(2)   VALUE SPACES.
00127          10  TL-INC         PIC Z,ZZZ,ZZ9.
00128          10  FILLER         PIC X(5)   VALUE SPACES.
00129          10  TL-UND         PIC Z,ZZZ,ZZ9.
00130          10  FILLER         PIC X(5)   VALUE SPACES.
00131          10  TL-OTH         PIC Z,ZZZ,ZZ9.
00132          SKIP2
00133  01  TOWN-TABLE-INFO.
00134          SKIP2
00135  COPY TWNINFOTBL.
00136          EJECT
00137      SKIP2
00138  PROCEDURE DIVISION.
00139      SKIP1
00140  100-HOUSEKEEPING.
00141      OPEN INPUT HOMEOWNER-MAST2
00142          OUTPUT PRINT-FILE.
00143         SKIP1
00144      MOVE FUNCTION CURRENT-DATE TO CURRENT-DATE-RETURN.
00145      COMPUTE CURRENT-DATE = SYSTEM-DATE * MDCY-OR-CYMD.
00146      MOVE CURRENT-DATE TO H1-DATE.
00147      WRITE PRINT-REC FROM BLANK-LINE AFTER ADVANCING PAGE.
00148      PERFORM 700-HEADING-ROUTINE.
00149      SKIP1
00150      PERFORM 200-MAINLINE
00151             UNTIL ERR OR HO-EOF.
00152      SKIP1
00153      IF TOT-RECS-READ NOT EQUAL 0
00154         PERFORM 400-PRINT-TOWN-BREAK.
00155      SKIP1
00156      PERFORM 600-PRINT-TOTAL-LINE.
00157      SKIP1
00158      CLOSE HOMEOWNER-MAST2
00159            PRINT-FILE.
00160         SKIP1
00161      STOP RUN.
00162         SKIP2
00163  200-MAINLINE.
00164      PERFORM 300-READ-HOMEOWNER-FILE.
00165      IF NOT (HO-EOF OR ERR)
00166         IF TOWN NOT EQUAL TO SAVE-TOWN
00167            PERFORM 400-PRINT-TOWN-BREAK
00168            MOVE TOWN TO SAVE-TOWN
00169            PERFORM 500-ACCUM-TOWN
00170         ELSE
00171            PERFORM 500-ACCUM-TOWN.
00172      SKIP2
00173  300-READ-HOMEOWNER-FILE.
00174      READ HOMEOWNER-MAST2
00175           AT END MOVE 'Y' TO HO-EOF-SW.
00176           SKIP1
00177      IF NOT HO-EOF
00178         ADD  +1         TO TOT-RECS-READ
00179         MOVE HO-TXCD2   TO TOWN-UNPK
00180         MOVE TOWN       TO HOCURR-TOWN
00181         MOVE HO-STAT1-2 TO HOCURR-STAT
00182         IF HO-CURR-KEY LESS THAN HO-PREV-KEY
00183            DISPLAY 'HOMEOWNER FILE OUT OF SEQUENCE'
00184            DISPLAY 'CURRENT  KEY IS  ' HO-CURR-KEY
00185            DISPLAY 'PREVIOUS KEY IS  ' HO-PREV-KEY
00186            MOVE 'Y' TO ERR-SW
00187            MOVE 16  TO RETURN-CODE
00188          ELSE
00189             MOVE HO-CURR-KEY TO HO-PREV-KEY
00190             IF FIRST-REC
00191                MOVE TOWN TO SAVE-TOWN.
00192      SKIP2
00193  400-PRINT-TOWN-BREAK.
00194      IF SAVE-TOWN GREATER THAN 39
00195         SUBTRACT 39 FROM SAVE-TOWN GIVING SUBT
00196      ELSE
00197         SUBTRACT  9 FROM SAVE-TOWN GIVING SUBT.
00198      MOVE TOWN-NAME (SUBT) TO DL-TOWN.
00199      MOVE RECS-TOWN   TO DL-RECS.
00200      ADD  RECS-TOWN   TO TOT-RECS-TOWN.
00201      MOVE RECS-NR     TO DL-NR.
00202      ADD  RECS-NR     TO TOT-RECS-NR.
00203      MOVE RECS-YES    TO DL-YES.
00204      ADD  RECS-YES    TO TOT-RECS-YES.
00205      MOVE RECS-NO     TO DL-NO.
00206      ADD  RECS-NO     TO TOT-RECS-NO.
00207      MOVE RECS-INC    TO DL-INC.
00208      ADD  RECS-INC    TO TOT-RECS-INC.
00209      MOVE RECS-UND    TO DL-UND.
00210      ADD  RECS-UND    TO TOT-RECS-UND.
00211      MOVE RECS-OTH    TO DL-OTH.
00212      ADD  RECS-OTH    TO TOT-RECS-OTH.
00213      MOVE ZEROES      TO RECS-TOWN  RECS-YES RECS-NO
00214                          RECS-INC   RECS-NR  RECS-UND  RECS-OTH.
00215         SKIP1
00216      WRITE PRINT-REC FROM DETAIL-LINE AFTER ADVANCING 1 LINE.
00217      SKIP2
00218  500-ACCUM-TOWN.
00219      ADD +1 TO RECS-TOWN.
00220      IF HO-STAT1-2 EQUAL 0
00221         ADD +1 TO RECS-NR
00222      ELSE
00223         IF HO-STAT1-2 EQUAL 1
00224            ADD +1 TO RECS-YES
00225         ELSE
00226            IF HO-STAT1-2 EQUAL 2
00227               ADD +1 TO RECS-NO
00228            ELSE
00229               IF HO-STAT1-2 EQUAL 3
00230                  ADD +1 TO RECS-INC
00231               ELSE
00232                  IF HO-STAT1-2 EQUAL 4
00233                     ADD +1 TO RECS-UND
00234                  ELSE
00235                     ADD +1 TO RECS-OTH.
00236                  SKIP2
00237  600-PRINT-TOTAL-LINE.
00238      MOVE TOT-RECS-TOWN  TO TL-RECS.
00239      MOVE TOT-RECS-NR    TO TL-NR.
00240      MOVE TOT-RECS-YES   TO TL-YES.
00241      MOVE TOT-RECS-NO    TO TL-NO.
00242      MOVE TOT-RECS-INC   TO TL-INC.
00243      MOVE TOT-RECS-UND   TO TL-UND.
00244      MOVE TOT-RECS-OTH   TO TL-OTH.
00245      WRITE PRINT-REC FROM TOTAL-LINE AFTER ADVANCING 2 LINES.
00246      SKIP2
00247  700-HEADING-ROUTINE.
00248      WRITE PRINT-REC FROM HEADING-1  AFTER ADVANCING PAGE.
00249      WRITE PRINT-REC FROM HEADING-2  AFTER ADVANCING 2 LINES.
00250      WRITE PRINT-REC FROM HEADING-3  AFTER ADVANCING 3 LINES.
00251      WRITE PRINT-REC FROM HEADING-4  AFTER ADVANCING 1 LINE.
00252      WRITE PRINT-REC FROM BLANK-LINE AFTER ADVANCING 1 LINE.
