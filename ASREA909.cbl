00001  IDENTIFICATION DIVISION.
00002                          SKIP1
00003  PROGRAM-ID.  ASREA909.
00004 *AUTHOR.  VINCENT FALK -- KATHY LEE FOLAN
00005 *DATE-WRITTEN.  MAY, 2004.
00006                          SKIP1
00007 *REMARKS.  THIS PROGRAM WILL READ THE ASSESSOR EXPIRED
00008 *    INCENTIVE FILE, AND THE ASSESSMENT MASTER FILE, IN TWO
00009 *    FILE MATCH MODE.  FOR ALL MATCHING RECORDS, THE FDP-EXP-INC
00010 *    FIELD ON THE FROZEN VALUE FILE WILL BE UPDATED.
00011                          SKIP2
00012 ******************************************************************
00013  ENVIRONMENT DIVISION.
00014                          SKIP1
00015  CONFIGURATION SECTION.
00016  SOURCE-COMPUTER.  IBM-370.
00017  OBJECT-COMPUTER.  IBM-370.
00018                          SKIP1
00019  INPUT-OUTPUT SECTION.
00020  FILE-CONTROL.
00021      SELECT ASSR-IN-FILE  ASSIGN TO UT-S-ASSRINFL.
00022      SELECT MASTER-IN  ASSIGN TO UT-S-MASTERFL.
00023      SELECT FRZDIV-PRCNT-FILE ASSIGN TO DA-FRZVAL
00024          ORGANIZATION IS INDEXED
00025          ACCESS IS RANDOM
00026          RECORD KEY IS FDP-KEY
00027          FILE STATUS IS FRZVAL-FILE-STAT FRZVAL-FILE-STAT-2.
00028                          EJECT
00029 ******************************************************************
00030  DATA DIVISION.
00031                          SKIP1
00032  FILE SECTION.
00033  FD  ASSR-IN-FILE
00034      LABEL RECORDS ARE STANDARD
00035      RECORDING MODE IS F
00036      BLOCK CONTAINS 0 RECORDS
00037      RECORD CONTAINS 38 CHARACTERS
00038      DATA RECORD IS ASSR-IN-REC.
00039  01  ASSR-IN-REC.
00040      05  ASSR-TOWN-X.
00041          10  ASSR-TOWN   PIC 99.
00042      05  ASSR-VOL-X.
00043          10  ASSR-VOL    PIC 999.
00044      05  ASSR-PIN-X.
00045          10  ASSR-PIN    PIC 9(14).
00046      05  ASSR-CLS-X.
00047          10  ASSR-CLS    PIC 999.
00048      05  ASSR-CNTRL-NUM-X.
00049          10  ASSR-CNTRL-NUM  PIC 999.
00050      05  ASSR-TXYR-X.
00051          10  ASSR-TXYR   PIC 9(4).
00052      05  ASSR-AV-AMT-X.
00053          10  ASSR-AV-AMT PIC 9(9).
00054                          SKIP1
00055  COPY ASREASFD01.
00056                          SKIP1
00057  COPY CLFZDVPCF1.
00058                          SKIP1
00059  WORKING-STORAGE SECTION.
00060 *****SWITCHES.
00061  77  EOF-ASSR-SW         PIC X            VALUE 'N'.
00062      88  EOF-ASSR                         VALUE 'Y'.
00063  77  EOF-MSTR-SW         PIC X            VALUE 'N'.
00064      88  EOF-MSTR                         VALUE 'Y'.
00065  77  SEVERE-ERR-SW       PIC X            VALUE 'N'.
00066      88  NO-SEVERE-ERR                    VALUE 'N'.
00067      88  SEVERE-ERR                       VALUE 'Y'.
00068  77  RECORD-EDIT-SW      PIC X            VALUE 'N'.
00069      88  INVALID-REC                      VALUE 'N'.
00070      88  VALID-REC                        VALUE 'Y'.
00071  77  PROCESS-REC-SW      PIC X            VALUE 'N'.
00072      88  PROCESS-REC-RESET                VALUE 'N'.
00073      88  PROCESS-REC                      VALUE 'Y'.
00074                          SKIP1
00075 *****ERROR MESSAGES.
00076  77  INV-TOWN-MSG1       PIC X(45)        VALUE
00077      'TOWN NUMBER NOT NUMERIC....................: '.
00078  77  INV-TOWN-MSG2       PIC X(45)        VALUE
00079      'TOWN NUMBER MUST BE 10 - 39 OR 70 - 77.....: '.
00080  77  INV-VOL-MSG1        PIC X(45)        VALUE
00081      'VOLUME NUMBER NOT NUMERIC..................: '.
00082  77  INV-VOL-MSG2        PIC X(45)        VALUE
00083      'VOLUME NUMBER MUST BE 001 - 601............: '.
00084  77  INV-PIN-MSG1        PIC X(45)        VALUE
00085      'PIN NUMBER NOT NUMERIC.....................: '.
00086  77  INV-PIN-MSG2        PIC X(45)        VALUE
00087      'PIN NUMBER MUST BE GREATER THAN ZEROS......: '.
00088  77  INV-AVAMT-MSG1      PIC X(45)        VALUE
00089      'ASSR AV AMOUNT NOT NUMERIC.................: '.
00090  77  INV-AVAMT-MSG2      PIC X(45)        VALUE
00091      'ASSR AV AMOUNT MUST BE GREATER THAN ZEROS..: '.
00092  77  NO-MATCH-MSG        PIC X(25)        VALUE
00093      'NO MATCHING MASTER RECORD'.
00094                          SKIP1
00095 *****WORK VARIABLES.
00096  77  CURRENT-DATE        PIC 99/99/9(4).
00097  77  CURRENT-TIME        PIC 99,99,99.
00098  77  CTR-DISPLAY         PIC Z,ZZZ,ZZ9.
00099                          SKIP1
00100  01  PACKED-DECIMAL-VARIABLES             PACKED-DECIMAL.
00101      05  ASSR-READ-CTR   PIC S9(7)        VALUE +0.
00102      05  MSTR-READ-CTR   PIC S9(7)        VALUE +0.
00103      05  FRZVAL-READ-CTR PIC S9(7)        VALUE +0.
00104      05  FRZVAL-ADDED-CTR PIC S9(7)       VALUE +0.
00105      05  ASSR-PROC-CTR   PIC S9(7)        VALUE +0.
00106      05  ASSR-ERR-CTR    PIC S9(7)        VALUE +0.
00107      05  ASSR-NOMATCH-CTR PIC S9(7)       VALUE +0.
00108      05  UPDT-CTR        PIC S9(7)        VALUE +0.
00109      05  MDCY-OR-CYMD    PIC S9(5)V9(4)   VALUE +10000.0001.
00110                          SKIP1
00111  01  GROUP-VARIABLES.
00112      05  CURRENT-DATE-RETURN              VALUE SPACES.
00113          10  SYSTEM-DATE PIC 9(8).
00114          10  SYSTEM-TIME PIC 9(6).
00115          10  FILLER      PIC X(7).
00116      05  FRZVAL-FILE-STAT  PIC 99         VALUE 0.
00117          88  FRZVAL-VALID-STAT   VALUE 00  23.
00118          88  FRZVAL-GOOD-READ    VALUE 00.
00119          88  FRZVAL-GOOD-WRITE   VALUE 00.
00120          88  FRZVAL-NOTFND       VALUE 23.
00121      05  FRZVAL-FILE-STAT-2                          BINARY.
00122          10  FRZVAL-RETURN   PIC 99       VALUE 0.
00123          10  FRZVAL-FUNCTION PIC 9        VALUE 0.
00124          10  FRZVAL-FEEDBACK PIC 999      VALUE 0.
00125      05  TXCD-HOLD        PIC 9(5)        VALUE 0.
00126      05  FILLER         REDEFINES TXCD-HOLD.
00127          10  TXCD-TOWN    PIC 99.
00128          10  FILLER       PIC XXX.
00129      05  ASSR-CURR-KEY    PIC X(19)       VALUE SPACES.
00130      05  FILLER         REDEFINES ASSR-CURR-KEY.
00131          10  AC-TOWN      PIC 99.
00132          10  AC-VOL       PIC 999.
00133          10  AC-PIN       PIC 9(14).
00134      05  ASSR-PREV-KEY    PIC X(19)       VALUE SPACES.
00135      05  MSTR-CURR-KEY    PIC X(19)       VALUE SPACES.
00136      05  FILLER         REDEFINES MSTR-CURR-KEY.
00137          10  MC-TOWN      PIC 99.
00138          10  MC-VOL       PIC 999.
00139          10  MC-PIN       PIC 9(14).
00140      05  MSTR-PREV-KEY    PIC X(19)       VALUE SPACES.
00141
00142      05  TOWN-CHECK       PIC 99          VALUE 0.
00143          88  VALID-TOWN                   VALUE 10 THRU 39
00144                                                 70 THRU 77.
00145      05  VOL-CHECK        PIC 999         VALUE 0.
00146          88  VALID-VOL                    VALUE 001 THRU 601.
00147      05  ERROR-DISPLAY-1.
00148          10  FILLER      PIC X(20)        VALUE
00149              'FOR ASSR IN RECORD: '.
00150          10  ERROR-KEY   PIC XXBXXXBX(14).
00151      05  ERROR-DISPLAY-2.
00152          10  FILLER      PIC X(04)        VALUE SPACES.
00153          10  ERROR-MSG   PIC X(45).
00154          10  ERROR-VAL   PIC X(25).
00155                          EJECT
00156  PROCEDURE DIVISION.
00157                          SKIP1
00158  0000-BEGIN.
00159      DISPLAY SPACES
00160      MOVE FUNCTION CURRENT-DATE  TO CURRENT-DATE-RETURN
00161      COMPUTE  CURRENT-DATE = SYSTEM-DATE * MDCY-OR-CYMD
00162      MOVE SYSTEM-TIME  TO CURRENT-TIME
00163      INSPECT CURRENT-TIME  REPLACING ALL ',' BY ':'
00164      DISPLAY 'PROGRAM ASREA909 RUN ON: '  CURRENT-DATE
00165              ' AT '  CURRENT-TIME
00166      DISPLAY SPACES
00167                          SKIP1
00168      OPEN  I-O  FRZDIV-PRCNT-FILE
00169      IF NOT FRZVAL-VALID-STAT
00170         MOVE 16  TO RETURN-CODE
00171         DISPLAY 'I/O ERROR ON FRZVAL FILE OPEN'
00172         DISPLAY '     FILE STATUS.........:  '  FRZVAL-FILE-STAT
00173         DISPLAY '     FILE STATUS FEEDBACK:  '  FRZVAL-RETURN
00174                 '  '  FRZVAL-FUNCTION '  ' FRZVAL-FEEDBACK
00175         STOP RUN
00176      END-IF
00177      OPEN  INPUT  ASSR-IN-FILE  MASTER-IN
00178      SET PROCESS-REC-RESET  TO TRUE
00179      PERFORM 1100-READ-ASSR-RTN  UNTIL
00180              PROCESS-REC  OR EOF-ASSR  OR SEVERE-ERR
00181      IF NO-SEVERE-ERR
00182         PERFORM 1150-READ-MSTR-RTN
00183      END-IF
00184      PERFORM 1000-MAINLINE-RTN  UNTIL
00185              (EOF-ASSR  AND  EOF-MSTR)  OR SEVERE-ERR
00186      CLOSE ASSR-IN-FILE  MASTER-IN
00187      IF FRZVAL-VALID-STAT
00188         CLOSE FRZDIV-PRCNT-FILE
00189         IF NOT FRZVAL-VALID-STAT
00190            MOVE 16  TO RETURN-CODE
00191            DISPLAY 'I/O ERROR ON FRAVAL FILE CLOSE'
00192            DISPLAY '     FILE STATUS.........:  '
00193                    FRZVAL-FILE-STAT
00194            DISPLAY '     FILE STATUS FEEDBACK:  '  FRZVAL-RETURN
00195                    '  '  FRZVAL-FUNCTION '  ' FRZVAL-FEEDBACK
00196         END-IF
00197      END-IF
00198      PERFORM 9100-DISPLAY-COUNTERS-RTN
00199      STOP RUN.
00200                          SKIP2
00201  1000-MAINLINE-RTN.
00202      EVALUATE  TRUE
00203         WHEN ASSR-CURR-KEY = MSTR-CURR-KEY
00204            PERFORM 2000-DETAIL-RTN
00205            SET PROCESS-REC-RESET  TO TRUE
00206            PERFORM 1100-READ-ASSR-RTN  UNTIL
00207                    PROCESS-REC  OR EOF-ASSR  OR SEVERE-ERR
00208            IF NO-SEVERE-ERR
00209               PERFORM 1150-READ-MSTR-RTN
00210            END-IF
00211         WHEN ASSR-CURR-KEY < MSTR-CURR-KEY
00212            ADD +1  TO ASSR-NOMATCH-CTR
00213            MOVE ASSR-CURR-KEY  TO ERROR-KEY
00214            MOVE NO-MATCH-MSG  TO ERROR-MSG
00215            MOVE SPACES  TO ERROR-VAL
00216            DISPLAY ERROR-DISPLAY-1
00217            DISPLAY ERROR-DISPLAY-2
00218            SET PROCESS-REC-RESET  TO TRUE
00219            PERFORM 1100-READ-ASSR-RTN  UNTIL
00220                    PROCESS-REC  OR EOF-ASSR  OR SEVERE-ERR
00221         WHEN ASSR-CURR-KEY > MSTR-CURR-KEY
00222            PERFORM 1150-READ-MSTR-RTN
00223      END-EVALUATE.
00224                          SKIP2
00225  1100-READ-ASSR-RTN.
00226      READ ASSR-IN-FILE
00227         AT END
00228            SET EOF-ASSR  TO TRUE
00229            MOVE ALL '9'  TO ASSR-CURR-KEY
00230         NOT AT END
00231            ADD +1  TO ASSR-READ-CTR
00232            PERFORM 1200-EDIT-RECORD-RTN
00233            IF VALID-REC
00234               MOVE ASSR-TOWN  TO AC-TOWN
00235               MOVE ASSR-VOL  TO AC-VOL
00236               MOVE ASSR-PIN  TO AC-PIN
00237               IF ASSR-CURR-KEY > ASSR-PREV-KEY
00238                  MOVE ASSR-CURR-KEY  TO ASSR-PREV-KEY
00239                  ADD +1  TO ASSR-PROC-CTR
00240                  SET PROCESS-REC  TO TRUE
00241               ELSE
00242                  MOVE 16  TO RETURN-CODE
00243                  SET SEVERE-ERR  TO TRUE
00244                  MOVE ASSR-READ-CTR  TO CTR-DISPLAY
00245                  DISPLAY 'SEQUENCE ERROR IN ''ASSR IN'' FILE AT '
00246                          'RECORD #: '  CTR-DISPLAY
00247                  DISPLAY '   PREVIOUS KEY: '  ASSR-PREV-KEY
00248                  DISPLAY '   CURRENT KEY.: '  ASSR-CURR-KEY
00249               END-IF
00250            ELSE
00251               ADD +1  TO ASSR-ERR-CTR
00252            END-IF
00253      END-READ.
00254                          SKIP2
00255  1150-READ-MSTR-RTN.
00256      READ MASTER-IN
00257         AT END
00258            SET EOF-MSTR  TO TRUE
00259            MOVE ALL '9'  TO MSTR-CURR-KEY
00260         NOT AT END
00261            ADD +1  TO MSTR-READ-CTR
00262            MOVE M-TXCD  TO TXCD-HOLD
00263            MOVE TXCD-TOWN  TO MC-TOWN
00264            MOVE M-VOL  TO MC-VOL
00265            MOVE M-PROP  TO MC-PIN
00266            IF MSTR-CURR-KEY > MSTR-PREV-KEY
00267               MOVE MSTR-CURR-KEY  TO MSTR-PREV-KEY
00268            ELSE
00269               MOVE 16  TO RETURN-CODE
00270               SET SEVERE-ERR  TO TRUE
00271               MOVE MSTR-READ-CTR  TO CTR-DISPLAY
00272               DISPLAY 'SEQUENCE ERROR IN ''MASTER IN'' FILE AT '
00273                       'RECORD #: '  CTR-DISPLAY
00274               DISPLAY '   PREVIOUS KEY: '  MSTR-PREV-KEY
00275               DISPLAY '   CURRENT KEY.: '  MSTR-CURR-KEY
00276            END-IF
00277      END-READ.
00278                          SKIP2
00279  1200-EDIT-RECORD-RTN.
00280      SET VALID-REC  TO TRUE
00281                          SKIP1
00282      IF ASSR-TOWN-X NUMERIC
00283         MOVE ASSR-TOWN  TO TOWN-CHECK
00284         IF NOT VALID-TOWN
00285            SET INVALID-REC  TO TRUE
00286            MOVE ASSR-CURR-KEY  TO ERROR-KEY
00287            MOVE INV-TOWN-MSG2  TO ERROR-MSG
00288            MOVE ASSR-TOWN-X  TO ERROR-VAL
00289            DISPLAY ERROR-DISPLAY-1
00290            DISPLAY ERROR-DISPLAY-2
00291         END-IF
00292      ELSE
00293         SET INVALID-REC  TO TRUE
00294         MOVE ASSR-CURR-KEY  TO ERROR-KEY
00295         MOVE INV-TOWN-MSG1  TO ERROR-MSG
00296         MOVE ASSR-TOWN-X  TO ERROR-VAL
00297         DISPLAY ERROR-DISPLAY-1
00298         DISPLAY ERROR-DISPLAY-2
00299      END-IF.
00300                          SKIP1
00301      IF ASSR-VOL-X NUMERIC
00302         MOVE ASSR-VOL  TO VOL-CHECK
00303         IF NOT VALID-VOL
00304            SET INVALID-REC  TO TRUE
00305            MOVE ASSR-CURR-KEY  TO ERROR-KEY
00306            MOVE INV-VOL-MSG2  TO ERROR-MSG
00307            MOVE ASSR-VOL-X  TO ERROR-VAL
00308            DISPLAY ERROR-DISPLAY-1
00309            DISPLAY ERROR-DISPLAY-2
00310         END-IF
00311      ELSE
00312         SET INVALID-REC  TO TRUE
00313         MOVE ASSR-CURR-KEY  TO ERROR-KEY
00314         MOVE INV-VOL-MSG1  TO ERROR-MSG
00315         MOVE ASSR-VOL-X  TO ERROR-VAL
00316         DISPLAY ERROR-DISPLAY-1
00317         DISPLAY ERROR-DISPLAY-2
00318      END-IF.
00319                          SKIP1
00320      IF ASSR-PIN-X NUMERIC
00321         IF ASSR-PIN = ZEROS
00322            SET INVALID-REC  TO TRUE
00323            MOVE ASSR-CURR-KEY  TO ERROR-KEY
00324            MOVE INV-PIN-MSG2  TO ERROR-MSG
00325            MOVE ASSR-PIN-X  TO ERROR-VAL
00326            DISPLAY ERROR-DISPLAY-1
00327            DISPLAY ERROR-DISPLAY-2
00328         END-IF
00329      ELSE
00330         SET INVALID-REC  TO TRUE
00331         MOVE ASSR-CURR-KEY  TO ERROR-KEY
00332         MOVE INV-PIN-MSG1  TO ERROR-MSG
00333         MOVE ASSR-PIN-X  TO ERROR-VAL
00334         DISPLAY ERROR-DISPLAY-1
00335         DISPLAY ERROR-DISPLAY-2
00336      END-IF.
00337                          SKIP1
00338      IF ASSR-AV-AMT-X NUMERIC
00339         IF ASSR-AV-AMT = ZEROS
00340            SET INVALID-REC  TO TRUE
00341            MOVE ASSR-CURR-KEY  TO ERROR-KEY
00342            MOVE INV-AVAMT-MSG2  TO ERROR-MSG
00343            MOVE ASSR-AV-AMT-X  TO ERROR-VAL
00344            DISPLAY ERROR-DISPLAY-1
00345            DISPLAY ERROR-DISPLAY-2
00346         END-IF
00347      ELSE
00348         SET INVALID-REC  TO TRUE
00349         MOVE ASSR-CURR-KEY  TO ERROR-KEY
00350         MOVE INV-AVAMT-MSG1  TO ERROR-MSG
00351         MOVE ASSR-AV-AMT-X  TO ERROR-VAL
00352         DISPLAY ERROR-DISPLAY-1
00353         DISPLAY ERROR-DISPLAY-2
00354      END-IF.
00355                          SKIP2
00356  2000-DETAIL-RTN.
00357      MOVE SPACES TO FRZDIV-PRCNT-REC
00358      MOVE M-PROP TO FDP-DIVNO
00359      MOVE M-TXCD TO FDP-TXCD
00360      PERFORM 2100-READ-FRZVAL-RTN
00361      IF FRZVAL-VALID-STAT
00362         MOVE ASSR-AV-AMT TO FDP-EXP-INC
00363         IF FRZVAL-GOOD-READ
00364            PERFORM 2150-REWRITE-FRZVAL-RTN
00365            ADD +1  TO UPDT-CTR
00366         ELSE
00367            IF FRZVAL-NOTFND
00368               PERFORM 2160-WRITE-FRZVAL-RTN
00369               ADD +1  TO FRZVAL-ADDED-CTR
00370            END-IF
00371         END-IF
00372      END-IF.
00373                          SKIP2
00374  2100-READ-FRZVAL-RTN.
00375      READ FRZDIV-PRCNT-FILE
00376      IF FRZVAL-VALID-STAT
00377         IF FRZVAL-GOOD-READ
00378            ADD +1  TO FRZVAL-READ-CTR
00379         ELSE
00380            IF FRZVAL-NOTFND
00381               PERFORM 2200-CREATE-NEW-FRZVAL-RTN
00382            END-IF
00383         END-IF
00384      ELSE
00385         MOVE 16  TO RETURN-CODE
00386         SET SEVERE-ERR  TO TRUE
00387         DISPLAY 'I/O ERROR ON FRZVAL FILE READ'
00388         DISPLAY '     FILE STATUS.........:  '  FRZVAL-FILE-STAT
00389         DISPLAY '     FILE STATUS FEEDBACK:  '  FRZVAL-RETURN
00390                 '  '  FRZVAL-FUNCTION '  ' FRZVAL-FEEDBACK
00391      END-IF.
00392                          SKIP2
00393  2200-CREATE-NEW-FRZVAL-RTN.
00394      MOVE ZEROES TO FDP-CURR-288-PCNT  FDP-FRST-PCNT
00395                     FDP-TXCD-COUNT  FDP-CURR-288-VAL
00396                     FDP-EXPR-288-VAL  FDP-FRST-VAL
00397                     FDP-EXP-INC.
00398
00399  2150-REWRITE-FRZVAL-RTN.
00400      REWRITE FRZDIV-PRCNT-REC
00401      IF NOT FRZVAL-VALID-STAT
00402         MOVE 16  TO RETURN-CODE
00403         SET SEVERE-ERR  TO TRUE
00404         DISPLAY 'I/O ERROR ON FRZVAL FILE REWRITE'
00405         DISPLAY '     FILE STATUS.........:  '  FRZVAL-FILE-STAT
00406         DISPLAY '     FILE STATUS FEEDBACK:  '  FRZVAL-RETURN
00407                 '  '  FRZVAL-FUNCTION '  ' FRZVAL-FEEDBACK
00408      END-IF.
00409                          SKIP2
00410  2160-WRITE-FRZVAL-RTN.
00411      WRITE FRZDIV-PRCNT-REC
00412      IF NOT FRZVAL-GOOD-WRITE
00413         MOVE 16  TO RETURN-CODE
00414         SET SEVERE-ERR  TO TRUE
00415         DISPLAY 'I/O ERROR ON FRAVAL FILE WRITE'
00416         DISPLAY '     FILE STATUS.........:  '  FRZVAL-FILE-STAT
00417         DISPLAY '     FILE STATUS FEEDBACK:  '  FRZVAL-RETURN
00418                 '  '  FRZVAL-FUNCTION '  ' FRZVAL-FEEDBACK
00419      END-IF.
00420                          SKIP2
00421  9100-DISPLAY-COUNTERS-RTN.
00422      DISPLAY SPACES
00423      DISPLAY '       PROGRAM COUNTERS'
00424      MOVE ASSR-READ-CTR  TO CTR-DISPLAY
00425      DISPLAY 'NUMBER OF ASSR IN RECORDS READ.........: '
00426              CTR-DISPLAY
00427      MOVE MSTR-READ-CTR  TO CTR-DISPLAY
00428      DISPLAY 'NUMBER OF MASTER IN RECORDS READ.......: '
00429              CTR-DISPLAY
00430      MOVE ASSR-PROC-CTR  TO CTR-DISPLAY
00431      DISPLAY 'NUMBER OF ASSR IN RECORDS PROCESSED....: '
00432              CTR-DISPLAY
00433      MOVE ASSR-ERR-CTR  TO CTR-DISPLAY
00434      DISPLAY 'NUMBER OF ASSR IN RECORDS IN ERROR.....: '
00435              CTR-DISPLAY
00436      MOVE ASSR-NOMATCH-CTR  TO CTR-DISPLAY
00437      DISPLAY 'NUMBER OF NON MATCHING ASSR IN RECORDS.: '
00438              CTR-DISPLAY
00439      MOVE FRZVAL-READ-CTR TO CTR-DISPLAY
00440      DISPLAY 'NUMBER OF FROZEN VALUE RECORDS READ.....: '
00441              CTR-DISPLAY
00442      MOVE FRZVAL-ADDED-CTR TO CTR-DISPLAY
00443      DISPLAY 'NUMBER OF FROZEN VALUE RECORDS ADDED    : '
00444              CTR-DISPLAY
00445      MOVE UPDT-CTR  TO CTR-DISPLAY
00446      DISPLAY 'NUMBER OF FROZEN VALUE RECORDS UPDATED..: '
00447              CTR-DISPLAY.
