00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASSRQ561.
00003  AUTHOR. JENDRAS - MOLIS.
00004  DATE-WRITTEN. JUL 15, 1992.
00005 *REMARKS. THIS PROGRAM PRINTS THE ASSESSMENT RECORDS IN THE
00006 *         CHICAGO CIRCULATOR WHICH WILL BE TAXED AND HAVE
00007 *         MIXED MAJOR CLASSES, AND APPLIES TAX TYPES TO THE
00008 *         ASSESSMENT MASTER FILES.
00009  ENVIRONMENT DIVISION.
00010  CONFIGURATION SECTION.
00011  SOURCE-COMPUTER. IBM-370.
00012  OBJECT-COMPUTER. IBM-370.
00013  INPUT-OUTPUT SECTION.
00014  FILE-CONTROL.
00015      SELECT MASTER-IN ASSIGN TO UT-S-MASTIN.
00016      SELECT MASTER-OUT-2 ASSIGN TO UT-S-MASTOUT.
00017      SELECT PRINT-FILE ASSIGN TO UT-S-PRINT.
00018  DATA DIVISION.
00019  FILE SECTION.
00020  COPY ASREASFD01.
00021  COPY ASREASFD09.
00022  COPY PRINTFILE.
00023  WORKING-STORAGE SECTION.
00024  77  AS-MAST-RECS       PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00025  77  RECS-PRINTED       PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00026  77  RECS-WRITTEN       PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00027  77  LINE-CNTR          PIC S9(3)     VALUE +60 PACKED-DECIMAL.
00028  77  PAGE-CNTR          PIC S9(5)     VALUE +0  PACKED-DECIMAL.
00029  77  TOTAL-LAND         PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00030  77  TOTAL-IMPRV        PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00031  77  TOTAL-TOTAL        PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00032  77  TOTAL-REC-LAND     PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00033  77  TOTAL-REC-IMPRV    PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00034  77  TOTAL-REC-VALUE    PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00035  77  TOTAL-TAX-LAND     PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00036  77  TOTAL-TAX-IMPRV    PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00037  77  TOTAL-TAX-VALUE    PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00038  77  TOTAL-NON-LAND     PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00039  77  TOTAL-NON-IMPRV    PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00040  77  TOTAL-NON-VALUE    PIC S9(13)    VALUE +0  PACKED-DECIMAL.
00041  77  SUB                PIC S9(4)     VALUE +0  BINARY.
00042  77  BLANK-LINE         PIC X         VALUE SPACE.
00043  77  AS-EOF-SW          PIC X         VALUE 'N'.
00044      88  AS-EOF                       VALUE 'Y'.
00045  77  SELECT-REC-SW      PIC X         VALUE 'N'.
00046      88  SELECTED                     VALUE 'Y'.
00047  77  COND-1-SW          PIC X         VALUE 'N'.
00048      88  COND-1                       VALUE 'Y'.
00049  77  COND-2-SW          PIC X         VALUE 'N'.
00050      88  COND-2                       VALUE 'Y'.
00051  77  TYPE-AS-SW         PIC X         VALUE 'N'.
00052      88  TYPE1                        VALUE '1'.
00053      88  TYPE2-5                      VALUE '2'.
00054  77  CLASS-CHECK        PIC 999.
00055      88  CLASS-100-199                VALUE 100 THRU 199.
00056      88  CLASS-200-399                VALUE 200 THRU 399.
00057      88  CLASS-400-999                VALUE 400 THRU 999.
00058      88  CLASS-600-699                VALUE 600 THRU 699.
00059  01  WS-TXCD            PIC 9(5).
00060  01  FILLER REDEFINES WS-TXCD.
00061      05  FILLER         PIC XX.
00062      05  WS-TXCDHO      PIC 9.
00063      05  FILLER         PIC XX.
00064  01  WORK-AREA.
00065      05  DSP-DATE.
00066          10  DSP-MO                 PIC 99.
00067          10  FILLER                 PIC X         VALUE '/'.
00068          10  DSP-DA                 PIC 99.
00069          10  FILLER                 PIC X         VALUE '/'.
00070          10  DSP-YR                 PIC 99.
00071      05  ACPT-DATE                  PIC 9(6).
00072      05  ACPT-DATE-X REDEFINES ACPT-DATE.
00073          10  ACPT-YR                PIC 99.
00074          10  ACPT-MO                PIC 99.
00075          10  ACPT-DA                PIC 99.
00076      05  HEADING1.
00077          10  FILLER          PIC XX.
00078          10  HD1-DATE        PIC X(8).
00079          10  FILLER          PIC X(40)   VALUE SPACES.
00080          10  FILLER          PIC X(68)   VALUE
00081              'OFFICE OF THE COOK COUNTY ASSESSOR'.
00082          10  FILLER          PIC X(5)    VALUE 'PAGE'.
00083          10  HD1-PAGE        PIC ZZ,ZZ9.
00084      SKIP1
00085      05  HEADING2.
00086          10  FILLER          PIC X(41)   VALUE '  ASSRQ560'.
00087          10  FILLER          PIC X(42)   VALUE
00088              '        PARCELS IN CHICAGO CIRCULATOR AREA'.
00089          10  FILLER          PIC X(36)   VALUE
00090              ' - MIXED RESIDENTIAL/NON-RESIDENTIAL'.
00091      SKIP1
00092      05  HEADING3.
00093          10  FILLER          PIC X(54)   VALUE
00094              ' VOL      PERMANENT         TAX  OVER-ALL   LAND'.
00095          10  FILLER          PIC X(19)   VALUE
00096              'IMPROVMNT     TOTAL'.
00097      SKIP1
00098      05  HEADING4.
00099          10  FILLER          PIC X(9).
00100          10  FILLER          PIC X(45)   VALUE
00101              'INDEX NUMBER      CODE   CLASS   VALUATION'.
00102          10  FILLER          PIC X(53)   VALUE
00103           'VALUATION   VALUATION   TYPE  CLASS  CDU    VALUATION'.
00104      SKIP1
00105      05  DETAIL-LINE                     VALUE SPACES.
00106          10  FILLER          PIC X.
00107          10  D-VOL           PIC 999.
00108          10  FILLER          PIC XX.
00109          10  D-PROP          PIC 99,99,999,999,9999.
00110          10  FILLER          PIC XX.
00111          10  D-TAXCD         PIC 99999.
00112          10  FILLER          PIC XXXX.
00113          10  D-CLASS         PIC 9,99.
00114          10  FILLER          PIC XX.
00115          10  D-VALUE-L       PIC ZZZ,ZZZ,ZZ9.
00116          10  FILLER          PIC X.
00117          10  D-VALUE-I       PIC ZZZ,ZZZ,ZZ9.
00118          10  FILLER          PIC X.
00119          10  D-VALUE-T       PIC ZZZ,ZZZ,ZZ9.
00120          10  FILLER          PIC XX.
00121          10  D-TYPE          PIC X(4).
00122          10  FILLER          PIC XXX.
00123          10  D-CLASS-DET     PIC 9,99.
00124          10  FILLER          PIC XXX.
00125          10  D-CDU           PIC XX.
00126          10  FILLER          PIC XXX.
00127          10  D-VALUE         PIC ZZZ,ZZZ,ZZ9.
00128          SKIP1
00129      05  TOTAL-MSG-LINE                  VALUE SPACES.
00130          10  FILLER          PIC X(5).
00131          10  T-MSG           PIC X(25).
00132          10  T-CNTR          PIC ZZZ,ZZ9.
00133          10  FILLER          PIC X(27).
00134          10  T-MSG-2         PIC X(29).
00135          10  T-CNTR-2        PIC ZZZ,ZZZ,ZZZ,ZZ9.
00136          SKIP1
00137      05  TOTAL-MSG-LINE-2                VALUE SPACES.
00138          10  FILLER          PIC X(26).
00139          10  T-MSG-2V        PIC X(51).
00140          10  T-CNTR-2V       PIC ZZZ,ZZZ,ZZZ,ZZ9.
00141  PROCEDURE DIVISION.
00142  000-HOUSEKEEPING.
00143      OPEN INPUT MASTER-IN
00144          OUTPUT MASTER-OUT-2 PRINT-FILE
00145      ACCEPT ACPT-DATE FROM DATE
00146      MOVE ACPT-MO TO DSP-MO
00147      MOVE ACPT-DA TO DSP-DA
00148      MOVE ACPT-YR TO DSP-YR
00149      MOVE DSP-DATE TO HD1-DATE
00150      WRITE PRINT-REC FROM BLANK-LINE AFTER ADVANCING PAGE
00151      PERFORM 100-MAINLINE UNTIL AS-EOF
00152      PERFORM 800-END-RTN
00153      CLOSE MASTER-IN MASTER-OUT-2 PRINT-FILE
00154      STOP RUN.
00155  100-MAINLINE.
00156      PERFORM 200-READ-AS-MAST
00157      IF NOT AS-EOF
00158          IF M-TXCD NOT = 74004 AND 74504 AND 76010 AND 76510
00159              AND 76011 AND 76511 AND 76012 AND 76512
00160              AND 76013 AND 76513 AND 76014 AND 76514
00161              AND 76015 AND 76515 AND 76016 AND 76516
00162              AND 77004 AND 77504 AND 77005 AND 77505
00163                  PERFORM 600-MOVE-MASTER-TO-OUTPUT
00164                  MOVE '0' TO AS-TXTYP
00165                  PERFORM 700-WRITE-MASTER
00166          ELSE
00167              MOVE 'N' TO SELECT-REC-SW COND-1-SW COND-2-SW
00168              PERFORM 250-EVALUATE-AS-DETAIL
00169                  VARYING SUB FROM 1 BY 1
00170                  UNTIL SUB > M-DTL-QST-CTR
00171              IF SELECTED
00172                  MOVE M-BASE TO AS-BASE
00173                  MOVE '0' TO AS-TXTYP
00174                  PERFORM 375-MOVE-SALES-SEGMENT
00175                  MOVE 0 TO AS-DTL-QST-CTR-1
00176                      TOTAL-REC-LAND TOTAL-REC-IMPRV
00177                      TOTAL-REC-VALUE
00178                  PERFORM 380-CREATE-COND-1-SEGS
00179                      VARYING SUB FROM 1 BY 1
00180                      UNTIL SUB > M-DTL-QST-CTR
00181                  PERFORM 300-PRINT-AS-DETAILS
00182                      VARYING SUB FROM 1 BY 1
00183                      UNTIL SUB > AS-DTL-QST-CTR-1
00184                  MOVE TOTAL-REC-LAND TO AS-VALUE-1 (7)
00185                  MOVE TOTAL-REC-IMPRV TO AS-VALUE-1 (8)
00186                  MOVE TOTAL-REC-VALUE TO AS-VALUE-1 (9)
00187                  PERFORM 350-MOVE-PRINT-FIXED-DETAIL
00188                  PERFORM 700-WRITE-MASTER
00189                  MOVE M-BASE TO AS-BASE
00190                  MOVE AS-TXCD TO WS-TXCD
00191                  MOVE 5 TO WS-TXCDHO
00192                  MOVE WS-TXCD TO AS-TXCD
00193                  MOVE '5' TO AS-TXTYP
00194                  PERFORM 375-MOVE-SALES-SEGMENT
00195                  MOVE 0 TO AS-DTL-QST-CTR-1
00196                      TOTAL-REC-LAND TOTAL-REC-IMPRV
00197                      TOTAL-REC-VALUE
00198                  PERFORM 390-CREATE-COND-2-SEGS
00199                      VARYING SUB FROM 1 BY 1
00200                      UNTIL SUB > M-DTL-QST-CTR
00201                  PERFORM 300-PRINT-AS-DETAILS
00202                      VARYING SUB FROM 1 BY 1
00203                      UNTIL SUB > AS-DTL-QST-CTR-1
00204                  MOVE TOTAL-REC-LAND TO AS-VALUE-1 (7)
00205                  MOVE TOTAL-REC-IMPRV TO AS-VALUE-1 (8)
00206                  MOVE TOTAL-REC-VALUE TO AS-VALUE-1 (9)
00207                  PERFORM 350-MOVE-PRINT-FIXED-DETAIL
00208                  PERFORM 700-WRITE-MASTER
00209              ELSE
00210                  PERFORM 600-MOVE-MASTER-TO-OUTPUT
00211                  MOVE '0' TO AS-TXTYP
00212                  PERFORM 700-WRITE-MASTER.
00213  200-READ-AS-MAST.
00214      READ MASTER-IN
00215          AT END MOVE 'Y' TO AS-EOF-SW.
00216      IF NOT AS-EOF
00217          ADD +1 TO AS-MAST-RECS.
00218  250-EVALUATE-AS-DETAIL.
00219      IF D1-TYPE1 (SUB) OR D2-TYPE2-5 (SUB)
00220          MOVE D1-CLS (SUB) TO CLASS-CHECK
00221          IF (CLASS-100-199)
00222              MOVE 'Y' TO COND-1-SW
00223          ELSE
00224              IF (CLASS-400-999) AND (D2-CDU (SUB) NOT = 'AP')
00225                  MOVE 'Y' TO COND-1-SW
00226              ELSE
00227                  IF (CLASS-200-399)
00228                      MOVE 'Y' TO COND-2-SW
00229                  ELSE
00230                      IF (CLASS-600-699)
00231                          AND (D2-CDU (SUB) EQUAL 'AP')
00232                          MOVE 'Y' TO COND-2-SW.
00233      IF COND-1 AND COND-2
00234          MOVE 'Y' TO SELECT-REC-SW.
00235      IF D2-QUES-CLASS (SUB)
00236          ADD +1 TO SUB.
00237  300-PRINT-AS-DETAILS.
00238      MOVE 'N' TO TYPE-AS-SW
00239      IF AS1-TYPE1 (SUB)
00240          MOVE '1' TO TYPE-AS-SW
00241      ELSE
00242          IF AS2-TYPE2-5 (SUB)
00243              MOVE '2' TO TYPE-AS-SW.
00244      IF TYPE1 OR TYPE2-5
00245          PERFORM 400-MOVE-SEGS-DETAIL.
00246      IF AS2-QUES-CLASS (SUB)
00247          ADD +1 TO SUB.
00248  350-MOVE-PRINT-FIXED-DETAIL.
00249      MOVE SPACES TO DETAIL-LINE
00250      MOVE AS-VOL TO D-VOL
00251      MOVE AS-PROP TO D-PROP
00252      INSPECT D-PROP REPLACING ALL ',' BY '-'
00253      MOVE AS-TXCD TO D-TAXCD
00254      MOVE AS-CLS TO D-CLASS
00255      INSPECT D-CLASS REPLACING ALL ',' BY '-'
00256      MOVE AS-VALUE-1 (7) TO D-VALUE-L
00257      ADD AS-VALUE-1 (7) TO TOTAL-LAND
00258      MOVE AS-VALUE-1 (8) TO D-VALUE-I
00259      ADD AS-VALUE-1 (8) TO TOTAL-IMPRV
00260      MOVE AS-VALUE-1 (9) TO D-VALUE-T
00261      ADD AS-VALUE-1 (9) TO TOTAL-TOTAL
00262      PERFORM 450-PRINT-DETAIL-FIX
00263      ADD +1 TO RECS-PRINTED.
00264  375-MOVE-SALES-SEGMENT.
00265      IF M-SALES-PRESENT
00266          MOVE 1 TO AS-SLS-CTR-1
00267          MOVE M-SALES (1) TO AS-SALES-1 (1).
00268  380-CREATE-COND-1-SEGS.
00269      MOVE D1-CLS (SUB) TO CLASS-CHECK
00270      IF (CLASS-100-199)
00271          OR (CLASS-400-999) AND (D2-CDU (SUB) NOT = 'AP')
00272          ADD +1 TO AS-DTL-QST-CTR-1
00273          MOVE D-TYP1 (SUB) TO AS1-TYP1 (AS-DTL-QST-CTR-1)
00274          IF D2-QUES-CLASS (SUB)
00275              ADD +1 TO SUB AS-DTL-QST-CTR-1
00276              MOVE D-TYP1 (SUB) TO AS1-TYP1 (AS-DTL-QST-CTR-1).
00277  390-CREATE-COND-2-SEGS.
00278      MOVE D1-CLS (SUB) TO CLASS-CHECK
00279      IF (CLASS-200-399)
00280          OR (CLASS-600-699) AND (D2-CDU (SUB) = 'AP')
00281          ADD +1 TO AS-DTL-QST-CTR-1
00282          MOVE D-TYP1 (SUB) TO AS1-TYP1 (AS-DTL-QST-CTR-1)
00283          IF D2-QUES-CLASS (SUB)
00284              ADD +1 TO SUB AS-DTL-QST-CTR-1
00285              MOVE D-TYP1 (SUB) TO AS1-TYP1 (AS-DTL-QST-CTR-1).
00286  400-MOVE-SEGS-DETAIL.
00287      MOVE SPACES TO DETAIL-LINE
00288      IF TYPE1
00289          MOVE 'LAND' TO D-TYPE
00290      ELSE
00291          IF TYPE2-5
00292              MOVE 'IMPV' TO D-TYPE
00293              MOVE AS2-CDU (SUB) TO D-CDU.
00294      MOVE AS1-CLS (SUB) TO D-CLASS-DET
00295      INSPECT D-CLASS-DET REPLACING ALL ',' BY '-'
00296      MOVE AS1-VAL (SUB) TO D-VALUE
00297      MOVE AS1-CLS (SUB) TO CLASS-CHECK
00298      IF AS1-TYPE1 (SUB)
00299          IF CLASS-100-199 OR CLASS-400-999
00300              ADD AS1-VAL (SUB) TO TOTAL-TAX-LAND TOTAL-TAX-VALUE
00301                  TOTAL-REC-LAND TOTAL-REC-VALUE
00302          ELSE
00303              ADD AS1-VAL (SUB) TO TOTAL-NON-LAND TOTAL-NON-VALUE
00304                  TOTAL-REC-LAND TOTAL-REC-VALUE
00305      ELSE
00306          IF AS2-TYPE2-5 (SUB)
00307              IF CLASS-100-199
00308                  OR  (CLASS-400-999
00309                      AND (AS2-CDU (SUB) NOT EQUAL 'AP'))
00310                      ADD AS2-VAL (SUB) TO TOTAL-TAX-IMPRV
00311                          TOTAL-TAX-VALUE TOTAL-REC-IMPRV
00312                          TOTAL-REC-VALUE
00313              ELSE
00314                  ADD AS2-VAL (SUB) TO TOTAL-NON-IMPRV
00315                      TOTAL-NON-VALUE TOTAL-REC-IMPRV
00316                      TOTAL-REC-VALUE.
00317      PERFORM 500-PRINT-DETAIL-SEG.
00318  450-PRINT-DETAIL-FIX.
00319      IF LINE-CNTR > +55
00320          PERFORM 900-HEADING-RTN.
00321      WRITE PRINT-REC FROM DETAIL-LINE AFTER ADVANCING 2
00322      ADD +2 TO LINE-CNTR.
00323  500-PRINT-DETAIL-SEG.
00324      IF LINE-CNTR > +55
00325          PERFORM 900-HEADING-RTN.
00326      WRITE PRINT-REC FROM DETAIL-LINE AFTER ADVANCING 1
00327      ADD +1 TO LINE-CNTR.
00328  600-MOVE-MASTER-TO-OUTPUT.
00329      MOVE M-SLS-CTR TO AS-SLS-CTR-1
00330      MOVE M-DTL-QST-CTR TO AS-DTL-QST-CTR-1
00331      MOVE MSTR-IN-REC TO MSTR-OUT-REC-2.
00332  700-WRITE-MASTER.
00333      WRITE MSTR-OUT-REC-2
00334      ADD +1 TO RECS-WRITTEN.
00335  800-END-RTN.
00336      IF LINE-CNTR > +55
00337          PERFORM 900-HEADING-RTN.
00338      MOVE SPACES TO TOTAL-MSG-LINE
00339      MOVE 'TOTAL RECORDS READ' TO T-MSG
00340      MOVE AS-MAST-RECS TO T-CNTR
00341      MOVE 'TOTAL LAND VALUATION' TO T-MSG-2
00342      MOVE TOTAL-LAND TO T-CNTR-2
00343      WRITE PRINT-REC FROM TOTAL-MSG-LINE AFTER ADVANCING 2
00344      MOVE SPACES TO TOTAL-MSG-LINE
00345      MOVE 'TOTAL RECORDS WRITTEN' TO T-MSG
00346      MOVE RECS-WRITTEN TO T-CNTR
00347      MOVE 'TOTAL IMPROVEMENT VALUATION' TO T-MSG-2
00348      MOVE TOTAL-IMPRV TO T-CNTR-2
00349      WRITE PRINT-REC FROM TOTAL-MSG-LINE AFTER ADVANCING 1
00350      MOVE SPACES TO TOTAL-MSG-LINE
00351      MOVE 'TOTAL RECORDS PRINTED' TO T-MSG
00352      MOVE RECS-PRINTED TO T-CNTR
00353      MOVE 'TOTAL VALUATION' TO T-MSG-2
00354      MOVE TOTAL-TOTAL TO T-CNTR-2
00355      WRITE PRINT-REC FROM TOTAL-MSG-LINE AFTER ADVANCING 1
00356      MOVE SPACES TO TOTAL-MSG-LINE-2
00357      MOVE 'TOTAL NON-RESIDENTIAL LAND VALUATION' TO T-MSG-2V
00358      MOVE TOTAL-TAX-LAND TO T-CNTR-2V
00359      WRITE PRINT-REC FROM TOTAL-MSG-LINE-2 AFTER ADVANCING 2
00360      MOVE 'TOTAL NON-RESIDENTIAL IMPROVEMENT VALUATION'
00361          TO T-MSG-2V
00362      MOVE TOTAL-TAX-IMPRV TO T-CNTR-2V
00363      WRITE PRINT-REC FROM TOTAL-MSG-LINE-2 AFTER ADVANCING 1
00364      MOVE 'TOTAL NON-RESIDENTIAL VALUATION' TO T-MSG-2V
00365      MOVE TOTAL-TAX-VALUE TO T-CNTR-2V
00366      WRITE PRINT-REC FROM TOTAL-MSG-LINE-2 AFTER ADVANCING 1
00367      MOVE SPACES TO TOTAL-MSG-LINE-2
00368      MOVE 'TOTAL RESIDENTIAL LAND VALUATION' TO T-MSG-2V
00369      MOVE TOTAL-NON-LAND TO T-CNTR-2V
00370      WRITE PRINT-REC FROM TOTAL-MSG-LINE-2 AFTER ADVANCING 2
00371      MOVE 'TOTAL RESIDENTIAL IMPROVEMENT VALUATION' TO T-MSG-2V
00372      MOVE TOTAL-NON-IMPRV TO T-CNTR-2V
00373      WRITE PRINT-REC FROM TOTAL-MSG-LINE-2 AFTER ADVANCING 1
00374      MOVE 'TOTAL RESIDENTIAL VALUATION' TO T-MSG-2V
00375      MOVE TOTAL-NON-VALUE TO T-CNTR-2V
00376      WRITE PRINT-REC FROM TOTAL-MSG-LINE-2 AFTER ADVANCING 1.
00377  900-HEADING-RTN.
00378      ADD +1 TO PAGE-CNTR
00379      MOVE PAGE-CNTR TO HD1-PAGE
00380      WRITE PRINT-REC FROM HEADING1 AFTER ADVANCING PAGE
00381      WRITE PRINT-REC FROM HEADING2 AFTER ADVANCING 2
00382      WRITE PRINT-REC FROM HEADING3 AFTER ADVANCING 2
00383      WRITE PRINT-REC FROM HEADING4 AFTER ADVANCING 1
00384      MOVE +6 TO LINE-CNTR.
