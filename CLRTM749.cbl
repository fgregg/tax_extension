00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. CLRTM749.
00003 *AUTHOR. RUGIS - MOLIS.
00004 *DATE-WRITTEN.  AUGUST 13, 1992.
00005      SKIP1
00006 *REMARKS. THE PURPOSE OF THIS PROGRAM IS TO CREATE THE NEW
00007 *         PROPERTY REPORT BY AGENCY NUMBER FOR THE CLERK'S
00008 *         OFFICE.
00009 *MODIFICATION.  N. FLEMING - R. DOBER.  APRIL 10, 2006.
00010 *         ADDED AN OUTPUT FILE DESCRIBING THE O/P PRINT FILE
00011 *         TOWN LINE WITH TOTALS.
00012 *
00013 * CHANGED 09/03/2009 TJM TO INCREASE SIZE OF TIF PRINT FIELDS.
00014 * CHANGED 09/08/2009 TJM TO INCREASE SIZE OF PRINT FIELDS.
00015      SKIP2
00016  ENVIRONMENT DIVISION.
00017  CONFIGURATION SECTION.
00018  SOURCE-COMPUTER. IBM-370.
00019  OBJECT-COMPUTER. IBM-370.
00020      SKIP1
00021  INPUT-OUTPUT SECTION.
00022  FILE-CONTROL.
00023      SELECT FRZAGCY-FILE ASSIGN TO UT-S-FRZAGIN.
00024      SELECT AGCY-EFTS-FLE ASSIGN TO AGCYEFTS
00025          ORGANIZATION IS INDEXED
00026          ACCESS  MODE IS RANDOM
00027          RECORD   KEY IS AGCY-EFTS-KEY
00028          FILE  STATUS IS EFTS-STATUS  EFTS-STATUS2.
00029      SELECT NEWPROP ASSIGN TO UT-S-NEWPROP.
00030      SELECT PRINT-FILE   ASSIGN TO UT-S-PRINTFLE.
00031      SELECT EQUAL-VAL-FILE ASSIGN TO UT-S-EQVALFIL.
00032  SKIP1
00033  DATA DIVISION.
00034  FILE SECTION.
00035      SKIP1
00036  COPY CLFRZAGYF1.
00037      SKIP2
00038  COPY TRAGEFTSF4.
00039      SKIP2
00040  COPY CLNEWPRPF2.
00041      SKIP2
00042  COPY PRINTFILE.
00043      SKIP2
00044  COPY EQVAL749FD.
00045      SKIP3
00046  WORKING-STORAGE SECTION.
00047      SKIP1
00048  77  RECS-READ-CNTR     PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00049      88  FIRST-TIME                   VALUE +1.
00050  77  RECS-PRINTED-CNTR  PIC S9(7)     VALUE +0  PACKED-DECIMAL.
00051  77  EQU-VAL-RECS-WRIT-CNTR  PIC S9(7) VALUE +0  PACKED-DECIMAL.
00052  77  ASSD-FRZ-ACCUM     PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00053  77  EQLZ-FRZ-ACCUM     PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00054  77  ANN-ASSD-ACCUM     PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00055  77  ANN-EQLZ-ACCUM     PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00056  77  DIS-ASSD-ACCUM     PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00057  77  DIS-EQLZ-ACCUM     PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00058  77  TIF-EQLZ-ACCUM     PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00059  77  EXPINC-ASSD-ACCUM  PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00060  77  EXPINC-EQLZ-ACCUM  PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00061  77  TOT-ASSD-FRZ-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00062  77  TOT-EQLZ-FRZ-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00063  77  TOT-ANN-ASSD-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00064  77  TOT-ANN-EQLZ-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00065  77  TOT-DIS-ASSD-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00066  77  TOT-DIS-EQLZ-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00067  77  TOT-TIF-EQLZ-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
00068  77  TOT-EXPINC-EQLZ-ACCUM PIC S9(15) VALUE +0  PACKED-DECIMAL.
00069  77  LINE-CNTR          PIC S9(3)     VALUE +60 PACKED-DECIMAL.
00070  77  PAGE-CNTR          PIC S9(5)     VALUE +0  PACKED-DECIMAL.
00071  77  PREV-AGCY          PIC 9(9)      VALUE ZEROES.
00072  77  SAVE-AGCY          PIC 9(9)      VALUE ZEROES.
00073  77  BLANK-LINE         PIC X         VALUE SPACE.
00074      SKIP1
00075  77  FRZ-EOF-SW         PIC X         VALUE 'N'.
00076      88  FRZ-EOF                      VALUE 'Y'.
00077  77  ERR-SW             PIC X         VALUE 'N'.
00078      88  ERR                          VALUE 'Y'.
00079  77  EFTS-STATUS        PIC 99.
00080      88  GOOD-EFTS                    VALUE 00.
00081      88  EFTS-NOTFND                  VALUE 23.
00082      SKIP2
00083  01  WORK-AREA.
00084      SKIP1
00085      05  EFTS-STATUS2               BINARY.
00086          10  ES-RETURN              PIC 99        VALUE 0.
00087          10  ES-FUNCTION            PIC 9         VALUE 0.
00088          10  ES-FEEDBACK            PIC 999       VALUE 0.
00089      SKIP1
00090      05  DSP-DATE.
00091          10  DSP-MO                 PIC 99.
00092          10  FILLER                 PIC X         VALUE '/'.
00093          10  DSP-DA                 PIC 99.
00094          10  FILLER                 PIC X         VALUE '/'.
00095          10  DSP-YR                 PIC 99.
00096      SKIP1
00097      05  ACPT-DATE                  PIC 9(6).
00098      05  ACPT-DATE-X REDEFINES ACPT-DATE.
00099          10  ACPT-YR                PIC 99.
00100          10  ACPT-MO                PIC 99.
00101          10  ACPT-DA                PIC 99.
00102      SKIP1
00103      05  HEADING1.
00104          10  FILLER       PIC XX      VALUE SPACE.
00105          10  FILLER       PIC X(38)   VALUE 'CLRTM749'.
00106          10  FILLER       PIC X(80)   VALUE
00107              'OFFICE OF THE COOK COUNTY CLERK'.
00108          10  FILLER       PIC X(5)    VALUE 'PAGE'.
00109          10  HD1-PAGE     PIC Z,ZZ9.
00110      SKIP2
00111      05  HEADING2.
00112          10  FILLER       PIC XX      VALUE SPACE.
00113          10  HD2-DATE     PIC X(8).
00114          10  FILLER       PIC X(15)   VALUE SPACES.
00115          10  FILLER       PIC X(8)    VALUE 'PTELL - '.
00116          10  HD2-YEAR     PIC X(4).
00117          10  FILLER       PIC X(55)   VALUE
00118              ' NEW PROPERTY, ANNEXED PROPERTY, DISCONNECTED PROPER
00119 -            'TY,'.
00120      SKIP2
00121      05  HEADING2A.
00122          10  FILLER       PIC X(25)   VALUE SPACES.
00123          10  FILLER       PIC X(77)   VALUE
00124              'EXPIRED INCENTIVES, RECOVERED TAX INCREMENT REPORT B
00125 -            'Y AGENCY'.
00126          10  FILLER       PIC X(21)   VALUE
00127              'EQUALIZATION FACTOR: '.
00128          10  HD2-FACT     PIC 9,9999.
00129      SKIP2
00130      05  HEADING3.
00131          10  FILLER       PIC X(57)   VALUE '  AGENCY'.
00132          10  FILLER       PIC X(16)   VALUE 'NEW PROPERTY'.
00133          10  FILLER       PIC X(18)   VALUE 'ANNEXED PROPERTY'.
00134          10  FILLER       PIC X(15)   VALUE 'DISC PROPERTY'.
00135          10  FILLER       PIC X(18)   VALUE 'RECOVERED TAX'.
00136          10  FILLER       PIC X(7)    VALUE 'EXPIRED'.
00137      SKIP2
00138      05  HEADING4.
00139          10  FILLER       PIC X(11)   VALUE '  NUMBER'.
00140          10  FILLER       PIC X(46)   VALUE 'AGENCY DESCRIPTION'.
00141          10  FILLER       PIC X(19)   VALUE 'EQLZD VALUE'.
00142          10  FILLER       PIC X(16)   VALUE 'EQLZD VALUE'.
00143          10  FILLER       PIC X(13)   VALUE 'EQLZD VALUE'.
00144          10  FILLER       PIC X(18)   VALUE 'INCREMENT VALUE'.
00145          10  FILLER       PIC X(10)   VALUE 'INCENTIVES'.
00146      SKIP2
00147      05  DETAIL-LINE                  VALUE SPACES.
00148          10  FILLER          PIC X.
00149 *        10  D-AGCY-NO       PIC 9(4),9.
00150          10  D-AGCY-NO       PIC 9(2),9(4),9(3).
00151          10  FILLER          PIC X.
00152          10  D-AGCY-DESC     PIC X(48).
00153          10  D-EQLZ-FRZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
00154          10  FILLER          PIC X(1).
00155          10  D-ANN-EQLZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
00156 *        10  FILLER          PIC X(3).
00157          10  FILLER          PIC X(1).
00158          10  D-DIS-EQLZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
00159 *        10  FILLER          PIC X(4).
00160 *        10  D-TIF-EQLZ      PIC ZZZ,ZZZ,ZZ9.
00161          10  FILLER          PIC X(1).
00162          10  D-TIF-EQLZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
00163 *        10  FILLER          PIC XX.
00164          10  D-EXP-INC       PIC Z,ZZZ,ZZZ,ZZ9.
00165      SKIP2
00166      05  COUNTY-LINE.
00167          10  FILLER          PIC X(10)   VALUE SPACES.
00168          10  FILLER          PIC X(46)   VALUE 'COUNTY TOTAL'.
00169          10  C-EQLZ-FRZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
00170 *        10  FILLER          PIC X(7).
00171          10  FILLER          PIC X(4).
00172          10  C-ANN-EQLZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
00173 *        10  FILLER          PIC X(4).
00174          10  FILLER          PIC X(1).
00175          10  C-DIS-EQLZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
00176 *        10  FILLER          PIC X(5).
00177 *        10  C-TIF-EQLZ      PIC ZZZ,ZZZ,ZZ9.
00178          10  FILLER          PIC X(2).
00179          10  C-TIF-EQLZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
00180 *        10  FILLER          PIC XX.
00181          10  FILLER          PIC X.
00182          10  C-EXPINC-EQLZ   PIC Z,ZZZ,ZZZ,ZZ9.
00183      SKIP2
00184      05  TOTAL-LINE.
00185          10  FILLER          PIC X(5)    VALUE SPACES.
00186          10  T-MSG           PIC X(27).
00187          10  T-CNTR          PIC ZZ,ZZ9.
00188      SKIP2
00189      05  TOTAL-LINEA.
00190          10  FILLER          PIC X(5)    VALUE SPACES.
00191          10  T-MSGA          PIC X(45).
00192          10  T-CNTRA         PIC ZZ,ZZ9.
00193      SKIP2
00194      05  TOTAL-LINEB.
00195          10  FILLER          PIC X(5)    VALUE SPACES.
00196          10  T-MSGB          PIC X(45).
00197          10  T-CNTRB         PIC ZZ,ZZZ,ZZZ,ZZZ,ZZ9.
00198      SKIP3
00199  LINKAGE SECTION.
00200      SKIP1
00201  01  LINK-INFO.
00202      05  LINK-LENGTH         PIC S9(4)  BINARY.
00203          88  VALID-LK-LENGTH VALUE +9.
00204      SKIP1
00205      05  LINK-YEAR.
00206          10  LINK-YEAR-N     PIC 9(4).
00207      05  LINK-EQFCTR.
00208          10  LINK-EQFCTR-N   PIC 9(5).
00209      EJECT
00210  PROCEDURE DIVISION USING LINK-INFO.
00211      SKIP1
00212  010-HOUSEKEEPING.
00213      SKIP1
00214      IF NOT VALID-LK-LENGTH
00215         DISPLAY 'LINKAGE LENGTH INVALID OR MISSING'
00216         MOVE 16 TO RETURN-CODE
00217         STOP RUN.
00218      SKIP1
00219      IF LINK-YEAR NUMERIC AND LINK-YEAR GREATER ZERO
00220         NEXT SENTENCE
00221      ELSE
00222         DISPLAY 'LINKAGE YEAR INVALID ' LINK-YEAR
00223         MOVE 16 TO RETURN-CODE.
00224      SKIP1
00225      IF LINK-EQFCTR NUMERIC AND LINK-EQFCTR GREATER ZERO
00226         NEXT SENTENCE
00227      ELSE
00228         DISPLAY 'LINKAGE EQFACTOR INVALID ' LINK-EQFCTR
00229         MOVE 16 TO RETURN-CODE.
00230      SKIP1
00231      IF RETURN-CODE = 16
00232         STOP RUN.
00233      SKIP1
00234      OPEN INPUT AGCY-EFTS-FLE.
00235      IF NOT GOOD-EFTS
00236          DISPLAY 'INVALID OPEN ON AGCY EFTS FILE'
00237          DISPLAY 'FILE STATUS IS ' EFTS-STATUS
00238          DISPLAY 'FEEDBACK STATUS IS ' ES-RETURN ' '
00239                    ES-FUNCTION ' ' ES-FEEDBACK
00240          MOVE 16 TO RETURN-CODE
00241          STOP RUN.
00242      SKIP1
00243      OPEN INPUT FRZAGCY-FILE
00244          OUTPUT NEWPROP PRINT-FILE EQUAL-VAL-FILE.
00245      SKIP1
00246      ACCEPT ACPT-DATE FROM DATE.
00247      MOVE ACPT-MO TO DSP-MO.
00248      MOVE ACPT-DA TO DSP-DA.
00249      MOVE ACPT-YR TO DSP-YR.
00250      MOVE DSP-DATE  TO HD2-DATE.
00251      MOVE LINK-EQFCTR-N TO HD2-FACT.
00252      INSPECT HD2-FACT REPLACING ALL ',' BY '.'.
00253      MOVE LINK-YEAR     TO HD2-YEAR.
00254      SKIP1
00255      WRITE PRINT-REC FROM BLANK-LINE AFTER ADVANCING PAGE.
00256      SKIP3
00257      PERFORM 020-MAINLINE
00258         UNTIL FRZ-EOF OR ERR.
00259      SKIP1
00260      PERFORM 040-ACCESS-AGCY-EFTS
00261      IF GOOD-EFTS
00262          PERFORM 050-BUILD-PRINT-DETAIL
00263          WRITE NEWPROP-REC.
00264      SKIP1
00265      MOVE TOT-EQLZ-FRZ-ACCUM TO C-EQLZ-FRZ.
00266      MOVE TOT-ANN-EQLZ-ACCUM TO C-ANN-EQLZ.
00267      MOVE TOT-DIS-EQLZ-ACCUM TO C-DIS-EQLZ.
00268      MOVE TOT-TIF-EQLZ-ACCUM TO C-TIF-EQLZ.
00269      MOVE TOT-EXPINC-EQLZ-ACCUM TO C-EXPINC-EQLZ.
00270      WRITE PRINT-REC FROM COUNTY-LINE AFTER ADVANCING 3.
00271      MOVE 'TOTAL FROZEN RECORDS READ:'  TO T-MSG
00272      MOVE RECS-READ-CNTR TO T-CNTR.
00273      WRITE PRINT-REC FROM TOTAL-LINE AFTER ADVANCING 2.
00274      MOVE 'TOTAL AGENCIES PRINTED...:'  TO T-MSG
00275      MOVE RECS-PRINTED-CNTR TO T-CNTR
00276      WRITE PRINT-REC FROM TOTAL-LINE AFTER ADVANCING 1.
00277      MOVE 'TOTAL EQUAL VAL RECS WRIT:'  TO T-MSG
00278      MOVE EQU-VAL-RECS-WRIT-CNTR TO T-CNTR
00279      WRITE PRINT-REC FROM TOTAL-LINE AFTER ADVANCING 1.
00280      SKIP1
00281      DISPLAY SPACES
00282      MOVE 'TOTAL FROZEN RECORDS READ..................:'
00283           TO T-MSGA
00284      MOVE RECS-READ-CNTR TO T-CNTRA.
00285      DISPLAY TOTAL-LINEA.
00286      MOVE 'TOTAL AGENCIES PRINTED.....................:'
00287           TO T-MSGA
00288      MOVE RECS-PRINTED-CNTR TO T-CNTRA
00289      DISPLAY TOTAL-LINEA.
00290      MOVE 'TOTAL NEW PROPERTY EQUALIZED VALUE.........:'
00291           TO T-MSGB
00292      MOVE TOT-EQLZ-FRZ-ACCUM  TO T-CNTRB.
00293      DISPLAY TOTAL-LINEB.
00294      MOVE 'TOTAL ANNEXED PROPERTY EQUALIZED VALUE.....:'
00295           TO T-MSGB
00296      MOVE TOT-ANN-EQLZ-ACCUM  TO T-CNTRB
00297      DISPLAY TOTAL-LINEB.
00298      MOVE 'TOTAL DISCONNECTED PROPERTY EQUALIZED VALUE:'
00299           TO T-MSGB
00300      MOVE TOT-DIS-EQLZ-ACCUM  TO T-CNTRB
00301      DISPLAY TOTAL-LINEB.
00302      MOVE 'TOTAL RECOVERED TAX INCREMENT VALUE........:'
00303           TO T-MSGB
00304      MOVE TOT-TIF-EQLZ-ACCUM  TO T-CNTRB
00305      DISPLAY TOTAL-LINEB.
00306      MOVE 'TOTAL EXPIRED INCENTIVES VALUE.............:'
00307           TO T-MSGB
00308      MOVE TOT-EXPINC-EQLZ-ACCUM TO T-CNTRB
00309      DISPLAY TOTAL-LINEB.
00310      SKIP1
00311      CLOSE AGCY-EFTS-FLE.
00312      IF NOT GOOD-EFTS
00313          DISPLAY 'INVALID CLOSE ON AGCY EFTS FILE'
00314          DISPLAY 'FILE STATUS IS ' EFTS-STATUS
00315          DISPLAY 'FEEDBACK STATUS IS ' ES-RETURN ' '
00316                    ES-FUNCTION ' ' ES-FEEDBACK
00317          MOVE 16 TO RETURN-CODE.
00318      SKIP1
00319      CLOSE FRZAGCY-FILE NEWPROP PRINT-FILE EQUAL-VAL-FILE.
00320      STOP RUN.
00321      SKIP3
00322  020-MAINLINE.
00323      PERFORM 030-READ-FROZEN-AGENCY.
00324      IF NOT (FRZ-EOF OR ERR)
00325          IF FA-AGCY EQUAL SAVE-AGCY
00326              ADD FA-TOT-FROZ   TO ASSD-FRZ-ACCUM
00327              ADD FA-FROZ-EQLZD TO EQLZ-FRZ-ACCUM
00328              ADD FA-ANNEXEDAV  TO ANN-ASSD-ACCUM
00329              ADD FA-ANNEXEDEV  TO ANN-EQLZ-ACCUM
00330              ADD FA-DISCONAV   TO DIS-ASSD-ACCUM
00331              ADD FA-DISCONEV   TO DIS-EQLZ-ACCUM
00332              ADD FA-TIFEV      TO TIF-EQLZ-ACCUM
00333              ADD FA-EXPINC     TO EXPINC-ASSD-ACCUM
00334              ADD FA-EXPINCEV   TO EXPINC-EQLZ-ACCUM
00335          ELSE
00336              PERFORM 040-ACCESS-AGCY-EFTS
00337              IF GOOD-EFTS
00338                  PERFORM 050-BUILD-PRINT-DETAIL
00339                  WRITE NEWPROP-REC
00340                  PERFORM 070-RESET-COUNTERS
00341                  MOVE FA-AGCY TO SAVE-AGCY.
00342      SKIP2
00343  030-READ-FROZEN-AGENCY.
00344      READ FRZAGCY-FILE
00345        AT END
00346        MOVE 'Y' TO FRZ-EOF-SW.
00347      IF NOT FRZ-EOF
00348          ADD +1 TO RECS-READ-CNTR
00349          IF FA-AGCY LESS PREV-AGCY
00350              DISPLAY 'FROZEN AGENCY FILE OUT OF SEQUENCE'
00351              DISPLAY 'CURRENT  KEY IS ' FA-AGCY
00352              DISPLAY 'PREVIOUS KEY IS ' PREV-AGCY
00353              MOVE 16  TO RETURN-CODE
00354              MOVE 'Y' TO ERR-SW
00355          ELSE
00356              MOVE FA-AGCY TO PREV-AGCY
00357              IF FIRST-TIME
00358                  MOVE FA-AGCY TO SAVE-AGCY.
00359      SKIP3
00360  040-ACCESS-AGCY-EFTS.
00361      MOVE SAVE-AGCY TO AE-AGCYNO.
00362      READ AGCY-EFTS-FLE.
00363      IF NOT GOOD-EFTS AND NOT EFTS-NOTFND
00364          DISPLAY 'INVALID READ ON AGCY EFTS FILE'
00365          DISPLAY 'FILE STATUS IS ' EFTS-STATUS
00366          DISPLAY 'FEEDBACK STATUS IS ' ES-RETURN ' '
00367                    ES-FUNCTION ' ' ES-FEEDBACK
00368          MOVE 'Y' TO ERR-SW
00369          MOVE 16 TO RETURN-CODE
00370      ELSE
00371          IF EFTS-NOTFND
00372              DISPLAY 'NO MATCHING AGENCY DESCRIPTION RECORD'
00373              DISPLAY 'AGECNY NUMBER IS ' FA-AGCY
00374              MOVE 'Y' TO ERR-SW
00375              MOVE 16 TO RETURN-CODE.
00376      SKIP3
00377  050-BUILD-PRINT-DETAIL.
00378      MOVE SPACES TO NEWPROP-REC.
00379      INITIALIZE     NEWPROP-REC
00380                     EQU-VAL-REC.
00381      MOVE AE-AGCYNO      TO D-AGCY-NO TC-AGCY EQU-AGENCY-NO.
00382      INSPECT D-AGCY-NO REPLACING ALL ',' BY '-'.
00383      MOVE AE-DESC        TO D-AGCY-DESC, EQU-AGENCY-DESC.
00384      MOVE ASSD-FRZ-ACCUM TO            TC-ASVAL
00385      MOVE EQLZ-FRZ-ACCUM TO D-EQLZ-FRZ TC-EQVAL
00386                                        EQU-NEW-PROP-EQLZ-VAL
00387      MOVE ANN-ASSD-ACCUM TO            TC-ANXASVAL
00388      MOVE ANN-EQLZ-ACCUM TO D-ANN-EQLZ TC-ANXEQVAL
00389                             EQU-ANNXD-PROP-EQLZ-VAL
00390      MOVE DIS-ASSD-ACCUM TO            TC-DISASVAL
00391      MOVE DIS-EQLZ-ACCUM TO D-DIS-EQLZ TC-DISEQVAL
00392                             EQU-DISC-PROP-EQLZ-VAL
00393      MOVE TIF-EQLZ-ACCUM TO D-TIF-EQLZ TC-TIFDIFEQVAL
00394                             EQU-RECOV-TAX-INCR-VAL
00395      MOVE EXPINC-ASSD-ACCUM TO TC-EXP-INC
00396      MOVE EXPINC-EQLZ-ACCUM TO D-EXP-INC TC-EXPINCEV
00397                                EQU-EXP-INCENT
00398      ADD ASSD-FRZ-ACCUM  TO TOT-ASSD-FRZ-ACCUM.
00399      ADD EQLZ-FRZ-ACCUM  TO TOT-EQLZ-FRZ-ACCUM.
00400      ADD ANN-ASSD-ACCUM  TO TOT-ANN-ASSD-ACCUM.
00401      ADD ANN-EQLZ-ACCUM  TO TOT-ANN-EQLZ-ACCUM.
00402      ADD DIS-ASSD-ACCUM  TO TOT-DIS-ASSD-ACCUM.
00403      ADD DIS-EQLZ-ACCUM  TO TOT-DIS-EQLZ-ACCUM.
00404      ADD TIF-EQLZ-ACCUM  TO TOT-TIF-EQLZ-ACCUM.
00405      ADD EXPINC-EQLZ-ACCUM TO TOT-EXPINC-EQLZ-ACCUM.
00406      SKIP1
00407      IF LINE-CNTR GREATER +54
00408          PERFORM 060-HEADING-RTN.
00409      WRITE PRINT-REC FROM DETAIL-LINE AFTER ADVANCING 2.
00410      WRITE EQU-VAL-REC.
00411      ADD +1 TO EQU-VAL-RECS-WRIT-CNTR.
00412      ADD +2 TO LINE-CNTR.
00413      ADD +1 TO RECS-PRINTED-CNTR.
00414      SKIP3
00415  060-HEADING-RTN.
00416      ADD +1 TO PAGE-CNTR.
00417      MOVE PAGE-CNTR TO HD1-PAGE.
00418      WRITE PRINT-REC FROM HEADING1 AFTER ADVANCING PAGE.
00419      WRITE PRINT-REC FROM HEADING2  AFTER ADVANCING 2.
00420      WRITE PRINT-REC FROM HEADING2A AFTER ADVANCING 1.
00421      WRITE PRINT-REC FROM HEADING3 AFTER ADVANCING 2.
00422      WRITE PRINT-REC FROM HEADING4 AFTER ADVANCING 1.
00423      MOVE +7 TO LINE-CNTR.
00424      SKIP3
00425  070-RESET-COUNTERS.
00426      MOVE FA-TOT-FROZ   TO ASSD-FRZ-ACCUM
00427      MOVE FA-FROZ-EQLZD TO EQLZ-FRZ-ACCUM
00428      MOVE FA-ANNEXEDAV  TO ANN-ASSD-ACCUM
00429      MOVE FA-ANNEXEDEV  TO ANN-EQLZ-ACCUM
00430      MOVE FA-DISCONAV   TO DIS-ASSD-ACCUM
00431      MOVE FA-DISCONEV   TO DIS-EQLZ-ACCUM
00432      MOVE FA-TIFEV      TO TIF-EQLZ-ACCUM
00433      MOVE FA-EXPINC     TO EXPINC-ASSD-ACCUM
00434      MOVE FA-EXPINCEV   TO EXPINC-EQLZ-ACCUM.
