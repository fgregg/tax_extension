00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. CLRTM752.
00003 *AUTHOR. LAURA DOWNS - STAN MOLIS.
00004 *DATE-WRITTEN. JUNE, 1995.
00005 *REMARKS. THIS PROGRAM APPLIES THE TAXING AGENCIES TO THE
00006 *         ASSESSMENT MASTER FILE.
00007 *****************************************************************
00008 *           PROGRAM WORK REQUEST (MODIFICATION)                 *
00009 *                                                               *
00010 * PROGRAMMER: DPSILVA                                           *
00011 * DATE: 09/02/99                                                *
00012 * REQUEST LETTER NAME: CL071799.001                             *
00013 *****************************************************************
00014 *                MODIFICATION DESCRIPTION                       *
00015 *                                                               *
00016 * MODIFIED PROGRAM CLRTM752 FOR YEAR 2000.                      *
00017 *                                                               *
00018 * 1. IN WORKING STORAGE:                                        *
00019 *   A). EXPANDED HD2-DATE FROM 8 TO 10 AND CHANGE THE PROGRAM   *
00020 *       TO DISPLAY THE CURRENT DATE ALONG WITH CENTURY IN THIS  *
00021 *       FIELD.                                                  *
00022 *****************************************************************
00023
00024  ENVIRONMENT DIVISION.
00025  CONFIGURATION SECTION.
00026  SOURCE-COMPUTER. IBM-370.
00027  OBJECT-COMPUTER. IBM-370.
00028
00029  INPUT-OUTPUT SECTION.
00030  FILE-CONTROL.
00031
00032      SELECT MASTER-IN ASSIGN TO UT-S-MASTERIN.
00033
00034      SELECT TXCDFILE ASSIGN TO DA-TXCDFILE
00035          ORGANIZATION IS INDEXED
00036          ACCESS  MODE IS RANDOM
00037          RECORD   KEY IS TR-TAX-CODE
00038          FILE  STATUS IS TXCD-STATUS.
00039
00040      SELECT AGENCY-ASMT-MASTER ASSIGN TO UT-S-AGCYMSTR.
00041
00042      SELECT PRINT-FILE ASSIGN TO UT-S-PRINTFLE.
00043
00044
00045  DATA DIVISION.
00046  FILE SECTION.
00047
00048  FD  MASTER-IN
00049      BLOCK CONTAINS 0 CHARACTERS
00050      RECORD CONTAINS 115 CHARACTERS.
00051  01  MSTR-IN-REC.
00052  COPY CLEQVLDV01.
00053
00054  COPY TAXCODEFD2.
00055
00056  COPY CLASRAGYF1.
00057
00058  COPY PRINTFILE.
00059
00060
00061  WORKING-STORAGE SECTION.
00062
00063  77  PAGE-CNTR              PIC S9(5)    PACKED-DECIMAL VALUE +0.
00064  77  LINE-CNTR              PIC S9(3)    PACKED-DECIMAL VALUE +60.
00065  77  MAST-READ-CNTR         PIC S9(7)    PACKED-DECIMAL VALUE +0.
00066  77  MAST-WRITTEN-CNTR      PIC S9(7)    PACKED-DECIMAL VALUE +0.
00067  77  MAST-NOMATCH-CNTR      PIC S9(7)    PACKED-DECIMAL VALUE +0.
00068  77  SUB                    PIC S9(4)    BINARY.
00069  77  SUB1                   PIC S9(4)    BINARY.
00070  77  BLNK                   PIC X        VALUE SPACE.
00071
00072  77  NO-MATCH-MSG           PIC X(34)    VALUE
00073      'NO MATCHING TAX CODE MASTER RECORD'.
00074
00075  77  MAST-EOF-SW            PIC X        VALUE 'N'.
00076      88  MAST-EOF           VALUE 'Y'.
00077
00078  77  ERR-SW                 PIC X        VALUE 'N'.
00079      88  ERR                VALUE 'Y'.
00080
00081  77  TXCD-STATUS            PIC 99.
00082      88  GOOD-TXCD          VALUE 00 97.
00083      88  TXCD-NOTFND        VALUE 23.
00084
00085  01  WORK-AREA.
00086
00087      05  TXCD-STATUS2       BINARY.
00088          10  TXCD-RETURN    PIC 99      VALUE 0.
00089          10  TXCD-FUNCTION  PIC 9       VALUE 0.
00090          10  TXCD-FEEDBACK  PIC 999     VALUE 0.
00091
00092      05  DSP-DATE.
00093          10  DSP-MO                 PIC 99.
00094          10  FILLER                 PIC X         VALUE '/'.
00095          10  DSP-DA                 PIC 99.
00096          10  FILLER                 PIC X         VALUE '/'.
00097          10  DSP-YR                 PIC 9999.
00098
00099      05  ACPT-DATE                  PIC 9(8).
00100      05  ACPT-DATE-X REDEFINES ACPT-DATE.
00101          10  ACPT-YR                PIC 9999.
00102          10  ACPT-MO                PIC 99.
00103          10  ACPT-DA                PIC 99.
00104
00105      05  UNPK-TXCD          PIC 9(5).
00106      05  FILLER REDEFINES UNPK-TXCD.
00107          10  UP-TOWN        PIC 99.
00108          10  FILLER         PIC X(3).
00109
00110      05  CURR-MAST.
00111          10  CM-TOWN        PIC 99.
00112          10  CM-VOL         PIC 9(3).
00113          10  CM-PROP        PIC 9(14).
00114          10  CM-TXTYP       PIC 9.
00115
00116      05  PREV-MAST          PIC X(20).
00117
00118      05  HEADING1.
00119          10  FILLER         PIC X       VALUE SPACE.
00120          10  FILLER         PIC X(48)   VALUE 'CLRTM752'.
00121          10  FILLER         PIC X(71)   VALUE
00122              'OFFICE OF THE COOK COUNTY CLERK'.
00123          10  FILLER         PIC X(5)    VALUE 'PAGE'.
00124          10  HD1-PAGE       PIC Z,ZZ9.
00125
00126      05  HEADING2.
00127          10  FILLER         PIC X        VALUE SPACE.
00128          10  HD2-DATE       PIC X(10).
00129          10  FILLER         PIC X(32)    VALUE SPACES.
00130          10  FILLER         PIC X(48)    VALUE
00131              'DISCONNECT/ANNEXATION AGENCY UPDATE ERROR REPORT'.
00132
00133      05  HEADING3.
00134          10  FILLER         PIC X(3)     VALUE SPACES.
00135          10  FILLER         PIC X(7)     VALUE 'VOL'.
00136          10  FILLER         PIC X(20)    VALUE 'PERMANENT INDEX'.
00137          10  FILLER         PIC X(7)     VALUE 'TAX'.
00138          10  FILLER         PIC X(3)     VALUE 'TAX'.
00139
00140      05  HEADING4.
00141          10  FILLER         PIC X(15)    VALUE SPACES.
00142          10  FILLER         PIC X(14)    VALUE 'NUMBER'.
00143          10  FILLER         PIC X(7)     VALUE 'TYPE'.
00144          10  FILLER         PIC X(4)     VALUE 'CODE'.
00145
00146      05  DETAIL-LINE                     VALUE SPACES.
00147          10  FILLER         PIC X(3).
00148          10  D-VOL          PIC 9(3).
00149          10  FILLER         PIC X(3).
00150          10  D-PROP         PIC 99,99,999,999,9999.
00151          10  FILLER         PIC X(3).
00152          10  D-TXTYP        PIC 9.
00153          10  FILLER         PIC X.
00154          10  D-TXCD         PIC 9(5).
00155          10  FILLER         PIC X(3).
00156          10  D-MSG          PIC X(35).
00157
00158      05  TOTAL-LINE.
00159          10  FILLER         PIC X(3).
00160          10  T-MSG          PIC X(43).
00161          10  T-CNT          PIC Z,ZZZ,ZZ9.
00162
00163      EJECT
00164  PROCEDURE DIVISION.
00165
00166  010-HOUSEKEEPING.
00167      OPEN INPUT TXCDFILE
00168      IF NOT GOOD-TXCD
00169          MOVE 16 TO RETURN-CODE
00170          DISPLAY 'TAX CODE FILE OPEN ERROR ' TXCD-STATUS
00171          DISPLAY 'TXCD FEEDBACK AREA ' TXCD-RETURN ' '
00172                    TXCD-FUNCTION ' ' TXCD-FEEDBACK.
00173
00174      OPEN INPUT MASTER-IN
00175          OUTPUT AGENCY-ASMT-MASTER  PRINT-FILE.
00176
00177 *    ACCEPT ACPT-DATE FROM DATE.
00178      MOVE FUNCTION CURRENT-DATE(1:8) TO ACPT-DATE.
00179      MOVE ACPT-MO TO DSP-MO
00180      MOVE ACPT-DA TO DSP-DA
00181      MOVE ACPT-YR TO DSP-YR
00182      MOVE DSP-DATE TO HD2-DATE
00183
00184      WRITE PRINT-REC FROM BLNK AFTER ADVANCING PAGE
00185
00186
00187      PERFORM 020-MAINLINE
00188         UNTIL MAST-EOF OR ERR
00189
00190
00191      IF LINE-CNTR GREATER +55
00192          PERFORM 080-HEADING-RTN.
00193
00194      MOVE 'TOTAL ASSESSMENT MASTER RECORDS READ' TO T-MSG.
00195      MOVE MAST-READ-CNTR TO T-CNT.
00196      WRITE PRINT-REC FROM TOTAL-LINE AFTER ADVANCING 3.
00197      DISPLAY TOTAL-LINE.
00198
00199      MOVE 'TOTAL ASSESSMENT MASTER RECORDS WRITTEN' TO T-MSG.
00200      MOVE MAST-WRITTEN-CNTR TO T-CNT.
00201      WRITE PRINT-REC FROM TOTAL-LINE AFTER ADVANCING 1.
00202      DISPLAY TOTAL-LINE.
00203
00204      MOVE 'TOTAL ASSESSMENT MASTER RECORDS UNMATCHED' TO T-MSG.
00205      MOVE MAST-NOMATCH-CNTR TO T-CNT.
00206      WRITE PRINT-REC FROM TOTAL-LINE AFTER ADVANCING 1.
00207      DISPLAY TOTAL-LINE.
00208
00209
00210      CLOSE MASTER-IN  AGENCY-ASMT-MASTER  PRINT-FILE
00211
00212      CLOSE TXCDFILE
00213      IF NOT GOOD-TXCD
00214          MOVE 16 TO RETURN-CODE
00215          DISPLAY 'TAX CODE FILE CLOSE ERROR ' TXCD-STATUS
00216          DISPLAY 'TXCD FEEDBACK AREA ' TXCD-RETURN ' '
00217                    TXCD-FUNCTION ' ' TXCD-FEEDBACK.
00218      STOP RUN.
00219
00220
00221  020-MAINLINE.
00222
00223      PERFORM 030-READ-MASTER
00224      IF NOT MAST-EOF
00225         PERFORM 040-ACCESS-TAX-CODE-FILE
00226         IF NOT ERR
00227             IF TXCD-NOTFND
00228                 PERFORM 050-NO-MATCH-RTN
00229             ELSE
00230                 PERFORM 060-MATCH-RTN.
00231
00232
00233  030-READ-MASTER.
00234      READ MASTER-IN
00235         AT END
00236           MOVE 'Y' TO MAST-EOF-SW.
00237
00238      IF NOT MAST-EOF
00239          ADD +1 TO MAST-READ-CNTR
00240          MOVE DV-TXCD TO UNPK-TXCD
00241          MOVE UP-TOWN   TO CM-TOWN
00242          MOVE DV-VOL    TO CM-VOL
00243          MOVE DV-PROP   TO CM-PROP
00244          MOVE DV-TXTYP  TO CM-TXTYP
00245          IF CURR-MAST NOT GREATER PREV-MAST
00246              DISPLAY 'R.E. DIVISION VAL FILE SEQUENCE ERROR'
00247              DISPLAY 'CURRENT  RECORD IS ' CURR-MAST
00248              DISPLAY 'PREVIOUS RECORD IS ' PREV-MAST
00249              MOVE 'Y' TO ERR-SW
00250              MOVE 16 TO RETURN-CODE
00251          ELSE
00252              MOVE CURR-MAST TO PREV-MAST.
00253
00254  040-ACCESS-TAX-CODE-FILE.
00255      MOVE DV-TXCD TO TR-TAX-CODE
00256
00257      READ TXCDFILE
00258      IF NOT GOOD-TXCD AND NOT TXCD-NOTFND
00259          DISPLAY 'TAX CODE FILE READ ERROR ' TXCD-STATUS
00260          DISPLAY 'TXCD FEEDBACK AREA ' TXCD-RETURN ' '
00261                    TXCD-FUNCTION ' ' TXCD-FEEDBACK
00262          MOVE 16 TO RETURN-CODE
00263          MOVE 'Y' TO ERR-SW.
00264
00265
00266  050-NO-MATCH-RTN.
00267      MOVE DV-VOL       TO D-VOL.
00268      MOVE DV-PROP      TO D-PROP.
00269      INSPECT D-PROP REPLACING ALL ',' BY '-'.
00270      MOVE DV-TXTYP     TO D-TXTYP.
00271      MOVE DV-TXCD      TO D-TXCD.
00272      MOVE NO-MATCH-MSG TO D-MSG.
00273      ADD +1 TO MAST-NOMATCH-CNTR.
00274
00275      WRITE PRINT-REC FROM DETAIL-LINE AFTER ADVANCING 2.
00276      ADD +2 TO LINE-CNTR.
00277
00278
00279  060-MATCH-RTN.
00280      MOVE DV-VOL         TO AM-VOL.
00281      MOVE DV-PROP        TO AM-PROP.
00282      MOVE DV-TXTYP       TO AM-TXTYP.
00283      MOVE DV-TXCD        TO AM-TXCD.
00284      MOVE DV-CLASS       TO AM-CLASS.
00285      MOVE DV-STAT        TO AM-STAT.
00286      MOVE DV-DIV-PROP-NO TO AM-DIVNO.
00287      MOVE DV-ASSD-VAL    TO AM-ASSD-VAL.
00288      MOVE DV-DV-VAL      TO AM-EQUL-VAL.
00289      MOVE TR-RATE        TO AM-RATE.
00290      MOVE 0 TO SUB1.
00291      PERFORM 070-MOVE-AGENCIES
00292         VARYING SUB FROM 1 BY 1
00293         UNTIL SUB GREATER 40.
00294      WRITE AGCY-MSTR-REC.
00295      ADD +1 TO MAST-WRITTEN-CNTR.
00296
00297  070-MOVE-AGENCIES.
00298      ADD +1 TO SUB1.
00299      MOVE TR-AGENCY (SUB) TO AM-AGENCY (SUB1).
00300
00301  080-HEADING-RTN.
00302      ADD +1 TO PAGE-CNTR
00303      MOVE PAGE-CNTR TO HD1-PAGE
00304      WRITE PRINT-REC FROM HEADING1 AFTER ADVANCING PAGE
00305      WRITE PRINT-REC FROM HEADING2 AFTER ADVANCING 2
00306      WRITE PRINT-REC FROM HEADING3 AFTER ADVANCING 2
00307      WRITE PRINT-REC FROM HEADING4 AFTER ADVANCING 1
00308      WRITE PRINT-REC FROM BLNK     AFTER ADVANCING 1
00309      MOVE +7 TO LINE-CNTR.
