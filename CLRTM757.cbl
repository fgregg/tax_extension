00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. CLRTM757.
00003 *AUTHOR. SQUILLACE-KISTINGER
00004 *DATE-WRITTEN. JULY,1995
00005 *REMARKS. THIS PROGRAM CREATES THE UPDATED TIF DIFFERENCE FILE,
00006 *                      UPDATED TIF DIFFERENCE REPORT AND
00007 *                      NO-MATCHING EQUALIZED VALUE REPORT.
00008 *****************************************************************
00009 *           PROGRAM WORK REQUEST (MODIFICATION)                 *
00010 *                                                               *
00011 * PROGRAMMER: DPSILVA                                           *
00012 * DATE: 09/02/99                                                *
00013 * REQUEST LETTER NAME: CL071799.001                             *
00014 *****************************************************************
00015 *                MODIFICATION DESCRIPTION                       *
00016 *                                                               *
00017 * MODIFIED PROGRAM CLRTM757 FOR YEAR 2000.                      *
00018 *                                                               *
00019 * 1. IN WORKING STORAGE:                                        *
00020 *   A). EXPANDED H2-DATE AND H2B-DATE FROM 8 TO 10 AND CHANGED  *
00021 *       PROGRAM TO DISPLAY THE CURRENT DATE ALONG WITH CENTURY  *
00022 *       IN THIS FIELD.                                          *
00023 *****************************************************************
00024      SKIP1
00025  ENVIRONMENT DIVISION.
00026  CONFIGURATION SECTION.
00027  SOURCE-COMPUTER. IBM-370.
00028  OBJECT-COMPUTER. IBM-370.
00029      SKIP1
00030  INPUT-OUTPUT SECTION.
00031  FILE-CONTROL.
00032      SELECT EQ-VAL-FILE  ASSIGN TO UT-S-EQVAL.
00033      SELECT CLTIFMSTP    ASSIGN TO UT-S-CLTIFP.
00034      SELECT CLTIFMSTC    ASSIGN TO UT-S-CLTIFC.
00035      SELECT PRINT-FILE   ASSIGN TO UT-S-PRINT.
00036      SELECT PRT-ERR-FILE ASSIGN TO UT-S-PRTERR.
00037      SELECT CLTIFDIF     ASSIGN TO UT-S-CLTIFDIF.
00038      SKIP1
00039  DATA DIVISION.
00040  FILE SECTION.
00041      SKIP1
00042  FD  CLTIFMSTP
00043      BLOCK CONTAINS 0 RECORDS
00044      RECORD CONTAINS 64 CHARACTERS
00045      LABEL RECORDS ARE STANDARD
00046      DATA RECORD IS CLTIFMSTP-REC.
00047  01  CLTIFMSTP-REC.
00048 *    * T A X   I N C R E M E N T   F I N A N C E   D E T A I L
00049 *                  P R O P E R T Y   M A S T E R   P R I O R
00050      05  TFP-VOL          PIC 9(3).
00051 *                                              1-3   VOLUME
00052      05  TFP-PROP         PIC 9(14).
00053 *                                              4-17  PROPERTY NO.
00054      05  TFP-TXTYP        PIC 9.
00055 *                                             18     TAX TYPE
00056      05  TFP-TXYR         PIC 9(4).
00057 *                                             19-22  TAX YEAR
00058 *                                                     ADDED
00059      05  TFP-TXCD         PIC 9(5).
00060 *                                             23-27  TAX CODE
00061      05  TFP-AGCY         PIC 9(9).
00062 *                                             28-36  AGENCY
00063      05  TFP-FRZEQVAL     PIC S9(11).
00064 *                                             37-47  FROZEN
00065 *                                                     EQUALIZED
00066 *                                                     VALUATION
00067      05  FILLER           PIC X(17).
00068 *                                             48-64  FILLER
00069      EJECT
00070  FD  CLTIFMSTC
00071      BLOCK CONTAINS 0 RECORDS
00072      RECORD CONTAINS 64 CHARACTERS
00073      LABEL RECORDS ARE STANDARD
00074      DATA RECORD IS CLTIFMSTC-REC.
00075  01  CLTIFMSTC-REC.
00076 *    * T A X   I N C R E M E N T   F I N A N C E   D E T A I L
00077 *            P R O P E R T Y   M A S T E R   C U R R E N T
00078      05  TFC-VOL          PIC 9(3).
00079 *                                              1-3   VOLUME
00080      05  TFC-PROP         PIC 9(14).
00081 *                                              4-17  PROPERTY NO.
00082      05  TFC-TXTYP        PIC 9.
00083 *                                             18     TAX TYPE
00084      05  TFC-TXYR         PIC 9(4).
00085 *                                             19-22  TAX YEAR
00086 *                                                     ADDED
00087      05  TFC-TXCD         PIC 9(5).
00088 *                                             23-27  TAX CODE
00089      05  TFC-AGCY         PIC 9(9).
00090 *                                             28-36  AGENCY
00091      05  TFC-FRZEQVAL     PIC S9(11).
00092 *                                             37-47  FROZEN
00093 *                                                     EQUALIZED
00094 *                                                     VALUATION
00095      05  FILLER           PIC X(17).
00096 *                                             48-64  FILLER
00097      EJECT
00098  COPY EQVALFD01.
00099      EJECT
00100  COPY PRINTFILE.
00101      SKIP3
00102  COPY PRINTERRFL.
00103      EJECT
00104  COPY CLTIFDIFF1.
00105      EJECT
00106  WORKING-STORAGE SECTION.
00107      SKIP1
00108  77  CHECK-PROCESSING       PIC X     VALUE 'Y'.
00109      88  STOP-PROCESSING              VALUE 'N'.
00110      88  PROCESSING-OK                VALUE 'Y'.
00111      SKIP1
00112  77  CLC-EOF-SW             PIC X     VALUE 'N'.
00113      88  CLC-EOF                      VALUE 'Y'.
00114      SKIP1
00115  77  CLP-EOF-SW             PIC X     VALUE 'N'.
00116      88  CLP-EOF                      VALUE 'Y'.
00117      SKIP1
00118  77  EQVAL-EOF-SW           PIC X     VALUE 'N'.
00119      88  EQVAL-EOF                    VALUE 'Y'.
00120      SKIP1
00121  77  WS-EQ-SW               PIC X     VALUE '0'.
00122      88  WS-EQ-ZERO                   VALUE '0'.
00123      88  WS-EQ-ONE                    VALUE '1'.
00124      SKIP1
00125  77  LINE-CNT               PIC S9(3) VALUE +58  PACKED-DECIMAL.
00126  77  LINE-CNTB              PIC S9(3) VALUE +58  PACKED-DECIMAL.
00127  77  PAGE-CNT               PIC S9(7) VALUE +0   PACKED-DECIMAL.
00128  77  CNTRL-WRITTEN          PIC S9(7) VALUE +0   PACKED-DECIMAL.
00129  77  CNTRL-WRITTENB         PIC S9(7) VALUE +0   PACKED-DECIMAL.
00130  77  PAGE-CNTB              PIC S9(7) VALUE +0   PACKED-DECIMAL.
00131  77  TOT-CLC-RD             PIC S9(7) VALUE +0   PACKED-DECIMAL.
00132  77  TOT-CLP-RD             PIC S9(7) VALUE +0   PACKED-DECIMAL.
00133  77  TOT-UNM-EQVAL          PIC S9(7) VALUE +0   PACKED-DECIMAL.
00134  77  TOT-RECS-WR            PIC S9(7) VALUE +0   PACKED-DECIMAL.
00135  77  EQVAL-RECS             PIC S9(7) VALUE +0   PACKED-DECIMAL.
00136  77  WS-TIF-DIFF            PIC S9(11) VALUE +0  PACKED-DECIMAL.
00137  77  SUB                    PIC S9(4) VALUE +0   BINARY.
00138  77  SUBB                   PIC S9(4) VALUE +0   BINARY.
00139  77  BLNK                   PIC X     VALUE SPACE.
00140      EJECT
00141  01  REPORT-LINES.
00142      SKIP1
00143      05  HL-1.
00144          10  FILLER         PIC X(40) VALUE ' CLRTM757-A'.
00145          10  FILLER         PIC X(80) VALUE
00146          'OFFICE OF THE COOK COUNTY CLERK'.
00147          10  FILLER         PIC X(05) VALUE 'PAGE'.
00148          10  H1-PAGE        PIC ZZ,ZZ9.
00149      SKIP1
00150      05  HL-1B.
00151          10  FILLER         PIC X(40) VALUE ' CLRTM757-B'.
00152          10  FILLER         PIC X(80) VALUE
00153          'OFFICE OF THE COOK COUNTY CLERK'.
00154          10  FILLER         PIC X(05) VALUE 'PAGE'.
00155          10  H1B-PAGE       PIC ZZ,ZZ9.
00156      SKIP1
00157      05  HL-2.
00158          10  FILLER         PIC X     VALUE SPACE.
00159          10  H2-DATE        PIC X(10).
00160          10  FILLER         PIC X(26) VALUE SPACE.
00161          10  FILLER         PIC X(41) VALUE
00162          'NO MATCHING EQUALIZED VALUE RECORD REPORT'.
00163      SKIP1
00164      05  HL-2B.
00165          10  FILLER         PIC X     VALUE SPACE.
00166          10  H2B-DATE       PIC X(10).
00167          10  FILLER         PIC X(31) VALUE SPACE.
00168          10  FILLER         PIC X(32) VALUE
00169          'UPDATED T.I.F. DIFFERENCE REPORT'.
00170      SKIP1
00171      05  HL-3.
00172          10  FILLER         PIC X(31) VALUE SPACE.
00173          10  FILLER         PIC X(20) VALUE
00174          'TAX   TAX   TAXYR'.
00175      SKIP1
00176      05  HL-4.
00177          10  FILLER         PIC X(05) VALUE SPACE.
00178          10  FILLER         PIC X(55) VALUE
00179          'VOL   PROPERTY INDEX NO.  TYPE  CODE  ADDED  AGENCY'.
00180          10  FILLER         PIC X(28) VALUE
00181          'FROZEN EQ-VALUE'.
00182      SKIP1
00183      05  HL-4B.
00184          10  FILLER         PIC X(05) VALUE SPACE.
00185          10  FILLER         PIC X(55) VALUE
00186          'VOL   PROPERTY INDEX NO.  TYPE  CODE  ADDED  AGENCY'.
00187          10  FILLER         PIC X(50) VALUE
00188          'FROZEN EQ-VALUE  CURRENT EQ-VALUE   TIF DIFFERENCE'.
00189      SKIP1
00190      05  DET-LN.
00191          10  FILLER         PIC X(5)  VALUE SPACE.
00192          10  DL-VOL         PIC 999.
00193          10  FILLER         PIC XXX   VALUE SPACE.
00194          10  DL-PROP        PIC 99,99,999,999,9999.
00195          10  FILLER         PIC X(3)  VALUE SPACE.
00196          10  DL-TXTYP       PIC 9.
00197          10  FILLER         PIC X(3)  VALUE SPACE.
00198          10  DL-TXCD        PIC 9(5).
00199          10  FILLER         PIC XX    VALUE SPACE.
00200          10  DL-TXYR-ADD    PIC 9999.
00201          10  FILLER         PIC XX    VALUE SPACE.
00202          10  DL-AGCY        PIC 9(2),9(4),9(3).
00203          10  FILLER         PIC X(3)  VALUE SPACE.
00204          10  DL-FREQVAL     PIC ZZ,ZZZ,ZZZ,ZZ9.
00205          10  FILLER         PIC X(3)  VALUE SPACE.
00206          10  DL-MSG         PIC X(46).
00207      SKIP1
00208      05  DET-LNB.
00209          10  FILLER         PIC X(5)  VALUE SPACE.
00210          10  DLB-VOL        PIC 999.
00211          10  FILLER         PIC XXX   VALUE SPACE.
00212          10  DLB-PROP       PIC 99,99,999,999,9999.
00213          10  FILLER         PIC X(3)  VALUE SPACE.
00214          10  DLB-TXTYP      PIC 9.
00215          10  FILLER         PIC X(3)  VALUE SPACE.
00216          10  DLB-TXCD       PIC 9(5).
00217          10  FILLER         PIC XX    VALUE SPACE.
00218          10  DLB-TXYR-ADD   PIC 9999.
00219          10  FILLER         PIC XX    VALUE SPACE.
00220          10  DLB-AGCY       PIC 9(2),9(4),9(3).
00221          10  FILLER         PIC X(3)  VALUE SPACE.
00222          10  DLB-FREQVAL    PIC ZZ,ZZZ,ZZZ,ZZ9.
00223          10  FILLER         PIC X(2)  VALUE SPACE.
00224          10  DLB-CUEQVAL    PIC ZZ,ZZZ,ZZZ,ZZ9.
00225          10  FILLER         PIC X(3)  VALUE SPACE.
00226          10  DLB-TIFDIFF    PIC ZZ,ZZZ,ZZZ,ZZ9.
00227      SKIP1
00228      05  TOT-LN.
00229          10  FILLER       PIC X(5)  VALUE SPACE.
00230          10  TL-MSG       PIC X(44) VALUE SPACE.
00231          10  TL-TOT       PIC ZZZ,ZZ9.
00232      SKIP1
00233      05  TOT-LNB.
00234          10  FILLER       PIC X(5)  VALUE SPACE.
00235          10  TLB-MSG      PIC X(54) VALUE SPACE.
00236          10  TLB-TOT      PIC ZZZ,ZZ9.
00237      EJECT
00238  01  WORK-AREA.
00239      SKIP1
00240      05  CLC-CURR-KEY                 VALUE ZEROS.
00241          10  CC-PROP        PIC 9(14).
00242          10  CC-TXYR-ADD    PIC 9999.
00243          10  CC-TXTYP       PIC 9.
00244      SKIP1
00245      05  CLC-PREV-KEY                 VALUE ZEROS.
00246          10  CP-PROP        PIC 9(14).
00247          10  CP-TXYR-ADD    PIC 9999.
00248          10  CP-TXTYP       PIC 9.
00249      SKIP1
00250      05  CLP-CURR-KEY                 VALUE ZEROS.
00251          10  PC-PROP        PIC 9(14).
00252          10  PC-TXYR-ADD    PIC 9999.
00253          10  PC-TXTYP       PIC 9.
00254      SKIP1
00255      05  CLP-PREV-KEY                 VALUE ZEROS.
00256          10  PP-PROP        PIC 9(14).
00257          10  PP-TXYR-ADD    PIC 9999.
00258          10  PP-TXTYP       PIC 9.
00259      SKIP1
00260      05  CURR-EQ-PROP       PIC 9(14) VALUE ZEROS.
00261      05  CURR-TXTYP         PIC 9     VALUE ZEROS.
00262      SKIP1
00263      05  PREV-EQ-PROP       PIC 9(14) VALUE ZEROS.
00264      05  PREV-TXTYP         PIC 9     VALUE ZEROS.
00265      SKIP1
00266      05  UNPK-TXCD          PIC 9(5).
00267      05  FILLER REDEFINES UNPK-TXCD.
00268          10  WS-TOWN        PIC 99.
00269          10  WS-TAX         PIC 999.
00270      SKIP1
00271      05  DSP-TIME                   PIC 99,99.
00272      05  DSP-DATE.
00273          10  DSP-MO                 PIC 99.
00274          10  FILLER                 PIC X         VALUE '/'.
00275          10  DSP-DA                 PIC 99.
00276          10  FILLER                 PIC X         VALUE '/'.
00277          10  DSP-YR                 PIC 9999.
00278      05  ACPT-DATE                  PIC 9(8).
00279      05  ACPT-DATE-X REDEFINES ACPT-DATE.
00280          10  ACPT-YR                PIC 9999.
00281          10  ACPT-MO                PIC 99.
00282          10  ACPT-DA                PIC 99.
00283      05  ACPT-TIME-HOLD             PIC 9(8).
00284      05  ACPT-TIME-HOLD-X REDEFINES ACPT-TIME-HOLD.
00285          10  ACPT-TIME              PIC 9(4).
00286          10  FILLER                 PIC 9(4).
00287      EJECT
00288  PROCEDURE DIVISION.
00289      SKIP1
00290      ACCEPT ACPT-TIME-HOLD FROM TIME.
00291      MOVE ACPT-TIME TO DSP-TIME.
00292      INSPECT DSP-TIME REPLACING ALL ',' BY ':'.
00293 *    ACCEPT ACPT-DATE FROM DATE.
00294      MOVE FUNCTION CURRENT-DATE(1:8) TO ACPT-DATE.
00295      MOVE ACPT-MO TO DSP-MO.
00296      MOVE ACPT-DA TO DSP-DA.
00297      MOVE ACPT-YR TO DSP-YR.
00298      DISPLAY 'PROGRAM CLRTM757 DATE AND TIME OF RUN = '
00299        DSP-DATE ' ' DSP-TIME.
00300      MOVE DSP-DATE     TO H2-DATE H2B-DATE.
00301      SKIP1
00302  020-OPEN-FILES.
00303      SKIP1
00304      OPEN INPUT CLTIFMSTC CLTIFMSTP EQ-VAL-FILE
00305           OUTPUT PRT-ERR-FILE CLTIFDIF PRINT-FILE
00306      WRITE PRT-ERR-REC FROM BLNK  AFTER ADVANCING PAGE
00307      WRITE PRINT-REC   FROM BLNK  AFTER ADVANCING PAGE.
00308      SKIP1
00309  030-INITIAL-READS.
00310      PERFORM 660-READ-CLTIFMSTP
00311      PERFORM 700-READ-CLTIFMSTC
00312      SKIP1
00313      PERFORM 050-MAINLINE UNTIL (CLC-EOF AND CLP-EOF)
00314                              OR STOP-PROCESSING.
00315      SKIP1
00316      IF LINE-CNT GREATER THAN +55
00317      PERFORM 550-HEADING-RTN.
00318      IF CNTRL-WRITTEN GREATER THAN +0
00319      PERFORM 999-LINECNT
00320      VARYING SUB FROM LINE-CNT BY +1
00321        UNTIL SUB NOT LESS +54
00322      MOVE 'TOTAL NUMBER NO-MATCHING EQ-VALUE RECORDS ' TO TL-MSG
00323      MOVE TOT-UNM-EQVAL TO TL-TOT
00324      WRITE PRINT-REC FROM TOT-LN AFTER ADVANCING 1
00325      DISPLAY
00326      'TOTAL NUMBER NO-MATCHING EQ-VALUE RECORDS ' TOT-UNM-EQVAL
00327      SKIP1
00328      IF LINE-CNTB GREATER THAN +55
00329      PERFORM 570-HEADING-RTN-B.
00330      IF CNTRL-WRITTENB GREATER THAN +0
00331      PERFORM 999-LINECNTB
00332      VARYING SUBB FROM LINE-CNTB BY +1
00333        UNTIL SUBB NOT LESS +54
00334      MOVE
00335      'TOTAL NUMBER PRIOR TAX YEAR TIF RECORDS READ' TO TLB-MSG
00336      MOVE TOT-CLP-RD    TO TLB-TOT
00337      WRITE PRT-ERR-REC FROM TOT-LNB AFTER ADVANCING 1
00338      DISPLAY
00339      'TOTAL NUMBER PRIOR TAX YEAR TIF RECORDS READ ' TOT-CLP-RD
00340      SKIP1
00341      MOVE
00342      'TOTAL NUMBER CURRENT TAX YEAR TIF RECORDS READ ' TO TLB-MSG
00343      MOVE TOT-CLC-RD    TO TLB-TOT
00344      WRITE PRT-ERR-REC FROM TOT-LNB AFTER ADVANCING 1
00345      DISPLAY
00346      'TOTAL NUMBER CURRENT TAX YEAR TIF RECORDS READ ' TOT-CLC-RD
00347      SKIP1
00348      MOVE 'TOTAL NUMBER UPDATED TIF DIFFERENCE RECORDS WRITTEN '
00349       TO TLB-MSG
00350      MOVE TOT-RECS-WR   TO TLB-TOT
00351      WRITE PRT-ERR-REC FROM TOT-LNB AFTER ADVANCING 1
00352      DISPLAY
00353      'TOTAL NUMBER UPDATED TIF DIFFERENCE RECORDS WRITTEN '
00354                                                        TOT-RECS-WR
00355      SKIP1
00356      CLOSE CLTIFMSTC CLTIFMSTP EQ-VAL-FILE
00357            PRT-ERR-FILE CLTIFDIF PRINT-FILE.
00358      SKIP1
00359      STOP RUN.
00360      SKIP1
00361  050-MAINLINE.
00362      IF CLP-CURR-KEY EQUAL CLC-CURR-KEY
00363         PERFORM 660-READ-CLTIFMSTP
00364         PERFORM 700-READ-CLTIFMSTC
00365      ELSE
00366         IF CLP-CURR-KEY GREATER THAN CLC-CURR-KEY
00367            PERFORM 700-READ-CLTIFMSTC
00368         ELSE
00369            IF WS-EQ-ZERO
00370               PERFORM 750-READ-EQVAL
00371               MOVE '1' TO WS-EQ-SW
00372            ELSE
00373               IF CURR-EQ-PROP EQUAL PC-PROP AND
00374                  CURR-TXTYP   EQUAL PC-TXTYP
00375                  PERFORM 200-PROP-MATCH-RTN
00376               ELSE
00377                  IF PC-PROP GREATER CURR-EQ-PROP
00378                     MOVE '0' TO WS-EQ-SW
00379                  ELSE
00380                     PERFORM 500-PRINT-REPA
00381                     MOVE '1' TO WS-EQ-SW
00382                     PERFORM 660-READ-CLTIFMSTP.
00383      SKIP3
00384  200-PROP-MATCH-RTN.
00385      COMPUTE WS-TIF-DIFF = EQ-EQ-VAL - TFP-FRZEQVAL
00386      PERFORM 350-WRITE-TIF-DIFF
00387      PERFORM 450-PRINT-REPB
00388      MOVE '0' TO WS-EQ-SW
00389      PERFORM 660-READ-CLTIFMSTP.
00390      SKIP3
00391  350-WRITE-TIF-DIFF.
00392      MOVE SPACES         TO CLTIFDIF-REC DET-LNB
00393      MOVE TFP-VOL        TO TD-VOL  DLB-VOL
00394      MOVE TFP-PROP       TO TD-PROP DLB-PROP
00395      INSPECT DLB-PROP REPLACING ALL ',' BY '-'
00396      MOVE TFP-TXTYP      TO TD-TXTYP DLB-TXTYP
00397      MOVE TFP-TXCD       TO TD-TXCD DLB-TXCD
00398      MOVE TFP-TXYR       TO TD-TXYR DLB-TXYR-ADD
00399      MOVE TFP-AGCY       TO TD-AGCY DLB-AGCY
00400      INSPECT DLB-AGCY REPLACING ALL ',' BY '-'
00401      MOVE TFP-FRZEQVAL   TO TD-FRZEQVAL DLB-FREQVAL
00402      MOVE EQ-EQ-VAL      TO TD-CUREQVAL DLB-CUEQVAL
00403      IF WS-TIF-DIFF LESS THAN ZERO
00404         MOVE ZEROS       TO TD-TIFDIFF DLB-TIFDIFF
00405      ELSE
00406         MOVE WS-TIF-DIFF TO TD-TIFDIFF DLB-TIFDIFF.
00407      PERFORM 600-WRITE-CLTIFDIF.
00408      SKIP3
00409  450-PRINT-REPB.
00410      ADD +1 TO CNTRL-WRITTENB
00411      IF LINE-CNTB GREATER THAN +55
00412         PERFORM 570-HEADING-RTN-B
00413         WRITE PRT-ERR-REC FROM DET-LNB AFTER ADVANCING 2
00414         ADD +2 TO LINE-CNTB
00415      ELSE
00416         WRITE PRT-ERR-REC FROM DET-LNB AFTER ADVANCING 1
00417         ADD +1 TO LINE-CNTB.
00418      SKIP3
00419  500-PRINT-REPA.
00420      MOVE SPACES         TO DET-LN
00421      MOVE TFP-VOL        TO DL-VOL
00422      MOVE TFP-PROP       TO DL-PROP
00423      INSPECT DL-PROP REPLACING ALL ',' BY '-'
00424      MOVE TFP-TXTYP      TO DL-TXTYP
00425      MOVE TFP-TXCD       TO DL-TXCD
00426      MOVE TFP-TXYR       TO DL-TXYR-ADD
00427      MOVE TFP-AGCY       TO DL-AGCY
00428      INSPECT DL-AGCY REPLACING ALL ',' BY '-'
00429      MOVE TFP-FRZEQVAL   TO DL-FREQVAL
00430      MOVE 'NO MATCHING EQUALIZED VALUE RECORD - DIVISION'
00431       TO DL-MSG
00432      ADD +1 TO CNTRL-WRITTEN
00433                TOT-UNM-EQVAL
00434      IF LINE-CNT GREATER THAN +55
00435         PERFORM 550-HEADING-RTN
00436         WRITE PRINT-REC FROM DET-LN AFTER ADVANCING 2
00437         ADD +2 TO LINE-CNT
00438      ELSE
00439         WRITE PRINT-REC FROM DET-LN AFTER ADVANCING 1
00440         ADD +1 TO LINE-CNT.
00441      SKIP3
00442  550-HEADING-RTN.
00443      ADD +1 TO PAGE-CNT
00444      MOVE PAGE-CNT TO H1-PAGE
00445      WRITE PRINT-REC FROM HL-1  AFTER ADVANCING PAGE
00446      WRITE PRINT-REC FROM HL-2  AFTER ADVANCING 2
00447      WRITE PRINT-REC FROM HL-3  AFTER ADVANCING 2
00448      WRITE PRINT-REC FROM HL-4  AFTER ADVANCING 1
00449      MOVE +6 TO LINE-CNT.
00450      SKIP3
00451  570-HEADING-RTN-B.
00452      ADD +1 TO PAGE-CNTB
00453      MOVE PAGE-CNTB TO H1B-PAGE
00454      WRITE PRT-ERR-REC FROM HL-1B  AFTER ADVANCING PAGE
00455      WRITE PRT-ERR-REC FROM HL-2B  AFTER ADVANCING 2
00456      WRITE PRT-ERR-REC FROM HL-3   AFTER ADVANCING 2
00457      WRITE PRT-ERR-REC FROM HL-4B  AFTER ADVANCING 1
00458      MOVE +6 TO LINE-CNTB.
00459      SKIP3
00460  600-WRITE-CLTIFDIF.
00461      WRITE CLTIFDIF-REC
00462      ADD +1 TO TOT-RECS-WR.
00463      SKIP3
00464  660-READ-CLTIFMSTP.
00465      READ CLTIFMSTP
00466        AT END
00467           MOVE 'Y' TO CLP-EOF-SW
00468           MOVE ALL '9' TO CLP-CURR-KEY.
00469      IF NOT CLP-EOF
00470         ADD +1 TO TOT-CLP-RD
00471         MOVE TFP-TXTYP TO PC-TXTYP
00472         MOVE TFP-TXYR TO PC-TXYR-ADD
00473         MOVE TFP-PROP TO PC-PROP
00474         IF CLP-CURR-KEY NOT GREATER THAN CLP-PREV-KEY
00475            DISPLAY 'PREV TIF DETAIL OUT OF SEQUENCE'
00476            DISPLAY 'CURRENT KEY IS ' PC-PROP SPACE PC-TXYR-ADD
00477                     SPACE PC-TXTYP
00478            DISPLAY 'PREVIOUS KEY IS ' PP-PROP SPACE PP-TXYR-ADD
00479                     SPACE PP-TXTYP
00480            MOVE 'N' TO CHECK-PROCESSING
00481            MOVE 16  TO RETURN-CODE
00482         ELSE
00483            MOVE CLP-CURR-KEY TO CLP-PREV-KEY.
00484      SKIP3
00485  700-READ-CLTIFMSTC.
00486      READ CLTIFMSTC
00487        AT END
00488           MOVE 'Y' TO CLC-EOF-SW
00489           MOVE ALL '9' TO CLC-CURR-KEY.
00490      IF NOT CLC-EOF
00491         ADD +1 TO TOT-CLC-RD
00492         MOVE TFC-TXTYP TO CC-TXTYP
00493         MOVE TFC-TXYR TO CC-TXYR-ADD
00494         MOVE TFC-PROP TO CC-PROP
00495         IF CLC-CURR-KEY NOT GREATER THAN CLC-PREV-KEY
00496            DISPLAY 'CURR TIF DETAIL OUT OF SEQUENCE'
00497            DISPLAY 'CURRENT KEY IS  ' CC-PROP SPACE CC-TXYR-ADD
00498                     SPACE CC-TXTYP
00499            DISPLAY 'PREVIOUS KEY IS ' CP-PROP SPACE CP-TXYR-ADD
00500                     SPACE CP-TXTYP
00501            MOVE 'N' TO CHECK-PROCESSING
00502            MOVE 16  TO RETURN-CODE
00503         ELSE
00504            MOVE CLC-CURR-KEY TO CLC-PREV-KEY.
00505      SKIP3
00506  750-READ-EQVAL.
00507      READ EQ-VAL-FILE
00508         AT END
00509            MOVE 'Y' TO EQVAL-EOF-SW
00510            MOVE ALL '9' TO CURR-EQ-PROP.
00511      IF NOT EQVAL-EOF
00512         ADD +1 TO EQVAL-RECS
00513         MOVE EQ-PROP TO CURR-EQ-PROP
00514         MOVE EQ-TXTYP TO CURR-TXTYP
00515      ELSE
00516         IF CURR-EQ-PROP NOT GREATER PREV-EQ-PROP
00517            DISPLAY 'EQUALIZED VALUATION FILE OUT OF SEQUENCE'
00518            DISPLAY 'CURRENT PROP NO IS ' CURR-EQ-PROP
00519            DISPLAY 'PREVIOUS PROP NO IS ' PREV-EQ-PROP
00520            MOVE 16 TO RETURN-CODE
00521            MOVE 'N' TO CHECK-PROCESSING
00522         ELSE
00523            MOVE CURR-TXTYP TO PREV-TXTYP
00524            MOVE CURR-EQ-PROP TO PREV-EQ-PROP.
00525      SKIP3
00526  999-LINECNT.
00527      WRITE PRINT-REC FROM BLNK.
00528      SKIP3
00529  999-LINECNTB.
00530      WRITE PRT-ERR-REC FROM BLNK.
