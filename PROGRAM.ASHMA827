 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID. ASHMA827.
 00003 *AUTHOR. HELEN TAYLOR - STANLEY MOLIS.
 00004 *DATE-WRITTEN.   MAY 1995.
 00005 ******************************************************************
 00006 *REMARKS.
 00007 *       THIS PROGRAM LOADS THE SEQUENTIAL SENIOR FREEZE
 00008 *       FILE TO THE PROPERTY INFORMATION DATA BASE CREATING
 00009 *       MASTER AND DETAIL SEGMENTS.
 00010 *
 00011 *THIS IS A COPY OF ASHMA827 ON 8-15-95 AT 11:30 AM
 00012 *
 00013 *MOD ON 04-04-96 - SQUILLACE
 00014 *
 00015 *MODIFICATION.  NANCY FLEMING-KATHY LEE FOLAN.  OCTOBER 31, 2006.
 00016 *       MODIFIED PROGRAM TO POPULATE NEW FIELD WITH 10.
 00017 ******************************************************************
 00018
 00019  ENVIRONMENT DIVISION.
 00020  CONFIGURATION SECTION.
 00021  SOURCE-COMPUTER. IBM-370.
 00022  OBJECT-COMPUTER. IBM-370.
 00023  INPUT-OUTPUT SECTION.
 00024  FILE-CONTROL.
 00025      SELECT    SENIORFREEZE ASSIGN TO UT-S-SENFRZ.
 00026      SELECT    PARM-CARD    ASSIGN TO UT-S-PARM.
 00027      SELECT    PRINT-FILE   ASSIGN TO UT-S-RPT.
 00028
 00029
 00030  DATA DIVISION.
 00031  FILE SECTION.
 00032
 00033  ++INCLUDE ASSFHHFD01
 00034  ++INCLUDE PRINTFILE
 00035
 00036
 00037  FD  PARM-CARD
 00038      RECORDING MODE IS F
 00039      BLOCK CONTAINS 0 RECORDS
 00040      RECORD CONTAINS 80 CHARACTERS
 00041      LABEL RECORDS ARE STANDARD
 00042      DATA RECORD IS PARM-RECORD.
 00043  01  PARM-RECORD.
 00044      03 PARM-CICV           PIC X(4).
 00045      03 PARM-VAL.
 00046         05 PARM-CICV-VAL    PIC 9(4).
 00047      03 PARM-YEAR           PIC X(4).
 00048      03 PARM-YR.
 00049         05 PARM-YEAR-N      PIC 9(4).
 00050      03 FILLER              PIC X(64).
 00051
 00052
 00053  WORKING-STORAGE SECTION.
 00054
 00055  77  SUB                    PIC S9(4)     VALUE +0 BINARY.
 00056  77  CIC-CTR                PIC S9(5)     VALUE +0 PACKED-DECIMAL.
 00057  77  SENFRZ-READ-CTR        PIC S9(7)     VALUE +0 PACKED-DECIMAL.
 00058  77  MAST-SEGS-ADDED-CTR    PIC S9(7)     VALUE +0 PACKED-DECIMAL.
 00059  77  MAST-SEGS-UPD-CTR      PIC S9(7)     VALUE +0 PACKED-DECIMAL.
 00060  77  DET-SEGS-ADDED-CTR     PIC S9(7)     VALUE +0 PACKED-DECIMAL.
 00061  77  DET-SEGS-UPD-CTR       PIC S9(7)     VALUE +0 PACKED-DECIMAL.
 00062  77  MAST-SEGS-DELTD-CTR    PIC S9(7)     VALUE +0 PACKED-DECIMAL.
 00063  77  LINE-CTR               PIC S9(3)   VALUE +999 PACKED-DECIMAL.
 00064  77  PAGE-CTR               PIC S9(5)     VALUE +0 PACKED-DECIMAL.
 00065
 00066  77  END-SENFRZ-SW                 PIC X VALUE 'N'.
 00067      88  SENFRZ-EOF                      VALUE 'Y'.
 00068  77  STOP-PROCCESSING-SW           PIC X VALUE 'N'.
 00069      88  STOP-PROCCESSING                VALUE 'S'.
 00070  77  MESG-SW                       PIC X VALUE '0'.
 00071      88  RESET-MSG-SW                    VALUE '0'.
 00072      88  MESG-1                          VALUE '1'.
 00073      88  MESG-2                          VALUE '2'.
 00074      88  MESG-3                          VALUE '3'.
 00075      88  MESG-4                          VALUE '4'.
 00076      88  MESG-5                          VALUE '5'.
 00077      88  MESG-6                          VALUE '6'.
 00078      88  MESG-7                          VALUE '7'.
 00079
 00080  01  WORK-AREA.
 00081
 00082
 00083 *------------- CHECKPOINT IDENTIFICATION NUMBER ---------------*
 00084      05  CHECK-PT-ID-NO.
 00085         10  FILLER              PIC XX     VALUE 'AS'.
 00086         10  CP-TRAN-NO          PIC 9(6)   VALUE 0.
 00087 *--------------------------------------------------------------*
 00088
 00089      05  WS-YEAR.
 00090          10  WS-CC              PIC 9(2) VALUE 20.
 00091          10  WS-YY              PIC 9(2).
 00092      05  WS-YEAR-N  REDEFINES  WS-YEAR PIC 9(4).
 00093
 00094      05  WS-TXCD                PIC 9(05).
 00095      05  WS-TXCD-X REDEFINES  WS-TXCD.
 00096          10  WS-TXCD-TOWN       PIC 99.
 00097          10  FILLER             PIC X(3).
 00098
 00099      05  CURR-KEY.
 00100          10 CURR-TOWN           PIC 99.
 00101          10 CURR-VOL            PIC 999   PACKED-DECIMAL.
 00102          10 CURR-PROP           PIC 9(15) PACKED-DECIMAL.
 00103          10 CURR-TXTYP          PIC 9.
 00104          10 CURR-RECCD          PIC 9.
 00105
 00106      05  PREV-KEY               PIC X(14).
 00107
 00108      05  WS-DOB.
 00109          10   WS-DOB-YY         PIC 99.
 00110          10   WS-DOB-MM         PIC 99.
 00111          10   WS-DOB-DD         PIC 99.
 00112
 00113      05  WS-MDCY.
 00114          10   WS-MO             PIC 99.
 00115          10   WS-DA             PIC 99.
 00116          10   WS-CE             PIC 99.
 00117          10   WS-YR             PIC 99.
 00118      05  WS-MMDDCCYY  REDEFINES WS-MDCY    PIC 9(8).
 00119
 00120      05  CURR-DATE.
 00121          10  CENT                   PIC  99       VALUE 20.
 00122          10  YR-MO-DAY              PIC  9(6)     VALUE  0.
 00123      05  CURR-DATE-N   REDEFINES    CURR-DATE PIC 9(8).
 00124
 00125      05  CURR-TIME-X.
 00126          10  CURR-TIME              PIC  9(6).
 00127          10  CURR-TIME-2            PIC  9(2).
 00128      05  CURR-TIME-N   REDEFINES    CURR-TIME-X PIC 9(8).
 00129
 00130      05  MO-DAY-YR                  PIC  9(6)     VALUE  0.
 00131
 00132      05  HEAD1.
 00133          10  FILLER             PIC X     VALUE SPACE.
 00134          10  H1-DATE            PIC 99B99B99.
 00135          10  FILLER             PIC X(29) VALUE SPACE.
 00136          10  FILLER             PIC X(34) VALUE
 00137              'OFFICE OF THE COOK COUNTY ASSESSOR'.
 00138          10  FILLER             PIC X(41) VALUE SPACE.
 00139          10  FILLER             PIC X(05) VALUE 'PAGE '.
 00140          10  H1-PAGE            PIC ZZ,ZZ9.
 00141
 00142      05  HEAD2.
 00143          10  FILLER             PIC X     VALUE SPACE.
 00144          10  FILLER             PIC X(08) VALUE
 00145              'ASHMA827'.
 00146          10  FILLER             PIC X(27) VALUE SPACE.
 00147          10  FILLER             PIC X(37) VALUE
 00148              'SENIOR FREEZE DATA BASE UPDATE REPORT'.
 00149
 00150      05  HEAD3.
 00151          10  FILLER             PIC X     VALUE SPACE.
 00152          10  FILLER             PIC X(44) VALUE
 00153              ' PERMANENT INDEX   PROCESS    TAX   TAX  FRE'.
 00154          10  FILLER             PIC X(44) VALUE
 00155              'EZE     D/B'.
 00156
 00157      05  HEAD4.
 00158          10  FILLER             PIC X     VALUE SPACE.
 00159          10  FILLER             PIC X(44) VALUE
 00160              '      NUMBER         YEAR    YEAR  TYPE  REC'.
 00161          10  FILLER             PIC X(44) VALUE
 00162              ' CD    REC CD  BIRTHDATE                    '.
 00163
 00164      05  DETAIL1.
 00165          10  FILLER             PIC X     VALUE SPACE.
 00166          10  D1-PIN             PIC 99B99B999B999B9999.
 00167          10  FILLER             PIC X(3)  VALUE SPACES.
 00168          10  D1-PROCYR          PIC 9(4).
 00169          10  FILLER             PIC X(5)  VALUE SPACES.
 00170          10  D1-TXYR            PIC 99.
 00171          10  FILLER             PIC X(5)  VALUE SPACES.
 00172          10  D1-TXTYP           PIC 9.
 00173          10  FILLER             PIC X(4)  VALUE SPACES.
 00174          10  D1-FRZ-CD          PIC 9.
 00175          10  FILLER             PIC X(10) VALUE SPACES.
 00176          10  D1-DB-CD           PIC 9.
 00177          10  FILLER             PIC X(5)  VALUE SPACES.
 00178          10  D1-DOB             PIC 99B99B9999.
 00179          10  FILLER             PIC X(4)  VALUE SPACES.
 00180          10  D1-MSG             PIC X(50).
 00181
 00182
 00183      05  MSG-1                  PIC X(50) VALUE
 00184          'NO MATCHING ROOT SEGMENT'.
 00185      05  MSG-2                  PIC X(50) VALUE
 00186          'NO MATCHING ASSESSMENT DATA SEGMENT'.
 00187      05  MSG-3                  PIC X(51) VALUE
 00188          'RECORD CODE FIELDS DIFFERENT - NO PROCESSING DONE'.
 00189      05  MSG-4                  PIC X(50) VALUE
 00190          'MASTER SEGMENT ADDED'.
 00191      05  MSG-5                  PIC X(50) VALUE
 00192          'DETAIL SEGMENT ADDED'.
 00193      05  MSG-6                  PIC X(50) VALUE
 00194          'MASTER SEGMENT UPDATED'.
 00195      05  MSG-7                  PIC X(50) VALUE
 00196          'DETAIL SEGMENT UPDATED'.
 00197
 00198
 00199  01  IO-AREA.
 00200  ++INCLUDE PIROOTSEGM
 00201  ++INCLUDE PIASSESSMT
 00202  ++INCLUDE ASAIS145SG
 00203  ++INCLUDE ASAIS150SG
 00204
 00205  ++INCLUDE IMSCALLS
 00206
 00207  ++INCLUDE ASAISSSA29
 00208 ******************************************************************
 00209  LINKAGE SECTION.
 00210  ++INCLUDE IOPCBDESC
 00211  01  PI-PCB1.
 00212  ++INCLUDE PIPCB1DESC
 00213          07  PI-SEG-KEY      PIC X(17).
 00214
 00215  PROCEDURE DIVISION.
 00216
 00217  100-HOUSEKEEPING.
 00218      ENTRY 'DLITCBL' USING IO-PCB  PI-PCB1
 00219
 00220      OPEN INPUT SENIORFREEZE
 00221                 PARM-CARD
 00222          OUTPUT PRINT-FILE.
 00223
 00224
 00225      READ PARM-CARD AT END
 00226         DISPLAY 'PARM CARD IS MISSING'
 00227         MOVE 16 TO RETURN-CODE.
 00228      IF RETURN-CODE NOT  EQUAL 16
 00229         PERFORM   995-CHECK-PARM-RTN
 00230         IF RETURN-CODE  EQUAL  16
 00231            STOP RUN
 00232         END-IF
 00233      END-IF.
 00234
 00235      ACCEPT   CURR-TIME-N   FROM  TIME.
 00236      ACCEPT    YR-MO-DAY    FROM  DATE.
 00237      MULTIPLY  YR-MO-DAY    BY    100.0001
 00238        GIVING  MO-DAY-YR.
 00239      MOVE      MO-DAY-YR    TO  H1-DATE.
 00240      INSPECT   H1-DATE   REPLACING  ALL  ' '  BY '/'.
 00241
 00242      PERFORM 200-MAINLINE
 00243          UNTIL  STOP-PROCCESSING
 00244           OR SENFRZ-EOF
 00245           OR RETURN-CODE EQUAL 16.
 00246
 00247      CLOSE      SENIORFREEZE
 00248                 PARM-CARD
 00249                 PRINT-FILE.
 00250
 00251      PERFORM    999-DISPLAYS.
 00252      GOBACK.
 00253
 00254  200-MAINLINE.
 00255
 00256      SET  RESET-MSG-SW  TO  TRUE.
 00257
 00258      PERFORM  300-READ-SENFRZ-FILE
 00259      IF NOT SENFRZ-EOF
 00260         IF SR-RECCD    EQUAL  2
 00261            MOVE  +999    TO   CIC-CTR
 00262         END-IF
 00263         PERFORM  900-DETERMINE-CHECKPOINT
 00264         IF RETURN-CODE NOT EQUAL 16
 00265          PERFORM  610-GU-ASMT-SEG
 00266          IF PI-SEG-FOUND
 00267            PERFORM  620-GHN-MASTER-SEG
 00268            IF PI-SEG-FOUND
 00269               IF C145-RECCODE EQUAL SR-RECCD
 00270                  MOVE  C145-RECCODE   TO   LVL3-ARG
 00271                  IF  C145-MAINTIND   NOT  EQUAL  1
 00272                     PERFORM  500-UPD-FLDS-MASTER-SEG
 00273                     PERFORM  650-REPL-MASTER-SEG
 00274 ********************PERFORM  800-WRITE-RPT
 00275                  ELSE
 00276                     PERFORM  500-UPD-FLDS-MASTER-SEG
 00277                     PERFORM  650-REPL-MASTER-SEG
 00278 ********************PERFORM  800-WRITE-RPT
 00279                  END-IF
 00280                  IF RETURN-CODE NOT EQUAL 16
 00281                     IF  SR-HMCNTR    EQUAL   0
 00282                         PERFORM  630-GHN-DETAIL-SEG
 00283                         IF PI-SEG-FOUND
 00284                             PERFORM  570-UPD-FLDS-DTL-FR-FIX-SEG
 00285                             PERFORM  660-REPL-DETAIL-SEG
 00286 ****************************PERFORM  800-WRITE-RPT
 00287                         ELSE
 00288                             IF RETURN-CODE NOT EQUAL 16
 00289                                PERFORM 550-ADD-FLDS-DTL-FR-FIX-SEG
 00290                                PERFORM 690-ISRT-DETAIL-SEG
 00291 *******************************PERFORM 800-WRITE-RPT
 00292                             END-IF
 00293                         END-IF
 00294                     ELSE
 00295                         PERFORM  400-PROCESS-UPD-OF-VARYING-SEG
 00296                          VARYING  SUB  FROM 1  BY  1
 00297                            UNTIL  SUB  GREATER  SR-HMCNTR
 00298                            OR  RETURN-CODE  EQUAL  16
 00299                     END-IF
 00300                  END-IF
 00301               ELSE
 00302                  SET  MESG-3     TO TRUE
 00303 *                DISPLAY 'CURR TOWN  : ' CURR-TOWN
 00304 *                DISPLAY 'CURR VOL   : ' CURR-VOL
 00305 *                DISPLAY 'CURR PROP  : ' CURR-PROP
 00306 *                DISPLAY 'CURR TXTYP : ' CURR-TXTYP
 00307 *                DISPLAY 'CURR RECCD : ' CURR-RECCD
 00308 *                DISPLAY '------------------------'
 00309                  PERFORM 700-DELETE-MASTER-SEG
 00310                  IF RETURN-CODE NOT EQUAL 16
 00311                     PERFORM 510-ADD-FLDS-MASTER-SEG
 00312                     PERFORM 680-ISRT-MASTER-SEG
 00313                     IF SR-HMCNTR  EQUAL  0
 00314                        IF RETURN-CODE NOT EQUAL  16
 00315                           PERFORM 550-ADD-FLDS-DTL-FR-FIX-SEG
 00316                           PERFORM 690-ISRT-DETAIL-SEG
 00317                        END-IF
 00318                     ELSE
 00319                        PERFORM  450-PROCESS-ADD-OF-VARYING-SEG
 00320                         VARYING  SUB  FROM 1  BY  1
 00321                           UNTIL  SUB  GREATER  SR-HMCNTR
 00322                           OR  RETURN-CODE  EQUAL  16
 00323                     END-IF
 00324                     PERFORM 800-WRITE-RPT
 00325                  END-IF
 00326               END-IF
 00327            ELSE
 00328               IF RETURN-CODE NOT EQUAL 16
 00329                  PERFORM 510-ADD-FLDS-MASTER-SEG
 00330                  PERFORM 680-ISRT-MASTER-SEG
 00331 *****************PERFORM 800-WRITE-RPT
 00332                  IF SR-HMCNTR  EQUAL  0
 00333                     IF RETURN-CODE NOT EQUAL  16
 00334                        PERFORM 550-ADD-FLDS-DTL-FR-FIX-SEG
 00335                        PERFORM 690-ISRT-DETAIL-SEG
 00336 ***********************PERFORM 800-WRITE-RPT
 00337                     END-IF
 00338                  ELSE
 00339                     PERFORM  450-PROCESS-ADD-OF-VARYING-SEG
 00340                      VARYING  SUB  FROM 1  BY  1
 00341                        UNTIL  SUB  GREATER  SR-HMCNTR
 00342                        OR  RETURN-CODE  EQUAL  16
 00343                  END-IF
 00344               END-IF
 00345            END-IF
 00346          ELSE
 00347            IF PI-DB-1ST-SEG-LEVEL
 00348               SET   MESG-2  TO TRUE
 00349            ELSE
 00350               SET   MESG-1  TO TRUE
 00351            END-IF
 00352            PERFORM 800-WRITE-RPT
 00353          END-IF
 00354         END-IF
 00355      END-IF.
 00356
 00357  300-READ-SENFRZ-FILE.
 00358
 00359      READ SENIORFREEZE
 00360          AT END  SET SENFRZ-EOF TO TRUE.
 00361
 00362      IF SENFRZ-EOF
 00363          CONTINUE
 00364      ELSE
 00365          ADD  +1 TO SENFRZ-READ-CTR
 00366          MOVE  SR-TXCD      TO   WS-TXCD
 00367          MOVE  WS-TXCD-TOWN TO   CURR-TOWN
 00368          MOVE  SR-VOL       TO   CURR-VOL
 00369          MOVE  SR-PROP      TO   CURR-PROP
 00370          MOVE  SR-TXTYP     TO   CURR-TXTYP
 00371          MOVE  SR-RECCD     TO   CURR-RECCD
 00372          IF PREV-KEY        GREATER  CURR-KEY
 00373              MOVE 16 TO RETURN-CODE
 00374              SET STOP-PROCCESSING   TO  TRUE
 00375              DISPLAY 'SEQUENCE ERROR IN DIVISION MASTER FILE'
 00376              DISPLAY 'PREVIOUS KEY = ' PREV-KEY
 00377              DISPLAY 'CURRENT KEY  = ' CURR-KEY
 00378          ELSE
 00379              MOVE CURR-KEY        TO PREV-KEY
 00380         END-IF
 00381      END-IF.
 00382
 00383  400-PROCESS-UPD-OF-VARYING-SEG.
 00384
 00385      MOVE  SR-HSG-BRTHDTE (SUB)   TO  WS-DOB
 00386      MOVE  WS-DOB-MM              TO  WS-MO
 00387      MOVE  WS-DOB-DD              TO  WS-DA
 00388      MOVE  WS-DOB-YY              TO  WS-YR
 00389      IF    WS-DOB-YY   GREATER   80
 00390          MOVE  18        TO        WS-CE
 00391      ELSE
 00392          MOVE  19        TO        WS-CE
 00393      END-IF
 00394      MOVE  WS-MMDDCCYY   TO      LVL4-ARG.
 00395
 00396      PERFORM  640-GHU-DETAIL-SEG
 00397      IF RETURN-CODE  NOT EQUAL  16
 00398         IF PI-SEG-FOUND
 00399            PERFORM  580-UPD-FLDS-DTL-FR-VAR-SEG
 00400            PERFORM  660-REPL-DETAIL-SEG
 00401 ********** PERFORM  800-WRITE-RPT
 00402         ELSE
 00403            PERFORM  560-ADD-FLDS-DTL-FR-VAR-SEG
 00404            PERFORM  690-ISRT-DETAIL-SEG
 00405 ***********PERFORM  800-WRITE-RPT
 00406         END-IF
 00407      END-IF.
 00408
 00409  450-PROCESS-ADD-OF-VARYING-SEG.
 00410
 00411      PERFORM  560-ADD-FLDS-DTL-FR-VAR-SEG
 00412      PERFORM  690-ISRT-DETAIL-SEG.
 00413 *****PERFORM  800-WRITE-RPT.
 00414
 00415  500-UPD-FLDS-MASTER-SEG.
 00416
 00417      MOVE   SR-SPLIT-CD    TO    C145-SPLTCD
 00418      MOVE   SR-OCCFAC      TO    C145-OCCFAC
 00419      MOVE   'ASHMA827'     TO    C145-UPDTID
 00420      MOVE   CURR-DATE-N    TO    C145-UPDTDATE
 00421      MOVE   CURR-TIME      TO    C145-UPDTTIME.
 00422
 00423  510-ADD-FLDS-MASTER-SEG.
 00424
 00425      MOVE  SPACES        TO   C145-SENFRZMASTER
 00426      INITIALIZE               C145-SENFRZMASTER
 00427      MOVE  SR-RECCD      TO   C145-RECCODE
 00428      MOVE  SR-PRORATE    TO   C145-PROPCT
 00429      MOVE  SR-KEYPCL     TO   C145-KEYPCL
 00430      MOVE  SR-SPLIT-CD   TO   C145-SPLTCD
 00431      MOVE  SR-OCCFAC     TO   C145-OCCFAC
 00432      MOVE  'ASHMA827'    TO   C145-UPDTID
 00433      MOVE  CURR-DATE-N   TO   C145-UPDTDATE
 00434      MOVE  CURR-TIME     TO   C145-UPDTTIME
 00435      MOVE  SR-CLASS      TO   C145-CYCLS
 00436      MOVE  SR-COOPQTY    TO   C145-HOUNITS
 00437      MOVE  SR-HMCNTR     TO   C145-HSUNITS.
 00438
 00439  550-ADD-FLDS-DTL-FR-FIX-SEG.
 00440      MOVE SPACES  TO   C150-SENFRZDETAIL.
 00441      INITIALIZE        C150-SENFRZDETAIL.
 00442      MOVE  ZEROS               TO   C150-BIRTHDTE
 00443      MOVE  SR-NAME             TO   C150-APPOLDNAM
 00444      MOVE  SR-ADDR             TO   C150-APPADDR
 00445      MOVE  SR-CITY             TO   C150-APPCITY
 00446      MOVE  SR-STATE            TO   C150-APPSTATE
 00447      MOVE  SR-ZIP              TO   C150-APPZIP
 00448      MOVE  01                  TO   C150-MISRTNCD (1)
 00449      IF  C145-RECCODE = 1
 00450          MOVE  25              TO   C150-MISRTNCD (2)
 00451      ELSE
 00452          MOVE  26              TO   C150-MISRTNCD (2)
 00453      END-IF
 00454      MOVE  'ASHMA827'          TO   C150-LSTUPID
 00455                                     C150-HOLSTUPD
 00456      MOVE  CURR-DATE-N         TO   C150-LSTUPDTE
 00457                                     C150-HOLSTUPDTE
 00458      MOVE  CURR-TIME           TO   C150-LSTUPTIM
 00459                                     C150-HOLSTUPTIM
 00460      MOVE  SR-STAT1            TO   C150-HOSTAT
 00461      MOVE  SR-YRAPPL           TO   WS-YY
 00462      MOVE  WS-YEAR-N           TO   C150-HOBASYR
 00463      MOVE  SR-IND              TO   C150-ELGIND
 00464
 00465      MOVE  SR-EQFCTR           TO   C150-HOBYEQFAC
 00466      MOVE  SR-ASSDVAL          TO   C150-HOBYAV
 00467      MOVE  SR-EQVAL            TO   C150-HOBYEV.
 00468      MOVE  10                  TO   C150-SFPCT.
 00469
 00470  560-ADD-FLDS-DTL-FR-VAR-SEG.
 00471      MOVE SPACES  TO   C150-SENFRZDETAIL.
 00472      INITIALIZE        C150-SENFRZDETAIL.
 00473
 00474      MOVE  SR-HSG-BRTHDTE (SUB)   TO  WS-DOB
 00475      MOVE  WS-DOB-MM              TO  WS-MO
 00476      MOVE  WS-DOB-DD              TO  WS-DA
 00477      MOVE  WS-DOB-YY              TO  WS-YR
 00478      IF    WS-DOB-YY   GREATER   80
 00479          MOVE  18        TO        WS-CE
 00480      ELSE
 00481          MOVE  19        TO        WS-CE
 00482      END-IF
 00483      MOVE  WS-MMDDCCYY         TO   C150-BIRTHDTE.
 00484
 00485      MOVE  SR-HSG-NAME  (SUB)  TO   C150-APPOLDNAM
 00486      MOVE  SR-HSG-ADDR  (SUB)  TO   C150-APPADDR
 00487      MOVE  SR-HSG-CITY  (SUB)  TO   C150-APPCITY
 00488      MOVE  SR-HSG-STATE (SUB)  TO   C150-APPSTATE
 00489      MOVE  SR-HSG-ZIP   (SUB)  TO   C150-APPZIP
 00490      MOVE  01                  TO   C150-MISRTNCD  (1)
 00491      IF  C145-RECCODE = 1
 00492          MOVE  25              TO   C150-MISRTNCD  (2)
 00493      ELSE
 00494          MOVE  26              TO   C150-MISRTNCD  (2)
 00495      END-IF
 00496      MOVE  'ASHMA827'          TO   C150-LSTUPID
 00497                                     C150-HSLSTUPD
 00498                                     C150-HOLSTUPD
 00499      MOVE  CURR-DATE-N         TO   C150-LSTUPDTE
 00500                                     C150-HSLSTUPDTE
 00501                                     C150-HOLSTUPDTE
 00502      MOVE  CURR-TIME           TO   C150-LSTUPTIM
 00503                                     C150-HSLSTUPTIM
 00504                                     C150-HOLSTUPTIM
 00505      MOVE  '1'                 TO   C150-HSSTAT
 00506      MOVE  SR-HSG-BATCH (SUB)  TO   C150-HSBATCH
 00507      MOVE  SR-STAT1            TO   C150-HOSTAT
 00508      MOVE  SR-YRAPPL           TO   WS-YY
 00509      MOVE  WS-YEAR-N           TO   C150-HOBASYR
 00510
 00511      MOVE  SR-HSG-YRAPPL (SUB) TO   C150-HSYRAPPLD
 00512      MOVE  SR-IND              TO   C150-ELGIND
 00513      MOVE  SR-EQFCTR           TO   C150-HOBYEQFAC
 00514      MOVE  SR-ASSDVAL          TO   C150-HOBYAV
 00515      MOVE  SR-EQVAL            TO   C150-HOBYEV.
 00516      MOVE  10                  TO   C150-SFPCT.
 00517
 00518  570-UPD-FLDS-DTL-FR-FIX-SEG.
 00519
 00520      MOVE  SR-NAME             TO   C150-APPOLDNAM
 00521      MOVE  SR-ADDR             TO   C150-APPADDR
 00522      MOVE  SR-CITY             TO   C150-APPCITY
 00523      MOVE  SR-STATE            TO   C150-APPSTATE
 00524      MOVE  SR-ZIP              TO   C150-APPZIP
 00525      MOVE  SR-STAT1            TO   C150-HOSTAT
 00526
 00527      MOVE  'ASHMA827'          TO   C150-HOLSTUPD
 00528      MOVE  CURR-DATE-N         TO   C150-HOLSTUPDTE
 00529      MOVE  CURR-TIME           TO   C150-HOLSTUPTIM.
 00530      MOVE  10                  TO   C150-SFPCT.
 00531
 00532  580-UPD-FLDS-DTL-FR-VAR-SEG.
 00533
 00534      MOVE  SR-HSG-NAME  (SUB)  TO   C150-APPOLDNAM
 00535      MOVE  SR-HSG-ADDR  (SUB)  TO   C150-APPADDR
 00536      MOVE  SR-HSG-CITY  (SUB)  TO   C150-APPCITY
 00537      MOVE  SR-HSG-STATE (SUB)  TO   C150-APPSTATE
 00538      MOVE  SR-HSG-ZIP   (SUB)  TO   C150-APPZIP
 00539      MOVE  '1'                 TO   C150-HSSTAT
 00540      MOVE  SR-HSG-BATCH (SUB)  TO   C150-HSBATCH
 00541      MOVE SR-HSG-YRAPPL (SUB)  TO   C150-HSYRAPPLD
 00542
 00543      MOVE  'ASHMA827'          TO   C150-HSLSTUPD
 00544                                     C150-HOLSTUPD
 00545      MOVE  CURR-DATE-N         TO   C150-HSLSTUPDTE
 00546                                     C150-HOLSTUPDTE
 00547      MOVE  CURR-TIME           TO   C150-HSLSTUPTIM
 00548                                     C150-HOLSTUPTIM
 00549      MOVE  SR-STAT1            TO   C150-HOSTAT
 00550      MOVE  SR-YRAPPL           TO   WS-YY
 00551      MOVE  WS-YEAR-N           TO   C150-HOBASYR
 00552      MOVE  SR-IND              TO   C150-ELGIND
 00553      MOVE  SR-EQFCTR           TO   C150-HOBYEQFAC.
 00554
 00555      IF C150-MAINTIND  NOT EQUAL  1
 00556         MOVE  SR-ASSDVAL       TO   C150-HOBYAV
 00557         MOVE  SR-EQVAL         TO   C150-HOBYEV
 00558      END-IF.
 00559      MOVE  10                  TO   C150-SFPCT.
 00560
 00561  610-GU-ASMT-SEG.
 00562
 00563
 00564      MOVE  SR-PROP      TO   LVL1-ARG
 00565      MOVE  PARM-YEAR-N  TO   LVL2-PROCYR
 00566                              LVL2-TXYR
 00567      MOVE  SR-TXTYP     TO   LVL2-TXTYP.
 00568
 00569      CALL 'CBLTDLI' USING GU
 00570                           PI-PCB1
 00571                           PY-ASMTDATA
 00572                           LVL1-QUAL-SSA
 00573                           LVL2-QUAL-SSA.
 00574
 00575      IF PI-DB-GOOD-STATUS
 00576         NEXT SENTENCE
 00577      ELSE
 00578         IF PI-SEG-NOTFND
 00579            NEXT SENTENCE
 00580         ELSE
 00581            DISPLAY 'DL/1 ERROR 610-GU-ASMT-SEG'
 00582            DISPLAY 'PCB  AREA: ' PI-PCB1
 00583            MOVE 16 TO RETURN-CODE
 00584            SET STOP-PROCCESSING   TO  TRUE
 00585      END-IF.
 00586
 00587  620-GHN-MASTER-SEG.
 00588
 00589      CALL 'CBLTDLI' USING GHN
 00590                           PI-PCB1
 00591                           C145-SENFRZMASTER
 00592                           LVL1-QUAL-SSA
 00593                           LVL2-QUAL-SSA
 00594                           LVL3-UNQUAL-SSA.
 00595
 00596      IF PI-DB-GOOD-STATUS
 00597         NEXT SENTENCE
 00598      ELSE
 00599         IF PI-DB-END   OR
 00600            PI-SEG-NOTFND
 00601            NEXT SENTENCE
 00602         ELSE
 00603            DISPLAY 'DL/1 ERROR  620-GHN-MASTER-SEG'
 00604            DISPLAY 'PCB  AREA: ' PI-PCB1
 00605            MOVE 16 TO RETURN-CODE
 00606            SET STOP-PROCCESSING   TO  TRUE
 00607         END-IF
 00608      END-IF.
 00609
 00610  630-GHN-DETAIL-SEG.
 00611
 00612      CALL 'CBLTDLI' USING GHN
 00613                           PI-PCB1
 00614                           C150-SENFRZDETAIL
 00615                           LVL1-QUAL-SSA
 00616                           LVL2-QUAL-SSA
 00617                           LVL3-QUAL-SSA
 00618                           LVL4-UNQUAL-SSA.
 00619
 00620      IF PI-DB-GOOD-STATUS
 00621         NEXT SENTENCE
 00622      ELSE
 00623         IF PI-DB-END   OR
 00624            PI-SEG-NOTFND
 00625            NEXT SENTENCE
 00626         ELSE
 00627            DISPLAY 'DL/1 ERROR  630-GHN-DETAIL-SEG'
 00628            DISPLAY 'PCB  AREA: ' PI-PCB1
 00629            MOVE 16 TO RETURN-CODE
 00630            SET STOP-PROCCESSING   TO  TRUE
 00631         END-IF
 00632      END-IF.
 00633
 00634  640-GHU-DETAIL-SEG.
 00635
 00636      CALL 'CBLTDLI' USING GHU
 00637                           PI-PCB1
 00638                           C150-SENFRZDETAIL
 00639                           LVL1-QUAL-SSA
 00640                           LVL2-QUAL-SSA
 00641                           LVL3-QUAL-SSA
 00642                           LVL4-QUAL-SSA.
 00643
 00644      IF PI-DB-GOOD-STATUS
 00645         NEXT SENTENCE
 00646      ELSE
 00647         IF PI-SEG-NOTFND
 00648            NEXT SENTENCE
 00649         ELSE
 00650            DISPLAY 'DL/1 ERROR  640-GHU-DETAIL-SEG'
 00651            DISPLAY 'PCB  AREA: ' PI-PCB1
 00652            MOVE 16 TO RETURN-CODE
 00653            SET STOP-PROCCESSING   TO  TRUE
 00654         END-IF
 00655      END-IF.
 00656
 00657  650-REPL-MASTER-SEG.
 00658
 00659      CALL 'CBLTDLI' USING REPL
 00660                           PI-PCB1
 00661                           C145-SENFRZMASTER.
 00662
 00663      IF PI-DB-GOOD-STATUS
 00664         ADD  +1  TO  CIC-CTR
 00665                      MAST-SEGS-UPD-CTR
 00666         SET   MESG-6    TO   TRUE
 00667      ELSE
 00668         DISPLAY 'DL/1 ERROR 650-REPL-MASTER-SEG'
 00669         DISPLAY 'PCB  AREA: ' PI-PCB1
 00670         MOVE 16 TO RETURN-CODE
 00671         SET STOP-PROCCESSING   TO  TRUE
 00672      END-IF.
 00673
 00674
 00675  660-REPL-DETAIL-SEG.
 00676
 00677      CALL 'CBLTDLI' USING REPL
 00678                           PI-PCB1
 00679                           C150-SENFRZDETAIL.
 00680
 00681      IF PI-DB-GOOD-STATUS
 00682         ADD  +1  TO  CIC-CTR
 00683                      DET-SEGS-UPD-CTR
 00684         SET     MESG-7    TO  TRUE
 00685      ELSE
 00686         DISPLAY 'DL/1 ERROR 660-REPL-DETAIL-SEG'
 00687         DISPLAY 'PCB  AREA: ' PI-PCB1
 00688         MOVE 16 TO RETURN-CODE
 00689         SET STOP-PROCCESSING   TO  TRUE
 00690      END-IF.
 00691
 00692
 00693  680-ISRT-MASTER-SEG.
 00694
 00695      CALL 'CBLTDLI' USING ISRT
 00696                           PI-PCB1
 00697                           C145-SENFRZMASTER
 00698                           LVL1-QUAL-SSA
 00699                           LVL2-QUAL-SSA
 00700                           LVL3-UNQUAL-SSA.
 00701
 00702      IF PI-DB-GOOD-STATUS
 00703         ADD  +1  TO  CIC-CTR
 00704                      MAST-SEGS-ADDED-CTR
 00705         MOVE  C145-RECCODE   TO   LVL3-ARG
 00706         SET  MESG-4   TO   TRUE
 00707      ELSE
 00708         DISPLAY 'DL/1 ERROR  680-ISRT-MASTER-SEG'
 00709         DISPLAY 'PCB  AREA: ' PI-PCB1
 00710         MOVE 16 TO RETURN-CODE
 00711         SET STOP-PROCCESSING   TO  TRUE
 00712      END-IF.
 00713
 00714  690-ISRT-DETAIL-SEG.
 00715
 00716      CALL 'CBLTDLI' USING ISRT
 00717                           PI-PCB1
 00718                           C150-SENFRZDETAIL
 00719                           LVL1-QUAL-SSA
 00720                           LVL2-QUAL-SSA
 00721                           LVL3-QUAL-SSA
 00722                           LVL4-UNQUAL-SSA.
 00723
 00724      IF PI-DB-GOOD-STATUS
 00725         ADD  +1  TO  CIC-CTR
 00726                      DET-SEGS-ADDED-CTR
 00727         SET     MESG-5    TO  TRUE
 00728      ELSE
 00729         DISPLAY 'DL/1 ERROR  690-ISRT-DETAIL-SEG'
 00730         DISPLAY 'PCB  AREA: ' PI-PCB1
 00731         MOVE 16 TO RETURN-CODE
 00732         SET STOP-PROCCESSING   TO  TRUE
 00733      END-IF.
 00734
 00735  700-DELETE-MASTER-SEG.
 00736
 00737      CALL 'CBLTDLI' USING DLET
 00738                           PI-PCB1
 00739                           C145-SENFRZMASTER.
 00740
 00741      IF PI-DB-GOOD-STATUS
 00742         ADD  +1  TO  CIC-CTR
 00743                      MAST-SEGS-DELTD-CTR
 00744      ELSE
 00745         DISPLAY 'DL/1 ERROR  700-DELETE-MASTER-SEG'
 00746         DISPLAY 'PCB  AREA: ' PI-PCB1
 00747         MOVE 16 TO RETURN-CODE
 00748         SET STOP-PROCCESSING   TO  TRUE
 00749      END-IF.
 00750
 00751  800-WRITE-RPT.
 00752
 00753      IF  LINE-CTR  GREATER  +55
 00754          PERFORM   830-HEADERS
 00755      END-IF
 00756      MOVE  SPACES   TO   DETAIL1.
 00757      MOVE SR-PROP       TO   D1-PIN
 00758      INSPECT  D1-PIN  REPLACING ALL ' ' BY  '-'
 00759      MOVE PARM-YEAR-N   TO   D1-PROCYR
 00760                              D1-TXYR
 00761      MOVE SR-TXTYP      TO   D1-TXTYP
 00762      MOVE SR-RECCD      TO   D1-FRZ-CD
 00763
 00764      EVALUATE TRUE
 00765         WHEN  MESG-1
 00766             MOVE  MSG-1  TO  D1-MSG
 00767         WHEN  MESG-2
 00768             MOVE  MSG-2  TO  D1-MSG
 00769         WHEN  MESG-3
 00770             MOVE C145-RECCODE  TO  D1-DB-CD
 00771             MOVE  MSG-3  TO  D1-MSG
 00772         WHEN  MESG-4
 00773             MOVE  MSG-4  TO  D1-MSG
 00774         WHEN  MESG-5
 00775
 00776             MOVE   C150-BIRTHDTE  TO  D1-DOB
 00777
 00778             INSPECT  D1-DOB  REPLACING ALL ' ' BY '-'
 00779             MOVE  MSG-5  TO  D1-MSG
 00780         WHEN  MESG-6
 00781             MOVE  MSG-6  TO  D1-MSG
 00782         WHEN  MESG-7
 00783
 00784             MOVE   C150-BIRTHDTE  TO  D1-DOB
 00785
 00786             INSPECT  D1-DOB  REPLACING ALL ' ' BY '-'
 00787             MOVE  MSG-7  TO  D1-MSG
 00788      END-EVALUATE
 00789
 00790
 00791      WRITE PRINT-REC FROM DETAIL1
 00792      AFTER ADVANCING 2
 00793      ADD  +2   TO  LINE-CTR.
 00794
 00795
 00796  830-HEADERS.
 00797
 00798      ADD     +1        TO    PAGE-CTR.
 00799      MOVE  PAGE-CTR    TO    H1-PAGE.
 00800
 00801      WRITE PRINT-REC FROM HEAD1
 00802            AFTER ADVANCING PAGE
 00803      WRITE PRINT-REC FROM HEAD2
 00804            AFTER ADVANCING 2
 00805      WRITE PRINT-REC FROM HEAD3
 00806            AFTER ADVANCING 2
 00807      WRITE PRINT-REC FROM HEAD4
 00808            AFTER ADVANCING 1
 00809      MOVE +6   TO    LINE-CTR.
 00810
 00811  900-DETERMINE-CHECKPOINT.
 00812
 00813      IF CIC-CTR GREATER THAN PARM-CICV-VAL
 00814         ADD +1                    TO CP-TRAN-NO
 00815         PERFORM 910-ISSUE-CHECKPOINT
 00816         MOVE ZEROES               TO CIC-CTR.
 00817
 00818  910-ISSUE-CHECKPOINT.
 00819
 00820      CALL 'CBLTDLI' USING  CHKP, IO-PCB, CHECK-PT-ID-NO.
 00821      IF IP-GOOD-STATUS
 00822         NEXT SENTENCE
 00823      ELSE
 00824         PERFORM 920-ISSUE-CHKPT-ERR.
 00825
 00826  920-ISSUE-CHKPT-ERR.
 00827
 00828      CALL 'CBLTDLI' USING ROLL.
 00829
 00830      DISPLAY 'CHECK POINT ISSUE ERROR'.
 00831      MOVE 0016 TO RETURN-CODE.
 00832      SET STOP-PROCCESSING   TO  TRUE
 00833      DISPLAY 'LOGICAL TERMINAL NAME:  '
 00834          IP-TERM-NAME.
 00835      DISPLAY 'STATUS CODE:  '
 00836          IP-STATUS-CODE.
 00837
 00838  995-CHECK-PARM-RTN.
 00839
 00840
 00841      IF PARM-CICV    EQUAL  'CICV'
 00842         CONTINUE
 00843      ELSE
 00844         MOVE   16   TO RETURN-CODE
 00845         DISPLAY 'CHECK POINT CONTROL VALUE NOT EQUAL ''CICV'''
 00846      END-IF.
 00847
 00848      IF PARM-VAL  NUMERIC
 00849         CONTINUE
 00850      ELSE
 00851         MOVE   16   TO RETURN-CODE
 00852         DISPLAY 'CICV  INVALID '
 00853      END-IF.
 00854
 00855      IF PARM-YEAR    EQUAL  'YEAR'
 00856         CONTINUE
 00857      ELSE
 00858         MOVE   16   TO RETURN-CODE
 00859         DISPLAY 'CHECK POINT CONTROL VALUE NOT EQUAL ''YEAR'''
 00860      END-IF.
 00861
 00862      IF PARM-YR      NUMERIC
 00863         CONTINUE
 00864      ELSE
 00865         MOVE   16   TO RETURN-CODE
 00866         DISPLAY 'DIVISION YEAR INVALID'
 00867      END-IF.
 00868
 00869  999-DISPLAYS.
 00870
 00871      DISPLAY 'END   OF EXECUTION OF ASHMA827'
 00872      DISPLAY
 00873      'TOTAL SENIOR FREEZE RECORDS READ   ' SENFRZ-READ-CTR
 00874      DISPLAY ' '
 00875      DISPLAY
 00876      'TOTAL MASTER SEGMENTS ADDED        ' MAST-SEGS-ADDED-CTR
 00877      DISPLAY ' '
 00878      DISPLAY
 00879      'TOTAL MASTER SEGMENTS UPDATED      ' MAST-SEGS-UPD-CTR
 00880      DISPLAY ' '
 00881      DISPLAY
 00882      'TOTAL DETAIL SEGMENTS ADDED        ' DET-SEGS-ADDED-CTR
 00883      DISPLAY ' '
 00884      DISPLAY
 00885      'TOTAL DETAIL SEGMENTS UPDATED      ' DET-SEGS-UPD-CTR
 00886      DISPLAY ' '.
 00887      DISPLAY
 00888      'TOTAL MASTER SEGMENTS DELETED      ' MAST-SEGS-DELTD-CTR
 00889      DISPLAY ' '.
