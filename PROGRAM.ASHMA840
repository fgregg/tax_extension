 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID. ASHMA840.
 00003 *AUTHOR. SQUILLACE - KALEMBA
 00004 *DATE-WRITTEN.   MAY 1996.
 00005 *****************************************************************
 00006 *REMARKS.
 00007 *       THIS PROGRAM UPDATES THE PRIOR YEAR QUALIFIED SENIOR
 00008 *       FREEZE BASE VALUE INFORMATION TO THE CURRENT SENIOR
 00009 *       FREEZE SEGMENTS ON THE PROPERTY INFORMATION DATA BASE.
 00010 *
 00011 *****************************************************************
 00012     SKIP1
 00013  ENVIRONMENT DIVISION.
 00014  CONFIGURATION SECTION.
 00015  SOURCE-COMPUTER. IBM-370.
 00016  OBJECT-COMPUTER. IBM-370.
 00017  INPUT-OUTPUT SECTION.
 00018  FILE-CONTROL.
 00019      SELECT    ASSFMSDT     ASSIGN TO UT-S-ASSFMSDT.
 00020      SELECT    PRINT-FILE   ASSIGN TO UT-S-RPT.
 00021      SELECT    CNTRL-CARD   ASSIGN TO UT-S-CARD.
 00022          SKIP1
 00023  DATA DIVISION.
 00024  FILE SECTION.
 00025  ++INCLUDE ASSFMSDTF1
 00026    SKIP3
 00027  ++INCLUDE PRINTFILE
 00028    EJECT
 00029  FD  CNTRL-CARD
 00030      RECORDING MODE IS F
 00031      BLOCK CONTAINS 0 RECORDS
 00032      RECORD CONTAINS 80 CHARACTERS
 00033      LABEL RECORDS ARE STANDARD
 00034      DATA RECORD IS CNTRL-RECORD.
 00035  01  CNTRL-RECORD.
 00036      03 CNTRL-CICV          PIC X(4).
 00037      03 CNTRL-VAL.
 00038         05 CNTRL-CICV-VAL   PIC 9(4).
 00039      03 CNTRL-YR-PROC       PIC X(8).
 00040      03 CNTRL-YR.
 00041         05 CNTRL-CENT       PIC 99.
 00042         05 CNTRL-YR-N       PIC 99.
 00043      03 FILLER              PIC X(60).
 00044     EJECT
 00045  WORKING-STORAGE SECTION.
 00046  77  SUB                    PIC S9(4)     VALUE +0 BINARY.
 00047  77  CIC-CTR                PIC S9(5)     VALUE +0 PACKED-DECIMAL
 00048  77  EXMT-READ-CTR          PIC S9(7)     VALUE +0 PACKED-DECIMAL
 00049  77  EXMT-SEL-CTR           PIC S9(7)     VALUE +0 PACKED-DECIMAL
 00050  77  MAST-SEGS-UPD-CTR      PIC S9(7)     VALUE +0 PACKED-DECIMAL
 00051  77  DET-SEGS-ADD-CTR       PIC S9(7)     VALUE +0 PACKED-DECIMAL
 00052  77  DET-SEGS-UPD-CTR       PIC S9(7)     VALUE +0 PACKED-DECIMAL
 00053  77  LINE-CTR               PIC S9(3)   VALUE +999 PACKED-DECIMAL
 00054  77  PAGE-CTR               PIC S9(5)     VALUE +0 PACKED-DECIMAL
 00055  77  END-EXMT-SW                   PIC X VALUE 'N'.
 00056      88  EXMT-EOF                        VALUE 'Y'.
 00057  77  STOP-PROCESSING-SW            PIC X VALUE 'N'.
 00058      88  STOP-PROCESSING                 VALUE 'S'.
 00059  77  MESG-SW                       PIC X VALUE '0'.
 00060      88  RESET-MSG-SW                    VALUE '0'.
 00061      88  MESG-1                          VALUE '1'.
 00062      88  MESG-2                          VALUE '2'.
 00063      88  MESG-3                          VALUE '3'.
 00064      88  MESG-4                          VALUE '4'.
 00065      88  MESG-5                          VALUE '5'.
 00066      88  MESG-6                          VALUE '6'.
 00067      88  MESG-7                          VALUE '7'.
 00068       EJECT
 00069  01  WORK-AREA.
 00070 *------------- CHECKPOINT IDENTIFICATION NUMBER ---------------*
 00071      05  CHECK-PT-ID-NO.
 00072         10  FILLER              PIC XX     VALUE 'AS'.
 00073         10  CP-TRAN-NO          PIC 9(6)   VALUE 0.
 00074 *--------------------------------------------------------------*
 00075          SKIP1
 00076      05  WS-YEAR.
 00077          10  WS-CC              PIC 9(2) VALUE 19.
 00078          10  WS-YY              PIC 9(2).
 00079      05  WS-YEAR-N  REDEFINES  WS-YEAR PIC 9(4).
 00080          SKIP1
 00081      05  WS-TXCD                PIC 9(05).
 00082      05  WS-TXCD-X REDEFINES  WS-TXCD.
 00083          10  WS-TXCD-TOWN       PIC 99.
 00084          10  FILLER             PIC X(3).
 00085               SKIP1
 00086      05  CURR-KEY.
 00087          10 CURR-PROP           PIC 9(15) PACKED-DECIMAL.
 00088                SKIP1
 00089      05  PREV-KEY               PIC X(14).
 00090                SKIP1
 00091      05  WS-DOB.
 00092          10   WS-DOB-YY         PIC 99.
 00093          10   WS-DOB-MM         PIC 99.
 00094          10   WS-DOB-DD         PIC 99.
 00095                SKIP1
 00096      05  WS-MDCY.
 00097          10   WS-MO             PIC 99.
 00098          10   WS-DA             PIC 99.
 00099          10   WS-CE             PIC 99.
 00100          10   WS-YR             PIC 99.
 00101      05  WS-MMDDCCYY  REDEFINES WS-MDCY    PIC 9(8).
 00102               SKIP1
 00103      05  CURR-DATE.
 00104          10  CENT                   PIC  99       VALUE  0.
 00105          10  YR-MO-DAY              PIC  9(6)     VALUE  0.
 00106          10  FILLER               REDEFINES YR-MO-DAY.
 00107              15  YR                 PIC 99.
 00108              15  FILLER             PIC X(4).
 00109      05  CURR-DATE-N   REDEFINES    CURR-DATE PIC 9(8).
 00110               SKIP1
 00111      05  CURR-TIME-X.
 00112          10  CURR-TIME              PIC  9(6).
 00113          10  CURR-TIME-2            PIC  9(2).
 00114      05  CURR-TIME-N   REDEFINES    CURR-TIME-X PIC 9(8).
 00115               SKIP1
 00116      05  MO-DAY-YR                  PIC  9(6)     VALUE  0.
 00117               SKIP1
 00118      05  HEAD1.
 00119          10  FILLER             PIC X     VALUE SPACE.
 00120          10  FILLER             PIC X(08) VALUE
 00121          'ASHMA840'.
 00122          10  FILLER             PIC X(29) VALUE SPACE.
 00123          10  FILLER             PIC X(34) VALUE
 00124          'OFFICE OF THE COOK COUNTY ASSESSOR'.
 00125          10  FILLER             PIC X(33) VALUE SPACE.
 00126          10  FILLER             PIC X(07) VALUE 'PAGE'.
 00127          10  H1-PAGE            PIC Z,ZZ9.
 00128                     SKIP1
 00129      05  HEAD2.
 00130          10  FILLER             PIC X     VALUE SPACE.
 00131          10  H2-DATE            PIC 99/99/99.
 00132          10  FILLER             PIC X(21) VALUE SPACE.
 00133          10  FILLER             PIC X(40) VALUE
 00134          'QUALIFIED SEN FRZ BASE VAL UPDATE REPORT'.
 00135                     SKIP1
 00136      05  HEAD3.
 00137          10  FILLER             PIC X     VALUE SPACE.
 00138          10  FILLER             PIC X(49) VALUE
 00139          'TOWN   VOL   PROPERTY INDEX NUMBER   REC. CODE'.
 00140          10  FILLER             PIC X(24) VALUE
 00141          'BIRTH DATE      MESSAGE:'.
 00142            SKIP1
 00143      05  DET-LN.
 00144          10  FILLER             PIC XX    VALUE SPACE.
 00145          10  DL-TOWN            PIC 99.
 00146          10  FILLER             PIC X(4)  VALUE SPACES.
 00147          10  DL-VOL             PIC 999.
 00148          10  FILLER             PIC X(4)  VALUE SPACES.
 00149          10  DL-PIN             PIC 99B99B999B999B9999.
 00150          10  FILLER             PIC X(9)  VALUE SPACES.
 00151          10  DL-RECCD           PIC 9.
 00152          10  FILLER             PIC X(7)  VALUE SPACES.
 00153          10  DL-BIRTH-DTE       PIC 99B99B9999.
 00154          10  FILLER             PIC X(6)  VALUE SPACES.
 00155          10  DL-MSG             PIC X(35).
 00156            SKIP1
 00157      05  TOT-LN.
 00158          10  FILLER     PIC XX    VALUE SPACE.
 00159          10  TL-MSG     PIC X(32).
 00160          10  FILLER     PIC XXXX  VALUE SPACE.
 00161          10  TL-TOT     PIC ZZ,ZZ9.
 00162            EJECT
 00163  01  MESSAGES.
 00164      05  MSG-1                  PIC X(50) VALUE
 00165          'NO MATCHING ROOT SEGMENT'.
 00166      05  MSG-2                  PIC X(50) VALUE
 00167          'NO MATCHING ASSESSMENT DATA SEGMENT'.
 00168      05  MSG-3                  PIC X(50) VALUE
 00169          'NO MATCHING EXEMPT MASTER SEGMENT'.
 00170      05  MSG-4                  PIC X(50) VALUE
 00171          'MASTER SEGMENT ADDED'.
 00172      05  MSG-5                  PIC X(50) VALUE
 00173          'DETAIL SEGMENT ADDED'.
 00174      05  MSG-6                  PIC X(50) VALUE
 00175          'MASTER SEGMENT UPDATED'.
 00176      05  MSG-7                  PIC X(50) VALUE
 00177          'DETAIL SEGMENT UPDATED'.
 00178       EJECT
 00179  01  IO-AREA.
 00180  ++INCLUDE PIROOTSEGM
 00181  SKIP3
 00182  ++INCLUDE PIASSESSMT
 00183  SKIP3
 00184  ++INCLUDE ASAIS145SG
 00185  SKIP3
 00186  ++INCLUDE ASAIS150SG
 00187       SKIP3
 00188  ++INCLUDE IMSCALLS
 00189       EJECT
 00190  ++INCLUDE ASAISSSA29
 00191       EJECT
 00192  LINKAGE SECTION.
 00193  ++INCLUDE IOPCBDESC
 00194  01  PI-PCB.
 00195  ++INCLUDE PIPCB1DESC
 00196          10  PI-SEG-KEY            PIC X(17).
 00197      EJECT
 00198  PROCEDURE DIVISION.
 00199      SKIP1
 00200  100-HOUSEKEEPING.
 00201      ENTRY 'DLITCBL' USING IO-PCB  PI-PCB
 00202            SKIP1
 00203      DISPLAY SPACES
 00204      OPEN INPUT ASSFMSDT
 00205                 CNTRL-CARD
 00206          OUTPUT PRINT-FILE.
 00207            SKIP1
 00208      READ CNTRL-CARD AT END
 00209         DISPLAY 'CONTROL CARD IS MISSING'
 00210         MOVE 16 TO RETURN-CODE.
 00211      DISPLAY 'CONTROL CARD  ' CNTRL-RECORD.
 00212
 00213      IF RETURN-CODE NOT  EQUAL 16
 00214         PERFORM 995-CHECK-CNTRL-RTN
 00215         IF RETURN-CODE  EQUAL  16
 00216            STOP RUN
 00217         END-IF
 00218      END-IF.
 00219      SKIP1
 00220      ACCEPT   CURR-TIME-N   FROM  TIME.
 00221      ACCEPT    YR-MO-DAY    FROM  DATE.
 00222      IF YR > 60
 00223         MOVE 19  TO CENT
 00224      ELSE
 00225         MOVE 20  TO CENT
 00226      END-IF
 00227      MULTIPLY  YR-MO-DAY    BY    100.0001
 00228        GIVING  MO-DAY-YR.
 00229      MOVE      MO-DAY-YR    TO  H2-DATE.
 00230                 SKIP1
 00231      PERFORM 200-MAINLINE
 00232          UNTIL  STOP-PROCESSING
 00233           OR EXMT-EOF
 00234           OR RETURN-CODE EQUAL 16.
 00235                  SKIP1
 00236      CLOSE      ASSFMSDT
 00237                 CNTRL-CARD
 00238                 PRINT-FILE.
 00239                       SKIP1
 00240      PERFORM    999-DISPLAYS.
 00241      GOBACK.
 00242       SKIP2
 00243  200-MAINLINE.
 00244      SET  RESET-MSG-SW  TO  TRUE
 00245      PERFORM  300-READ-EXMT-FILE
 00246      IF NOT STOP-PROCESSING  AND  NOT EXMT-EOF
 00247         IF OF-C150-SGSFSTAT EQUAL 'Q'
 00248                 OR OF-C150-SGBSYR-IND EQUAL '1'
 00249            MOVE  OF-PROP-NO   TO   LVL1-ARG
 00250            ADD +1 TO EXMT-SEL-CTR
 00251            PERFORM 620-GHU-MASTER-SEG
 00252            IF PI-DB-GOOD-STATUS
 00253               MOVE C145-RECCODE  TO LVL3-ARG
 00254               PERFORM 500-UPD-FLDS-MASTER-SEG
 00255               PERFORM 650-REPL-MASTER-SEG
 00256               IF PI-DB-GOOD-STATUS
 00257                  MOVE OF-C150-SGBIRTHDTE TO LVL4-ARG
 00258                  PERFORM 630-GHU-DETAIL-SEG
 00259                  IF NOT STOP-PROCESSING
 00260                     IF PI-SEG-FOUND
 00261                        PERFORM 570-UPD-FLDS-DTL-FR-FIX-SEG
 00262                        PERFORM 660-REPL-DETAIL-SEG
 00263                        ADD +4  TO CIC-CTR
 00264                     ELSE
 00265                        PERFORM 550-ADD-FLDS-DTL-FR-FIX-SEG
 00266                        PERFORM 690-ISRT-DETAIL-SEG
 00267                        ADD +3  TO CIC-CTR
 00268                     END-IF
 00269                     PERFORM 900-DETERMINE-CHECKPOINT
 00270                  END-IF
 00271               END-IF
 00272            END-IF
 00273         END-IF
 00274      END-IF.
 00275      SKIP3
 00276  300-READ-EXMT-FILE.
 00277      READ ASSFMSDT
 00278          AT END  SET EXMT-EOF TO TRUE.
 00279      IF EXMT-EOF
 00280          CONTINUE
 00281      ELSE
 00282          ADD  +1 TO EXMT-READ-CTR
 00283          MOVE  OF-PROP-NO   TO   CURR-PROP
 00284          IF PREV-KEY        GREATER  CURR-KEY
 00285              MOVE 16 TO RETURN-CODE
 00286              SET STOP-PROCESSING   TO  TRUE
 00287              DISPLAY 'SEQUENCE ERROR IN DIVISION MASTER FILE'
 00288              DISPLAY '   PREVIOUS KEY.: ' PREV-KEY
 00289              DISPLAY '   CURRENT KEY..: ' CURR-KEY
 00290          ELSE
 00291              MOVE CURR-KEY        TO PREV-KEY
 00292         END-IF
 00293      END-IF.
 00294       SKIP3
 00295  500-UPD-FLDS-MASTER-SEG.
 00296      MOVE OF-C145-BLDGUNITS    TO C145-BLDGUNITS
 00297      MOVE OF-C145-BLDGSHARES   TO C145-BLDGSHARES
 00298      MOVE OF-C145-BASVALYR     TO C145-BASVALYR
 00299      MOVE OF-C145-BVYCLS       TO C145-BVYCLS
 00300      MOVE OF-C145-BVYFULLAV    TO C145-BVYFULLAV
 00301      MOVE OF-C145-BVYEV        TO C145-BVYEV
 00302      MOVE OF-C145-BVYELCOMPFULLAV TO C145-BVYELCOMPFULLAV
 00303      MOVE OF-C145-BVYTOTELCOMPEV  TO C145-BVYTOTELCOMPEV
 00304      MOVE ZEROS                TO C145-SFSHARES
 00305      MOVE 'ASHMA840'           TO C145-UPDTID
 00306      MOVE CURR-DATE-N          TO C145-UPDTDATE.
 00307       SKIP3
 00308  550-ADD-FLDS-DTL-FR-FIX-SEG.
 00309      MOVE OF-C150-SGSENFRZDETAIL TO C150-SENFRZDETAIL
 00310      MOVE OF-C145-BASVALYR       TO C150-BASE-YR
 00311      MOVE '1'                  TO C150-BSYR-IND
 00312      MOVE OF-C145-BVYTOTELCOMPEV TO C150-BSYR-EQVAL
 00313      MOVE 'ASHMA840'           TO C150-LSTUPID
 00314      MOVE CURR-DATE-N          TO C150-LSTUPDTE
 00315      MOVE SPACES TO C150-SFSTAT       C150-NOINCOME
 00316                     C150-SIGNED       C150-NOTARIZED
 00317                     C150-ORIGEMPNO    C150-HSSTAT
 00318                     C150-HOSTAT
 00319      MOVE ZEROS  TO C150-DENIALDTE    C150-FSTAPPDTE
 00320                     C150-LSTAPPDTE    C150-QUALDTE
 00321                     C150-RTNDDTE      C150-SSINC
 00322                     C150-RRBEN        C150-CSBEN
 00323                     C150-OTHBEN       C150-VETBEN
 00324                     C150-PUBAID       C150-WAGES
 00325                     C150-INT          C150-NETRENT
 00326                     C150-NETCAPGAIN   C150-OTHINC
 00327                     C150-TOTINC       C150-BATCH
 00328                     C150-ORIGDTE      C150-KEYTIME
 00329                     C150-HSBATCH.
 00330       SKIP3
 00331  570-UPD-FLDS-DTL-FR-FIX-SEG.
 00332      MOVE OF-C145-BASVALYR       TO C150-BASE-YR
 00333      MOVE '1'                  TO C150-BSYR-IND
 00334      MOVE OF-C145-BVYTOTELCOMPEV TO C150-BSYR-EQVAL
 00335      MOVE 'ASHMA840'           TO C150-LSTUPID
 00336      MOVE CURR-DATE-N          TO C150-LSTUPDTE.
 00337      SKIP3
 00338  620-GHU-MASTER-SEG.
 00339      CALL 'CBLTDLI' USING GHU
 00340                           PI-PCB
 00341                           C145-SENFRZMASTER
 00342                           LVL1-QUAL-SSA
 00343                           LVL2-QUAL-SSA
 00344                           LVL3-UNQUAL-SSA
 00345      EVALUATE  TRUE
 00346         WHEN PI-DB-GOOD-STATUS
 00347            CONTINUE
 00348         WHEN PI-SEG-NOTFND
 00349            EVALUATE  TRUE
 00350               WHEN PI-DB-2ND-SEG-LEVEL
 00351                  SET MESG-3 TO TRUE
 00352                  PERFORM 800-WRITE-RPT
 00353               WHEN PI-DB-1ST-SEG-LEVEL
 00354                  SET MESG-2 TO TRUE
 00355                  PERFORM 800-WRITE-RPT
 00356               WHEN OTHER
 00357                  SET MESG-1 TO TRUE
 00358                  PERFORM 800-WRITE-RPT
 00359            END-EVALUATE
 00360         WHEN OTHER
 00361            DISPLAY 'DL/1 ERROR  620-GHU-MASTER-SEG'
 00362            DISPLAY 'PCB  AREA: ' PI-PCB
 00363            MOVE 16 TO RETURN-CODE
 00364            SET STOP-PROCESSING TO TRUE
 00365      END-EVALUATE.
 00366      SKIP3
 00367  630-GHU-DETAIL-SEG.
 00368      CALL 'CBLTDLI' USING GHU
 00369                           PI-PCB
 00370                           C150-SENFRZDETAIL
 00371                           LVL1-QUAL-SSA
 00372                           LVL2-QUAL-SSA
 00373                           LVL3-QUAL-SSA
 00374                           LVL4-QUAL-SSA
 00375      EVALUATE  TRUE
 00376         WHEN PI-DB-GOOD-STATUS
 00377            CONTINUE
 00378         WHEN PI-SEG-NOTFND
 00379            CONTINUE
 00380         WHEN OTHER
 00381            DISPLAY 'DL/1 ERROR  630-GHU-DETAIL-SEG'
 00382            DISPLAY 'PCB  AREA: ' PI-PCB
 00383            MOVE 16 TO RETURN-CODE
 00384            SET STOP-PROCESSING   TO  TRUE
 00385      END-EVALUATE.
 00386       SKIP3
 00387  650-REPL-MASTER-SEG.
 00388      CALL 'CBLTDLI' USING REPL
 00389                           PI-PCB
 00390                           C145-SENFRZMASTER
 00391      IF PI-DB-GOOD-STATUS
 00392         ADD  +1  TO  CIC-CTR
 00393                      MAST-SEGS-UPD-CTR
 00394         SET   MESG-6    TO   TRUE
 00395      ELSE
 00396         DISPLAY 'DL/1 ERROR 650-REPL-MASTER-SEG'
 00397         DISPLAY 'PCB  AREA: ' PI-PCB
 00398         MOVE 16 TO RETURN-CODE
 00399         SET STOP-PROCESSING   TO  TRUE
 00400      END-IF.
 00401      SKIP3
 00402  660-REPL-DETAIL-SEG.
 00403      CALL 'CBLTDLI' USING REPL
 00404                           PI-PCB
 00405                           C150-SENFRZDETAIL.
 00406
 00407      IF PI-DB-GOOD-STATUS
 00408         ADD  +1  TO  DET-SEGS-UPD-CTR
 00409         SET     MESG-7    TO  TRUE
 00410      ELSE
 00411         DISPLAY 'DL/1 ERROR 660-REPL-DETAIL-SEG'
 00412         DISPLAY 'PCB  AREA: ' PI-PCB
 00413         MOVE 16 TO RETURN-CODE
 00414         SET STOP-PROCESSING   TO  TRUE
 00415      END-IF.
 00416      SKIP3
 00417      SKIP3
 00418  690-ISRT-DETAIL-SEG.
 00419      CALL 'CBLTDLI' USING ISRT
 00420                           PI-PCB
 00421                           C150-SENFRZDETAIL
 00422                           LVL1-QUAL-SSA
 00423                           LVL2-QUAL-SSA
 00424                           LVL3-QUAL-SSA
 00425                           LVL4-UNQUAL-SSA
 00426      IF PI-DB-GOOD-STATUS
 00427         ADD  +1  TO  DET-SEGS-ADD-CTR
 00428         SET     MESG-5    TO  TRUE
 00429      ELSE
 00430         DISPLAY 'DL/1 ERROR  690-ISRT-DETAIL-SEG'
 00431         DISPLAY 'PCB  AREA: ' PI-PCB
 00432         MOVE 16 TO RETURN-CODE
 00433         SET STOP-PROCESSING   TO  TRUE
 00434      END-IF.
 00435       SKIP3
 00436  800-WRITE-RPT.
 00437      IF  LINE-CTR  GREATER  +55
 00438          PERFORM   830-HEADERS
 00439      END-IF
 00440      MOVE  SPACES   TO   DET-LN.
 00441      MOVE OF-TOWN-NO    TO   DL-TOWN
 00442      MOVE OF-VOLUME     TO   DL-VOL
 00443      MOVE OF-PROP-NO    TO   DL-PIN
 00444      INSPECT  DL-PIN  REPLACING ALL ' ' BY  '-'
 00445      MOVE OF-C145-RECCODE TO DL-RECCD
 00446      MOVE OF-C150-SGBIRTHDTE TO DL-BIRTH-DTE
 00447      EVALUATE TRUE
 00448         WHEN  MESG-1
 00449             MOVE  MSG-1  TO  DL-MSG
 00450         WHEN  MESG-2
 00451             MOVE  MSG-2  TO  DL-MSG
 00452         WHEN  MESG-3
 00453             MOVE C145-RECCODE  TO  DL-RECCD
 00454             MOVE  MSG-3  TO  DL-MSG
 00455         WHEN  MESG-4
 00456             MOVE  MSG-4  TO  DL-MSG
 00457         WHEN  MESG-5
 00458             MOVE   C150-BIRTHDTE  TO  DL-BIRTH-DTE
 00459             INSPECT  DL-BIRTH-DTE REPLACING ALL ' ' BY '-'
 00460             MOVE  MSG-5  TO  DL-MSG
 00461         WHEN  MESG-6
 00462             MOVE  MSG-6  TO  DL-MSG
 00463         WHEN  MESG-7
 00464             MOVE   C150-BIRTHDTE  TO  DL-BIRTH-DTE
 00465             INSPECT  DL-BIRTH-DTE REPLACING ALL ' ' BY '-'
 00466             MOVE  MSG-7  TO  DL-MSG
 00467      END-EVALUATE
 00468      WRITE PRINT-REC FROM DET-LN
 00469      AFTER ADVANCING 2
 00470      ADD  +2   TO  LINE-CTR.
 00471      SKIP3
 00472  830-HEADERS.
 00473      ADD     +1        TO    PAGE-CTR.
 00474      MOVE  PAGE-CTR    TO    H1-PAGE.
 00475      WRITE PRINT-REC FROM HEAD1
 00476            AFTER ADVANCING PAGE
 00477      WRITE PRINT-REC FROM HEAD2
 00478            AFTER ADVANCING 2
 00479      WRITE PRINT-REC FROM HEAD3
 00480            AFTER ADVANCING 3
 00481      MOVE +6   TO    LINE-CTR.
 00482       SKIP3
 00483  900-DETERMINE-CHECKPOINT.
 00484      IF CIC-CTR GREATER THAN CNTRL-CICV-VAL
 00485         ADD +1                    TO CP-TRAN-NO
 00486         PERFORM 910-ISSUE-CHECKPOINT
 00487         MOVE ZEROES               TO CIC-CTR
 00488      END-IF.
 00489        SKIP3
 00490  910-ISSUE-CHECKPOINT.
 00491      CALL 'CBLTDLI' USING  CHKP, IO-PCB, CHECK-PT-ID-NO.
 00492      IF IP-GOOD-STATUS
 00493         NEXT SENTENCE
 00494      ELSE
 00495         PERFORM 920-ISSUE-CHKPT-ERR.
 00496       SKIP3
 00497  920-ISSUE-CHKPT-ERR.
 00498      DISPLAY 'CHECK POINT ISSUE ERROR'.
 00499      MOVE 0016 TO RETURN-CODE.
 00500      SET STOP-PROCESSING   TO  TRUE
 00501      DISPLAY 'LOGICAL TERMINAL NAME:  '
 00502          IP-TERM-NAME.
 00503      DISPLAY 'STATUS CODE:  '
 00504          IP-STATUS-CODE.
 00505      DISPLAY SPACES
 00506      DISPLAY 'ROLL ISSURED TO TERMINATE PROGRAM'
 00507      CALL 'CBLTDLI' USING ROLL.
 00508      SKIP3
 00509  995-CHECK-CNTRL-RTN.
 00510      IF CNTRL-CICV   EQUAL  'CICV'
 00511         CONTINUE
 00512      ELSE
 00513         MOVE   16   TO RETURN-CODE
 00514         DISPLAY 'POS 1-4 MUST BE EQUAL TO ''CICV'''
 00515      END-IF.
 00516           SKIP1
 00517      IF CNTRL-VAL NUMERIC
 00518         CONTINUE
 00519      ELSE
 00520         MOVE   16   TO RETURN-CODE
 00521         DISPLAY 'POS 5-8 MUST BE NUMERIC'
 00522      END-IF.
 00523           SKIP1
 00524      IF CNTRL-YR-PROC EQUAL 'PROCYEAR'
 00525         CONTINUE
 00526      ELSE
 00527         MOVE   16   TO RETURN-CODE
 00528         DISPLAY 'POS 9-16 MUST BE EQUAL TO ''PROCYEAR'''
 00529      END-IF.
 00530
 00531      IF CNTRL-YR     NUMERIC
 00532         CONTINUE
 00533      ELSE
 00534         MOVE   16   TO RETURN-CODE
 00535         DISPLAY 'POS 17-20 MUST BE A NUMERIC VALUE'
 00536      END-IF.
 00537      IF RETURN-CODE NOT = 16
 00538         MOVE  CNTRL-YR-N   TO   LVL2-PROCYR
 00539                                 LVL2-TXYR
 00540         MOVE  0            TO   LVL2-TXTYP
 00541      END-IF.
 00542       SKIP3
 00543  999-DISPLAYS.
 00544      DISPLAY 'END OF EXECUTION OF ASHMA840'
 00545      DISPLAY
 00546      'TOTAL INPUT RECORDS READ.................: '
 00547                                        EXMT-READ-CTR
 00548      DISPLAY
 00549      'TOTAL INPUT RECORDS SELECTED (SFSTAT = Q): '
 00550                                       EXMT-SEL-CTR
 00551      DISPLAY
 00552      'TOTAL SF MASTER SEGMENTS UPDATED.........: '
 00553                                       MAST-SEGS-UPD-CTR
 00554      DISPLAY
 00555      'TOTAL SF DETAIL SEGMENTS UPDATED.........: '
 00556                                       DET-SEGS-UPD-CTR
 00557      'TOTAL SF DETAIL SEGMENTS CREATED.........: '
 00558                                       DET-SEGS-ADD-CTR
 00559      DISPLAY SPACES.
