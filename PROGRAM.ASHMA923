 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID.    ASHMA923.
 00003 *****************************************************************
 00004 * AUTHOR.                                                       *
 00005 *       ANALYST    - RICHARD KALEMBA                            *
 00006 *       PROGRAMMER - THOMAS D. BROWN                            *
 00007 *                                                               *
 00008 * DATE-WRITTEN.                                                 *
 00009 *       APRIL 1996.                                             *
 00010 *                                                               *
 00011 * REMARKS.                                                      *
 00012 *       THIS PROGRAM IS TO CREATE AN OUTPUT FILE OF DATA FROM   *
 00013 *       THE MASTER AND D1 SEGMENTS ON THE ASSESSORS DATABASE.   *
 00014 *****************************************************************
 00015  SKIP2
 00016  ENVIRONMENT DIVISION.
 00017  INPUT-OUTPUT SECTION.
 00018  FILE-CONTROL.
 00019      SELECT ASSFMSDT ASSIGN TO UT-S-OUTFILE.
 00020  EJECT
 00021  DATA DIVISION.
 00022  FILE SECTION.
 00023  ++INCLUDE ASSFMSDTF1
 00024  EJECT
 00025  WORKING-STORAGE SECTION.
 00026  77  WS-SEGMENTS-CNTR       PIC 9(7)     VALUE 0 PACKED-DECIMAL.
 00027  77  WS-MASTER-CNTR         PIC 9(7)     VALUE 0 PACKED-DECIMAL.
 00028  77  WS-DETAIL-CNTR         PIC 9(7)     VALUE 0 PACKED-DECIMAL.
 00029  77  WS-OUTPUT-CNTR         PIC 9(7)     VALUE 0 PACKED-DECIMAL.
 00030  77  EOF-SW                 PIC X        VALUE 'N'.
 00031      88 END-OF-DATABASE                  VALUE 'Y'.
 00032      88 TERMI                            VALUE 'T'.
 00033
 00034  01  CONTROL-CARD.
 00035      05  CONTROL-YEAR-C     PIC X(4).
 00036      05  CONTROL-YEAR-N     PIC 9(4).
 00037      05  FILLER             PIC X(72).
 00038
 00039  01  IO-AREA.
 00040  ++INCLUDE PIROOTSEGM
 00041  ++INCLUDE PIASSESSMT
 00042  ++INCLUDE PILOCATSEG
 00043  ++INCLUDE ASAIS145SG
 00044  ++INCLUDE ASAIS150SG
 00045  ++INCLUDE ASAIS155SG
 00046  ++INCLUDE IMSCALLS
 00047
 00048  ++INCLUDE ASAISSSA29
 00049
 00050 *****************************************************************
 00051  LINKAGE SECTION.
 00052  ++INCLUDE IOPCBDESC
 00053
 00054  01  PI-PCB1.
 00055  ++INCLUDE PIPCB1DESC
 00056          07  PI-KEY-FB-ITEM          PIC X(22).
 00057      EJECT
 00058  PROCEDURE DIVISION.
 00059  0000-MAINLINE.
 00060  SKIP2
 00061      ENTRY 'DLITCBL' USING IO-PCB  PI-PCB1.
 00062  SKIP2
 00063      PERFORM 1000-INITIALIZATION.
 00064      PERFORM 2000-PROCESS-DATABASE UNTIL RETURN-CODE = 16
 00065              OR END-OF-DATABASE OR TERMI.
 00066      PERFORM 9000-FINALIZATION.
 00067      STOP RUN.
 00068  EJECT
 00069  1000-INITIALIZATION.
 00070      OPEN OUTPUT ASSFMSDT
 00071      ACCEPT CONTROL-CARD.
 00072      DISPLAY 'CONTROL CARD     '  CONTROL-CARD.
 00073      IF CONTROL-YEAR-N NOT NUMERIC OR CONTROL-YEAR-N <= 1993
 00074         OR CONTROL-YEAR-C NOT EQUAL TO 'YEAR'
 00075         DISPLAY 'SELECTION YEAR INVALID ON PARM CARD'
 00076         DISPLAY 'PARM CARD = ' CONTROL-CARD
 00077         MOVE 16 TO RETURN-CODE
 00078      END-IF.
 00079
 00080  2000-PROCESS-DATABASE.
 00081      PERFORM 2100-GN-ROOT.
 00082      IF PI-DB-GOOD-STATUS
 00083         PERFORM 3100-GNP-ASSESSMENT
 00084         IF PI-DB-GOOD-STATUS
 00085            PERFORM 4100-GNP-SENIOR-FREEZE
 00086            IF PI-DB-GOOD-STATUS
 00087               PERFORM 5100-GNP-SENIOR-DETAIL
 00088                       UNTIL NOT PI-DB-GOOD-STATUS
 00089            END-IF
 00090         END-IF
 00091      END-IF.
 00092
 00093  2100-GN-ROOT.
 00094      CALL 'CBLTDLI' USING GN, PI-PCB1, P-ROOT, LVL1-UNQUAL-SSA.
 00095      EVALUATE TRUE
 00096      WHEN PI-DB-GOOD-STATUS
 00097           ADD +1 TO WS-SEGMENTS-CNTR
 00098           MOVE P-PROP TO LVL1-ARG
 00099      WHEN PI-DB-END
 00100           MOVE 'Y' TO EOF-SW
 00101      WHEN PI-SEG-NOTFND
 00102           DISPLAY 'DATA BASE DOES NOT CONTAIN A ROOT SEGMENT'
 00103           PERFORM 9900-DATABASE-ERROR
 00104      WHEN OTHER
 00105           DISPLAY 'ERROR IN MODULE 2100-GN-ROOT'
 00106           PERFORM 9900-DATABASE-ERROR
 00107      END-EVALUATE.
 00108
 00109  3100-GNP-ASSESSMENT.
 00110      MOVE CONTROL-YEAR-N(3:2) TO LVL2-PROCYR.
 00111      MOVE CONTROL-YEAR-N(3:2) TO LVL2-TXYR.
 00112      MOVE ZERO                TO LVL2-TXTYP.
 00113      CALL 'CBLTDLI' USING GNP, PI-PCB1, PY-ASMTDATA,
 00114                           LVL1-QUAL-SSA, LVL2-QUAL-SSA.
 00115      EVALUATE TRUE
 00116         WHEN PI-DB-GOOD-STATUS
 00117              CONTINUE
 00118         WHEN PI-SEG-NOTFND
 00119              CONTINUE
 00120         WHEN PI-DB-END
 00121              MOVE 'Y' TO EOF-SW
 00122         WHEN OTHER
 00123              DISPLAY 'ERROR IN MODULE 3100-GNP-ASSESSMENT'
 00124              PERFORM 9900-DATABASE-ERROR
 00125      END-EVALUATE.
 00126
 00127  4100-GNP-SENIOR-FREEZE.
 00128      CALL 'CBLTDLI' USING GNP, PI-PCB1, C145-SENFRZMASTER,
 00129                           LVL1-QUAL-SSA, LVL2-QUAL-SSA,
 00130                           LVL3-UNQUAL-SSA.
 00131      EVALUATE TRUE
 00132         WHEN PI-DB-GOOD-STATUS
 00133              ADD +1 TO WS-MASTER-CNTR
 00134         WHEN PI-SEG-NOTFND
 00135              CONTINUE
 00136         WHEN PI-DB-END
 00137              MOVE 'Y' TO EOF-SW
 00138         WHEN OTHER
 00139              DISPLAY 'ERROR IN MODULE 4100-GNP-SENIOR-FREEZE'
 00140              PERFORM 9900-DATABASE-ERROR
 00141      END-EVALUATE.
 00142
 00143  5100-GNP-SENIOR-DETAIL.
 00144      MOVE C145-RECCODE TO LVL3-ARG.
 00145      CALL 'CBLTDLI' USING GNP, PI-PCB1, C150-SENFRZDETAIL,
 00146                           LVL1-QUAL-SSA, LVL2-QUAL-SSA,
 00147                           LVL3-QUAL-SSA, LVL4-UNQUAL-SSA.
 00148      EVALUATE TRUE
 00149         WHEN PI-DB-GOOD-STATUS
 00150              ADD +1 TO WS-DETAIL-CNTR
 00151              PERFORM 6100-CREATE-OUTPUT-RECORD
 00152         WHEN PI-SEG-NOTFND
 00153              CONTINUE
 00154         WHEN PI-DB-END
 00155              MOVE 'Y' TO EOF-SW
 00156         WHEN OTHER
 00157              DISPLAY 'ERROR IN MODULE 5100-GNP-SENIOR-DETAIL'
 00158              PERFORM 9900-DATABASE-ERROR
 00159      END-EVALUATE.
 00160
 00161  6100-CREATE-OUTPUT-RECORD.
 00162      MOVE P-TOWN            TO OF-TOWN-NO.
 00163      MOVE P-VOL             TO OF-VOLUME.
 00164      MOVE P-PROP            TO OF-PROP-NO.
 00165      MOVE PY-YEAR           TO OF-YEAR.
 00166      MOVE PY-TXYR           TO OF-TAX-YEAR.
 00167      MOVE PY-TXTYPE         TO OF-TAX-TYPE.
 00168      MOVE C145-SENFRZMASTER TO OF-C145-SENFRZMASTER
 00169      MOVE C150-SENFRZDETAIL TO OF-C150-SGSENFRZDETAIL
 00170      WRITE ASSFMSDT-REC.
 00171      ADD +1 TO WS-OUTPUT-CNTR.
 00172
 00173  9000-FINALIZATION.
 00174      DISPLAY 'TOTAL SEGMENTS READ.............' WS-SEGMENTS-CNTR.
 00175      DISPLAY 'TOTAL MASTER SEGMENTS WRITTEN...' WS-MASTER-CNTR.
 00176      DISPLAY 'TOTAL DETAIL SEGMENTS WRITTEN...' WS-DETAIL-CNTR.
 00177      DISPLAY 'TOTAL OUTPUT RECORDS WRITTEN....' WS-OUTPUT-CNTR.
 00178      CLOSE ASSFMSDT.
 00179
 00180  9900-DATABASE-ERROR.
 00181      MOVE 'T' TO EOF-SW.
 00182      MOVE 16  TO RETURN-CODE.
 00183      DISPLAY 'DBD NAME................' PI-DBD-NAME.
 00184      DISPLAY 'SEGMENT LEVEL...........' PI-SEGMENT-LVL.
 00185      DISPLAY 'STATUS CODE.............' PI-STATUS-CODE.
 00186      DISPLAY 'PROCESSING OPTIONS......' PI-PROC-OPT.
 00187      DISPLAY 'LEVEL1 ARGUEMENT........' P-PROP.
 00188      DISPLAY 'KEY LENGTH..............' PI-KEY-FDBK-LNG.
 00189      DISPLAY 'SENSITIVE SEGMENTS......' PI-SEN-SEG.
 00190      DISPLAY 'KEY FEEDBACK AREA IS....' PI-KEY-FDBK.
