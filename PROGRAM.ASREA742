 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID.    ASREA742.
 00003  AUTHOR.        GOLD - MOLIS.
 00004  DATE-WRITTEN.  AUG 3, 1992.
 00005  SKIP1
 00006 *REMARKS. THE PURPOSE OF THIS PROGRAM IS TO CREATE THE FROZEN
 00007 *         VALUATION DETAIL SELECTION FILE.
 00008  SKIP2
 00009  ENVIRONMENT DIVISION.
 00010  CONFIGURATION SECTION.
 00011  SOURCE-COMPUTER. IBM-370.
 00012  OBJECT-COMPUTER. IBM-370.
 00013  SKIP1
 00014  INPUT-OUTPUT SECTION.
 00015  FILE-CONTROL.
 00016      SELECT MASTER-IN         ASSIGN TO UT-S-MASTNEW.
 00017      SELECT FRZVAL-SEL-FILE   ASSIGN TO UT-S-FRZSELC.
 00018      SELECT PRINT-FILE        ASSIGN TO UT-S-PRINTFLE.
 00019  SKIP1
 00020  DATA DIVISION.
 00021  FILE SECTION.
 00022  SKIP1
 00023  ++INCLUDE ASREASFD06
 00024  SKIP1
 00025  ++INCLUDE CLFRZSELF1
 00026  SKIP1
 00027  ++INCLUDE PRINTFILE
 00028  EJECT
 00029  WORKING-STORAGE SECTION.
 00030      SKIP1
 00031  77  NEW-MAST-RECS      PIC S9(13)    VALUE +0  PACKED-DECIMAL.
 00032      88  FIRST-REC-NEW                VALUE +1.
 00033  77  RECS-WRIT          PIC S9(13)    VALUE +0  PACKED-DECIMAL.
 00034  77  RECS-ACCUM         PIC S9(13)    VALUE +0  PACKED-DECIMAL.
 00035  77  WS-A1              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
 00036  77  WS-A2              PIC S9(13)    VALUE +0  PACKED-DECIMAL.
 00037  77  LINE-CNTR          PIC S9(3)     VALUE +60 PACKED-DECIMAL.
 00038  77  PAGE-CNTR          PIC S9(5)     VALUE +0  PACKED-DECIMAL.
 00039  77  SUB                PIC S9(4)     VALUE +0  BINARY.
 00040  77  BLANK-LINE         PIC X         VALUE SPACE.
 00041      SKIP1
 00042  77  NEW-EOF-SW         PIC X         VALUE 'N'.
 00043      88  NEW-EOF                      VALUE 'Y'.
 00044  77  ERR-SW             PIC X         VALUE 'N'.
 00045      88  ERR                          VALUE 'Y'.
 00046  77  WS-WRITE-SW        PIC X         VALUE 'N'.
 00047      88  WS-WRITE                     VALUE 'Y'.
 00048  77  WS-AGE-CHECK       PIC 999.
 00049      88  WS-AGE-1                     VALUE 001.
 00050      88  WS-AGE-2-999                 VALUE 002 THRU 999.
 00051  77  WS-CLS-CHECK       PIC 999.
 00052      88  WS-CLS-288                   VALUE 288.
 00053      SKIP1
 00054  01  WORK-AREA.
 00055      SKIP1
 00056           SKIP1
 00057      05  CURR-NEW-KEY.
 00058          10  CNEW-DIV    PIC 9(14)    VALUE ZEROES.
 00059          10  CNEW-PROP   PIC 9(15).
 00060      05  PREV-NEW-KEY    PIC X(29)    VALUE LOW-VALUES.
 00061      SKIP1
 00062      05  DSP-DATE.
 00063          10  DSP-MO                 PIC 99.
 00064          10  FILLER                 PIC X         VALUE '/'.
 00065          10  DSP-DA                 PIC 99.
 00066          10  FILLER                 PIC X         VALUE '/'.
 00067          10  DSP-YR                 PIC 99.
 00068      05  ACPT-DATE                  PIC 9(6).
 00069      05  ACPT-DATE-X REDEFINES ACPT-DATE.
 00070          10  ACPT-YR                PIC 99.
 00071          10  ACPT-MO                PIC 99.
 00072          10  ACPT-DA                PIC 99.
 00073      SKIP1
 00074      05  HEADING1.
 00075          10  FILLER          PIC X(45)   VALUE '  ASREA742'.
 00076          10  FILLER          PIC X(70)   VALUE
 00077              'OFFICE OF THE COOK COUNTY ASSESSOR'.
 00078          10  FILLER          PIC X(5)    VALUE 'PAGE'.
 00079          10  HD1-PAGE        PIC ZZZ,ZZ9.
 00080      SKIP1
 00081      05  HEADING2.
 00082          10  FILLER          PIC X(7)    VALUE '  DATE'.
 00083          10  HD2-DATE        PIC X(8).
 00084          10  FILLER          PIC X(30)   VALUE SPACES.
 00085          10  FILLER          PIC X(40)   VALUE
 00086              'CREATE FROZEN VALUE SELECTION FILE'.
 00087      SKIP1
 00088      05  HEADING3.
 00089          10  FILLER          PIC X(20)   VALUE '   DIVISION'.
 00090          10  FILLER          PIC X(42)   VALUE
 00091              'PERMANENT INDEX      TAX   CLASS'.
 00092          10  FILLER          PIC X(46)   VALUE
 00093              'CURRENT 288            FIRST TIME'.
 00094      SKIP1
 00095      05  HEADING4.
 00096          10  FILLER          PIC X(40)   VALUE
 00097              '    NUMBER               NUMBER'.
 00098          10  FILLER          PIC X(48)   VALUE
 00099              'CODE   MA MI             VALUE'.
 00100          10  FILLER          PIC X(35)   VALUE
 00101              'VALUE'.
 00102      SKIP1
 00103      05  DETAIL-LINE-1                   VALUE SPACES.
 00104          10  FILLER          PIC XX.
 00105          10  D-DIVNO-1       PIC 9(14).
 00106          10  FILLER          PIC XX.
 00107          10  D-PROP-1        PIC 99,99,999,999,9999.
 00108          10  FILLER          PIC XXX.
 00109          10  D-TAXCD-1       PIC 99999.
 00110          10  FILLER          PIC X(4).
 00111          10  D-CLASS-1       PIC 9,99.
 00112          10  FILLER          PIC X(6).
 00113          10  D-CUVAL-1       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
 00114          10  FILLER          PIC X(6).
 00115          10  D-FIRST-1       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
 00116          SKIP1
 00117      05  TOTAL-LINE.
 00118          10  FILLER          PIC XXX.
 00119          10  TOTAL-MSG       PIC X(33).
 00120          10  TOTAL-AMT       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
 00121         EJECT
 00122  LINKAGE SECTION.
 00123      SKIP1
 00124  01  LINK-INFO.
 00125      05  LINK-LENGTH         PIC S9(4)   BINARY.
 00126          88  VAL-L-LENGTH                VALUE +2.
 00127      05  LINK-PARM.
 00128          10 LINK-CURRYR-N    PIC 99.
 00129      SKIP2
 00130  PROCEDURE DIVISION USING LINK-INFO.
 00131      SKIP1
 00132  010-HOUSEKEEPING.
 00133      IF NOT VAL-L-LENGTH
 00134         DISPLAY 'INVALID LINKAGE LENGTH'
 00135         MOVE 16 TO RETURN-CODE
 00136         STOP RUN.
 00137      SKIP1
 00138      IF LINK-PARM NOT NUMERIC
 00139         DISPLAY 'CURRENT YEAR PARM MUST BE NUMERIC ' LINK-PARM
 00140         MOVE 16 TO RETURN-CODE
 00141         STOP RUN.
 00142      SKIP1
 00143      OPEN INPUT  MASTER-IN
 00144           OUTPUT PRINT-FILE
 00145                  FRZVAL-SEL-FILE.
 00146      SKIP1
 00147      ACCEPT ACPT-DATE FROM DATE.
 00148      MOVE ACPT-MO TO DSP-MO.
 00149      MOVE ACPT-DA TO DSP-DA.
 00150      MOVE ACPT-YR TO DSP-YR.
 00151      MOVE DSP-DATE TO HD2-DATE;
 00152      SKIP1
 00153      WRITE PRINT-REC FROM BLANK-LINE AFTER ADVANCING PAGE.
 00154      SKIP1
 00155      PERFORM 020-MAINLINE UNTIL ERR OR NEW-EOF.
 00156      SKIP1
 00157      IF NOT ERR
 00158         PERFORM 100-TOTAL-RTN.
 00159      SKIP1
 00160      CLOSE MASTER-IN
 00161            PRINT-FILE
 00162            FRZVAL-SEL-FILE.
 00163      SKIP1
 00164      STOP RUN.
 00165      SKIP2
 00166  020-MAINLINE.
 00167      PERFORM 030-ZERO-NEW.
 00168      PERFORM 040-READ-ACCUM-NEW-MAST.
 00169      SKIP1
 00170      IF NOT (ERR OR NEW-EOF)
 00171         IF WS-WRITE
 00172            PERFORM 070-MOVE-WRITE-REPORT
 00173            PERFORM 080-MOVE-WRITE-OUTFILE
 00174         ELSE
 00175            PERFORM 080-MOVE-WRITE-OUTFILE.
 00176      SKIP2
 00177  030-ZERO-NEW.
 00178      MOVE ZEROES TO WS-A1 WS-A2.
 00179      MOVE 'N'    TO WS-WRITE-SW.
 00180      SKIP2
 00181  040-READ-ACCUM-NEW-MAST.
 00182      PERFORM 050-READ-NEW-MAST.
 00183      SKIP1
 00184      IF NOT (ERR OR NEW-EOF)
 00185         PERFORM 045-ACCUM-NEW-MAST.
 00186      SKIP2
 00187  045-ACCUM-NEW-MAST.
 00188      IF M-DTL-QST-CTR > 1
 00189         PERFORM 060-READ-NEW-DETAIL VARYING SUB FROM 1 BY 1
 00190                 UNTIL SUB > M-DTL-QST-CTR.
 00191      SKIP2
 00192  050-READ-NEW-MAST.
 00193      READ MASTER-IN
 00194          AT END
 00195             MOVE ALL '9' TO CURR-NEW-KEY
 00196             MOVE 'Y'     TO NEW-EOF-SW.
 00197              SKIP1
 00198      IF NOT NEW-EOF
 00199         ADD +1              TO NEW-MAST-RECS
 00200         MOVE M-DIVNO-PROP   TO CNEW-DIV
 00201         MOVE M-PROP         TO CNEW-PROP
 00202         IF CURR-NEW-KEY NOT GREATER PREV-NEW-KEY
 00203            DISPLAY 'NEW BA MAST FILE OUT OF SEQUENCE'
 00204            DISPLAY 'CURRENT  KEY IS ' CURR-NEW-KEY
 00205            DISPLAY 'PREVIOUS KEY IS ' PREV-NEW-KEY
 00206            MOVE 16  TO RETURN-CODE
 00207            MOVE 'Y' TO ERR-SW
 00208         ELSE
 00209            MOVE CURR-NEW-KEY TO PREV-NEW-KEY.
 00210      SKIP2
 00211  060-READ-NEW-DETAIL.
 00212      IF D2-TYPE2-5 (SUB)
 00213         MOVE D2-AGE (SUB) TO WS-AGE-CHECK
 00214         MOVE D2-CLS (SUB) TO WS-CLS-CHECK
 00215         IF WS-AGE-1 AND NOT WS-CLS-288
 00216            ADD +1           TO RECS-ACCUM
 00217            MOVE 'Y'         TO WS-WRITE-SW
 00218            ADD D2-VAL (SUB) TO WS-A2.
 00219      SKIP1
 00220      IF D2-TYPE2-5 (SUB)
 00221         IF D3-TYPE3 (SUB)
 00222            IF WS-CLS-288 AND (D3-YR (SUB) = LINK-CURRYR-N)
 00223               AND (M-PERMIND = '2')
 00224               ADD +1           TO RECS-ACCUM
 00225               MOVE 'Y'         TO WS-WRITE-SW
 00226               ADD D3-VAL (SUB) TO WS-A1.
 00227      SKIP1
 00228      IF D2-QUES-CLASS (SUB)
 00229         ADD +1 TO SUB.
 00230      SKIP2
 00231  070-MOVE-WRITE-REPORT.
 00232      MOVE M-DIVNO-PROP   TO D-DIVNO-1.
 00233      MOVE M-PROP         TO D-PROP-1.
 00234      INSPECT D-PROP-1 REPLACING ALL ',' BY '-'.
 00235      MOVE M-TXCD         TO D-TAXCD-1.
 00236      MOVE M-CLS          TO D-CLASS-1.
 00237      INSPECT D-CLASS-1 REPLACING ALL ',' BY '-'.
 00238      MOVE WS-A1          TO D-CUVAL-1.
 00239      MOVE WS-A2          TO D-FIRST-1.
 00240      SKIP1
 00241      PERFORM 090-PRINT-DETAIL-REPT.
 00242      SKIP2
 00243  080-MOVE-WRITE-OUTFILE.
 00244      MOVE SPACES TO FRZVAL-SEL-REC.
 00245      SKIP1
 00246      MOVE M-DIVNO-PROP   TO FVS-DIVNO.
 00247      MOVE M-PROP         TO FVS-PROPNO.
 00248      MOVE M-TXCD         TO FVS-TXCD.
 00249      MOVE M-CLS          TO FVS-CLASS.
 00250      MOVE WS-A1          TO FVS-CURR-288-AV-VAL.
 00251      MOVE WS-A2          TO FVS-FRST-AV-VAL.
 00252      MOVE ZEROES         TO FVS-EXPR-288-AV-VAL FVS-TXCD-CNT
 00253                             FVS-CURR-288-PCT FVS-FRST-PCT
 00254                             FVS-EXPR-288-PCT FVS-CURR-288-VAL
 00255                             FVS-FRST-VAL FVS-EXPR-288-VAL.
 00256         SKIP1
 00257      WRITE FRZVAL-SEL-REC.
 00258      ADD +1 TO RECS-WRIT.
 00259      SKIP2
 00260  090-PRINT-DETAIL-REPT.
 00261      IF LINE-CNTR > +55
 00262         PERFORM 200-HEADING-RTN.
 00263         SKIP1
 00264      WRITE PRINT-REC FROM DETAIL-LINE-1 AFTER ADVANCING 2.
 00265      ADD +2 TO LINE-CNTR.
 00266      SKIP2
 00267  100-TOTAL-RTN.
 00268      IF LINE-CNTR > +55
 00269         PERFORM 200-HEADING-RTN.
 00270         SKIP1
 00271      MOVE 'TOTAL MASTER RECORDS READ      ' TO TOTAL-MSG.
 00272      MOVE NEW-MAST-RECS                     TO TOTAL-AMT.
 00273      WRITE PRINT-REC FROM TOTAL-LINE  AFTER ADVANCING 2.
 00274      DISPLAY TOTAL-LINE.
 00275         SKIP1
 00276      MOVE 'TOTAL MASTER RECORDS WRITTEN   ' TO TOTAL-MSG.
 00277      MOVE RECS-WRIT                         TO TOTAL-AMT.
 00278      WRITE PRINT-REC FROM TOTAL-LINE  AFTER ADVANCING 1.
 00279      DISPLAY TOTAL-LINE.
 00280         SKIP1
 00281      MOVE 'TOTAL DETAIL SEGMENTS SELECTED ' TO TOTAL-MSG.
 00282      MOVE RECS-ACCUM                        TO TOTAL-AMT.
 00283      WRITE PRINT-REC FROM TOTAL-LINE  AFTER ADVANCING 1.
 00284      DISPLAY TOTAL-LINE.
 00285      SKIP2
 00286  200-HEADING-RTN.
 00287      ADD +1 TO PAGE-CNTR.
 00288      MOVE PAGE-CNTR TO HD1-PAGE.
 00289      WRITE PRINT-REC FROM HEADING1 AFTER ADVANCING PAGE.
 00290      WRITE PRINT-REC FROM HEADING2 AFTER ADVANCING 2.
 00291      WRITE PRINT-REC FROM HEADING3 AFTER ADVANCING 4.
 00292      WRITE PRINT-REC FROM HEADING4 AFTER ADVANCING 1.
 00293      MOVE +8 TO LINE-CNTR.
 00294      SKIP2
