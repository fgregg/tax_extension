 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID.    ASREA744.
 00003  AUTHOR.        GOLD - MOLIS.
 00004  DATE-WRITTEN.  AUG 4, 1992.
 00005  SKIP1
 00006 *REMARKS. THE PURPOSE OF THIS PROGRAM IS TO CREATE THE FROZEN
 00007 *         VALUATION PERCENTAGES ON THE DIVISION TAXCODE FILE.
 00008  SKIP2
 00009  ENVIRONMENT DIVISION.
 00010  CONFIGURATION SECTION.
 00011  SOURCE-COMPUTER. IBM-370.
 00012  OBJECT-COMPUTER. IBM-370.
 00013  SKIP1
 00014  INPUT-OUTPUT SECTION.
 00015  FILE-CONTROL.
 00016      SELECT FRZVAL-TXCD-FILE  ASSIGN TO UT-S-FRZTXCD.
 00017      SELECT FRZVAL-DIV-FILE   ASSIGN TO UT-S-FRZDIVN.
 00018      SELECT FRZDIV-PRCNT-FILE ASSIGN TO UT-S-FRZDIVP.
 00019      SELECT PRINT-FILE        ASSIGN TO UT-S-PRINTFLE.
 00020  SKIP1
 00021  DATA DIVISION.
 00022  FILE SECTION.
 00023  SKIP1
 00024  ++INCLUDE CLFRZTAXF1
 00025  SKIP1
 00026  ++INCLUDE CLFRZDIVF1
 00027  SKIP1
 00028  ++INCLUDE CLFZDVPCF1
 00029  SKIP1
 00030  ++INCLUDE PRINTFILE
 00031  EJECT
 00032  WORKING-STORAGE SECTION.
 00033      SKIP1
 00034  77  DIV-RECS-READ      PIC S9(7)     VALUE +0  PACKED-DECIMAL.
 00035  77  TAX-RECS-READ      PIC S9(7)     VALUE +0  PACKED-DECIMAL.
 00036  77  RECS-WRITTEN       PIC S9(7)     VALUE +0  PACKED-DECIMAL.
 00037  77  RECS-UNMATCH       PIC S9(7)     VALUE +0  PACKED-DECIMAL.
 00038  77  WS-CURR-288        PIC S99V9(7)  VALUE +0  PACKED-DECIMAL.
 00039  77  WS-FRST-TIME       PIC S99V9(7)  VALUE +0  PACKED-DECIMAL.
 00040  77  LINE-CNTR          PIC S9(3)     VALUE +60 PACKED-DECIMAL.
 00041  77  PAGE-CNTR          PIC S9(5)     VALUE +0  PACKED-DECIMAL.
 00042  77  SUB                PIC S9(4)     VALUE +0  BINARY.
 00043  77  SUBT               PIC S9(4)     VALUE +0  BINARY.
 00044  77  BLANK-LINE         PIC X         VALUE SPACE.
 00045      SKIP1
 00046  77  DIV-EOF-SW         PIC X         VALUE 'N'.
 00047      88  DIV-EOF                      VALUE 'Y'.
 00048  77  TAX-EOF-SW         PIC X         VALUE 'N'.
 00049      88  TAX-EOF                      VALUE 'Y'.
 00050  77  ERR-SW             PIC X         VALUE 'N'.
 00051      88  ERR                          VALUE 'Y'.
 00052      SKIP1
 00053  01  WORK-AREA.
 00054      SKIP1
 00055      05  CURR-TAX-KEY.
 00056          10  CTAX-DIV    PIC 9(14).
 00057          10  CTAX-TAXCD  PIC 9(5).
 00058      05  PREV-TAX-KEY    PIC X(19)    VALUE LOW-VALUES.
 00059      SKIP1
 00060      05  CURR-DIV-KEY.
 00061          10  CDIV-DIV    PIC 9(14).
 00062          10  CDIV-TAXCD  PIC 9(5).
 00063      05  PREV-DIV-KEY    PIC X(19)    VALUE LOW-VALUES.
 00064      SKIP1
 00065      05  DSP-DATE.
 00066          10  DSP-MO                 PIC 99.
 00067          10  FILLER                 PIC X         VALUE '/'.
 00068          10  DSP-DA                 PIC 99.
 00069          10  FILLER                 PIC X         VALUE '/'.
 00070          10  DSP-YR                 PIC 99.
 00071      05  ACPT-DATE                  PIC 9(6).
 00072      05  ACPT-DATE-X REDEFINES ACPT-DATE.
 00073          10  ACPT-YR                PIC 99.
 00074          10  ACPT-MO                PIC 99.
 00075          10  ACPT-DA                PIC 99.
 00076      SKIP1
 00077      05  HEADING1.
 00078          10  FILLER          PIC X(45)   VALUE '  ASREA744'.
 00079          10  FILLER          PIC X(70)   VALUE
 00080              'OFFICE OF THE COOK COUNTY ASSESSOR'.
 00081          10  FILLER          PIC X(5)    VALUE 'PAGE'.
 00082          10  HD1-PAGE        PIC ZZZ,ZZ9.
 00083      SKIP1
 00084      05  HEADING2.
 00085          10  FILLER          PIC X(7)    VALUE '  DATE'.
 00086          10  HD2-DATE        PIC X(8).
 00087          10  FILLER          PIC X(25)   VALUE SPACES.
 00088          10  FILLER          PIC X(42)   VALUE
 00089              'UPDATE FROZEN VALUE FILE WITH PERCENTAGES'.
 00090      SKIP1
 00091      05  HEADING3.
 00092          10  FILLER          PIC X(44)   VALUE SPACES.
 00093          10  FILLER          PIC X(42)   VALUE
 00094              'NO MATCHING DIVISION ERROR REPORT'.
 00095      SKIP1
 00096      05  HEADING4.
 00097          10  FILLER          PIC X(20)   VALUE '   DIVISION'.
 00098          10  FILLER          PIC X(39)   VALUE
 00099              'TAX     CURRENT 288         FIRST TIME'.
 00100      SKIP1
 00101      05  HEADING5.
 00102          10  FILLER          PIC X(30)   VALUE
 00103              '    NUMBER         CODE'.
 00104          10  FILLER          PIC X(30)   VALUE
 00105              'VALUE                VALUE'.
 00106      SKIP1
 00107      05  DETAIL-LINE-1                   VALUE SPACES.
 00108          10  FILLER          PIC XX.
 00109          10  D-DIVNO-1       PIC 9(14).
 00110          10  FILLER          PIC XX.
 00111          10  D-TAXCD-1       PIC 99999.
 00112          10  FILLER          PIC X(3).
 00113          10  D-CUVAL-1       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
 00114          10  FILLER          PIC X(5).
 00115          10  D-FIRST-1       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
 00116          SKIP1
 00117      05  TOTAL-LINE.
 00118          10  FILLER          PIC XXX.
 00119          10  TOTAL-MSG       PIC X(33).
 00120          10  TOTAL-AMT       PIC Z,ZZZ,ZZ9.
 00121         EJECT
 00122  PROCEDURE DIVISION.
 00123      SKIP1
 00124  010-HOUSEKEEPING.
 00125      OPEN INPUT  FRZVAL-TXCD-FILE
 00126                  FRZVAL-DIV-FILE
 00127           OUTPUT PRINT-FILE
 00128                  FRZDIV-PRCNT-FILE.
 00129      SKIP1
 00130      ACCEPT ACPT-DATE FROM DATE.
 00131      MOVE ACPT-MO TO DSP-MO.
 00132      MOVE ACPT-DA TO DSP-DA.
 00133      MOVE ACPT-YR TO DSP-YR.
 00134      MOVE DSP-DATE  TO HD2-DATE.
 00135      SKIP1
 00136      WRITE PRINT-REC FROM BLANK-LINE AFTER ADVANCING PAGE.
 00137      SKIP1
 00138      PERFORM 030-READ-TAX-FILE.
 00139      SKIP1
 00140      PERFORM 040-READ-DIV-FILE.
 00141      SKIP1
 00142      PERFORM 020-MAINLINE UNTIL ERR OR TAX-EOF.
 00143      SKIP1
 00144      IF NOT ERR
 00145         PERFORM 100-TOTAL-RTN.
 00146      SKIP1
 00147      CLOSE FRZVAL-TXCD-FILE
 00148            FRZVAL-DIV-FILE
 00149            PRINT-FILE
 00150            FRZDIV-PRCNT-FILE.
 00151      SKIP1
 00152      STOP RUN.
 00153      SKIP2
 00154  020-MAINLINE.
 00155      IF NOT (ERR OR TAX-EOF)
 00156         IF CTAX-DIV = CDIV-DIV
 00157            PERFORM 050-CALC-MOVE-RTN
 00158            PERFORM 060-MOVE-WRITE-OUTFILE
 00159            PERFORM 030-READ-TAX-FILE
 00160         ELSE
 00161            IF CTAX-DIV > CDIV-DIV
 00162               PERFORM 040-READ-DIV-FILE
 00163            ELSE
 00164               PERFORM 070-MOVE-WRITE-REPORT
 00165               PERFORM 030-READ-TAX-FILE.
 00166      SKIP2
 00167  030-READ-TAX-FILE.
 00168      READ FRZVAL-TXCD-FILE
 00169          AT END
 00170             MOVE ALL '9' TO CURR-TAX-KEY
 00171             MOVE 'Y'     TO TAX-EOF-SW.
 00172              SKIP1
 00173      IF NOT TAX-EOF
 00174         ADD +1         TO TAX-RECS-READ
 00175         MOVE FVT-DIVNO TO CTAX-DIV
 00176         MOVE FVT-TXCD  TO CTAX-TAXCD
 00177         IF CURR-TAX-KEY NOT GREATER PREV-TAX-KEY
 00178            DISPLAY 'FOZEN VALUE TAX CODE FILE OUT OF SEQUENCE'
 00179            DISPLAY 'CURRENT  KEY IS ' CURR-TAX-KEY
 00180            DISPLAY 'PREVIOUS KEY IS ' PREV-TAX-KEY
 00181            MOVE 16  TO RETURN-CODE
 00182            MOVE 'Y' TO ERR-SW
 00183         ELSE
 00184            MOVE CURR-TAX-KEY TO PREV-TAX-KEY.
 00185      SKIP2
 00186  040-READ-DIV-FILE.
 00187      READ FRZVAL-DIV-FILE
 00188          AT END
 00189             MOVE ALL '9' TO CURR-DIV-KEY
 00190             MOVE 'Y'     TO DIV-EOF-SW.
 00191              SKIP1
 00192      IF NOT DIV-EOF
 00193         ADD +1         TO DIV-RECS-READ
 00194         MOVE FVD-DIVNO TO CDIV-DIV
 00195         MOVE FVD-TXCD  TO CDIV-TAXCD
 00196         IF CURR-DIV-KEY NOT GREATER PREV-DIV-KEY
 00197            DISPLAY 'FOZEN VALUE DIV CODE FILE OUT OF SEQUENCE'
 00198            DISPLAY 'CURRENT  KEY IS ' CURR-DIV-KEY
 00199            DISPLAY 'PREVIOUS KEY IS ' PREV-DIV-KEY
 00200            MOVE 16  TO RETURN-CODE
 00201            MOVE 'Y' TO ERR-SW
 00202         ELSE
 00203            MOVE CURR-DIV-KEY TO PREV-DIV-KEY.
 00204      SKIP2
 00205  050-CALC-MOVE-RTN.
 00206      MOVE SPACES TO FRZDIV-PRCNT-REC.
 00207      SKIP1
 00208      IF FVD-CURR-288-VAL > 0
 00209         COMPUTE WS-CURR-288 = FVT-CURR-288-VAL / FVD-CURR-288-VAL
 00210         MOVE WS-CURR-288 TO FDP-CURR-288-PCNT
 00211      ELSE
 00212         MOVE ZEROES TO FDP-CURR-288-PCNT.
 00213      SKIP1
 00214      IF FVD-FRST-VAL > 0
 00215         COMPUTE WS-FRST-TIME = FVT-FRST-VAL / FVD-FRST-VAL
 00216         MOVE WS-FRST-TIME TO FDP-FRST-PCNT
 00217      ELSE
 00218         MOVE ZEROES TO FDP-FRST-PCNT.
 00219      SKIP2
 00220  060-MOVE-WRITE-OUTFILE.
 00221      SKIP1
 00222      MOVE FVT-DIVNO      TO FDP-DIVNO.
 00223      MOVE FVT-TXCD       TO FDP-TXCD.
 00224      MOVE FVD-TXCD-COUNT TO FDP-TXCD-COUNT.
 00225      MOVE ZEROES         TO FDP-CURR-288-VAL FDP-EXPR-288-VAL
 00226                             FDP-FRST-VAL.
 00227         SKIP1
 00228      WRITE FRZDIV-PRCNT-REC.
 00229      ADD +1 TO RECS-WRITTEN.
 00230      SKIP2
 00231  070-MOVE-WRITE-REPORT.
 00232      MOVE FVT-DIVNO        TO D-DIVNO-1.
 00233      MOVE FVT-TXCD         TO D-TAXCD-1.
 00234      MOVE FVT-CURR-288-VAL TO D-CUVAL-1.
 00235      MOVE FVT-FRST-VAL     TO D-FIRST-1.
 00236      SKIP1
 00237      PERFORM 090-PRINT-DETAIL-REPT.
 00238      SKIP2
 00239  090-PRINT-DETAIL-REPT.
 00240      IF LINE-CNTR > +55
 00241         PERFORM 200-HEADING-RTN.
 00242         SKIP1
 00243      WRITE PRINT-REC FROM DETAIL-LINE-1 AFTER ADVANCING 2.
 00244      ADD +2 TO LINE-CNTR.
 00245      ADD +1 TO RECS-UNMATCH.
 00246      SKIP2
 00247  100-TOTAL-RTN.
 00248      IF LINE-CNTR > +55
 00249         PERFORM 200-HEADING-RTN.
 00250         SKIP1
 00251      MOVE 'TOTAL DIVISION RECORDS READ      ' TO TOTAL-MSG.
 00252      MOVE DIV-RECS-READ                       TO TOTAL-AMT.
 00253      WRITE PRINT-REC FROM TOTAL-LINE  AFTER ADVANCING 2.
 00254      DISPLAY TOTAL-LINE.
 00255         SKIP1
 00256      MOVE 'TOTAL TAX CODE RECORDS READ      ' TO TOTAL-MSG.
 00257      MOVE TAX-RECS-READ                       TO TOTAL-AMT.
 00258      WRITE PRINT-REC FROM TOTAL-LINE  AFTER ADVANCING 1.
 00259      DISPLAY TOTAL-LINE.
 00260         SKIP1
 00261      MOVE 'TOTAL TAX CODE RECORDS WRITTEN   ' TO TOTAL-MSG.
 00262      MOVE RECS-WRITTEN                        TO TOTAL-AMT.
 00263      WRITE PRINT-REC FROM TOTAL-LINE  AFTER ADVANCING 1.
 00264      DISPLAY TOTAL-LINE.
 00265         SKIP1
 00266      MOVE 'TOTAL TAX CODE RECORDS UNMATCHED ' TO TOTAL-MSG.
 00267      MOVE RECS-UNMATCH                        TO TOTAL-AMT.
 00268      WRITE PRINT-REC FROM TOTAL-LINE AFTER ADVANCING 1.
 00269      DISPLAY TOTAL-LINE.
 00270      SKIP2
 00271  200-HEADING-RTN.
 00272      ADD +1 TO PAGE-CNTR.
 00273      MOVE PAGE-CNTR TO HD1-PAGE.
 00274      WRITE PRINT-REC FROM HEADING1 AFTER ADVANCING PAGE.
 00275      WRITE PRINT-REC FROM HEADING2 AFTER ADVANCING 2.
 00276      WRITE PRINT-REC FROM HEADING3 AFTER ADVANCING 1.
 00277      WRITE PRINT-REC FROM HEADING4 AFTER ADVANCING 4.
 00278      WRITE PRINT-REC FROM HEADING5 AFTER ADVANCING 1.
 00279      MOVE +8 TO LINE-CNTR.
 00280      SKIP2
