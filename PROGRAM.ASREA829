 *          DATA SET ASREA829E  AT LEVEL 073 AS OF 08/26/11
 MACRO
 FILE INFILE
 RECA    1 130 A
 TOWNA   11 3 P
 STAT1A  14 1 N
 INSTAT  21 2 A
 NPHEA   48 5 P

 FIRSTSW W 1 A VALUE 'Y'
 HSTAT   W 2 A HEADING ('H/O','MAINT')
 HTOWN   W 2 N HEADING ('TOWN')
 WTXCD      W  5  N
 WTAX WTXCD    2  N
 FILL WTXCD +2 3  N
 CNT     W 6 P 0 HEADING ('TOTAL H/O','COUNT')
 TOTCNT  W 6 P 0
 WAMT   W 12 N HEADING ('TOTAL H/O','AMOUNT') MASK ('ZZZ,ZZZ,ZZZ,999')

 PARM-DATA  W 6 A
   PARM-LENGTH  PARM-DATA     2 B 0
   PARM-YR      PARM-DATA  +2 4 N

 JOB INPUT INFILE START CHK-PARM  FINISH FINISH-PROC
 *
 IF STAT1A NE 1
    GO TO JOB
 END-IF

 WTXCD = TOWNA

 IF FIRSTSW = 'Y'
   HSTAT = INSTAT
   HTOWN = WTAX
   FIRSTSW = 'N'
 END-IF
 IF HTOWN = WTAX
    IF HSTAT = INSTAT
       WAMT = NPHEA + WAMT
       CNT = CNT + 1
       TOTCNT = TOTCNT + 1
    ELSE
 *     DISPLAY 'TOWN ' HTOWN ' ' 'STATUS  '  HSTAT ' ' CNT ' ' WAMT
 *     DISPLAY '                     '
       PRINT OUTLIST
       CNT = +1
       TOTCNT = TOTCNT + 1
       HSTAT = INSTAT
       WAMT = NPHEA
    END-IF
 ELSE
 * DISPLAY 'TOWN ' HTOWN ' ' 'STATUS  '  HSTAT ' ' CNT ' ' WAMT
   PRINT OUTLIST
   DISPLAY '                     '
   CNT = +1
   TOTCNT = TOTCNT + 1
   HSTAT = INSTAT
   HTOWN = WTAX
   WAMT = NPHEA
 END-IF
 GO TO JOB.
 FINISH-PROC. PROC.
 *DISPLAY 'TOWN ' HTOWN ' ' STATUS  '  HSTAT ' ' CNT ' ' WAMT
 *DISPLAY '                     '
 PRINT OUTLIST
 DISPLAY 'TOTAL        '  TOTCNT
 END-PROC.
 *
 CHK-PARM. PROC.
 %GETPARM PARM-DATA 2
 CALL EZTPX01 USING(PARM-REGISTER PARM-DATA)
 IF PARM-LENGTH EQ 4
   CONTINUE
 ELSE
   DISPLAY 'INVALID PARM LENGTH  ' PARM-LENGTH
 END-IF.
 IF PARM-YR NUMERIC AND PARM-YR > 0
   CONTINUE
 ELSE
   DISPLAY 'INVALID YEAR         ' PARM-YR
 END-IF.
 *DISPLAY 'HOMEOWNER MASTER STATUS COUNTS FOR TAXYEAR  ' PARM-YR
 *DISPLAY '                                            '
 END-PROC.
 REPORT OUTLIST SPACE 1
 TITLE 1 'HOMEOWNER MASTER MAINT COUNTS FOR TAXYEAR  ' PARM-YR
 TITLE 2 '        ONLY RECORDS WITH STAT1 = 1        '
 LINE HTOWN HSTAT CNT WAMT
