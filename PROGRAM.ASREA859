 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID. ASREA859.
 00003 *AUTHOR. SQUILLACE - MOLIS
 00004 *DATE-WRITTEN.  APRIL,1995
 00005 *REMARKS. THIS PROGRAM WILL CREATE SENIOR FREEZE FILE
 00006 *COPIED AND MODIFIED FROM ASREA853
 00007      SKIP2
 00008  ENVIRONMENT DIVISION.
 00009  CONFIGURATION SECTION.
 00010  SOURCE-COMPUTER. IBM-370.
 00011  OBJECT-COMPUTER. IBM-370.
 00012  INPUT-OUTPUT SECTION.
 00013  FILE-CONTROL.
 00014      SELECT  MASTER-IN
 00015               ASSIGN  TO UT-S-MASTER.
 00016      SELECT  HOMEOWNER-MAST
 00017               ASSIGN TO UT-S-HOMEIN.
 00018      SELECT  HOMEOWNER-EXEMPT
 00019               ASSIGN TO UT-S-HOMEOUT.
 00020      SELECT  PRNT-FILE
 00021               ASSIGN  TO UT-S-PRNTOUT.
 00022      SKIP2
 00023  DATA DIVISION.
 00024  FILE SECTION.
 00025  ++INCLUDE ASREASFD01
 00026      SKIP2
 00027  ++INCLUDE HOMOWNFD01
 00028      SKIP2
 00029  ++INCLUDE ASHMOWFD01
 00030      SKIP2
 00031  FD  PRNT-FILE
 00032      BLOCK CONTAINS 0 RECORDS
 00033      RECORD CONTAINS 133 CHARACTERS
 00034      LABEL RECORDS ARE STANDARD
 00035      RECORDING MODE IS F
 00036      DATA RECORD IS PRNT-REC.
 00037  01  PRNT-REC               PIC X(133).
 00038      EJECT
 00039  WORKING-STORAGE SECTION.
 00040      SKIP1
 00041  77  WS-PROG           PIC X(8)  VALUE 'ASREA003'.
 00042  77  PAGE-CNTR         PIC S9(5) VALUE +0    PACKED-DECIMAL.
 00043  77  LINE-CNTR         PIC S9(3) VALUE +54   PACKED-DECIMAL.
 00044      88  PRINT-HEADER            VALUE +54 THRU +59.
 00045  77  MSTR-READ         PIC S9(7) VALUE +0    PACKED-DECIMAL.
 00046  77  HOME-READ         PIC S9(7) VALUE +0    PACKED-DECIMAL.
 00047  77  HOME-WRITTEN      PIC S9(7) VALUE +0    PACKED-DECIMAL.
 00048  77  SUB               PIC S9(4) VALUE +0    BINARY.
 00049  77  KP-SUB            PIC S9(4) VALUE +0    BINARY.
 00050  77  SC-SUB            PIC S9(4) VALUE +0    BINARY.
 00051  77  WS-TOTL-PRORATE   PIC 99V9(5) VALUE 0   PACKED-DECIMAL.
 00052  77  WS-TOTL-PRORATER REDEFINES WS-TOTL-PRORATE
 00053                        PIC V9(7)             PACKED-DECIMAL.
 00054  77  WS-PRORATE        PIC S9(4)V9(5) VALUE +0 PACKED-DECIMAL.
 00055  77  PRORATE-CNTR      PIC S9(3) VALUE +0    PACKED-DECIMAL.
 00056      88  PRORATE-CNTR-ZERO       VALUE +0.
 00057      88  PRORATE-CNTR-ONE        VALUE +1.
 00058  77  KEYPCL-CNT        PIC S9(3) VALUE +0    PACKED-DECIMAL.
 00059  77  SC-CNT            PIC S9(3) VALUE +0    PACKED-DECIMAL.
 00060  77  SET-HOME-SW       PIC X     VALUE 'N'.
 00061      88  READ-NEXT-HOMEOWNER     VALUE 'N'.
 00062  77  SET-MSTR-SW       PIC X     VALUE 'N'.
 00063      88  READ-NEXT-MSTR          VALUE 'N'.
 00064      88  VALID-REC               VALUE 'Y'.
 00065  77  CHK-CLS-299       PIC X     VALUE 'N'.
 00066      88 CLS-299-PRESENT          VALUE 'Y'.
 00067  77  CHK-HOME-CLS      PIC X     VALUE 'N'.
 00068      88  HOME-SEG-PRESENT        VALUE 'Y'.
 00069  77  CHK-VALU-CLS      PIC X     VALUE 'N'.
 00070      88  VALU-SEG-PRESENT        VALUE 'Y'.
 00071  77  WS-TYPE2-3-SW     PIC X     VALUE 'N'.
 00072      88  WS-TYPE2-3-ON           VALUE 'Y'.
 00073  77  WS-TYPE5-SW       PIC X     VALUE 'N'.
 00074      88  WS-TYPE5-ON             VALUE 'Y'.
 00075      SKIP1
 00076  77  WS-IND-SW-ALL     PIC X     VALUE 'N'.
 00077      88  WS-IND-ON-ALL           VALUE 'Y'.
 00078      88  WS-IND-OFF-ALL          VALUE 'N'.
 00079      SKIP1
 00080  77  WS-IND-SW-297     PIC X     VALUE 'N'.
 00081      88  WS-IND-ON-297           VALUE 'Y'.
 00082      88  WS-IND-OFF-297          VALUE 'Y'.
 00083      SKIP1
 00084  77  WS-PROP-SW        PIC X     VALUE 'N'.
 00085      88  WS-PROP-ON              VALUE 'Y'.
 00086      88  WS-PROP-OFF             VALUE 'N'.
 00087      SKIP1
 00088  77  WS-KP-ZER0-SW     PIC X     VALUE 'N'.
 00089      88  WS-KP-ZERO-ON           VALUE 'Y'.
 00090      88  WS-KP-ZERO-OFF          VALUE 'N'.
 00091      SKIP1
 00092  77  WS-MULTI-KP-SW    PIC X     VALUE 'N'.
 00093      88 WS-MULTI-KP-ON           VALUE 'Y'.
 00094      88 WS-MULTI-KP-OFF          VALUE 'N'.
 00095      SKIP1
 00096  77  WS-KP-NGZERO-SW     PIC X   VALUE 'N'.
 00097      88  WS-KP-NGZERO-ON         VALUE 'Y'.
 00098      88  WS-KP-NGZERO-OFF        VALUE 'N'.
 00099      SKIP1
 00100  77  WS-SC-ZERO-SW     PIC X     VALUE 'N'.
 00101      88  WS-SC-ZERO-ON           VALUE 'Y'.
 00102      88  WS-SC-ZERO-OFF          VALUE 'N'.
 00103      SKIP1
 00104  77  WS-MULTI-SC-SW    PIC X     VALUE 'N'.
 00105      88 WS-MULTI-SC-ON           VALUE 'Y'.
 00106      88 WS-MULTI-SC-OFF          VALUE 'N'.
 00107      SKIP1
 00108  77  WS-SC-NGZERO-SW     PIC X   VALUE 'N'.
 00109      88  WS-SC-NGZERO-ON         VALUE 'Y'.
 00110      88  WS-SC-NGZERO-OFF        VALUE 'N'.
 00111      EJECT
 00112  01  WORK-AREA.
 00113      05  WS-DATE.
 00114          10  YY                      PIC 9(2)    VALUE ZEROS.
 00115          10  MM                      PIC 9(2)    VALUE ZEROS.
 00116          10  DD                      PIC 9(2)    VALUE ZEROS.
 00117      05  WS-DATE-MMDDYY.
 00118          10  MM                      PIC 9(2)    VALUE ZEROS.
 00119          10  FILLER                  PIC X       VALUE '/'.
 00120          10  DD                      PIC 9(2)    VALUE ZEROS.
 00121          10  FILLER                  PIC X       VALUE '/'.
 00122          10  YY                      PIC 9(2)    VALUE ZEROS.
 00123      SKIP2
 00124      05  ADD-MESG      PIC X(12) VALUE 'PARCEL ADDED'.
 00125      05  BYPASS-MESG   PIC X(25) VALUE
 00126                                      'PARCEL IS NON-RESIDENTIAL'.
 00127      05  DELETE-MESG   PIC X(23) VALUE 'PARCEL NO LONGER EXISTS'.
 00128      05  KEYPCL-MESG   PIC X(24) VALUE
 00129                        'PARCEL HAS KEY PARCEL NO'.
 00130      SKIP1
 00131      05  MULTI-PCL-MESG PIC X(31) VALUE
 00132                        'PARCEL HAS MULTIPLE KEY PARCELS'.
 00133      SKIP1
 00134      05  SPLIT-MESG    PIC X(21) VALUE
 00135                        'PARCEL HAS SPLIT CODE'.
 00136      SKIP1
 00137      05  MULTI-SPLIT-MESG PIC X(31) VALUE
 00138                        'PARCEL HAS MULTIPLE SPLIT CODES'.
 00139      SKIP1
 00140      05  HOME-KEY                VALUE LOW-VALUES.
 00141          88  HOME-EOF            VALUE HIGH-VALUES.
 00142          10  HK-VOL         PIC 9(3).
 00143          10  HK-PROP        PIC 9(15).
 00144          10  HK-TXTYP       PIC 9.
 00145      SKIP1
 00146      05  MSTR-KEY                VALUE LOW-VALUES.
 00147          88  MSTR-EOF            VALUE HIGH-VALUES.
 00148          10  MK-VOL         PIC 9(3).
 00149          10  MK-PROP        PIC 9(15).
 00150          10  MK-TXTYP       PIC 9.
 00151      SKIP1
 00152      05  WS-OCCFAC          PIC 9999V9    VALUE 0.
 00153      SKIP1
 00154      05  SV-KEYPCL          PIC 9(15)     VALUE 0.
 00155      SKIP1
 00156      05  WS-KEYPCL          PIC 9(15)     VALUE 0.
 00157      SKIP1
 00158      05  SV-SC              PIC X         VALUE SPACE.
 00159      SKIP1
 00160      05  WS-PROP            PIC 9(14).
 00161      05  WS-PROP-R REDEFINES WS-PROP.
 00162          10  WS-PROP1       PIC 9(2).
 00163          10  WS-PROP2       PIC 9(2).
 00164          10  WS-PROP3       PIC 9(3).
 00165          10  WS-PROP4       PIC 9(3).
 00166          10  WS-PROP5       PIC 9(4).
 00167      SKIP1
 00168      05  WS-CLS             PIC 9(3).
 00169          88 WS-100-997-CLS       VALUE 100 THRU 997.
 00170          88 WS-297-CLS           VALUE 297.
 00171          88 WS-999-CLS           VALUE 999.
 00172          88 WS-299-CLS           VALUE 299.
 00173          88 WS-299-959-CLS       VALUE 299 399 489 499
 00174                                        589 599
 00175                                        679 689 789 799 889 899
 00176                                        959.
 00177          88 WS-202-295-CLS       VALUE 202 THRU 213 218 219
 00178                                        220 221 234 236
 00179                                        278 294 295.
 00180      EJECT
 00181  01  PRINT-LINES.
 00182      SKIP1
 00183      05  HDR-LINE1.
 00184          10  FILLER    PIC X(3)  VALUE SPACES.
 00185          10  H1-DATE   PIC X(8).
 00186          10  FILLER    PIC X(44) VALUE SPACES.
 00187          10  FILLER    PIC X(19) VALUE 'OFFICE OF THE COOK'.
 00188          10  FILLER    PIC X(46) VALUE 'COUNTY ASSESSOR'.
 00189          10  FILLER    PIC X(5)  VALUE 'PAGE '.
 00190          10  H1-PAGE   PIC ZZ,ZZ9.
 00191      SKIP1
 00192      05  HDR-LINE2.
 00193          10  FILLER    PIC X(53) VALUE '   ASREA859'.
 00194          10  FILLER    PIC X(24) VALUE 'SENIOR FREEZE FILE CREAT'.
 00195          10  FILLER    PIC X(16) VALUE 'ION ERROR REPORT'.
 00196      SKIP1
 00197      05  HDR-LINE3.
 00198          10  FILLER    PIC X(09) VALUE SPACES.
 00199          10  FILLER    PIC X(52) VALUE
 00200          'PERMANENT INDEX     TAX    TAX   HOME    MSTR'.
 00201          10  FILLER    PIC X(23) VALUE
 00202          'KEYPARCEL         SPLIT'.
 00203      SKIP1
 00204      05  HDR-LINE4.
 00205          10  FILLER    PIC X(02) VALUE SPACES.
 00206          10  FILLER    PIC X(47) VALUE
 00207          'VOL         NUMBER        TYPE   CODE  CLASS'.
 00208          10  FILLER    PIC X(35) VALUE
 00209          'CLASS        NUMBER            CODE'.
 00210      SKIP1
 00211      05  DETAIL-LINE             VALUE SPACES.
 00212          10  FILLER    PIC X(2).
 00213          10  D-VOL     PIC 999.
 00214          10  FILLER    PIC XXX.
 00215          10  D-PROP    PIC 99,99,999,999,9999.
 00216          10  FILLER    PIC X(4).
 00217          10  D-TXTYP   PIC 9.
 00218          10  FILLER    PIC X(3).
 00219          10  D-TXCD    PIC 9(5).
 00220          10  FILLER    PIC X(3).
 00221          10  D-H-CLS   PIC 9,99.
 00222          10  FILLER    PIC X(4).
 00223          10  D-M-CLS   PIC 9,99.
 00224          10  FILLER    PIC X(4).
 00225          10  D-K-PCL   PIC 99,99,999,999,9999.
 00226          10  FILLER    PIC X(5).
 00227          10  D-SPL-CD  PIC 9.
 00228          10  FILLER    PIC X(6).
 00229          10  D-MESG    PIC X(50).
 00230      SKIP1
 00231      05  FINAL-LINE.
 00232          10  FILLER    PIC X(8)  VALUE '  TOTAL '.
 00233          10  FIN-MSG   PIC X(26).
 00234          10  FIN-TOT   PIC Z,ZZZ,ZZ9.
 00235      EJECT
 00236  LINKAGE SECTION.
 00237  01  LINK-INFO.
 00238      05  LI-LENGTH      PIC S9(4)  BINARY.
 00239          88  LI-VALID-LENGTH       VALUE +1.
 00240      05  LI-NEWYEAR     PIC X.
 00241          88  LI-VALID-NEWYR        VALUE 'N' 'Y'.
 00242          88  LI-NEWYEAR-YES        VALUE 'Y'.
 00243      SKIP2
 00244  PROCEDURE DIVISION USING LINK-INFO.
 00245  010-CHECK-LINKAGE.
 00246      IF  NOT LI-VALID-LENGTH
 00247           DISPLAY  'LINKAGE LENGTH INVALID'
 00248           MOVE  16 TO RETURN-CODE
 00249           STOP RUN
 00250      ELSE
 00251           IF  NOT LI-VALID-NEWYR
 00252                DISPLAY  'NEW YR PARM INVALID = ' LI-NEWYEAR
 00253                MOVE  16 TO RETURN-CODE
 00254                STOP RUN.
 00255      SKIP2
 00256  020-BEGIN.
 00257      OPEN  INPUT  MASTER-IN
 00258            INPUT  HOMEOWNER-MAST
 00259      OPEN  OUTPUT HOMEOWNER-EXEMPT
 00260            OUTPUT PRNT-FILE
 00261      ACCEPT WS-DATE FROM DATE
 00262      MOVE CORR WS-DATE TO WS-DATE-MMDDYY
 00263      MOVE WS-DATE-MMDDYY TO H1-DATE
 00264      SKIP1
 00265      PERFORM  700-READ-HOMEOWNER
 00266      PERFORM  750-READ-MASTER
 00267                UNTIL  MSTR-EOF
 00268                     OR
 00269                       VALID-REC
 00270      SKIP1
 00271      PERFORM  100-MAINLINE
 00272                UNTIL  (MSTR-EOF AND  HOME-EOF)
 00273      SKIP1
 00274      PERFORM 850-PRINT-TOTALS
 00275      SKIP1
 00276      CLOSE   MASTER-IN
 00277              HOMEOWNER-MAST
 00278              HOMEOWNER-EXEMPT
 00279              PRNT-FILE
 00280      STOP RUN.
 00281      EJECT
 00282  100-MAINLINE.
 00283      IF  MSTR-KEY GREATER HOME-KEY
 00284           MOVE  'N' TO SET-HOME-SW
 00285           PERFORM  200-MSTR-HIGH-ROUTINE
 00286      ELSE
 00287           MOVE  'N' TO SET-MSTR-SW
 00288           IF  NOT M-EXEMPT
 00289             AND
 00290               NOT M-RAILROAD
 00291                MOVE  'N' TO CHK-CLS-299 CHK-VALU-CLS CHK-HOME-CLS
 00292                        WS-TYPE2-3-SW WS-TYPE5-SW
 00293                        WS-IND-SW-ALL WS-IND-SW-297
 00294                        WS-PROP-SW WS-KP-ZER0-SW WS-MULTI-KP-SW
 00295                        WS-KP-NGZERO-SW WS-SC-NGZERO-SW
 00296                        WS-SC-ZERO-SW WS-MULTI-SC-SW
 00297                MOVE  ZERO TO PRORATE-CNTR WS-PRORATE
 00298                      WS-OCCFAC KEYPCL-CNT SC-CNT SV-KEYPCL
 00299                      WS-KEYPCL
 00300                PERFORM 300-SEARCH-SEGMENTS
 00301                 VARYING SUB FROM 1 BY 1
 00302                  UNTIL  SUB GREATER THAN M-DTL-QST-CTR
 00303                IF HOME-SEG-PRESENT
 00304                   IF MSTR-KEY EQUAL HOME-KEY
 00305                      MOVE 'N' TO SET-HOME-SW
 00306                      PERFORM 400-MATCH-ROUTINE
 00307                   ELSE
 00308                      PERFORM 500-MSTR-LOW-ROUTINE
 00309                ELSE
 00310                   PERFORM 600-BYPASS-MSTR-ROUTINE
 00311           ELSE
 00312              PERFORM 600-BYPASS-MSTR-ROUTINE.
 00313      SKIP1
 00314      IF  READ-NEXT-HOMEOWNER
 00315           PERFORM  700-READ-HOMEOWNER.
 00316      SKIP1
 00317      PERFORM  750-READ-MASTER
 00318       UNTIL  MSTR-EOF
 00319            OR
 00320              VALID-REC.
 00321      SKIP3
 00322  200-MSTR-HIGH-ROUTINE.
 00323      MOVE  HO-VOL TO D-VOL
 00324      MOVE  HO-PROP TO D-PROP
 00325      INSPECT D-PROP REPLACING ALL ',' BY '-'
 00326      MOVE  HO-CLASS TO D-H-CLS
 00327      INSPECT D-H-CLS REPLACING ALL ',' BY '-'
 00328      MOVE  HO-TXCD TO D-TXCD
 00329      MOVE  HO-TXTYP TO D-TXTYP
 00330      MOVE  DELETE-MESG TO D-MESG
 00331      PERFORM  800-PRINT-DETAIL.
 00332      SKIP3
 00333  300-SEARCH-SEGMENTS.
 00334      IF  D2-TYPE2-5 (SUB)
 00335           MOVE  D1-CLS (SUB) TO  WS-CLS
 00336           IF  WS-202-295-CLS
 00337               MOVE 'Y' TO WS-IND-SW-ALL
 00338           END-IF
 00339      END-IF.
 00340      SKIP1
 00341      IF WS-IND-OFF-ALL
 00342         IF D2-TYPE2-5 (SUB)
 00343              MOVE D1-CLS (SUB) TO WS-CLS
 00344              IF WS-297-CLS
 00345                    AND
 00346                      D2-VAL (SUB) GREATER THAN 1000
 00347                  MOVE 'Y' TO WS-IND-SW-297
 00348                  IF D4-TYPE4 (SUB)
 00349                       PERFORM 350-RECOMPUTE-TYPE4
 00350                  ELSE
 00351                       IF D5-TYPE5 (SUB)
 00352                            PERFORM 370-RECOMPUTE-TYPE5
 00353                       END-IF
 00354                  END-IF
 00355              ELSE
 00356                  CONTINUE
 00357              END-IF
 00358         END-IF
 00359      END-IF.
 00360      SKIP1
 00361 *    MOVE M-PROP TO WS-PROP
 00362 *    IF WS-PROP5 NOT LESS 1000 AND
 00363 *       WS-PROP5 NOT GREATER THAN 2999
 00364 *       MOVE 'Y'      TO WS-PROP-SW
 00365 *    ELSE
 00366 *       MOVE 'N'      TO WS-PROP-SW
 00367 *    END-IF.
 00368      SKIP1
 00369      IF D2-TYPE2-5 (SUB)
 00370          IF WS-100-997-CLS
 00371             MOVE 'Y' TO CHK-HOME-CLS
 00372             IF WS-PROP-OFF
 00373                IF D2-PCASSD (SUB) GREATER THAN ZERO
 00374                     ADD +1 TO PRORATE-CNTR
 00375                     ADD D2-PCASSD (SUB) TO WS-PRORATE
 00376                ELSE
 00377                     NEXT SENTENCE
 00378                END-IF
 00379             ELSE
 00380                NEXT SENTENCE
 00381             END-IF
 00382          ELSE
 00383               IF WS-999-CLS
 00384                 AND
 00385                   D2-VAL (SUB) GREATER THAN 1000
 00386                    MOVE 'Y' TO CHK-VALU-CLS
 00387                    IF D2-PCASSD (SUB) GREATER THAN ZERO
 00388                         ADD +1 TO PRORATE-CNTR
 00389                         ADD D2-PCASSD (SUB) TO WS-PRORATE
 00390                    END-IF
 00391               END-IF
 00392          END-IF
 00393      END-IF.
 00394      SKIP1
 00395      IF  D2-TYPE2-5 (SUB)
 00396        AND
 00397          WS-299-CLS
 00398           MOVE 'Y' TO CHK-CLS-299.
 00399      SKIP1
 00400      IF (D2-TYPE2 (SUB) OR D3-TYPE3 (SUB))
 00401           MOVE 'Y' TO WS-TYPE2-3-SW
 00402      ELSE
 00403        IF D5-TYPE5 (SUB)
 00404           MOVE 'Y' TO WS-TYPE5-SW.
 00405      SKIP1
 00406      IF D4-TYPE4 (SUB)
 00407         IF D4-OCCFAC (SUB) GREATER THAN WS-OCCFAC
 00408            MOVE D4-OCCFAC (SUB) TO WS-OCCFAC.
 00409      SKIP1
 00410      IF D5-TYPE5 (SUB)
 00411         IF D5-OCCFAC (SUB) GREATER THAN WS-OCCFAC
 00412            MOVE D5-OCCFAC (SUB) TO WS-OCCFAC.
 00413      SKIP1
 00414      SKIP1
 00415      IF D2-TYPE2-5 (SUB)
 00416         IF ((KEYPCL-CNT GREATER THAN ZERO)    AND
 00417              (D2-KEYPCL (SUB) GREATER THAN ZERO) AND
 00418              (D2-KEYPCL (SUB) NOT EQUAL SV-KEYPCL))
 00419              MOVE D2-KEYPCL (SUB) TO SV-KEYPCL
 00420              MOVE 'Y' TO WS-MULTI-KP-SW
 00421         ELSE
 00422           IF D2-KEYPCL (SUB) GREATER THAN ZERO
 00423              MOVE 'Y' TO WS-KP-ZER0-SW
 00424              ADD +1 TO KEYPCL-CNT
 00425              MOVE D2-KEYPCL (SUB) TO SV-KEYPCL
 00426           END-IF
 00427         END-IF
 00428      END-IF.
 00429      SKIP1
 00430      SKIP1
 00431      IF D2-TYPE2-5 (SUB)
 00432         IF ((D2-SC (SUB) = '1' OR '2') AND
 00433             (SC-CNT GREATER THAN ZERO) AND
 00434             (D2-SC (SUB) NOT EQUAL SV-SC))
 00435              MOVE D2-SC (SUB) TO SV-SC
 00436              MOVE 'Y' TO WS-MULTI-SC-SW
 00437         ELSE
 00438             IF (D2-SC (SUB) = '1' OR '2')
 00439                 MOVE 'Y' TO WS-SC-ZERO-SW
 00440                 ADD +1 TO SC-CNT
 00441                 MOVE D2-SC (SUB) TO SV-SC
 00442             END-IF
 00443         END-IF
 00444      END-IF.
 00445      SKIP1
 00446      SKIP1
 00447      IF  D2-TYPE2-5 (SUB)
 00448        AND
 00449          D2-QUES-CLASS (SUB)
 00450           ADD +1 TO SUB.
 00451      SKIP3
 00452  350-RECOMPUTE-TYPE4.
 00453      MOVE  D4-TOTVAL (SUB) TO D4-REPCST (SUB)
 00454      CALL  WS-PROG    USING M-DTL-QST (SUB).
 00455      SKIP3
 00456  370-RECOMPUTE-TYPE5.
 00457      IF  D5-OCCFAC (SUB) NOT EQUAL ZERO
 00458           COMPUTE  D5-REPCST (SUB) =
 00459            (D5-REPCST (SUB) * 100) / D5-OCCFAC (SUB)
 00460           CALL  WS-PROG    USING M-DTL-QST (SUB).
 00461      SKIP3
 00462  400-MATCH-ROUTINE.
 00463      MOVE  HO-REC  TO  HE-REC
 00464      IF  LI-NEWYEAR-YES
 00465           MOVE  ZERO TO HE-PRORATE HE-COOPQTY HE-OCCFAC
 00466                         HE-KEYPCL HE-IND HE-SPLIT-CD
 00467      ELSE
 00468         IF HE-COOPQTY = 00001
 00469            MOVE '1' TO HE-RECCD
 00470         ELSE
 00471            MOVE '2' TO HE-RECCD.
 00472      SKIP1
 00473      MOVE  M-CLS TO  HE-CLASS
 00474      MOVE  M-TXCD TO  HE-TXCD
 00475      SKIP1
 00476      IF WS-IND-ON-ALL
 00477         MOVE '1'    TO HE-IND
 00478      ELSE
 00479         MOVE '0'    TO HE-IND.
 00480      SKIP1
 00481      IF WS-IND-ON-297
 00482         MOVE '1'    TO HE-IND
 00483      ELSE
 00484         MOVE '0'    TO HE-IND.
 00485      SKIP1
 00486      IF WS-TYPE2-3-ON
 00487         MOVE 0100.0 TO HE-OCCFAC
 00488      ELSE
 00489         IF WS-TYPE5-ON
 00490            MOVE WS-OCCFAC TO HE-OCCFAC
 00491         ELSE
 00492            MOVE 0100.0 TO HE-OCCFAC.
 00493      SKIP1
 00494      SKIP1
 00495      IF KEYPCL-CNT GREATER THAN +0
 00496         MOVE SV-KEYPCL TO HE-KEYPCL
 00497      ELSE
 00498         MOVE ZEROS       TO HE-KEYPCL.
 00499      SKIP1
 00500      IF SC-CNT GREATER THAN +0
 00501         MOVE SV-SC       TO HE-SPLIT-CD
 00502      ELSE
 00503         MOVE ZEROS       TO HE-SPLIT-CD.
 00504      SKIP1
 00505      PERFORM 550-COMPUTE-PRORATE
 00506      PERFORM 900-WRITE-HOMEOWNER.
 00507      SKIP3
 00508  500-MSTR-LOW-ROUTINE.
 00509      MOVE  SPACES TO HE-REC
 00510      MOVE  M-VOL TO  HE-VOL
 00511      MOVE  M-PROP TO  HE-PROP
 00512      MOVE  M-CLS TO  HE-CLASS
 00513      MOVE  M-TXCD TO  HE-TXCD
 00514      MOVE  M-TXTYP TO  HE-TXTYP
 00515      MOVE  ZERO TO HE-STAT1 HE-STAT2 HE-YRAPPL
 00516            HE-H-V HE-ASSDVAL HE-EQVAL HE-ZIP HE-EQFCTR
 00517      MOVE  '00001' TO HE-COOPQTY
 00518 *    MOVE  1.4153  TO HE-EQFCTR
 00519      MOVE '1' TO HE-RECCD.
 00520      SKIP1
 00521      IF WS-IND-ON-ALL
 00522         MOVE '1'    TO HE-IND
 00523      ELSE
 00524         MOVE '0'    TO HE-IND.
 00525      SKIP1
 00526      IF WS-IND-ON-297
 00527         MOVE '1'    TO HE-IND
 00528      ELSE
 00529         MOVE '0'    TO HE-IND.
 00530      SKIP1
 00531      SKIP1
 00532      IF WS-TYPE2-3-ON
 00533         MOVE 0100.0 TO HE-OCCFAC
 00534      ELSE
 00535         IF WS-TYPE5-ON
 00536            MOVE WS-OCCFAC TO HE-OCCFAC
 00537         ELSE
 00538            MOVE 0100.0 TO HE-OCCFAC.
 00539            SKIP1
 00540            SKIP1
 00541      IF KEYPCL-CNT GREATER THAN ZERO
 00542         MOVE SV-KEYPCL    TO D-K-PCL HE-KEYPCL
 00543         INSPECT D-K-PCL REPLACING ALL ',' BY '-'
 00544      ELSE
 00545         MOVE ZEROS           TO HE-KEYPCL.
 00546            SKIP1
 00547      IF SC-CNT GREATER THAN ZERO
 00548         MOVE SV-SC       TO HE-SPLIT-CD D-SPL-CD
 00549      ELSE
 00550         MOVE ZEROS       TO HE-SPLIT-CD.
 00551            SKIP1
 00552      PERFORM  550-COMPUTE-PRORATE
 00553      MOVE  ADD-MESG TO D-MESG
 00554      PERFORM  570-MOVE-MSTR-TO-PRNT
 00555      PERFORM  800-PRINT-DETAIL
 00556            SKIP1
 00557      IF WS-KP-ZERO-ON
 00558         MOVE SPACES      TO DETAIL-LINE
 00559         MOVE KEYPCL-MESG TO D-MESG
 00560         PERFORM 800-PRINT-DETAIL.
 00561            SKIP1
 00562      IF WS-MULTI-KP-ON
 00563         MOVE SPACES      TO DETAIL-LINE
 00564         MOVE SV-KEYPCL   TO D-K-PCL
 00565         INSPECT D-K-PCL REPLACING ALL ',' BY '-'
 00566         MOVE MULTI-PCL-MESG TO D-MESG
 00567         PERFORM 800-PRINT-DETAIL.
 00568            SKIP1
 00569      IF WS-SC-ZERO-ON
 00570         MOVE SPACES      TO DETAIL-LINE
 00571         MOVE SPLIT-MESG TO D-MESG
 00572         PERFORM 800-PRINT-DETAIL.
 00573            SKIP1
 00574      IF WS-MULTI-SC-ON
 00575         MOVE SPACES      TO DETAIL-LINE
 00576         MOVE MULTI-SPLIT-MESG TO D-MESG
 00577         PERFORM 800-PRINT-DETAIL.
 00578            SKIP1
 00579            SKIP1
 00580      PERFORM  900-WRITE-HOMEOWNER.
 00581      SKIP3
 00582  550-COMPUTE-PRORATE.
 00583      IF CLS-299-PRESENT
 00584        OR
 00585          PRORATE-CNTR-ZERO
 00586           MOVE 1.000000 TO HE-PRORATE
 00587      ELSE
 00588           IF  PRORATE-CNTR-ONE
 00589                MOVE  WS-PRORATE TO WS-TOTL-PRORATE
 00590                MOVE  WS-TOTL-PRORATER TO HE-PRORATE
 00591           ELSE
 00592                COMPUTE  WS-TOTL-PRORATE ROUNDED =
 00593                             WS-PRORATE / PRORATE-CNTR
 00594                MOVE  WS-TOTL-PRORATER TO HE-PRORATE.
 00595      SKIP3
 00596  570-MOVE-MSTR-TO-PRNT.
 00597      MOVE  M-VOL TO  D-VOL
 00598      MOVE  M-PROP TO D-PROP
 00599      INSPECT D-PROP REPLACING ALL ',' BY '-'
 00600      MOVE  M-TXCD TO D-TXCD
 00601      MOVE  M-TXTYP TO D-TXTYP
 00602      MOVE  M-CLS TO D-H-CLS
 00603      INSPECT D-H-CLS REPLACING ALL ',' BY '-'.
 00604      SKIP3
 00605  600-BYPASS-MSTR-ROUTINE.
 00606      IF  MSTR-KEY EQUAL HOME-KEY
 00607           MOVE  'N' TO SET-HOME-SW
 00608           MOVE  HO-VOL TO D-VOL
 00609           MOVE  HO-PROP TO D-PROP
 00610           INSPECT D-PROP REPLACING ALL ',' BY '-'
 00611           MOVE  HO-TXCD TO D-TXCD
 00612           MOVE  HO-CLASS TO D-H-CLS
 00613           INSPECT D-H-CLS REPLACING ALL ',' BY '-'
 00614           MOVE  M-TXTYP TO D-TXTYP
 00615           MOVE  M-CLS TO D-M-CLS
 00616           INSPECT  D-M-CLS REPLACING ALL ',' BY '-'
 00617           MOVE  BYPASS-MESG TO D-MESG
 00618           PERFORM  800-PRINT-DETAIL.
 00619      SKIP3
 00620  700-READ-HOMEOWNER.
 00621      READ  HOMEOWNER-MAST
 00622             AT END  MOVE HIGH-VALUE TO HOME-KEY.
 00623      IF  NOT HOME-EOF
 00624           ADD +1 TO HOME-READ
 00625           MOVE HO-VOL  TO  HK-VOL
 00626           MOVE HO-PROP TO  HK-PROP
 00627           MOVE HO-TXTYP TO  HK-TXTYP.
 00628      SKIP1
 00629      MOVE  'Y' TO SET-HOME-SW.
 00630      SKIP3
 00631  750-READ-MASTER.
 00632      READ  MASTER-IN
 00633             AT END MOVE HIGH-VALUES TO MSTR-KEY.
 00634      IF  NOT MSTR-EOF
 00635           ADD +1 TO MSTR-READ
 00636           IF  M-RE-VOL
 00637                MOVE  M-VOL   TO MK-VOL
 00638                MOVE  M-PROP  TO MK-PROP
 00639                MOVE  M-TXTYP TO MK-TXTYP
 00640                MOVE  'Y' TO SET-MSTR-SW.
 00641      SKIP3
 00642  800-PRINT-DETAIL.
 00643      IF  PRINT-HEADER
 00644           PERFORM  870-HDR-ROUTINE.
 00645      SKIP1
 00646      INSPECT  D-PROP REPLACING ALL ','    BY '-'
 00647      INSPECT  D-H-CLS REPLACING ALL ','    BY '-'
 00648      WRITE  PRNT-REC FROM DETAIL-LINE AFTER ADVANCING   2
 00649      ADD  +2 TO LINE-CNTR
 00650      MOVE  SPACES TO DETAIL-LINE.
 00651      SKIP3
 00652  850-PRINT-TOTALS.
 00653      IF  PRINT-HEADER
 00654           PERFORM 870-HDR-ROUTINE.
 00655      SKIP1
 00656      MOVE  'MASTER RECORDS READ' TO FIN-MSG
 00657      MOVE  MSTR-READ TO  FIN-TOT
 00658      WRITE  PRNT-REC FROM FINAL-LINE AFTER ADVANCING   3
 00659      MOVE  'HOMEOWNERS RECORDS READ' TO FIN-MSG
 00660      MOVE  HOME-READ TO FIN-TOT
 00661      WRITE  PRNT-REC FROM FINAL-LINE AFTER ADVANCING   1
 00662      MOVE  'HOMEOWNERS RECORDS WRITTEN' TO FIN-MSG
 00663      MOVE  HOME-WRITTEN TO  FIN-TOT
 00664      WRITE  PRNT-REC FROM FINAL-LINE AFTER ADVANCING   1.
 00665      SKIP3
 00666  870-HDR-ROUTINE.
 00667      ADD  +1 TO PAGE-CNTR
 00668      MOVE  PAGE-CNTR TO H1-PAGE
 00669      MOVE  +6 TO LINE-CNTR
 00670      WRITE  PRNT-REC FROM HDR-LINE1 AFTER ADVANCING   PAGE
 00671      WRITE  PRNT-REC FROM HDR-LINE2 AFTER ADVANCING   2
 00672      WRITE  PRNT-REC FROM HDR-LINE3 AFTER ADVANCING   2
 00673      WRITE  PRNT-REC FROM HDR-LINE4 AFTER ADVANCING   1.
 00674      SKIP3
 00675  900-WRITE-HOMEOWNER.
 00676      WRITE  HE-REC
 00677      ADD  +1 TO HOME-WRITTEN.
