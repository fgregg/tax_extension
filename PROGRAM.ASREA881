 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID. ASREA881.
 00003 *AUTHOR. HELEN TAYLOR - STANLEY MOLIS.
 00004 *DATE-WRITTEN.   JULY  1992.
 00005 ******************************************************************
 00006 *REMARKS.
 00007 *       THE PURPOSE OF THIS PROGRAM IS TO UPDATE THE
 00008 *       HOMEOWNER FILE WITH TAX TYPE.
 00009 *
 00010 ******************************************************************
 00011
 00012  ENVIRONMENT DIVISION.
 00013  CONFIGURATION SECTION.
 00014  SOURCE-COMPUTER. IBM-370.
 00015  OBJECT-COMPUTER. IBM-370.
 00016  INPUT-OUTPUT SECTION.
 00017  FILE-CONTROL.
 00018      SELECT    MASTER-IN    ASSIGN TO UT-S-MASTER.
 00019      SELECT    HOWNER-IN    ASSIGN TO UT-S-HOWNERI.
 00020      SELECT    HOWNER-OUT   ASSIGN TO UT-S-HOWNERO.
 00021
 00022
 00023  DATA DIVISION.
 00024  FILE SECTION.
 00025
 00026  ++INCLUDE ASREASFD01
 00027
 00028  FD  HOWNER-IN
 00029      BLOCK CONTAINS 0 RECORDS
 00030      RECORD CONTAINS 130 CHARACTERS
 00031      LABEL RECORDS ARE STANDARD
 00032      DATA RECORD IS HOWNER-IN-REC.
 00033
 00034  01  HOWNER-IN-REC.
 00035  ++INCLUDE HOMEXERD01
 00036
 00037  FD  HOWNER-OUT
 00038      BLOCK CONTAINS 0 RECORDS
 00039      RECORD CONTAINS 130 CHARACTERS
 00040      LABEL RECORDS ARE STANDARD
 00041      DATA RECORD IS HOWNER-OUT-REC.
 00042
 00043  01  HOWNER-OUT-REC.
 00044
 00045      05  HO-REC        PIC X(129).
 00046      05  HO-TXTYP      PIC 9.
 00047
 00048  WORKING-STORAGE SECTION.
 00049
 00050  77  MASTER-READ-CTR      PIC S9(7)     VALUE +0 PACKED-DECIMAL.
 00051  77  HOWNER-READ-CTR      PIC S9(7)     VALUE +0 PACKED-DECIMAL.
 00052  77  HOWNER-WRITE-CTR     PIC S9(7)     VALUE +0 PACKED-DECIMAL.
 00053  77  HOWNER-UPD-5-CTR     PIC S9(7)     VALUE +0 PACKED-DECIMAL.
 00054  77  HOWNER-UPD-0-CTR     PIC S9(7)     VALUE +0 PACKED-DECIMAL.
 00055  77  END-OF-MASTER-SW              PIC X VALUE 'N'.
 00056      88  NOT-MASTER-EOF                  VALUE 'N'.
 00057      88  MASTER-EOF                      VALUE 'Y'.
 00058  77  END-OF-HOWNER-SW              PIC X VALUE 'N'.
 00059      88  NOT-HOWNER-EOF                  VALUE 'N'.
 00060      88  HOWNER-EOF                      VALUE 'Y'.
 00061  77  VALID-MASTER-SW               PIC X VALUE 'N'.
 00062      88  VALID-MASTER-REC                VALUE 'Y'.
 00063
 00064  01  WORK-AREA.
 00065
 00066      05  CURR-MASTER-KEY-X      PIC X(13)  VALUE LOW-VALUES.
 00067      05  CURR-MASTER-KEY-R  REDEFINES  CURR-MASTER-KEY-X.
 00068          10 M-KEY-TXCD          PIC S99.
 00069          10 M-KEY-VOLPROP       PIC X(10).
 00070          10 M-KEY-TXTYP         PIC X.
 00071      05  CURR-MASTER-KEY REDEFINES CURR-MASTER-KEY-X PIC X(12).
 00072
 00073      05  PREV-MASTER-KEY        PIC X(13).
 00074
 00075      05  CURR-HOWNER-KEY-X      PIC X(12)  VALUE LOW-VALUES.
 00076      05  CURR-HOWNER-KEY REDEFINES  CURR-HOWNER-KEY-X.
 00077          10 H-KEY-TXCD          PIC S99.
 00078          10 H-KEY-VOL           PIC S9(3)    PACKED-DECIMAL.
 00079          10 H-KEY-PROP          PIC S9(14)   PACKED-DECIMAL.
 00080
 00081      05  PREV-HOWNER-KEY        PIC X(12).
 00082
 00083      05  WS-TXCD                PIC 9(05).
 00084      05  WS-TXCD-X REDEFINES  WS-TXCD.
 00085          10  WS-TXCD-R          PIC 99.
 00086          10  FILLER             PIC X(3).
 00087
 00088  PROCEDURE DIVISION.
 00089
 00090  100-HOUSEKEEPING.
 00091
 00092      OPEN INPUT MASTER-IN
 00093                 HOWNER-IN
 00094          OUTPUT HOWNER-OUT.
 00095
 00096      PERFORM  450-READ-HOWNER-FILE.
 00097
 00098      PERFORM 200-MAINLINE
 00099          UNTIL  HOWNER-EOF
 00100           OR RETURN-CODE EQUAL 16.
 00101
 00102      CLOSE      MASTER-IN
 00103                 HOWNER-IN
 00104                 HOWNER-OUT.
 00105
 00106      DISPLAY 'END   OF EXECUTION OF ASREA881'
 00107      DISPLAY
 00108      'TOTAL HOMEOWNER RECORDS READ             ' HOWNER-READ-CTR
 00109      DISPLAY ' '
 00110      DISPLAY
 00111      'TOTAL HOMEOWNER RECORDS WRITTEN          ' HOWNER-WRITE-CTR
 00112      DISPLAY ' '
 00113      DISPLAY
 00114      'TOTAL MASTER RECORDS READ                ' MASTER-READ-CTR
 00115      DISPLAY ' '
 00116      DISPLAY
 00117      'TOTAL HOMEOWNER RECS UPD WITH TAX TYPE 5 ' HOWNER-UPD-5-CTR
 00118      DISPLAY ' '
 00119      DISPLAY
 00120      'TOTAL HOMEOWNER RECS UPD WITH TAX TYPE 0 ' HOWNER-UPD-0-CTR
 00121      STOP RUN.
 00122
 00123  200-MAINLINE.
 00124
 00125      PERFORM  400-READ-MASTER-FILE
 00126        UNTIL  VALID-MASTER-REC
 00127           OR  RETURN-CODE  EQUAL  16
 00128           OR  MASTER-EOF.
 00129
 00130      IF VALID-MASTER-REC
 00131         IF CURR-HOWNER-KEY  EQUAL  CURR-MASTER-KEY
 00132            PERFORM  300-PROCESS-HOWNER-REC
 00133            MOVE 'N'  TO  VALID-MASTER-SW
 00134         ELSE
 00135            PERFORM  300-PROCESS-HOWNER-REC
 00136              UNTIL  CURR-HOWNER-KEY GREATER CURR-MASTER-KEY
 00137                 OR  HOWNER-EOF
 00138                 OR  RETURN-CODE  EQUAL  16
 00139            MOVE 'N'  TO  VALID-MASTER-SW
 00140      ELSE
 00141         IF MASTER-EOF
 00142            PERFORM 320-READ-WRITE-HOWNER-REC
 00143              UNTIL  HOWNER-EOF
 00144                 OR RETURN-CODE  EQUAL  16.
 00145
 00146
 00147
 00148  300-PROCESS-HOWNER-REC.
 00149
 00150      IF NOT HOWNER-EOF
 00151         MOVE  HOWNER-IN-REC  TO  HOWNER-OUT-REC
 00152         IF  CURR-MASTER-KEY  EQUAL  CURR-HOWNER-KEY
 00153             MOVE     5           TO  HO-TXTYP
 00154             ADD  +1 TO HOWNER-UPD-5-CTR
 00155             PERFORM  500-WRITE-HOWNER-REC
 00156         ELSE
 00157             MOVE     0           TO  HO-TXTYP
 00158             ADD  +1 TO HOWNER-UPD-0-CTR
 00159             PERFORM  500-WRITE-HOWNER-REC.
 00160
 00161      PERFORM  450-READ-HOWNER-FILE.
 00162
 00163  320-READ-WRITE-HOWNER-REC.
 00164
 00165      IF NOT  HOWNER-EOF
 00166          MOVE  HOWNER-IN-REC  TO  HOWNER-OUT-REC
 00167          MOVE     0           TO  HO-TXTYP
 00168          ADD  +1 TO HOWNER-UPD-0-CTR
 00169          PERFORM  500-WRITE-HOWNER-REC.
 00170
 00171      PERFORM  450-READ-HOWNER-FILE.
 00172
 00173  400-READ-MASTER-FILE.
 00174
 00175      IF NOT-MASTER-EOF
 00176         READ MASTER-IN
 00177           AT END  MOVE 'Y' TO END-OF-MASTER-SW.
 00178
 00179      IF NOT-MASTER-EOF
 00180          ADD  +1 TO MASTER-READ-CTR
 00181          MOVE  M-TXCD            TO  WS-TXCD
 00182          MOVE  WS-TXCD-R         TO  M-KEY-TXCD
 00183          MOVE  M-VOLPROP         TO  M-KEY-VOLPROP
 00184          MOVE  M-TXTYP           TO  M-KEY-TXTYP
 00185          IF PREV-MASTER-KEY  LESS   CURR-MASTER-KEY-X
 00186              MOVE CURR-MASTER-KEY-X TO  PREV-MASTER-KEY
 00187          ELSE
 00188              MOVE 16 TO RETURN-CODE
 00189              DISPLAY 'SEQUENCE ERROR IN MASTER FILE'
 00190              DISPLAY 'PREVIOUS KEY = ' PREV-MASTER-KEY
 00191              DISPLAY 'CURRENT KEY  = ' CURR-MASTER-KEY-X.
 00192
 00193      IF NOT-MASTER-EOF
 00194         IF M-TXTYP EQUAL  5
 00195            MOVE 'Y'  TO VALID-MASTER-SW.
 00196
 00197  450-READ-HOWNER-FILE.
 00198
 00199      READ HOWNER-IN
 00200          AT END  MOVE 'Y' TO END-OF-HOWNER-SW.
 00201
 00202      IF NOT-HOWNER-EOF
 00203          ADD  +1 TO HOWNER-READ-CTR
 00204          MOVE HM-TXCD            TO  WS-TXCD
 00205          MOVE WS-TXCD-R          TO  H-KEY-TXCD
 00206          MOVE HM-VOL             TO  H-KEY-VOL
 00207          MOVE HM-PROP            TO  H-KEY-PROP
 00208          IF PREV-HOWNER-KEY  LESS   CURR-HOWNER-KEY
 00209              MOVE CURR-HOWNER-KEY   TO  PREV-HOWNER-KEY
 00210          ELSE
 00211              MOVE 16 TO RETURN-CODE
 00212              DISPLAY 'SEQUENCE ERROR IN HOMEOWNER FILE'
 00213              DISPLAY 'PREVIOUS KEY = ' PREV-HOWNER-KEY
 00214              DISPLAY 'CURRENT KEY  = ' CURR-HOWNER-KEY.
 00215
 00216
 00217  500-WRITE-HOWNER-REC.
 00218
 00219      WRITE  HOWNER-OUT-REC.
 00220
 00221      ADD   +1           TO   HOWNER-WRITE-CTR.
