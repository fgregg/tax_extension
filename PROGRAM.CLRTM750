 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID. CLRTM750.
 00003 *AUTHOR. RUGIS - MOLIS.
 00004 *DATE-WRITTEN.  AUGUST 14, 1992.
 00005      SKIP1
 00006 *REMARKS. THE PURPOSE OF THIS PROGRAM IS TO CREATE THE NEW
 00007 *         PROPERTY REPORT BY TOWN WITHIN AGENCY FOR THE
 00008 *         CLERK'S OFFICE.
 00009 * 09/09/2009 TJM CHANGED FIELD SIZES IN PRINT LINES.
 00010      SKIP2
 00011  ENVIRONMENT DIVISION.
 00012  INPUT-OUTPUT SECTION.
 00013  FILE-CONTROL.
 00014      SELECT FRZAGCY-FILE ASSIGN TO UT-S-FRZAGIN.
 00015      SELECT AGCY-EFTS-FLE ASSIGN TO AGCYEFTS
 00016          ORGANIZATION IS INDEXED
 00017          ACCESS  MODE IS RANDOM
 00018          RECORD   KEY IS AGCY-EFTS-KEY
 00019          FILE  STATUS IS EFTS-STATUS  EFTS-STATUS2.
 00020      SELECT PRINT-FILE   ASSIGN TO UT-S-PRINTFLE.
 00021      SELECT APPEND-FILE  ASSIGN TO UT-S-APPENDED.
 00022  SKIP1
 00023  DATA DIVISION.
 00024  FILE SECTION.
 00025      SKIP1
 00026  ++INCLUDE CLFRZAGYF1
 00027      SKIP2
 00028  ++INCLUDE TRAGEFTSF4
 00029      SKIP2
 00030  ++INCLUDE PRINTFILE
 00031      SKIP3
 00032  FD  APPEND-FILE
 00033      RECORDING MODE IS F
 00034      RECORD CONTAINS 150 CHARACTERS
 00035      BLOCK CONTAINS 0 RECORDS.
 00036  01  APPEND-REC         PIC X(150).
 00037      SKIP1
 00038  WORKING-STORAGE SECTION.
 00039      SKIP1
 00040  77  RECS-READ-CNTR     PIC S9(7)     VALUE +0  PACKED-DECIMAL.
 00041      88  FIRST-TIME                   VALUE +1.
 00042  77  RECS-PRINTED-CNTR  PIC S9(7)     VALUE +0  PACKED-DECIMAL.
 00043  77  EQLZ-FRZ-ACCUM     PIC S9(15)    VALUE +0  PACKED-DECIMAL.
 00044  77  ANN-EQLZ-ACCUM     PIC S9(15)    VALUE +0  PACKED-DECIMAL.
 00045  77  DIS-EQLZ-ACCUM     PIC S9(15)    VALUE +0  PACKED-DECIMAL.
 00046  77  TIF-EQLZ-ACCUM     PIC S9(15)    VALUE +0  PACKED-DECIMAL.
 00047  77  EXPINC-EQLZ-ACCUM  PIC S9(15)    VALUE +0  PACKED-DECIMAL.
 00048  77  TOT-EQLZ-FRZ-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
 00049  77  TOT-ANN-EQLZ-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
 00050  77  TOT-DIS-EQLZ-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
 00051  77  TOT-TIF-EQLZ-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
 00052  77  TOT-EXPINC-EQLZ-ACCUM PIC S9(15) VALUE +0  PACKED-DECIMAL.
 00053  77  FIN-EQLZ-FRZ-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
 00054  77  FIN-ANN-EQLZ-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
 00055  77  FIN-DIS-EQLZ-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
 00056  77  FIN-TIF-EQLZ-ACCUM PIC S9(15)    VALUE +0  PACKED-DECIMAL.
 00057  77  FIN-EXPINC-EQLZ-ACCUM PIC S9(15) VALUE +0  PACKED-DECIMAL.
 00058  77  PAGE-CNTR          PIC S9(5)     VALUE +0  PACKED-DECIMAL.
 00059  77  SUB                PIC S9(4)     VALUE +0  BINARY.
 00060  77  SAVE-AGCY          PIC 9(9)      VALUE ZEROES.
 00061  77  SAVE-TOWN          PIC 99        VALUE ZEROES.
 00062      88  SUBURBAN       VALUE 10 THRU 39.
 00063  77  BLANK-LINE         PIC X         VALUE SPACE.
 00064      SKIP1
 00065  77  FRZ-EOF-SW         PIC X         VALUE 'N'.
 00066      88  FRZ-EOF                      VALUE 'Y'.
 00067  77  ERR-SW             PIC X         VALUE 'N'.
 00068      88  ERR                          VALUE 'Y'.
 00069  77  EFTS-STATUS        PIC 99.
 00070      88  GOOD-EFTS                    VALUE 00.
 00071      88  EFTS-NOTFND                  VALUE 23.
 00072      SKIP2
 00073  01  WORK-AREA.
 00074      SKIP1
 00075      05  CURR-FRZAGCY.
 00076          10  CF-AGCY         PIC 9(9).
 00077          10  CF-TOWN         PIC 99.
 00078      SKIP1
 00079      05  PREV-FRZAGCY        PIC X(11)     VALUE LOW-VALUES.
 00080      SKIP2
 00081      05  UNPK-TXCD           PIC 9(5).
 00082      05  UNPK-TXCD-R REDEFINES UNPK-TXCD.
 00083          10  UP-TOWN         PIC 99.
 00084          10  FILLER          PIC 9(3).
 00085      SKIP1
 00086      05  EFTS-STATUS2        BINARY.
 00087          10  ES-RETURN       PIC 99        VALUE 0.
 00088          10  ES-FUNCTION     PIC 9         VALUE 0.
 00089          10  ES-FEEDBACK     PIC 999       VALUE 0.
 00090      SKIP2
 00091      05  ACPT-DATE           PIC 9(8).
 00092      SKIP2
 00093      05  WS-APPEND-REC.
 00094          10  APPEND-AGENCY   PIC 9(9).
 00095          10  APPEND-YEAR     PIC 9(4).
 00096          10  APPEND-DETAIL   PIC X(133).
 00097          10  APPEND-FILLER   PIC X(4).
 00098      SKIP2
 00099      05  HEADING1.
 00100          10  FILLER       PIC XX      VALUE SPACE.
 00101          10  FILLER       PIC X(38)   VALUE 'CLRTM750'.
 00102          10  FILLER       PIC X(80)   VALUE
 00103              'OFFICE OF THE COOK COUNTY CLERK'.
 00104          10  FILLER       PIC X(5)    VALUE 'PAGE'.
 00105          10  HD1-PAGE     PIC Z,ZZ9.
 00106      SKIP2
 00107      05  HEADING2.
 00108          10  FILLER       PIC XX      VALUE SPACE.
 00109          10  HD2-DATE     PIC X(8).
 00110          10  FILLER       PIC X(22)   VALUE SPACES.
 00111          10  FILLER       PIC X(8)    VALUE 'PTELL - '.
 00112          10  HD2-YEAR     PIC X(4).
 00113          10  FILLER       PIC X(54)   VALUE
 00114              ' NEW PROPERTY, ANNEXED PROPERTY, DISCONNECTED PROPER
 00115 -            'TY'.
 00116      SKIP2
 00117      05  HEADING2A.
 00118          10  FILLER       PIC X(41)   VALUE SPACES.
 00119          10  FILLER       PIC X(61)   VALUE
 00120          'RECOVERED TAX INCREMENT REPORT BY TOWN WITHIN AGENCY'.
 00121          10  FILLER       PIC X(21)   VALUE
 00122              'EQUALIZATION FACTOR: '.
 00123          10  HD2-FACT     PIC 9,9999.
 00124      SKIP2
 00125      05  HEADING3.
 00126          10  FILLER       PIC XXX     VALUE SPACES.
 00127          10  FILLER       PIC X(15)   VALUE 'AGENCY NUMBER:'.
 00128          10  HD3-AGCY-NO  PIC 99,9999,999.
 00129          10  FILLER       PIC X(4)    VALUE SPACES.
 00130          10  FILLER       PIC X(20)   VALUE 'AGENCY DESCRIPTION:'.
 00131          10  HD3-AGCY-DES PIC X(48).
 00132      SKIP2
 00133      05  HEADING4.
 00134          10  FILLER       PIC X(12)   VALUE SPACES.
 00135          10  FILLER       PIC X(13)   VALUE 'TOWN'.
 00136          10  FILLER       PIC X(25)   VALUE 'TOWN'.
 00137          10  FILLER       PIC X(16)   VALUE 'NEW PROPERTY'.
 00138          10  FILLER       PIC X(18)   VALUE 'ANNEXED PROPERTY'.
 00139          10  FILLER       PIC X(15)   VALUE 'DISC PROPERTY'.
 00140          10  FILLER       PIC X(18)   VALUE 'RECOVERED TAX'.
 00141          10  FILLER       PIC X(7)    VALUE 'EXPIRED'.
 00142      SKIP2
 00143      05  HEADING5.
 00144          10  FILLER       PIC X(11)   VALUE SPACES.
 00145          10  FILLER       PIC X(14)   VALUE 'NUMBER'.
 00146          10  FILLER       PIC X(25)   VALUE 'NAME'.
 00147          10  FILLER       PIC X(19)   VALUE 'EQLZD VALUE'.
 00148          10  FILLER       PIC X(16)   VALUE 'EQLZD VALUE'.
 00149          10  FILLER       PIC X(13)   VALUE 'EQLZD VALUE'.
 00150          10  FILLER       PIC X(18)   VALUE 'INCREMENT VALUE'.
 00151          10  FILLER       PIC X(10)   VALUE 'INCENTIVES'.
 00152      SKIP2
 00153      05  DETAIL-LINE                  VALUE SPACES.
 00154          10  FILLER          PIC X(13).
 00155          10  D-TOWN          PIC 99.
 00156          10  FILLER          PIC X(8).
 00157          10  D-NAME          PIC X(21).
 00158          10  FILLER          PIC X(3).
 00159          10  D-EQLZ-FRZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
 00160          10  FILLER          PIC X(4).
 00161          10  D-ANN-EQLZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
 00162          10  FILLER          PIC X(1).
 00163          10  D-DIS-EQLZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
 00164          10  FILLER          PIC X(2).
 00165          10  D-TIF-EQLZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
 00166          10  FILLER          PIC X.
 00167          10  D-EXP-INC       PIC ZZ,ZZZ,ZZZ,ZZ9.
 00168      SKIP2
 00169      05  AGENCY-LINE.
 00170          10  FILLER          PIC X(47)   VALUE '  AGENCY TOTAL'.
 00171          10  A-EQLZ-FRZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
 00172          10  FILLER          PIC X(4)    VALUE SPACES.
 00173          10  A-ANN-EQLZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
 00174          10  FILLER          PIC X(1)    VALUE SPACES.
 00175          10  A-DIS-EQLZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
 00176          10  FILLER          PIC X(2)    VALUE SPACES.
 00177          10  A-TIF-EQLZ      PIC ZZ,ZZZ,ZZZ,ZZ9.
 00178          10  FILLER          PIC X      VALUE SPACES.
 00179          10  A-EXPINC-EQLZ   PIC ZZ,ZZZ,ZZZ,ZZ9.
 00180      SKIP2
 00181      05  TOTAL-LINE.
 00182          10  FILLER          PIC X(5)    VALUE SPACES.
 00183          10  T-MSG           PIC X(30).
 00184          10  T-CNTR          PIC ZZ,ZZ9.
 00185      05  TOTAL-LINEA.
 00186          10  FILLER          PIC X(5)    VALUE SPACES.
 00187          10  T-MSGA          PIC X(45).
 00188          10  T-CNTRA         PIC ZZ,ZZ9.
 00189      05  TOTAL-LINEB.
 00190          10  FILLER          PIC X(5)    VALUE SPACES.
 00191          10  T-MSGB          PIC X(45).
 00192          10  T-CNTRB         PIC ZZ,ZZZ,ZZZ,ZZZ,ZZ9.
 00193      SKIP2
 00194  01  FILLER.
 00195  ++INCLUDE TWNINFOTBL
 00196      SKIP3
 00197  LINKAGE SECTION.
 00198      SKIP1
 00199  01  LINK-INFO.
 00200      05  LINK-LENGTH         PIC S9(4)  BINARY.
 00201          88  VALID-LK-LENGTH VALUE +9.
 00202      SKIP1
 00203      05  LINK-YEAR.
 00204          10  LINK-YEAR-N     PIC 9(4).
 00205      05  LINK-EQFCTR.
 00206          10  LINK-EQFCTR-N   PIC 9(5).
 00207          EJECT
 00208  PROCEDURE DIVISION USING LINK-INFO.
 00209      SKIP1
 00210  010-HOUSEKEEPING.
 00211      SKIP1
 00212      IF NOT VALID-LK-LENGTH
 00213         DISPLAY 'LINKAGE LENGTH INVALID OR MISSING'
 00214         MOVE 16 TO RETURN-CODE
 00215         STOP RUN.
 00216      SKIP1
 00217      IF LINK-YEAR NUMERIC AND LINK-YEAR GREATER ZERO
 00218         NEXT SENTENCE
 00219      ELSE
 00220         DISPLAY 'LINKAGE YEAR INVALID ' LINK-YEAR
 00221         MOVE 16 TO RETURN-CODE.
 00222      SKIP1
 00223      IF LINK-EQFCTR NUMERIC AND LINK-EQFCTR GREATER ZERO
 00224         NEXT SENTENCE
 00225      ELSE
 00226         DISPLAY 'LINKAGE EQFACTOR INVALID ' LINK-EQFCTR
 00227         MOVE 16 TO RETURN-CODE.
 00228      SKIP1
 00229      IF RETURN-CODE = 16
 00230         STOP RUN.
 00231      SKIP1
 00232      OPEN INPUT AGCY-EFTS-FLE.
 00233      IF NOT GOOD-EFTS
 00234          DISPLAY 'INVALID OPEN ON AGCY EFTS FILE'
 00235          DISPLAY 'FILE STATUS IS ' EFTS-STATUS
 00236          DISPLAY 'FEEDBACK STATUS IS ' ES-RETURN ' '
 00237                    ES-FUNCTION ' ' ES-FEEDBACK
 00238          MOVE 16 TO RETURN-CODE
 00239          STOP RUN.
 00240      SKIP1
 00241      OPEN INPUT FRZAGCY-FILE
 00242          OUTPUT PRINT-FILE APPEND-FILE.
 00243      SKIP1
 00244      MOVE FUNCTION CURRENT-DATE(1:8) TO ACPT-DATE.
 00245      STRING ACPT-DATE(5:2) '/' ACPT-DATE(7:2) '/' ACPT-DATE(3:2)
 00246             DELIMITED BY SIZE INTO HD2-DATE.
 00247      MOVE LINK-EQFCTR-N TO HD2-FACT.
 00248      INSPECT HD2-FACT REPLACING ALL ',' BY '.'.
 00249      MOVE LINK-YEAR TO HD2-YEAR APPEND-YEAR.
 00250      SKIP1
 00251      WRITE PRINT-REC FROM BLANK-LINE AFTER ADVANCING PAGE.
 00252      SKIP3
 00253      PERFORM 020-MAINLINE
 00254         UNTIL FRZ-EOF OR ERR.
 00255      SKIP1
 00256      PERFORM 050-TOWN-BREAK-RTN.
 00257      MOVE TOT-EQLZ-FRZ-ACCUM TO A-EQLZ-FRZ.
 00258      MOVE TOT-ANN-EQLZ-ACCUM TO A-ANN-EQLZ.
 00259      MOVE TOT-DIS-EQLZ-ACCUM TO A-DIS-EQLZ.
 00260      MOVE TOT-TIF-EQLZ-ACCUM TO A-TIF-EQLZ.
 00261      MOVE TOT-EXPINC-EQLZ-ACCUM TO A-EXPINC-EQLZ.
 00262      WRITE PRINT-REC FROM AGENCY-LINE AFTER ADVANCING 3.
 00263      MOVE SPACES TO APPEND-DETAIL
 00264      WRITE APPEND-REC FROM WS-APPEND-REC
 00265      MOVE SPACES TO APPEND-DETAIL
 00266      WRITE APPEND-REC FROM WS-APPEND-REC
 00267      MOVE AGENCY-LINE TO APPEND-DETAIL
 00268      WRITE APPEND-REC FROM WS-APPEND-REC
 00269      MOVE 'TOTAL FROZEN RECORDS READ...:' TO T-MSG.
 00270      MOVE RECS-READ-CNTR TO T-CNTR.
 00271      WRITE PRINT-REC FROM TOTAL-LINE AFTER ADVANCING 2.
 00272      MOVE 'TOTAL AGENCY RECORDS PRINTED:'  TO T-MSG.
 00273      MOVE RECS-PRINTED-CNTR TO T-CNTR.
 00274      WRITE PRINT-REC FROM TOTAL-LINE AFTER ADVANCING 2.
 00275      SKIP1
 00276      DISPLAY SPACES
 00277      MOVE 'TOTAL FROZEN RECORDS READ..................:'
 00278           TO T-MSGA
 00279      MOVE RECS-READ-CNTR TO T-CNTRA.
 00280      DISPLAY TOTAL-LINEA.
 00281      MOVE 'TOTAL FROZEN RECORDS PRINTED...............:'
 00282           TO T-MSGA
 00283      MOVE RECS-PRINTED-CNTR TO T-CNTRA
 00284      DISPLAY TOTAL-LINEA.
 00285      MOVE 'TOTAL NEW PROPERTY EQUALIZED VALUE.........:'
 00286           TO T-MSGB
 00287      MOVE FIN-EQLZ-FRZ-ACCUM TO T-CNTRB
 00288      DISPLAY TOTAL-LINEB.
 00289      MOVE 'TOTAL ANNEXEC PROPERTY EQUALIZED VALUE.....:'
 00290           TO T-MSGB
 00291      MOVE FIN-ANN-EQLZ-ACCUM TO T-CNTRB
 00292      DISPLAY TOTAL-LINEB.
 00293      MOVE 'TOTAL DISCONNECTED PROPERTY EQUALIZED VALUE:'
 00294           TO T-MSGB
 00295      MOVE FIN-DIS-EQLZ-ACCUM TO T-CNTRB
 00296      DISPLAY TOTAL-LINEB.
 00297      MOVE 'TOTAL RECOVERED TAX INCREMENT VALUE........:'
 00298           TO T-MSGB
 00299      MOVE FIN-TIF-EQLZ-ACCUM TO T-CNTRB
 00300      DISPLAY TOTAL-LINEB.
 00301      MOVE 'TOTAL EXPIRED INCENTIVES VALUE.............:'
 00302           TO T-MSGB
 00303      MOVE FIN-EXPINC-EQLZ-ACCUM TO T-CNTRB
 00304      DISPLAY TOTAL-LINEB.
 00305      SKIP1
 00306      CLOSE AGCY-EFTS-FLE.
 00307      IF NOT GOOD-EFTS
 00308          DISPLAY 'INVALID CLOSE ON AGCY EFTS FILE'
 00309          DISPLAY 'FILE STATUS IS ' EFTS-STATUS
 00310          DISPLAY 'FEEDBACK STATUS IS ' ES-RETURN ' '
 00311                    ES-FUNCTION ' ' ES-FEEDBACK
 00312          MOVE 16 TO RETURN-CODE.
 00313      SKIP1
 00314      CLOSE FRZAGCY-FILE  PRINT-FILE  APPEND-FILE.
 00315      STOP RUN.
 00316      SKIP3
 00317  020-MAINLINE.
 00318      PERFORM 030-READ-FROZEN-AGENCY.
 00319      IF NOT (FRZ-EOF OR ERR)
 00320          IF CF-AGCY EQUAL SAVE-AGCY
 00321              IF CF-TOWN EQUAL SAVE-TOWN
 00322                  ADD FA-FROZ-EQLZD TO EQLZ-FRZ-ACCUM
 00323                  ADD FA-ANNEXEDEV  TO ANN-EQLZ-ACCUM
 00324                  ADD FA-DISCONEV   TO DIS-EQLZ-ACCUM
 00325                  ADD FA-TIFEV      TO TIF-EQLZ-ACCUM
 00326                  ADD FA-EXPINCEV   TO EXPINC-EQLZ-ACCUM
 00327              ELSE
 00328                  PERFORM 050-TOWN-BREAK-RTN
 00329          ELSE
 00330              PERFORM 040-AGENCY-BREAK-RTN.
 00331      SKIP2
 00332  030-READ-FROZEN-AGENCY.
 00333      READ FRZAGCY-FILE
 00334        AT END
 00335        MOVE 'Y' TO FRZ-EOF-SW.
 00336      IF NOT FRZ-EOF
 00337          ADD +1 TO RECS-READ-CNTR
 00338          MOVE FA-AGCY TO CF-AGCY
 00339          MOVE FA-TXCD TO UNPK-TXCD
 00340          MOVE UP-TOWN TO CF-TOWN
 00341          IF CURR-FRZAGCY LESS PREV-FRZAGCY
 00342              DISPLAY 'FROZEN AGENCY FILE OUT OF SEQUENCE'
 00343              DISPLAY 'CURRENT  KEY IS ' CURR-FRZAGCY
 00344              DISPLAY 'PREVIOUS KEY IS ' PREV-FRZAGCY
 00345              MOVE 16  TO RETURN-CODE
 00346              MOVE 'Y' TO ERR-SW
 00347          ELSE
 00348              MOVE CURR-FRZAGCY TO PREV-FRZAGCY
 00349              IF FIRST-TIME
 00350                  MOVE CF-AGCY TO SAVE-AGCY
 00351                  MOVE CF-TOWN TO SAVE-TOWN
 00352                  PERFORM 040-AGENCY-BREAK-RTN.
 00353      SKIP3
 00354  040-AGENCY-BREAK-RTN.
 00355      MOVE CF-AGCY TO SAVE-AGCY.
 00356      IF NOT FIRST-TIME
 00357          PERFORM 050-TOWN-BREAK-RTN
 00358          MOVE TOT-EQLZ-FRZ-ACCUM TO A-EQLZ-FRZ
 00359          MOVE TOT-ANN-EQLZ-ACCUM TO A-ANN-EQLZ
 00360          MOVE TOT-DIS-EQLZ-ACCUM TO A-DIS-EQLZ
 00361          MOVE TOT-TIF-EQLZ-ACCUM TO A-TIF-EQLZ
 00362          MOVE TOT-EXPINC-EQLZ-ACCUM TO A-EXPINC-EQLZ
 00363          WRITE PRINT-REC FROM AGENCY-LINE AFTER ADVANCING 3
 00364          MOVE SPACES TO APPEND-DETAIL
 00365          WRITE APPEND-REC FROM WS-APPEND-REC
 00366          MOVE SPACES TO APPEND-DETAIL
 00367          WRITE APPEND-REC FROM WS-APPEND-REC
 00368          MOVE AGENCY-LINE TO APPEND-DETAIL
 00369          WRITE APPEND-REC FROM WS-APPEND-REC
 00370          ADD TOT-EQLZ-FRZ-ACCUM TO FIN-EQLZ-FRZ-ACCUM
 00371          ADD TOT-ANN-EQLZ-ACCUM TO FIN-ANN-EQLZ-ACCUM
 00372          ADD TOT-DIS-EQLZ-ACCUM TO FIN-DIS-EQLZ-ACCUM
 00373          ADD TOT-TIF-EQLZ-ACCUM TO FIN-TIF-EQLZ-ACCUM
 00374          ADD TOT-EXPINC-EQLZ-ACCUM TO FIN-EXPINC-EQLZ-ACCUM
 00375          MOVE ZEROES TO TOT-EQLZ-FRZ-ACCUM  TOT-ANN-EQLZ-ACCUM
 00376                         TOT-DIS-EQLZ-ACCUM  TOT-TIF-EQLZ-ACCUM
 00377                         TOT-EXPINC-EQLZ-ACCUM.
 00378      SKIP1
 00379      MOVE SAVE-AGCY TO AE-AGCYNO  HD3-AGCY-NO  APPEND-AGENCY.
 00380      INSPECT HD3-AGCY-NO REPLACING ALL ',' BY '-'.
 00381      PERFORM 060-ACCESS-AGCY-EFTS.
 00382      IF GOOD-EFTS
 00383          MOVE AE-DESC TO HD3-AGCY-DES
 00384          PERFORM 070-HEADING-RTN.
 00385      SKIP3
 00386  050-TOWN-BREAK-RTN.
 00387      MOVE SAVE-TOWN TO D-TOWN.
 00388      IF SUBURBAN
 00389          SUBTRACT  9 FROM SAVE-TOWN GIVING SUB
 00390      ELSE
 00391          SUBTRACT 39 FROM SAVE-TOWN GIVING SUB.
 00392      MOVE TOWN-NAME (SUB) TO D-NAME.
 00393      MOVE EQLZ-FRZ-ACCUM TO D-EQLZ-FRZ
 00394      MOVE ANN-EQLZ-ACCUM TO D-ANN-EQLZ
 00395      MOVE DIS-EQLZ-ACCUM TO D-DIS-EQLZ
 00396      MOVE TIF-EQLZ-ACCUM TO D-TIF-EQLZ
 00397      MOVE EXPINC-EQLZ-ACCUM TO D-EXP-INC
 00398      WRITE PRINT-REC FROM DETAIL-LINE AFTER ADVANCING 1.
 00399      ADD +1 TO RECS-PRINTED-CNTR.
 00400      SKIP1
 00401      MOVE DETAIL-LINE TO APPEND-DETAIL.
 00402      WRITE APPEND-REC FROM WS-APPEND-REC.
 00403      SKIP1
 00404      ADD EQLZ-FRZ-ACCUM TO TOT-EQLZ-FRZ-ACCUM.
 00405      ADD ANN-EQLZ-ACCUM TO TOT-ANN-EQLZ-ACCUM.
 00406      ADD DIS-EQLZ-ACCUM TO TOT-DIS-EQLZ-ACCUM.
 00407      ADD TIF-EQLZ-ACCUM TO TOT-TIF-EQLZ-ACCUM.
 00408      ADD EXPINC-EQLZ-ACCUM TO TOT-EXPINC-EQLZ-ACCUM.
 00409      SKIP1
 00410      MOVE CF-TOWN       TO SAVE-TOWN.
 00411      MOVE FA-FROZ-EQLZD TO EQLZ-FRZ-ACCUM
 00412      MOVE FA-ANNEXEDEV  TO ANN-EQLZ-ACCUM
 00413      MOVE FA-DISCONEV   TO DIS-EQLZ-ACCUM
 00414      MOVE FA-TIFEV      TO TIF-EQLZ-ACCUM
 00415      MOVE FA-EXPINCEV   TO EXPINC-EQLZ-ACCUM.
 00416      SKIP3
 00417  060-ACCESS-AGCY-EFTS.
 00418      READ AGCY-EFTS-FLE.
 00419      IF NOT GOOD-EFTS AND NOT EFTS-NOTFND
 00420          DISPLAY 'INVALID READ ON AGCY EFTS FILE'
 00421          DISPLAY 'FILE STATUS IS ' EFTS-STATUS
 00422          DISPLAY 'FEEDBACK STATUS IS ' ES-RETURN ' '
 00423                    ES-FUNCTION ' ' ES-FEEDBACK
 00424          MOVE 'Y' TO ERR-SW
 00425          MOVE 16 TO RETURN-CODE
 00426      ELSE
 00427          IF EFTS-NOTFND
 00428              DISPLAY 'NO MATCHING AGENCY DESCRIPTION RECORD'
 00429              DISPLAY 'AGECNY NUMBER IS ' SAVE-AGCY
 00430              MOVE 'Y' TO ERR-SW
 00431              MOVE 16 TO RETURN-CODE.
 00432      SKIP3
 00433  070-HEADING-RTN.
 00434      ADD +1 TO PAGE-CNTR.
 00435      MOVE PAGE-CNTR TO HD1-PAGE.
 00436      WRITE PRINT-REC FROM HEADING1 AFTER ADVANCING PAGE.
 00437      WRITE PRINT-REC FROM HEADING2  AFTER ADVANCING 2.
 00438      WRITE PRINT-REC FROM HEADING2A AFTER ADVANCING 1.
 00439      WRITE PRINT-REC FROM HEADING3 AFTER ADVANCING 2.
 00440      WRITE PRINT-REC FROM HEADING4 AFTER ADVANCING 2.
 00441      WRITE PRINT-REC FROM HEADING5 AFTER ADVANCING 1.
 00442
 00443      MOVE SPACES TO APPEND-FILLER.
 00444      MOVE HEADING1(1:115) TO APPEND-DETAIL.
 00445      WRITE APPEND-REC FROM WS-APPEND-REC.
 00446      MOVE SPACES TO APPEND-DETAIL.
 00447      WRITE APPEND-REC FROM WS-APPEND-REC.
 00448      MOVE HEADING2 TO APPEND-DETAIL.
 00449      WRITE APPEND-REC FROM WS-APPEND-REC.
 00450      MOVE HEADING2A TO APPEND-DETAIL.
 00451      WRITE APPEND-REC FROM WS-APPEND-REC.
 00452      MOVE SPACES TO APPEND-DETAIL.
 00453      WRITE APPEND-REC FROM WS-APPEND-REC.
 00454      MOVE HEADING3 TO APPEND-DETAIL.
 00455      WRITE APPEND-REC FROM WS-APPEND-REC.
 00456      MOVE SPACES TO APPEND-DETAIL.
 00457      WRITE APPEND-REC FROM WS-APPEND-REC.
 00458      MOVE HEADING4 TO APPEND-DETAIL.
 00459      WRITE APPEND-REC FROM WS-APPEND-REC.
 00460      MOVE HEADING5 TO APPEND-DETAIL.
 00461      WRITE APPEND-REC FROM WS-APPEND-REC.
