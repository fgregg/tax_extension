 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID. CLRTM756.
 00003 *AUTHOR. RUBINGER/SUBRAMANIAN.
 00004 *DATE-WRITTEN. 06/95.
 00005 *REMARKS. CLRTM756 WILL UPDATE THE NEW PROPERTY EQUALIZED
 00006 *         VALUATION, ANNEXED PROPERTY EQUALIZED VALUATION,
 00007 *         DISCONNECTED PROPERTY EQUALIZED VALUATION AND THE
 00008 *         DISCONNECTED TIF VALUATION DIFFERENCE OF THE VSAM
 00009 *         AGENCY EQUALIZED VALUATION FILE.
 00010 *****************************************************************
 00011 *           PROGRAM WORK REQUEST (MODIFICATION)                 *
 00012 *                                                               *
 00013 * PROGRAMMER: DPSILVA                                           *
 00014 * DATE: 09/02/99                                                *
 00015 * REQUEST LETTER NAME: CL071799.001                             *
 00016 *****************************************************************
 00017 *                MODIFICATION DESCRIPTION                       *
 00018 *                                                               *
 00019 * MODIFIED PROGRAM CLRTM756 FOR YEAR 2000.                      *
 00020 *                                                               *
 00021 * 1. IN WORKING STORAGE:                                        *
 00022 *   A). EXPANDED H2-CURR-DTE  FROM 8 TO 10 AND CHANGED THE      *
 00023 *       PROGRAM TO DISPLAY THE CURRENT DATE ALONG WITH CENTURY  *
 00024 *       IN THIS FIELD.                                          *
 00025 *****************************************************************
 00026  EJECT
 00027  ENVIRONMENT DIVISION.
 00028  CONFIGURATION SECTION.
 00029  SOURCE-COMPUTER. IBM-370.
 00030  OBJECT-COMPUTER. IBM-370.
 00031  INPUT-OUTPUT SECTION.
 00032  FILE-CONTROL.
 00033      SELECT NEWPROP         ASSIGN TO UT-S-NEWPROP.
 00034      SELECT AGCY-EQVAL-FLE  ASSIGN TO DA-AGYEQVAL
 00035                             RECORD KEY IS AGCY-EQV-KEY
 00036                             ORGANIZATION IS INDEXED
 00037                             ACCESS IS RANDOM
 00038                             FILE STATUS IS FILE-STATUS,
 00039                                            FILE-STATUSA.
 00040      SELECT PRINT-FILE      ASSIGN TO UT-S-PRINTFLE.
 00041
 00042  DATA DIVISION.
 00043  FILE SECTION.
 00044
 00045  ++INCLUDE CLNEWPRPF2
 00046
 00047  ++INCLUDE AGYEQVALF3
 00048
 00049  ++INCLUDE PRINTFILE
 00050
 00051  WORKING-STORAGE SECTION.
 00052  77  LN-CTR                 PIC S9(4) VALUE +99    BINARY.
 00053  77  PG-CTR                 PIC S9(4) VALUE +0     BINARY.
 00054  77  NEWPROP-REC-CTR        PIC S9(8) VALUE +0     BINARY.
 00055  77  AGCY-EQVAL-READ-CTR    PIC S9(8) VALUE +0     BINARY.
 00056  77  AGCY-EQVAL-REWR-CTR    PIC S9(8) VALUE +0     BINARY.
 00057  77  NEWPROP-EOF-SW         PIC 9     VALUE ZEROES.
 00058      88 NEWPROP-EOF                   VALUE 1.
 00059  77  FILE-STATUS            PIC 99.
 00060      88 NORMAL-STATUS                 VALUE ZEROES.
 00061      88 RECORD-NOT-FOUND              VALUE 23.
 00062  77  CURR-AGCY              PIC X(9).
 00063  77  PREV-AGCY              PIC X(9)  VALUE LOW-VALUES.
 00064
 00065  01  WORK-AREAS.
 00066      03 FILE-STATUSA.
 00067         05 FS-RETURN        PIC S9(4)              BINARY.
 00068         05 FS-FUNC          PIC S9(4)              BINARY.
 00069         05 FS-FEEDBACK      PIC S9(4)              BINARY.
 00070      03 MDY-DATE.
 00071         05 MO               PIC 99.
 00072         05 FILLER           PIC X     VALUE '/'.
 00073         05 DA               PIC 99.
 00074         05 FILLER           PIC X     VALUE '/'.
 00075 *       05 CY               PIC XX    VALUE '19'.
 00076         05 YR               PIC 9999.
 00077      03 YMD-DATE.
 00078         05 YR               PIC 9999.
 00079         05 MO               PIC 99.
 00080         05 DA               PIC 99.
 00081
 00082      03 HEAD1.
 00083         05 FILLER           PIC X(10) VALUE '  CLRTM756'.
 00084         05 FILLER           PIC X(30) VALUE SPACES.
 00085         05 FILLER           PIC X(19) VALUE 'OFFICE OF THE COOK '.
 00086         05 FILLER           PIC X(12) VALUE 'COUNTY CLERK'.
 00087         05 FILLER           PIC X(49) VALUE SPACES.
 00088         05 FILLER           PIC X(5)  VALUE 'PAGE '.
 00089         05 H1-PAGE          PIC ZZ9.
 00090
 00091      03 HEAD2.
 00092         05 FILLER           PIC XX    VALUE SPACES.
 00093         05 H2-CURR-DTE      PIC X(10).
 00094         05 FILLER           PIC X(29) VALUE SPACES.
 00095         05 FILLER           PIC X(19) VALUE 'NEW PROPERTY FILE E'.
 00096         05 FILLER           PIC X(11) VALUE 'RROR REPORT'.
 00097
 00098      03 HEAD3.
 00099         05 FILLER           PIC X(5)  VALUE SPACES.
 00100         05 FILLER           PIC X(19) VALUE 'AGENCY    ERROR MES'.
 00101         05 FILLER           PIC X(4)  VALUE 'SAGE'.
 00102
 00103      03 DETAIL1.
 00104         05 FILLER           PIC X(3)  VALUE SPACES.
 00105         05 D1-AGCY          PIC X(9).
 00106         05 FILLER           PIC X(3)  VALUE SPACES.
 00107         05 D1-MESSG         PIC X(53).
 00108
 00109      03 TOTAL1.
 00110         05 FILLER           PIC X(5)  VALUE SPACES.
 00111         05 T1-MESSG         PIC X(56).
 00112         05 FILLER           PIC XXX   VALUE SPACES.
 00113         05 T1-TOTAL         PIC ZZ,ZZ9.
 00114      EJECT
 00115  PROCEDURE DIVISION.
 00116  010-BEGIN.
 00117      DISPLAY 'PROGRAM CLRTM756'
 00118      DISPLAY SPACES
 00119      OPEN I-O AGCY-EQVAL-FLE
 00120      IF NOT NORMAL-STATUS
 00121         DISPLAY 'AGCY-EQVAL-FLE OPEN FAILED  ' FILE-STATUS
 00122                 '   RETURN  '   FS-RETURN
 00123                 '   FUNCTION  ' FS-FUNC
 00124                 '   FEEDBACK  ' FS-FEEDBACK
 00125         MOVE 16 TO RETURN-CODE
 00126         STOP RUN.
 00127      OPEN INPUT  NEWPROP
 00128           OUTPUT PRINT-FILE
 00129 *    ACCEPT YMD-DATE             FROM DATE
 00130      MOVE FUNCTION CURRENT-DATE(1:8) TO YMD-DATE
 00131      MOVE CORRESPONDING YMD-DATE TO MDY-DATE
 00132      MOVE MDY-DATE               TO H2-CURR-DTE
 00133
 00134      PERFORM 100-MAINLINE
 00135      UNTIL NEWPROP-EOF OR RETURN-CODE EQUAL 16.
 00136
 00137  070-END.
 00138      IF NEWPROP-EOF
 00139         PERFORM 500-FORMAT-TOTAL-LINE.
 00140      DISPLAY 'TOTAL NEW PROPERTY RECORDS READ
 00141 -            '' NEWPROP-REC-CTR
 00142      DISPLAY 'TOTAL SELECTED FUNDS AGENCY EXTENSION RECORDS READ
 00143 -            '' AGCY-EQVAL-READ-CTR
 00144      DISPLAY 'TOTAL VSAM AGENCY EQ-VALUATION RECORDS UPDATED
 00145 -            '' AGCY-EQVAL-REWR-CTR
 00146      CLOSE NEWPROP, PRINT-FILE
 00147      CLOSE AGCY-EQVAL-FLE
 00148      IF NOT NORMAL-STATUS
 00149         DISPLAY 'AGCY-EQVAL-FLE CLOSE FAILED  ' FILE-STATUS
 00150                 '   RETURN  '   FS-RETURN
 00151                 '   FUNCTION  ' FS-FUNC
 00152                 '   FEEDBACK  ' FS-FEEDBACK
 00153         MOVE 16 TO RETURN-CODE.
 00154      STOP RUN.
 00155
 00156  100-MAINLINE.
 00157      PERFORM 200-READ-NEWPROP
 00158      IF NOT NEWPROP-EOF AND RETURN-CODE EQUAL ZEROES
 00159         PERFORM 250-READ-AGCY-EQVAL-FLE
 00160         IF NORMAL-STATUS
 00161            PERFORM 300-UPDATE-REC
 00162            PERFORM 350-REWRITE-AGCY-EQVAL-REC
 00163         ELSE
 00164            IF RECORD-NOT-FOUND
 00165               PERFORM 400-FORMAT-DETAIL-LINE.
 00166
 00167  200-READ-NEWPROP.
 00168      READ NEWPROP AT END
 00169         MOVE 1       TO NEWPROP-EOF-SW.
 00170      IF NOT NEWPROP-EOF
 00171         ADD 1        TO NEWPROP-REC-CTR
 00172         MOVE TC-AGCY TO CURR-AGCY
 00173         IF CURR-AGCY NOT GREATER PREV-AGCY
 00174            DISPLAY 'NEWPROP IS OUT OF SEQUENCE'
 00175            DISPLAY 'CURRENT RECORD IS   ' CURR-AGCY
 00176            DISPLAY 'PREVIOUS RECORD IS  ' PREV-AGCY
 00177            MOVE 16        TO RETURN-CODE
 00178         ELSE
 00179            MOVE CURR-AGCY TO PREV-AGCY.
 00180
 00181  250-READ-AGCY-EQVAL-FLE.
 00182      MOVE TC-AGCY TO AEV-AGCYNO
 00183      READ AGCY-EQVAL-FLE
 00184      IF NORMAL-STATUS
 00185         ADD 1 TO AGCY-EQVAL-READ-CTR
 00186      ELSE
 00187         IF RECORD-NOT-FOUND
 00188            NEXT SENTENCE
 00189         ELSE
 00190            DISPLAY 'AGCY-EQVAL-FLE READ FAILED  ' FILE-STATUS
 00191                    '   RETURN  '   FS-RETURN
 00192                    '   FUNCTION  ' FS-FUNC
 00193                    '   FEEDBACK  ' FS-FEEDBACK
 00194            MOVE 16 TO RETURN-CODE.
 00195
 00196  300-UPDATE-REC.
 00197      COMPUTE AEV-NEW-PROP-EQV = TC-EQVAL + TC-EXPINCEV
 00198      MOVE TC-ANXEQVAL    TO AEV-ANX-PROP-EQV
 00199      MOVE TC-DISEQVAL    TO AEV-DIS-PROP-EQV
 00200      MOVE TC-TIFDIFEQVAL TO AEV-DIS-TIF-DIF.
 00201
 00202  350-REWRITE-AGCY-EQVAL-REC.
 00203      REWRITE AGCY-EQVAL-REC
 00204      IF NORMAL-STATUS
 00205         ADD 1 TO AGCY-EQVAL-REWR-CTR
 00206      ELSE
 00207         DISPLAY 'AGCY-EQVAL-FLE REWRITE FAILED  ' FILE-STATUS
 00208                 '   RETURN  '   FS-RETURN
 00209                 '   FUNCTION  ' FS-FUNC
 00210                 '   FEEDBACK  ' FS-FEEDBACK
 00211         MOVE 16 TO RETURN-CODE.
 00212
 00213  400-FORMAT-DETAIL-LINE.
 00214      MOVE TC-AGCY TO D1-AGCY
 00215      MOVE 'NO MATCHING AGENCY ON AGENCY EQUALIZED VALUATION FILE'
 00216                   TO D1-MESSG
 00217      IF LN-CTR GREATER 57
 00218         PERFORM 450-HDR-RTN.
 00219      WRITE PRINT-REC FROM DETAIL1 AFTER ADVANCING 2
 00220      ADD 2        TO LN-CTR.
 00221
 00222  450-HDR-RTN.
 00223      ADD 1       TO PG-CTR
 00224      MOVE PG-CTR TO H1-PAGE
 00225      WRITE PRINT-REC FROM HEAD1 AFTER ADVANCING PAGE
 00226      WRITE PRINT-REC FROM HEAD2 AFTER ADVANCING 2
 00227      WRITE PRINT-REC FROM HEAD3 AFTER ADVANCING 3
 00228      MOVE 6      TO LN-CTR.
 00229
 00230  500-FORMAT-TOTAL-LINE.
 00231      IF LN-CTR GREATER 52
 00232         PERFORM 450-HDR-RTN.
 00233      MOVE 'TOTAL NUMBER OF CLERK''S NEW PROPERTY RECORDS READ'
 00234                               TO T1-MESSG
 00235      MOVE NEWPROP-REC-CTR     TO T1-TOTAL
 00236      WRITE PRINT-REC FROM TOTAL1 AFTER ADVANCING 3
 00237      MOVE 'TOTAL NUMBER OF CLERK''S NEW PROPERTY RECORDS UNMATCHED
 00238 -         ''                  TO T1-MESSG
 00239      MOVE AGCY-EQVAL-READ-CTR TO T1-TOTAL
 00240      WRITE PRINT-REC FROM TOTAL1 AFTER ADVANCING 1
 00241      MOVE 'TOTAL NUMBER OF VSAM AGENCY EQ-VALUATION RECORDS UPDATE
 00242 -         'D'                 TO T1-MESSG
 00243      MOVE AGCY-EQVAL-REWR-CTR TO T1-TOTAL
 00244      WRITE PRINT-REC FROM TOTAL1 AFTER ADVANCING 1.
