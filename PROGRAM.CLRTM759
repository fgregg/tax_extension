 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID.    CLRTM759.
 00003 *AUTHOR.        FLEMING-KISTINGER.
 00004 *DATE-WRITTEN.  JULY, 1994.
 00005      SKIP1
 00006 *REMARKS.  THIS PROGRAM IS DESIGNED TO UPDATE TIF PRIOR YEAR
 00007 *          FROZEN EQUALIZED VALUE AND THE TIF DIFFERENCE EQUALIZED
 00008 *          VALUE OF THE FROZEN AGENCY FILE USING THE VALUES OF THE
 00009 *          UPDATED TIF DIFFERENCE FILE.
 00010 *****************************************************************
 00011 *****************************************************************
 00012 *           PROGRAM WORK REQUEST (MODIFICATION)                 *
 00013 *                                                               *
 00014 * PROGRAMMER: DPSILVA                                           *
 00015 * DATE: 09/02/99                                                *
 00016 * REQUEST LETTER NAME: CL071799.001                             *
 00017 *****************************************************************
 00018 *                MODIFICATION DESCRIPTION                       *
 00019 *                                                               *
 00020 * MODIFIED PROGRAM CLRTM759 FOR YEAR 2000.                      *
 00021 *                                                               *
 00022 * 1. IN WORKING STORAGE:                                        *
 00023 *   A). EXPANDED HL2-DATE     FROM 8 TO 10 AND CHANGED THE      *
 00024 *       PROGRAM TO DISPLAY THE CURRENT DATE ALONG WITH CENTURY  *
 00025 *       IN THIS FIELD.                                          *
 00026 *****************************************************************
 00027      SKIP2
 00028  ENVIRONMENT DIVISION.
 00029  CONFIGURATION SECTION.
 00030  SOURCE-COMPUTER. IBM-370.
 00031  OBJECT-COMPUTER. IBM-370.
 00032      SKIP1
 00033  INPUT-OUTPUT SECTION.
 00034  FILE-CONTROL.
 00035      SKIP1
 00036      SELECT TAX-CODE-FILE    ASSIGN TO DA-TAXCODE
 00037                              ACCESS IS RANDOM
 00038                              ORGANIZATION IS INDEXED
 00039                              RECORD KEY IS TM-CODE
 00040                              FILE STATUS IS TAX-CODE-STATUS
 00041                                             TAX-CODE-STATUS2.
 00042      SKIP1
 00043      SELECT FRZAGCY-FILE     ASSIGN TO DA-FROZFILE
 00044                              ACCESS IS RANDOM
 00045                              ORGANIZATION IS INDEXED
 00046                              RECORD KEY IS FA-KEY
 00047                              FILE STATUS IS FROZ-FILE-STATUS
 00048                                             FROZ-FILE-STATUS2.
 00049      SKIP1
 00050      SELECT CLTIFDIF         ASSIGN TO UT-S-TIFDIFLE.
 00051      SKIP1
 00052      SELECT PRINT-FILE       ASSIGN TO UT-S-PRINTFLE.
 00053      SKIP2
 00054  DATA DIVISION.
 00055  FILE SECTION.
 00056      SKIP2
 00057  ++INCLUDE CLFRZAGYF1
 00058      SKIP2
 00059  ++INCLUDE CLTIFDIFF1
 00060      EJECT
 00061  FD  PRINT-FILE
 00062      RECORDING MODE IS F
 00063      RECORD CONTAINS 133 CHARACTERS
 00064      BLOCK CONTAINS 0 RECORDS
 00065      LABEL RECORDS ARE STANDARD
 00066      DATA RECORD IS PRINT-REC.
 00067  01  PRINT-REC               PIC X(133).
 00068      SKIP2
 00069  FD  TAX-CODE-FILE
 00070      RECORD CONTAINS 207 CHARACTERS
 00071      BLOCK CONTAINS 0 RECORDS.
 00072  01  TAX-CODE-REC.
 00073  ++INCLUDE CLRRTXCD
 00074      EJECT
 00075  WORKING-STORAGE SECTION.
 00076      SKIP1
 00077  77  FROZEN-RECS-UPDTD      PIC S9(5)  VALUE +0  PACKED-DECIMAL.
 00078  77  FROZEN-RECS-ADDED      PIC S9(5)  VALUE +0  PACKED-DECIMAL.
 00079  77  TIFDIF-RECS-READ       PIC S9(5)  VALUE +0  PACKED-DECIMAL.
 00080  77  PRINT-RECS-WRITTEN     PIC S9(5)  VALUE +0  PACKED-DECIMAL.
 00081  77  PAGE-CNTR              PIC S9(5)  VALUE +0  PACKED-DECIMAL.
 00082  77  LINE-CNTR              PIC S9(3)  VALUE +90 PACKED-DECIMAL.
 00083  77  WORK-LINE-CNTR         PIC S9(3)  VALUE +0  PACKED-DECIMAL.
 00084  77  SUB                    PIC S9(3)  VALUE +0  PACKED-DECIMAL.
 00085  77  SUB1                   PIC S9(4)  VALUE +0  COMP.
 00086      SKIP1
 00087  77  BLANK-LINE             PIC X      VALUE SPACE.
 00088  77  1ST-TD-REC-SW          PIC X      VALUE 'Y'.
 00089      88  1ST-TD-REC                    VALUE 'Y'.
 00090      88  REST-OF-TD-RECS               VALUE 'N'.
 00091      SKIP1
 00092  77  TIF-DIF-EOF-SW         PIC X      VALUE 'N'.
 00093      88  TIF-DIF-OK                    VALUE 'N'.
 00094      88  EOF-TIF-DIF                   VALUE 'Y'.
 00095      SKIP1
 00096  77  CHECK-PROCESSING       PIC X      VALUE 'Y'.
 00097      88  PROCESSING-OK                 VALUE 'Y'.
 00098      88  STOP-PROCESSING               VALUE 'N'.
 00099      SKIP1
 00100  01  WORK-AREA.
 00101      SKIP1
 00102      03  TAX-CODE-STATUS        PIC 99.
 00103          88  VALID-TAX-STAT                VALUE 00.
 00104          88  TAX-NOT-FND                   VALUE 23.
 00105      03  TAX-CODE-STATUS2                  BINARY.
 00106          10  TAX-RETURN         PIC 99     VALUE 0.
 00107          10  TAX-FUNCTION       PIC 9      VALUE 0.
 00108          10  TAX-FEEDBACK       PIC 999    VALUE 0.
 00109      03  FROZ-FILE-STATUS       PIC 99.
 00110          88  VALID-FROZ-STAT               VALUE 00 97.
 00111          88  FROZ-NOT-FND                  VALUE 23.
 00112      03  FROZ-FILE-STATUS2                 BINARY.
 00113          10  FROZ-RETURN        PIC 99     VALUE 0.
 00114          10  FROZ-FUNCTION      PIC 9      VALUE 0.
 00115          10  FROZ-FEEDBACK      PIC 999    VALUE 0.
 00116      SKIP1
 00117      EJECT
 00118  01  PRINT-LINES.
 00119      SKIP1
 00120      03  HEADER-LINE-1.
 00121          05  FILLER         PIC X       VALUE SPACE.
 00122          05  FILLER         PIC X(8)    VALUE 'CLRTM759'.
 00123          05  FILLER         PIC X(30)   VALUE SPACES.
 00124          05  FILLER         PIC X(31)   VALUE
 00125              'OFFICE OF THE COOK COUNTY CLERK'.
 00126          05  FILLER         PIC X(49)   VALUE SPACES.
 00127          05  FILLER         PIC X(5)    VALUE 'PAGE '.
 00128          05  HL1-PAGE-NO    PIC ZZ,ZZ9.
 00129      SKIP1
 00130      03  HEADER-LINE-2.
 00131          05  FILLER         PIC X       VALUE SPACE.
 00132          05  HL2-DATE.
 00133              10  HL2-MO     PIC 99.
 00134              10  FILLER     PIC X       VALUE '/'.
 00135              10  HL2-DA     PIC 99.
 00136              10  FILLER     PIC X       VALUE '/'.
 00137 *            10  HL2-CENT   PIC 99      VALUE 19.
 00138              10  HL2-YR     PIC 9999.
 00139          05  FILLER         PIC X(28)   VALUE SPACES.
 00140          05  FILLER         PIC X(32)   VALUE
 00141              'FROZEN AGENCY FILE UPDATE REPORT'.
 00142      SKIP1
 00143      03  HEADER-LINE-3.
 00144          05  FILLER         PIC X(31)   VALUE SPACES.
 00145          05  FILLER         PIC X(14)   VALUE
 00146              'TIF PRIOR YEAR'.
 00147          05  FILLER         PIC X(5)    VALUE SPACES.
 00148          05  FILLER         PIC X(14)   VALUE
 00149              'TIF DIFFERENCE'.
 00150          05  FILLER         PIC X(6)    VALUE SPACES.
 00151          05  FILLER         PIC X(11)   VALUE
 00152              'TIF CURRENT'.
 00153      SKIP1
 00154      03  HEADER-LINE-4.
 00155          05  FILLER         PIC X(2)    VALUE SPACES.
 00156          05  FILLER         PIC X(7)    VALUE 'TX CODE'.
 00157          05  FILLER         PIC X(2)    VALUE SPACES.
 00158          05  FILLER         PIC X(6)    VALUE 'AGENCY'.
 00159          05  FILLER         PIC X(2)    VALUE SPACES.
 00160          05  FILLER         PIC X(8)    VALUE 'TAX RATE'.
 00161          05  FILLER         PIC X(3)    VALUE SPACES.
 00162          05  FILLER         PIC X(15)   VALUE
 00163              'FROZEN EQ-VALUE'.
 00164          05  FILLER         PIC X(4)    VALUE SPACES.
 00165          05  FILLER         PIC X(15)   VALUE
 00166              'EQUALIZED VALUE'.
 00167          05  FILLER         PIC X(4)    VALUE SPACES.
 00168          05  FILLER         PIC X(15)   VALUE
 00169              'EQUALIZED VALUE'.
 00170          05  FILLER         PIC X(4)    VALUE SPACES.
 00171          05  FILLER         PIC X(7)    VALUE 'MESSAGE'.
 00172      SKIP1
 00173      03  PRINT-LINE-1.
 00174          05  FILLER         PIC X(2)    VALUE SPACES.
 00175          05  PL1-TAX-CODE   PIC 9(5).
 00176          05  FILLER         PIC X(1)    VALUE SPACES.
 00177          05  PL1-AGENCY     PIC 9(2),9(4),9(3).
 00178          05  FILLER         PIC X(1)    VALUE SPACES.
 00179          05  PL1-TAX-RATE   PIC 999.999.
 00180          05  FILLER         PIC X(2)    VALUE SPACES.
 00181          05  PL1-PY-FR-EQ-VAL PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
 00182          05  FILLER         PIC X(2)    VALUE SPACES.
 00183          05  PL1-DIFF-EQ-VAL  PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
 00184          05  FILLER         PIC X(2)    VALUE SPACES.
 00185          05  PL1-CURR-EQ-VAL  PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.
 00186          05  FILLER         PIC X(2)    VALUE SPACES.
 00187          05  PL1-MSG        PIC X(43).
 00188      SKIP1
 00189  01  WORK-AREAS.
 00190      SKIP1
 00191      03  CURR-TD-KEY                    VALUE ZEROES.
 00192          05  CURR-TD-TAX-CODE   PIC X(5).
 00193      SKIP1
 00194      03  PREV-TD-KEY                    VALUE ZEROES.
 00195          05  PREV-TD-TAX-CODE   PIC X(5).
 00196      SKIP1
 00197      03  UPDT-MSG.
 00198          05  FILLER         PIC X(8)    VALUE 'TX-CODE '.
 00199          05  UPDT-MSG-TAX-CODE  PIC X(5).
 00200          05  FILLER         PIC X(30)   VALUE
 00201          ' UPDATED ON FROZEN AGENCY FILE'.
 00202      SKIP1
 00203      03  ADD-MSG.
 00204          05  FILLER         PIC X(8)    VALUE 'TX-CODE '.
 00205          05  ADD-MSG-TAX-CODE  PIC X(5).
 00206          05  FILLER         PIC X(30)   VALUE
 00207          ' ADDED TO FROZEN AGENCY FILE  '.
 00208      SKIP1
 00209      03  TOTAL-LINE-1.
 00210          05  FILLER         PIC X(9)    VALUE SPACES.
 00211          05  FILLER         PIC X(48)   VALUE
 00212          'TOTAL NUMBER OF TIF DIFFERENCE RECORDS READ     '.
 00213          05  TL1-TIF-DIF-RECS-READ   PIC ZZZ,ZZ9.
 00214      SKIP1
 00215      03  TOTAL-LINE-2.
 00216          05  FILLER         PIC X(9)    VALUE SPACES.
 00217          05  FILLER         PIC X(48)   VALUE
 00218          'TOTAL NUMBER OF FROZEN AGENCY RECORDS ADDED     '.
 00219          05  TL2-FR-AGCY-RECS-ADDED  PIC ZZZ,ZZ9.
 00220      SKIP1
 00221      03  TOTAL-LINE-3.
 00222          05  FILLER         PIC X(9)    VALUE SPACES.
 00223          05  FILLER         PIC X(48)   VALUE
 00224          'TOTAL NUMBER OF FROZEN AGENCY RECORDS UPDATED   '.
 00225          05  TL3-FR-AGCY-RECS-UPDTD  PIC ZZZ,ZZ9.
 00226      SKIP2
 00227  01  DATE-WORK-AREA.
 00228      05  DSP-TIME           PIC 99,99.
 00229      05  DSP-DATE.
 00230          10  DSP-MO         PIC 99.
 00231          10  FILLER         PIC X       VALUE '/'.
 00232          10  DSP-DA         PIC 99.
 00233          10  FILLER         PIC X       VALUE '/'.
 00234          10  DSP-YR         PIC 9999.
 00235      05  ACPT-DATE          PIC 9(8).
 00236      05  ACPT-DATE-X REDEFINES ACPT-DATE.
 00237          10  ACPT-YR        PIC 9999.
 00238          10  ACPT-MO        PIC 99.
 00239          10  ACPT-DA        PIC 99.
 00240      05  ACPT-TIME-HOLD     PIC 9(8).
 00241      05  ACPT-TIME-HOLD-X REDEFINES ACPT-TIME-HOLD.
 00242          10  ACPT-TIME      PIC 9(4).
 00243          10  FILLER         PIC 9(4).
 00244      EJECT
 00245  PROCEDURE DIVISION.
 00246      SKIP1
 00247  010-HOUSEKEEPING.
 00248      ACCEPT ACPT-TIME-HOLD FROM TIME.
 00249      MOVE ACPT-TIME TO DSP-TIME.
 00250      INSPECT DSP-TIME REPLACING ALL ',' BY ':'.
 00251 *    ACCEPT ACPT-DATE FROM DATE.
 00252      MOVE FUNCTION CURRENT-DATE(1:8) TO ACPT-DATE.
 00253      MOVE ACPT-MO TO DSP-MO, HL2-MO.
 00254      MOVE ACPT-DA TO DSP-DA, HL2-DA.
 00255      MOVE ACPT-YR TO DSP-YR, HL2-YR.
 00256      DISPLAY 'PROGRAM CLRTM759 DATE AND TIME OF RUN = '
 00257          DSP-DATE ' ' DSP-TIME.
 00258      OPEN INPUT TAX-CODE-FILE.
 00259      IF NOT VALID-TAX-STAT
 00260          DISPLAY 'VSAM OPEN INCOMPLETE FOR TAX CODE FILE'
 00261          DISPLAY 'TAX AGCY FILE STATUS  IS ' TAX-CODE-STATUS
 00262          DISPLAY 'TAX AGCY RETURN       IS ' TAX-RETURN
 00263          DISPLAY 'TAX AGCY FUNCTION     IS ' TAX-FUNCTION
 00264          DISPLAY 'TAX AGCY FEEDBACK     IS ' TAX-FEEDBACK
 00265          MOVE 'N' TO CHECK-PROCESSING
 00266          MOVE 16 TO RETURN-CODE
 00267          STOP RUN
 00268      END-IF.
 00269      OPEN I-O   FRZAGCY-FILE.
 00270      IF NOT VALID-FROZ-STAT
 00271          DISPLAY 'VSAM OPEN INCOMPLETE FOR FROZ AGCY FILE'
 00272          DISPLAY 'FROZ AGCY FILE STATUS  IS ' FROZ-FILE-STATUS
 00273          DISPLAY 'FROZ AGCY RETURN       IS ' FROZ-RETURN
 00274          DISPLAY 'FROZ AGCY FUNCTION     IS ' FROZ-FUNCTION
 00275          DISPLAY 'FROZ AGCY FEEDBACK     IS ' FROZ-FEEDBACK
 00276          MOVE 'N' TO CHECK-PROCESSING
 00277          MOVE 16 TO RETURN-CODE
 00278          STOP RUN
 00279      END-IF.
 00280      OPEN INPUT CLTIFDIF.
 00281      OPEN OUTPUT PRINT-FILE.
 00282      WRITE PRINT-REC FROM BLANK-LINE AFTER ADVANCING PAGE.
 00283      PERFORM 020-MAINLINE UNTIL EOF-TIF-DIF
 00284                            OR   STOP-PROCESSING.
 00285      IF LINE-CNTR NOT GREATER THAN 57
 00286          SUBTRACT LINE-CNTR FROM 57 GIVING WORK-LINE-CNTR
 00287          PERFORM 090-WRITE-BLANK-LINE VARYING SUB FROM 1 BY 1
 00288              UNTIL  SUB  =  WORK-LINE-CNTR.
 00289      MOVE TIFDIF-RECS-READ TO TL1-TIF-DIF-RECS-READ.
 00290      WRITE PRINT-REC FROM TOTAL-LINE-1.
 00291      MOVE FROZEN-RECS-ADDED TO TL2-FR-AGCY-RECS-ADDED.
 00292      WRITE PRINT-REC FROM TOTAL-LINE-2.
 00293      MOVE FROZEN-RECS-UPDTD TO TL3-FR-AGCY-RECS-UPDTD.
 00294      WRITE PRINT-REC FROM TOTAL-LINE-3.
 00295      CLOSE PRINT-FILE
 00296            CLTIFDIF.
 00297      CLOSE TAX-CODE-FILE.
 00298      IF NOT VALID-TAX-STAT
 00299          DISPLAY 'VSAM CLOSE INCOMPLETE FOR TAX CODE FILE'
 00300          DISPLAY 'TAX CODE FILE STATUS  IS   '
 00301              TAX-CODE-STATUS
 00302          DISPLAY 'TAX CODE RETURN       IS   '
 00303              TAX-RETURN
 00304          DISPLAY 'TAX CODE FUNCTION     IS   '
 00305              TAX-FUNCTION
 00306          DISPLAY 'TAX CODE FEEDBACK     IS   '
 00307              TAX-FEEDBACK
 00308          MOVE 16 TO RETURN-CODE.
 00309      CLOSE FRZAGCY-FILE.
 00310      IF NOT VALID-FROZ-STAT
 00311          DISPLAY 'VSAM CLOSE INCOMPLETE FOR FROZEN AGCY FILE'
 00312          DISPLAY 'FROZ AGCY FILE STATUS IS   '
 00313              FROZ-FILE-STATUS
 00314          DISPLAY 'FROZ RETURN           IS   '
 00315              FROZ-RETURN
 00316          DISPLAY 'FROZ FUNCTION         IS   '
 00317              FROZ-FUNCTION
 00318          DISPLAY 'FROZ FEEDBACK         IS   '
 00319              FROZ-FEEDBACK
 00320          MOVE 16 TO RETURN-CODE
 00321          STOP RUN
 00322      ELSE
 00323          DISPLAY 'FROZEN AGENCY RECORDS UPDATED  '
 00324              FROZEN-RECS-UPDTD
 00325          DISPLAY 'FROZEN AGENCY RECORDS ADDED    '
 00326              FROZEN-RECS-ADDED
 00327          DISPLAY 'TIFF DIFF RECORDS READ         '
 00328              TIFDIF-RECS-READ
 00329          DISPLAY 'PRINTER REPORT RECORDS WRITTEN '
 00330              PRINT-RECS-WRITTEN
 00331          STOP RUN
 00332      END-IF.
 00333      SKIP1
 00334  020-MAINLINE.
 00335      PERFORM 030-READ-TIF-DIF-FILE.
 00336      IF EOF-TIF-DIF OR STOP-PROCESSING
 00337          CONTINUE
 00338      ELSE
 00339          PERFORM 040-READ-TAX-CODE-FILE
 00340      END-IF.
 00341      SKIP1
 00342  030-READ-TIF-DIF-FILE.
 00343      READ CLTIFDIF   AT END
 00344          MOVE 99999 TO CURR-TD-KEY
 00345          MOVE 'Y' TO TIF-DIF-EOF-SW.
 00346      IF NOT EOF-TIF-DIF
 00347          MOVE TD-TXCD TO CURR-TD-TAX-CODE
 00348          ADD +1 TO TIFDIF-RECS-READ
 00349      END-IF.
 00350      IF 1ST-TD-REC
 00351          MOVE TD-TXCD TO CURR-TD-TAX-CODE
 00352          MOVE TD-TXCD TO PREV-TD-TAX-CODE
 00353          MOVE 'N' TO 1ST-TD-REC-SW
 00354      END-IF.
 00355      IF CURR-TD-KEY LESS THAN PREV-TD-KEY
 00356          DISPLAY 'SRTD TIF DIF FILE OUT OF SEQUENCE '
 00357          DISPLAY 'OLD SEQUENCE   ' PREV-TD-KEY
 00358          DISPLAY 'NEW SEQUENCE   ' CURR-TD-KEY
 00359          MOVE 'N' TO CHECK-PROCESSING
 00360          MOVE 16 TO RETURN-CODE
 00361      ELSE
 00362          MOVE CURR-TD-KEY TO PREV-TD-KEY
 00363      END-IF.
 00364      SKIP1
 00365  040-READ-TAX-CODE-FILE.
 00366      MOVE TD-TXCD TO TM-CODE.
 00367      READ TAX-CODE-FILE.
 00368      IF VALID-TAX-STAT
 00369          PERFORM 045-FROZEN-AGENCY-ROUTINE
 00370                  VARYING SUB1 FROM 1 BY 1 UNTIL SUB1 >= 40
 00371                  OR TM-AGCY(SUB1) = ZERO
 00372      ELSE
 00373         IF TAX-NOT-FND
 00374             DISPLAY 'NO MATCHING RECORD ON TAX CODE MASTER '
 00375             DISPLAY 'FOR TAX CODE : ' TM-CODE
 00376             MOVE 15 TO RETURN-CODE
 00377         ELSE
 00378             DISPLAY 'VSAM READ INCOMPLETE FOR TAX CODE FILE'
 00379             DISPLAY 'TAX CODE FILE STATUS  IS   ' TAX-CODE-STATUS
 00380             DISPLAY 'TAX CODE RETURN       IS   ' TAX-RETURN
 00381             DISPLAY 'TAX CODE FUNCTION     IS   ' TAX-FUNCTION
 00382             DISPLAY 'TAX CODE FEEDBACK     IS   ' TAX-FEEDBACK
 00383             MOVE 16 TO RETURN-CODE
 00384         END-IF
 00385      END-IF.
 00386      SKIP1
 00387  045-FROZEN-AGENCY-ROUTINE.
 00388      MOVE TD-TXCD       TO FA-TXCD.
 00389      MOVE TM-AGCY(SUB1) TO FA-AGCY.
 00390      READ FRZAGCY-FILE.
 00391      IF VALID-FROZ-STAT
 00392          PERFORM 050-REWRITE-FROZ-AGCY-REC
 00393          PERFORM 070-PRINT-DETAIL-FROZ-REC
 00394      ELSE
 00395          PERFORM 060-WRITE-FROZ-AGCY-REC
 00396          PERFORM 070-PRINT-DETAIL-FROZ-REC
 00397      END-IF.
 00398      SKIP1
 00399  050-REWRITE-FROZ-AGCY-REC.
 00400      ADD  TD-FRZEQVAL TO FA-TIFAV.
 00401      ADD  TD-TIFDIFF  TO FA-TIFEV.
 00402      REWRITE FRZAGCY-REC.
 00403      IF NOT VALID-FROZ-STAT
 00404          DISPLAY 'VSAM REWRITE INCOMPLETE FOR FROZ AGCY FILE'
 00405          DISPLAY 'FROZ FILE STATUS IS     '  FROZ-FILE-STATUS
 00406          DISPLAY 'FROZ RETURN      IS     '  FROZ-RETURN
 00407          DISPLAY 'FROZ FUNCTION    IS     '  FROZ-FUNCTION
 00408          DISPLAY 'FROZ FEEDBACK    IS     '  FROZ-FEEDBACK
 00409          DISPLAY 'FROZ AGCY KEY    IS     '  FA-TXCD, FA-AGCY
 00410          MOVE 16 TO RETURN-CODE
 00411          MOVE 'N' TO CHECK-PROCESSING
 00412      ELSE
 00413          ADD +1 TO FROZEN-RECS-UPDTD
 00414          MOVE FA-TXCD TO UPDT-MSG-TAX-CODE
 00415          MOVE UPDT-MSG TO PL1-MSG
 00416      END-IF.
 00417      SKIP1
 00418  060-WRITE-FROZ-AGCY-REC.
 00419      MOVE SPACES TO FRZAGCY-REC.
 00420      MOVE TD-TXCD TO FA-TXCD.
 00421      MOVE TM-AGCY (SUB1) TO FA-AGCY.
 00422      MOVE ZEROES TO FA-TXRTE
 00423                     FA-CURR-288
 00424                     FA-EXPR-288
 00425                     FA-FRST-TIME
 00426                     FA-TOT-FROZ
 00427                     FA-FROZ-EQLZD
 00428                     FA-FROZ-TXAMT
 00429                     FA-ANNEXEDAV
 00430                     FA-ANNEXEDEV
 00431                     FA-DISCONAV
 00432                     FA-DISCONEV
 00433                     FA-EXPINC
 00434                     FA-EXPINCEV.
 00435      MOVE TD-FRZEQVAL TO FA-TIFAV.
 00436      MOVE TD-TIFDIFF TO FA-TIFEV.
 00437      MOVE TD-CUREQVAL TO FA-CURREV.
 00438      WRITE FRZAGCY-REC.
 00439      IF NOT VALID-FROZ-STAT
 00440          DISPLAY 'VSAM WRITE INCOMPLETE FOR FROZ AGCY FILE'
 00441          DISPLAY 'FROZ   FILE STATUS IS   ' FROZ-FILE-STATUS
 00442          DISPLAY 'FROZ   RETURN      IS   ' FROZ-RETURN
 00443          DISPLAY 'FROZ   FUNCTION    IS   ' FROZ-FUNCTION
 00444          DISPLAY 'FROZ   FEEDBACK    IS   ' FROZ-FEEDBACK
 00445          DISPLAY 'FROZ AGCY KEY      IS    ' FA-TXCD
 00446                                              FA-AGCY
 00447          MOVE 16 TO RETURN-CODE
 00448          MOVE 'N' TO CHECK-PROCESSING
 00449      ELSE
 00450          ADD +1 TO FROZEN-RECS-ADDED
 00451          MOVE FA-TXCD TO ADD-MSG-TAX-CODE
 00452          MOVE ADD-MSG TO PL1-MSG.
 00453      SKIP1
 00454  070-PRINT-DETAIL-FROZ-REC.
 00455      MOVE FA-TXCD TO PL1-TAX-CODE.
 00456      MOVE FA-AGCY TO PL1-AGENCY.
 00457      INSPECT PL1-AGENCY REPLACING ALL ',' BY '-'.
 00458      MOVE FA-TXRTE TO PL1-TAX-RATE.
 00459      MOVE FA-TIFAV TO PL1-PY-FR-EQ-VAL.
 00460      MOVE FA-TIFEV TO PL1-DIFF-EQ-VAL.
 00461      MOVE FA-CURREV TO PL1-CURR-EQ-VAL.
 00462      IF LINE-CNTR > 60
 00463          PERFORM 080-WRITE-HEADERS
 00464      END-IF.
 00465      WRITE PRINT-REC FROM PRINT-LINE-1 AFTER ADVANCING 1.
 00466      ADD +1 TO PRINT-RECS-WRITTEN
 00467                LINE-CNTR.
 00468      SKIP1
 00469  080-WRITE-HEADERS.
 00470      ADD +1 TO PAGE-CNTR.
 00471      MOVE PAGE-CNTR TO HL1-PAGE-NO.
 00472      WRITE PRINT-REC FROM HEADER-LINE-1 AFTER ADVANCING PAGE.
 00473      WRITE PRINT-REC FROM HEADER-LINE-2 AFTER ADVANCING 2.
 00474      WRITE PRINT-REC FROM HEADER-LINE-3 AFTER ADVANCING 2.
 00475      WRITE PRINT-REC FROM HEADER-LINE-4 AFTER ADVANCING 1.
 00476      WRITE PRINT-REC FROM BLANK-LINE AFTER ADVANCING 1.
 00477      MOVE +7 TO LINE-CNTR.
 00478      SKIP1
 00479  090-WRITE-BLANK-LINE.
 00480      WRITE PRINT-REC FROM BLANK-LINE AFTER ADVANCING 1.
