 *          DATA SET ASHMA361E  AT LEVEL 020 AS OF 05/30/18
 MACRO
 * 02/05/2013 TJM CHANGED NPHEB TO UNSIGNED.
 * DATA DESCRIPTION
 * MASTER U
 FILE FILEA
   VOLA        2   2 P
   PROPA       4   8 P     HEADING ('PIN')
   TAXCDA     14   3 P
   SRFRZA     57   5 P 0   HEADING ('SF AMT')
 * HOMEOWNER INPUT
 FILE FILEB
   VOLB        1   2 P
   PROPB       3   8 P   HEADING ('PIN') MASK('Z99-99-999-999-9999')
   STAT       21   2 A
 * NPHEB      48   5 P 0 HEADING ('H/O AMT')
   NPHEB      48   5 P   HEADING ('H/O AMT')
 * HOMEOWNER OUTPUT
 FILE FILEC

 PARM-DATA  W 6 A
   PARM-LENGTH  PARM-DATA     2 B 0
   PARM-AMT     PARM-DATA  +2 5 N

 * WORKING STORAGE FIELDS
  MINCNT    W 4 P
  HINCNT    W 4 P
  MTCCNT    W 4 P
  OUTCNT    W 4 P
  UPDCNT    W 4 P
  CPCNT     W 4 P
  UNCNT     W 4 P
  WTXCD     W 5 N
  WTOWN  WTXCD +0 2 N      HEADING ('TOWN')
 *
 JOB INPUT (FILEA KEY(VOLA PROPA) +
            FILEB KEY(VOLB PROPB)) START (CHK-PARM) FINISH EOJ
 IF FILEA
    MINCNT = MINCNT + 1
 END-IF
 IF FILEB
    HINCNT = HINCNT + 1
 END-IF
 IF MATCHED
    MTCCNT = MTCCNT + 1
    IF SRFRZA > 0
       IF NPHEB > PARM-AMT
          IF STAT = 'CP'
             PUT FILEC FROM FILEB
             CPCNT  = CPCNT + 1
             OUTCNT = OUTCNT + 1
          ELSE
             WTXCD  =  TAXCDA
             PRINT RPT
             NPHEB = PARM-AMT
             UPDCNT = UPDCNT + 1
             PUT FILEC FROM FILEB
             OUTCNT   = OUTCNT + 1
             GOTO JOB
          END-IF
       ELSE
          PUT FILEC FROM FILEB
          OUTCNT = OUTCNT + 1
       END-IF
    ELSE
      PUT FILEC FROM FILEB
      OUTCNT = OUTCNT + 1
    END-IF
 ELSE
    IF FILEB
       OUTCNT = OUTCNT + 1
       PUT FILEC FROM FILEB
    END-IF
 END-IF

 CHK-PARM. PROC.
 %GETPARM PARM-DATA 2
 CALL EZTPX01 USING(PARM-REGISTER PARM-DATA)
 IF PARM-LENGTH EQ 5
   CONTINUE
 ELSE
   DISPLAY 'INVALID PARM LENGTH  ' PARM-LENGTH
 END-IF.
 IF PARM-AMT NUMERIC AND PARM-AMT > 0
   CONTINUE
 ELSE
   DISPLAY 'INVALID H/O AMOUNT   ' PARM-AMT
 END-IF.
 END-PROC

 EOJ. PROC
 DISPLAY 'TOTALS FOR ASHMA361 **********'
 DISPLAY 'PARM AMOUNT   '   PARM-AMT
 DISPLAY ' '
 DISPLAY 'MATCHED RECORDS             ' MTCCNT
 DISPLAY 'UPDATED RECORDS             ' UPDCNT
 DISPLAY ' '
 DISPLAY ' '
 DISPLAY 'MASTER U RECS. READ       = ' MINCNT
 DISPLAY 'H/O      RECS. READ       = ' HINCNT
 DISPLAY 'MATCHED        RECORDS    = ' MTCCNT
 DISPLAY 'COOP STAT      RECORDS    = ' CPCNT
 DISPLAY 'H/O      RECS. UPDATED    = ' UPDCNT
 DISPLAY 'H/O      RECS. WRITTEN    = ' OUTCNT
 END-PROC
 REPORT RPT
 SEQUENCE
 TITLE 1 '                         ASHMA361             '
 TITLE 2 '           OFFICE OF THE COOK COUNTY ASSESSOR'
 TITLE 3 '      SENIOR FREEZE RECORDS MATCHED TO THE H/O FILE'
 TITLE 4 '     RECORDS WITH SENIOR FREEZE AND H/O > ' PARM-AMT
 LINE 1 WTOWN PROPB STAT SRFRZA NPHEB
