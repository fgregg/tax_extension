 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID. ASHMA550.
 00003  AUTHOR.     PROGRAMMER - WARD MARTIN
 00004              ANALYST    - STAN MOLIS.
 00005  DATE-WRITTEN. MAY 26,1978.
 00006 *REMARKS.
 00007 *            THIS PROGRAM UPDATES ASSESSOR'S MASTER FILES WITH
 00008 *            HOMESTEAD AND VETERAN INDICATORS.
 00009 *
 00010  ENVIRONMENT DIVISION.
 00011  CONFIGURATION SECTION.
 00012  SOURCE-COMPUTER. IBM-370.
 00013  OBJECT-COMPUTER. IBM-370.
 00014  INPUT-OUTPUT SECTION.
 00015  FILE-CONTROL.
 00016      SELECT MASTER-IN ASSIGN TO UT-S-MASTIN.
 00017      SELECT MASTER-OUT ASSIGN TO UT-S-MASTOUT.
 00018      SELECT VET-MASTER ASSIGN TO UT-S-VETMAST.
 00019      SELECT V-HOMESTEAD ASSIGN TO UT-S-HOMESTD.
 00020      SELECT PRNTFILE ASSIGN TO UT-S-PRNTFILE.
 00021      EJECT
 00022  DATA DIVISION.
 00023  FILE SECTION.
 00024  FD  MASTER-IN
 00025      BLOCK CONTAINS 0 CHARACTERS
 00026      RECORD CONTAINS 122 TO 18706 CHARACTERS
 00027      LABEL RECORDS ARE STANDARD
 00028      RECORDING MODE IS S
 00029      DATA RECORD IS MIN-REC.
 00030  01  MIN-REC.
 00031  ++INCLUDE ASREASRD01
 00032  FD  MASTER-OUT
 00033      BLOCK CONTAINS 0 CHARACTERS
 00034      RECORD CONTAINS 122 TO 18706 CHARACTERS
 00035      LABEL RECORDS ARE STANDARD
 00036      RECORDING MODE IS S
 00037      DATA RECORD IS MOUT-REC.
 00038  01  MOUT-REC.
 00039  ++INCLUDE ASREASRD02
 00040      SKIP2
 00041  ++INCLUDE FDVETMST01
 00042      SKIP2
 00043  ++INCLUDE FDVSAMHMST
 00044      SKIP2
 00045  FD  PRNTFILE
 00046      LABEL RECORDS ARE STANDARD
 00047      BLOCK CONTAINS 0 RECORDS
 00048      RECORD CONTAINS 133 CHARACTERS.
 00049  01  PRNT-REC          PIC X(133).
 00050      EJECT
 00051  WORKING-STORAGE SECTION.
 00052  77  LINE-CNT          PIC S999    COMP-3  VALUE +59.
 00053  77  PAGE-NO           PIC S9(5)   COMP-3  VALUE ZERO.
 00054  77  CNT-MASTIN        PIC S9(7)   COMP-3  VALUE ZERO.
 00055  77  CNT-MASTOUT       PIC S9(7)   COMP-3  VALUE ZERO.
 00056  77  CNT-MST-UPD-HMA   PIC S9(7)   COMP-3  VALUE ZERO.
 00057  77  CNT-MST-UPD-VET   PIC S9(7)   COMP-3  VALUE ZERO.
 00058  77  CNT-NOMATCH-HMA   PIC S9(7)   COMP-3  VALUE ZERO.
 00059  77  CNT-NOMATCH-VET   PIC S9(7)   COMP-3  VALUE ZERO.
 00060  77  CNT-EXRR-HMA      PIC S9(7)   COMP-3  VALUE ZERO.
 00061  77  CNT-EXRR-VET      PIC S9(7)   COMP-3  VALUE ZERO.
 00062  77  SAVE-KEY          PIC X(20)           VALUE LOW-VALUES.
 00063  77  VET-EOF-SW        PIC X               VALUE 'N'.
 00064      88 VET-EOF       VALUE 'Y'.
 00065  77  HMA-EOF-SW        PIC X               VALUE 'N'.
 00066      88 HMA-EOF       VALUE 'Y'.
 00067  77  MST-EOF-SW        PIC X               VALUE 'N'.
 00068      88 MST-EOF       VALUE 'Y'.
 00069  77  SEQ-ERR-SW        PIC X               VALUE 'N'.
 00070      88 SEQ-ERR       VALUE 'Y'.
 00071  77  PARM-ERR-SW       PIC X               VALUE 'N'.
 00072      88 PARM-ERR      VALUE 'Y'.
 00073  77  END-SW            PIC X               VALUE 'N'.
 00074      88 FINISHED      VALUE 'Y'.
 00075  77  READ-HMA-SW       PIC X               VALUE 'N'.
 00076      88 READ-HMA      VALUE 'Y'.
 00077  77  READ-VET-SW       PIC X               VALUE 'N'.
 00078      88 READ-VET      VALUE 'Y'.
 00079  77  READ-MST-SW       PIC X               VALUE 'Y'.
 00080      88 READ-MST      VALUE 'Y'.
 00081  77  STATUS-CHECK      PIC X               VALUE SPACE.
 00082      88 EXEMPT        VALUE '1'.
 00083      88 RAILROAD      VALUE '2'.
 00084      88 HM-NON-COOP   VALUE '3'.
 00085      88 HM-COOP       VALUE '5'.
 00086  77  REC-CODE-CHECK    PIC X               VALUE SPACE.
 00087      88 NON-COOP      VALUE '1'.
 00088      88 COOP          VALUE '2'.
 00089  77  MSG-NO            PIC S9999 COMP SYNC VALUE ZERO.
 00090      88 MOVE-FROM-HMA VALUES +5 THRU +7.
 00091  77  SUB               PIC S9999 COMP SYNC VALUE ZERO.
 00092  77  STATUS-RESET      PIC X               VALUE '0'.
 00093  77  STATUS-NONCOOP    PIC X               VALUE '3'.
 00094  77  STATUS-VET        PIC X               VALUE '4'.
 00095  77  STATUS-COOP       PIC X               VALUE '5'.
 00096  77  TOTMSG-1          PIC X(25)           VALUE
 00097      'TOTAL MASTER RECORDS READ'.
 00098  77  TOTMSG-2          PIC X(28)           VALUE
 00099      'TOTAL MASTER RECORDS WRITTEN'.
 00100  77  TOTMSG-3          PIC X(54)           VALUE
 00101      'TOTAL MASTER RECORDS UPDATED WITH HOMESTEAD INDICATORS'.
 00102  77  TOTMSG-4          PIC X(52)           VALUE
 00103      'TOTAL MASTER RECORDS UPDATED WITH VETERAN INDICATORS'.
 00104  77  TOTMSG-5          PIC X(42)           VALUE
 00105      'TOTAL NO-MATCHING MASTER RECORDS-HOMESTEAD'.
 00106  77  TOTMSG-6          PIC X(41)           VALUE
 00107      'TOTAL NO-MATCHING MASTER RECORDS-VETERANS'.
 00108  77  TOTMSG-7          PIC X(39)           VALUE
 00109      'TOTAL EXEMPT/RAILROAD-HOMESTEAD RECORDS'.
 00110  77  TOTMSG-8          PIC X(37)           VALUE
 00111      'TOTAL EXEMPT/RAILROAD-VETERAN RECORDS'.
 00112  01  TABLES-AND-WORK-AREA.
 00113  ++INCLUDE TWNINFOTBL
 00114      EJECT
 00115      05 MSG-TABLE.
 00116         10 FILLER      PIC X(34)           VALUE
 00117         'HOME/VET-PARCEL IS EXEMPT'.
 00118         10 FILLER      PIC X(34)           VALUE
 00119         'HOME/VET-PARCEL IS RAILROAD'.
 00120         10 FILLER      PIC X(34)           VALUE
 00121         'HOME/VET PRESENT'.
 00122         10 FILLER      PIC X(34)           VALUE
 00123         'HOME/VET-NO MATCHING MASTER RECORD'.
 00124         10 FILLER      PIC X(34)           VALUE
 00125         'HOME-PARCEL IS EXEMPT'.
 00126         10 FILLER      PIC X(34)           VALUE
 00127         'HOME-PARCEL IS RAILROAD'.
 00128         10 FILLER      PIC X(34)           VALUE
 00129         'HOME-NO MATCHING MASTER RECORD'.
 00130         10 FILLER      PIC X(34)           VALUE
 00131         'VET-PARCEL IS EXEMPT'.
 00132         10 FILLER      PIC X(34)           VALUE
 00133         'VET-PARCEL IS RAILROAD'.
 00134         10 FILLER      PIC X(34)           VALUE
 00135         'VET-NO MATCHING MASTER RECORD'.
 00136      05 MSG REDEFINES MSG-TABLE OCCURS 10 TIMES PIC X(34).
 00137      SKIP2
 00138      05 PREV-HMA-KEY.
 00139         10 PREV-HMA-TOWN    PIC 99            VALUE ZERO.
 00140         10 PREV-HMA-VOL     PIC 9(3)          VALUE ZERO.
 00141         10 PREV-HMA-PROP    PIC 9(15)         VALUE ZERO.
 00142      05 PREV-MST-KEY.
 00143         10 PREV-MST-TOWN    PIC 99            VALUE ZERO.
 00144         10 PREV-MST-VOL     PIC 9(3)          VALUE ZERO.
 00145         10 PREV-MST-PROP    PIC 9(15)         VALUE ZERO.
 00146      05 PREV-VET-KEY.
 00147         10 PREV-VET-TOWN    PIC 99            VALUE ZERO.
 00148         10 PREV-VET-VOL     PIC 9(3)          VALUE ZERO.
 00149         10 PREV-VET-PROP    PIC 9(15)         VALUE ZERO.
 00150      05 CURR-HMA-KEY.
 00151         10 CURR-HMA-TOWN    PIC 99            VALUE ZERO.
 00152         10 CURR-HMA-VOL     PIC 9(3)          VALUE ZERO.
 00153         10 CURR-HMA-PROP    PIC 9(15)         VALUE ZERO.
 00154      05 CURR-VET-KEY.
 00155         10 CURR-VET-TOWN    PIC 99            VALUE ZERO.
 00156         10 CURR-VET-VOL     PIC 9(3)          VALUE ZERO.
 00157         10 CURR-VET-PROP    PIC 9(15)         VALUE ZERO.
 00158      05 CURR-MST-KEY.
 00159         10 CURR-MST-TOWN    PIC 99            VALUE ZERO.
 00160         10 CURR-MST-VOL     PIC 9(3)          VALUE ZERO.
 00161         10 CURR-MST-PROP    PIC 9(15)         VALUE ZERO.
 00162      05 TXCD-UNPK      PIC 9(5)            VALUE ZERO.
 00163      05 FILLER REDEFINES TXCD-UNPK.
 00164         10 TXCD-TOWN     PIC 99.
 00165         10 FILLER        PIC XXX.
 00166      EJECT
 00167  01  PRINT-AREAS.
 00168      05 H-1.
 00169         10 FILLER      PIC XXX             VALUE SPACE.
 00170         10 H-DATE      PIC X(8).
 00171         10 FILLER      PIC X(44)           VALUE SPACE.
 00172         10 FILLER      PIC X(66)           VALUE
 00173         'OFFICE OF THE ASSESSOR'.
 00174         10 FILLER      PIC X(4)            VALUE 'PAGE'.
 00175         10 H-PAGE      PIC ZZ,ZZ9.
 00176      05 H-2.
 00177         10 FILLER      PIC X(46)           VALUE '   ASHMA550'.
 00178         10 FILLER      PIC X(44)           VALUE
 00179         'VETERAN/HOMESTEAD INDICATOR UPDATE REPORT - '.
 00180         10 H-TOWN      PIC X(13).
 00181      05 H-3.
 00182         10 FILLER      PIC X(49)           VALUE
 00183         ' VOL   PROPERTY NUMBER   NAME'.
 00184         10 FILLER      PIC X(24)           VALUE 'ADDRESS'.
 00185         10 FILLER      PIC X(13)           VALUE 'CITY'.
 00186         10 FILLER      PIC X(7)            VALUE 'ST  ZIP'.
 00187      05 P-LINE.
 00188         10 FILLER      PIC X.
 00189         10 P-VOL       PIC 999.
 00190         10 FILLER      PIC X               VALUE SPACE.
 00191         10 P-PROP      PIC Z99,99,999,999,9999.
 00192         10 FILLER      PIC X               VALUE SPACE.
 00193         10 P-NAME      PIC X(22).
 00194         10 FILLER      PIC XX              VALUE SPACE.
 00195         10 P-ADDR      PIC X(22).
 00196         10 FILLER      PIC XX              VALUE SPACE.
 00197         10 P-CITY      PIC X(12).
 00198         10 FILLER      PIC X               VALUE SPACE.
 00199         10 P-STATE     PIC XX.
 00200         10 FILLER      PIC X               VALUE SPACE.
 00201         10 P-ZIP       PIC X(5).
 00202         10 FILLER      PIC XX              VALUE SPACE.
 00203         10 P-MSG       PIC X(34).
 00204      05 T-LINE.
 00205         10 FILLER      PIC XXX             VALUE SPACE.
 00206         10 T-MSG       PIC X(54).
 00207         10 FILLER      PIC X               VALUE SPACE.
 00208         10 T-CNT       PIC Z,ZZZ,ZZ9.
 00209  PROCEDURE DIVISION.
 00210      MOVE    CURRENT-DATE TO H-DATE.
 00211      MOVE    SPACES TO H-TOWN.
 00212      OPEN    INPUT V-HOMESTEAD.
 00213      OPEN    INPUT MASTER-IN.
 00214      OPEN    INPUT VET-MASTER.
 00215      OPEN    OUTPUT MASTER-OUT.
 00216      OPEN    OUTPUT PRNTFILE.
 00217      PERFORM 300-READ-HMA THRU 310-EXIT.
 00218      MOVE    CURR-HMA-KEY TO SAVE-KEY.
 00219      MOVE    ZERO TO TXCD-UNPK.
 00220      PERFORM 400-READ-VET THRU 410-EXIT.
 00221      PERFORM 100-MAINLINE THRU 110-EXIT
 00222              UNTIL FINISHED.
 00223      IF      LINE-CNT GREATER THAN 48
 00224              PERFORM 800-HEADINGS THRU 810-EXIT.
 00225      PERFORM 900-FINAL-TOTALS THRU 910-EXIT.
 00226      CLOSE   MASTER-IN
 00227              MASTER-OUT
 00228              V-HOMESTEAD
 00229              VET-MASTER
 00230              PRNTFILE.
 00231      STOP    RUN.
 00232      EJECT
 00233  100-MAINLINE.
 00234      IF      READ-MST
 00235              PERFORM 200-READ-MST THRU 210-EXIT.
 00236      IF      READ-HMA
 00237              PERFORM 300-READ-HMA THRU 310-EXIT UNTIL
 00238                CURR-HMA-KEY NOT EQUAL SAVE-KEY
 00239              MOVE CURR-HMA-KEY TO SAVE-KEY.
 00240      IF      READ-VET
 00241              PERFORM 400-READ-VET THRU 410-EXIT.
 00242      IF      SEQ-ERR OR MST-EOF
 00243              MOVE 'Y' TO END-SW
 00244      ELSE
 00245              IF CURR-MST-KEY LESS THAN CURR-HMA-KEY  AND
 00246                 CURR-MST-KEY LESS THAN CURR-VET-KEY
 00247                 PERFORM 510-UNMATCHED-MST THRU 515-EXIT
 00248              ELSE
 00249                IF CURR-MST-KEY EQUAL CURR-HMA-KEY  AND
 00250                   CURR-MST-KEY LESS THAN CURR-VET-KEY
 00251                   PERFORM 520-MATCHED-MST-HMA THRU 525-EXIT
 00252                ELSE
 00253                  IF CURR-MST-KEY EQUAL CURR-VET-KEY  AND
 00254                     CURR-MST-KEY LESS THAN CURR-HMA-KEY
 00255                     PERFORM 530-MATCHED-MST-VET THRU 535-EXIT
 00256                  ELSE
 00257                    IF CURR-MST-KEY EQUAL CURR-HMA-KEY  AND
 00258                       CURR-MST-KEY EQUAL CURR-VET-KEY
 00259                       PERFORM 540-ALL-MATCH THRU 545-EXIT
 00260                    ELSE
 00261                      IF CURR-HMA-KEY LESS THAN CURR-MST-KEY  AND
 00262                         CURR-HMA-KEY LESS THAN CURR-VET-KEY
 00263                         PERFORM 550-UNMATCHED-HMA THRU 555-EXIT
 00264                      ELSE
 00265                        IF CURR-VET-KEY LESS THAN CURR-MST-KEY AND
 00266                           CURR-VET-KEY LESS THAN CURR-HMA-KEY
 00267                           PERFORM 560-UNMATCHED-VET THRU 565-EXIT
 00268                        ELSE
 00269                           PERFORM 570-MATCHED-HMA-VET THRU
 00270                             575-EXIT.
 00271  110-EXIT.   EXIT.
 00272      EJECT
 00273  200-READ-MST.
 00274      READ    MASTER-IN AT END
 00275              MOVE 'Y' TO MST-EOF-SW
 00276              MOVE 'N' TO READ-MST-SW
 00277              MOVE HIGH-VALUES TO CURR-MST-KEY.
 00278      IF      NOT MST-EOF
 00279              ADD 1 TO CNT-MASTIN
 00280              MOVE M-TXCD OF MIN-REC TO TXCD-UNPK
 00281              MOVE TXCD-TOWN         TO CURR-MST-TOWN
 00282              MOVE M-VOL  OF MIN-REC TO CURR-MST-VOL
 00283              MOVE M-PROP OF MIN-REC TO CURR-MST-PROP
 00284              IF CURR-MST-KEY LESS THAN PREV-MST-KEY
 00285                 DISPLAY 'MASTER FILE OUT OF SEQUENCE'
 00286                 DISPLAY 'CURR. TOWN-VOL-PROP ' CURR-MST-TOWN SPACE
 00287                                                CURR-MST-VOL SPACE
 00288                                                CURR-MST-PROP
 00289                 DISPLAY 'PREV. TOWN-VOL-PROP ' PREV-MST-TOWN SPACE
 00290                                                PREV-MST-VOL SPACE
 00291                                                PREV-MST-PROP
 00292                 MOVE 16 TO RETURN-CODE
 00293                 MOVE 'Y' TO SEQ-ERR-SW
 00294              ELSE
 00295                 MOVE M-STAT2 OF MIN-REC TO STATUS-CHECK
 00296                 MOVE CURR-MST-KEY TO PREV-MST-KEY.
 00297  210-EXIT.   EXIT.
 00298      SKIP2
 00299  300-READ-HMA.
 00300      READ    V-HOMESTEAD AT END
 00301              MOVE 'Y' TO HMA-EOF-SW
 00302              MOVE 'N' TO READ-HMA-SW
 00303              MOVE HIGH-VALUES TO CURR-HMA-KEY.
 00304      IF      NOT HMA-EOF
 00305              MOVE V-TXCD    TO TXCD-UNPK
 00306              MOVE TXCD-TOWN TO CURR-HMA-TOWN
 00307              MOVE V-VOL     TO CURR-HMA-VOL
 00308              MOVE V-PROP    TO CURR-HMA-PROP
 00309              IF CURR-HMA-KEY LESS THAN PREV-HMA-KEY
 00310                 DISPLAY 'HOMESTEAD FILE OUT OF SEQUENCE'
 00311                 DISPLAY 'CURR. TOWN-VOL-PROP ' CURR-HMA-TOWN SPACE
 00312                                                CURR-HMA-VOL SPACE
 00313                                                CURR-HMA-PROP
 00314                 DISPLAY 'PREV. TOWN-VOL-PROP ' PREV-HMA-TOWN SPACE
 00315                                                PREV-HMA-VOL SPACE
 00316                                                PREV-HMA-PROP
 00317                 MOVE 16 TO RETURN-CODE
 00318                 MOVE 'Y' TO SEQ-ERR-SW
 00319              ELSE
 00320                    MOVE V-REC-CD TO REC-CODE-CHECK
 00321                    MOVE CURR-HMA-KEY TO PREV-HMA-KEY.
 00322  310-EXIT.   EXIT.
 00323      SKIP2
 00324  400-READ-VET.
 00325      READ    VET-MASTER AT END
 00326              MOVE 'Y' TO VET-EOF-SW
 00327              MOVE 'N' TO READ-VET-SW
 00328              MOVE HIGH-VALUES TO CURR-VET-KEY.
 00329      IF      NOT VET-EOF
 00330              MOVE VET-TWNCD TO CURR-VET-TOWN
 00331              MOVE VET-VOL   TO CURR-VET-VOL
 00332              MOVE VET-PROP  TO CURR-VET-PROP
 00333              IF CURR-VET-KEY LESS THAN PREV-VET-KEY
 00334                 DISPLAY 'VETERANS FILE OUT OF SEQUENCE'
 00335                 DISPLAY 'CURR. TOWN-VOL-PROP ' CURR-VET-TOWN SPACE
 00336                                                CURR-VET-VOL SPACE
 00337                                                CURR-VET-PROP
 00338                 DISPLAY 'PREV. TOWN-VOL-PROP ' PREV-VET-TOWN SPACE
 00339                                                PREV-VET-VOL SPACE
 00340                                                PREV-VET-PROP
 00341                 MOVE 16 TO RETURN-CODE
 00342                 MOVE 'Y' TO SEQ-ERR-SW
 00343              ELSE
 00344                    MOVE CURR-VET-KEY TO PREV-VET-KEY.
 00345  410-EXIT.   EXIT.
 00346      EJECT
 00347  510-UNMATCHED-MST.
 00348      IF      NOT EXEMPT AND NOT RAILROAD
 00349              MOVE STATUS-RESET TO M-STAT2 OF MIN-REC.
 00350      PERFORM 600-WRITE-MST THRU 610-EXIT.
 00351      MOVE    'Y' TO READ-MST-SW.
 00352      MOVE    'N' TO READ-HMA-SW READ-VET-SW.
 00353  515-EXIT.   EXIT.
 00354      SKIP2
 00355  520-MATCHED-MST-HMA.
 00356      IF      EXEMPT
 00357              ADD 1 TO CNT-EXRR-HMA
 00358              MOVE 5 TO MSG-NO
 00359      ELSE
 00360              IF RAILROAD
 00361                 ADD 1 TO CNT-EXRR-HMA
 00362                 MOVE 6 TO MSG-NO
 00363              ELSE
 00364                 IF NON-COOP
 00365                    ADD 1 TO CNT-MST-UPD-HMA
 00366                    MOVE STATUS-NONCOOP TO M-STAT2 OF MIN-REC
 00367                 ELSE
 00368                    IF COOP
 00369                       ADD 1 TO CNT-MST-UPD-HMA
 00370                       MOVE STATUS-COOP TO M-STAT2 OF MIN-REC.
 00371      PERFORM 600-WRITE-MST THRU 610-EXIT.
 00372      IF      MSG-NO NOT EQUAL ZERO
 00373              PERFORM 700-PRINT THRU 710-EXIT.
 00374      MOVE    'Y' TO READ-HMA-SW READ-MST-SW.
 00375      MOVE    'N' TO READ-VET-SW.
 00376  525-EXIT.   EXIT.
 00377      SKIP2
 00378  530-MATCHED-MST-VET.
 00379      IF      EXEMPT
 00380              ADD 1 TO CNT-EXRR-VET
 00381              MOVE 8 TO MSG-NO
 00382      ELSE
 00383              IF RAILROAD
 00384                 ADD 1 TO CNT-EXRR-VET
 00385                 MOVE 9 TO MSG-NO
 00386              ELSE
 00387                 ADD 1 TO CNT-MST-UPD-VET
 00388                 MOVE STATUS-VET TO M-STAT2 OF MIN-REC.
 00389      PERFORM 600-WRITE-MST THRU 610-EXIT.
 00390      IF      MSG-NO NOT EQUAL ZERO
 00391              PERFORM 700-PRINT THRU 710-EXIT.
 00392      MOVE    'Y' TO READ-VET-SW READ-MST-SW.
 00393      MOVE    'N' TO READ-HMA-SW.
 00394  535-EXIT.   EXIT.
 00395      SKIP2
 00396  540-ALL-MATCH.
 00397      IF      EXEMPT
 00398              ADD 1 TO CNT-EXRR-HMA CNT-EXRR-VET
 00399              MOVE 1 TO MSG-NO
 00400      ELSE
 00401              IF RAILROAD
 00402                 ADD 1 TO CNT-EXRR-HMA CNT-EXRR-VET
 00403                 MOVE 2 TO MSG-NO
 00404              ELSE
 00405                 ADD 1 TO CNT-MST-UPD-VET
 00406                 MOVE 3 TO MSG-NO
 00407                 MOVE STATUS-VET TO M-STAT2 OF MIN-REC.
 00408      PERFORM 600-WRITE-MST THRU 610-EXIT.
 00409      PERFORM 700-PRINT THRU 710-EXIT.
 00410      MOVE    'Y' TO READ-HMA-SW READ-VET-SW READ-MST-SW.
 00411  545-EXIT.   EXIT.
 00412      SKIP2
 00413  550-UNMATCHED-HMA.
 00414      ADD     1 TO CNT-NOMATCH-HMA.
 00415      MOVE    7 TO MSG-NO.
 00416      PERFORM 700-PRINT THRU 710-EXIT.
 00417      MOVE    'Y' TO READ-HMA-SW.
 00418      MOVE    'N' TO READ-VET-SW READ-MST-SW.
 00419  555-EXIT.   EXIT.
 00420      SKIP2
 00421  560-UNMATCHED-VET.
 00422      ADD     1 TO CNT-NOMATCH-VET.
 00423      MOVE    10 TO MSG-NO.
 00424      PERFORM 700-PRINT THRU 710-EXIT.
 00425      MOVE    'Y' TO READ-VET-SW.
 00426      MOVE    'N' TO READ-HMA-SW READ-MST-SW.
 00427  565-EXIT.   EXIT.
 00428      SKIP2
 00429  570-MATCHED-HMA-VET.
 00430      ADD     1 TO CNT-NOMATCH-HMA CNT-NOMATCH-VET.
 00431      MOVE    4 TO MSG-NO.
 00432      PERFORM 700-PRINT THRU 710-EXIT.
 00433      MOVE    'Y' TO READ-HMA-SW READ-VET-SW.
 00434      MOVE    'N' TO READ-MST-SW.
 00435  575-EXIT.   EXIT.
 00436      EJECT
 00437  600-WRITE-MST.
 00438      MOVE    M-REC-CTRS TO M-REC-CTRS-1.
 00439      MOVE    MIN-REC TO MOUT-REC.
 00440      WRITE   MOUT-REC.
 00441      ADD     1 TO CNT-MASTOUT.
 00442  610-EXIT.   EXIT.
 00443      SKIP2
 00444  700-PRINT.
 00445      IF      LINE-CNT GREATER THAN 58
 00446              PERFORM 800-HEADINGS THRU 810-EXIT.
 00447      IF      MOVE-FROM-HMA
 00448              MOVE V-VOL    TO P-VOL
 00449              MOVE V-PROP   TO P-PROP
 00450              MOVE V-NAME   TO P-NAME
 00451              MOVE V-ADDR   TO P-ADDR
 00452              MOVE V-CITY   TO P-CITY
 00453              MOVE V-STATE  TO P-STATE
 00454              MOVE V-ZIP    TO P-ZIP
 00455      ELSE
 00456              MOVE VET-VOL    TO P-VOL
 00457              MOVE VET-PROP   TO P-PROP
 00458              MOVE VET-NAME   TO P-NAME
 00459              MOVE VET-ADDR   TO P-ADDR
 00460              MOVE VET-CITY   TO P-CITY
 00461              MOVE VET-STATE  TO P-STATE
 00462              MOVE VET-ZIP     TO P-ZIP.
 00463      EXAMINE P-PROP REPLACING ALL ',' BY '-'.
 00464      MOVE    MSG (MSG-NO) TO P-MSG.
 00465      WRITE   PRNT-REC FROM P-LINE AFTER POSITIONING 2.
 00466      ADD     2 TO LINE-CNT.
 00467      MOVE    ZERO TO MSG-NO.
 00468  710-EXIT.   EXIT.
 00469      SKIP2
 00470  800-HEADINGS.
 00471      ADD     1 TO PAGE-NO.
 00472      MOVE    PAGE-NO TO H-PAGE.
 00473      WRITE   PRNT-REC FROM H-1 AFTER POSITIONING 0.
 00474      WRITE   PRNT-REC FROM H-2 AFTER POSITIONING 2.
 00475      WRITE   PRNT-REC FROM H-3 AFTER POSITIONING 2.
 00476      MOVE    5 TO LINE-CNT.
 00477  810-EXIT.   EXIT.
 00478      EJECT
 00479  900-FINAL-TOTALS.
 00480      DISPLAY SPACE.
 00481      MOVE    TOTMSG-1 TO T-MSG.
 00482      MOVE    CNT-MASTIN TO T-CNT.
 00483      WRITE   PRNT-REC FROM T-LINE AFTER POSITIONING 3.
 00484      DISPLAY T-LINE.
 00485      MOVE    TOTMSG-2 TO T-MSG.
 00486      MOVE    CNT-MASTOUT TO T-CNT.
 00487      WRITE   PRNT-REC FROM T-LINE AFTER POSITIONING 1.
 00488      DISPLAY T-LINE.
 00489      MOVE    TOTMSG-3 TO T-MSG.
 00490      MOVE    CNT-MST-UPD-HMA TO T-CNT.
 00491      WRITE   PRNT-REC FROM T-LINE AFTER POSITIONING 1.
 00492      DISPLAY T-LINE.
 00493      MOVE    TOTMSG-4 TO T-MSG.
 00494      MOVE    CNT-MST-UPD-VET TO T-CNT.
 00495      WRITE   PRNT-REC FROM T-LINE AFTER POSITIONING 1.
 00496      DISPLAY T-LINE.
 00497      MOVE    TOTMSG-5 TO T-MSG.
 00498      MOVE    CNT-NOMATCH-HMA TO T-CNT.
 00499      WRITE   PRNT-REC FROM T-LINE AFTER POSITIONING 1.
 00500      DISPLAY T-LINE.
 00501      MOVE    TOTMSG-6 TO T-MSG.
 00502      MOVE    CNT-NOMATCH-VET TO T-CNT.
 00503      WRITE   PRNT-REC FROM T-LINE AFTER POSITIONING 1.
 00504      DISPLAY T-LINE.
 00505      MOVE    TOTMSG-7 TO T-MSG.
 00506      MOVE    CNT-EXRR-HMA TO T-CNT.
 00507      WRITE   PRNT-REC FROM T-LINE AFTER POSITIONING 1.
 00508      DISPLAY T-LINE.
 00509      MOVE    TOTMSG-8 TO T-MSG.
 00510      MOVE    CNT-EXRR-VET TO T-CNT.
 00511      WRITE   PRNT-REC FROM T-LINE AFTER POSITIONING 1.
 00512      DISPLAY T-LINE.
 00513  910-EXIT.   EXIT.
