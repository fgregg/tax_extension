 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID. ASHMA831.
 00003 *AUTHOR. KATHY LEE FOLAN - TOM MCMAHON.
 00004 *DATE-WRITTEN.   AUGUST 16, 2004.
 00005 *****************************************************************
 00006 *REMARKS.
 00007 *       THIS PROGRAM WILL POPULATE THE CURRENT TOTAL FIELD
 00008 *       (M-VALUE(9)) OF THE CURRENT ASSESSMENT MASTER.
 00009 *****************************************************************
 00010 *MODIFICATION.  JUNE 7, 2005.  NANCY T. FLEMING-KATHY LEE FOLAN.
 00011 *       MODIFY PROGRAM TO ADD AN INDICATOR PARAMETER TO THE
 00012 *       LINKAGE SECTION.
 00013 *****************************************************************
 00014
 00015  ENVIRONMENT DIVISION.
 00016  CONFIGURATION SECTION.
 00017  SOURCE-COMPUTER. IBM-370.
 00018  OBJECT-COMPUTER. IBM-370.
 00019  INPUT-OUTPUT SECTION.
 00020  FILE-CONTROL.
 00021      SELECT MASTER-IN           ASSIGN TO UT-S-MSTRIN.
 00022      SELECT MASTER2-IN          ASSIGN TO UT-S-MSTRIN2.
 00023      SELECT COE-FILE            ASSIGN TO UT-S-COEFILE.
 00024      SELECT MASTER-OUT-FILE     ASSIGN TO UT-S-MSTROUT.
 00025
 00026  DATA DIVISION.
 00027  FILE SECTION.
 00028  ++INCLUDE ASREASFD01
 00029  FD  MASTER2-IN
 00030      BLOCK  CONTAINS 0 CHARACTERS
 00031      RECORD CONTAINS 122 TO 18706 CHARACTERS
 00032      LABEL  RECORDS  ARE STANDARD
 00033      RECORDING MODE IS S
 00034      DATA RECORD IS MSTR2-IN-REC.
 00035  01  MSTR2-IN-REC.
 00036  ++INCLUDE ASREASRD08
 00037  FD  COE-FILE
 00038      LABEL RECORDS ARE STANDARD
 00039      BLOCK CONTAINS 0 RECORDS
 00040      RECORD CONTAINS 610 CHARACTERS
 00041      RECORDING MODE F
 00042      DATA RECORD COE-RECORD.
 00043  01  COE-RECORD.
 00044  ++INCLUDE ASCOEPRWR1
 00045   FD  MASTER-OUT-FILE
 00046       BLOCK  CONTAINS 0 CHARACTERS
 00047       RECORD CONTAINS 122 TO 18706 CHARACTERS
 00048       LABEL  RECORDS  ARE STANDARD
 00049       RECORDING MODE  IS  S
 00050       DATA RECORD IS MSTR-OUT-REC.
 00051   01  MSTR-OUT-REC.
 00052  ++INCLUDE ASREASRD03
 00053
 00054  WORKING-STORAGE SECTION.
 00055  77  CURRENT-DATE           PIC 99/99/9(4).
 00056  77  MDCY-OR-CYMD           PIC S9(5)V9(4) VALUE +10000.0001.
 00057  77  WS-MAST-IN-CNT         PIC S9(7) COMP-3 VALUE 0.
 00058  77  WS-MAST2-IN-CNT        PIC S9(7) COMP-3 VALUE 0.
 00059  77  WS-COE-CNT             PIC S9(7) COMP-3 VALUE 0.
 00060  77  WS-MAST-OUT-CNT        PIC S9(7) COMP-3 VALUE 0.
 00061  77  WS-UPDT-CNT            PIC S9(7) COMP-3 VALUE 0.
 00062  77  WS-UPDT-CNT-2          PIC S9(7) COMP-3 VALUE 0.
 00063  77  WS-M-VALUE             PIC S9(9) COMP-3 VALUE 0.
 00064  77  DISPLAY-HELP           PIC  9(9)        VALUE 0.
 00065  77  WS-HELP                PIC S9(9) COMP-3 VALUE 0.
 00066  77  WS-HELPX               PIC S9(9) COMP-3 VALUE 0.
 00067  77  WS-HELPY               PIC S9(9) COMP-3 VALUE 0.
 00068  77  WS-ERROR-SW            PIC X            VALUE 'N'.
 00069  77  WS-HELP-SW             PIC X            VALUE 'N'.
 00070      88  HELP-REC                            VALUE 'Y'.
 00071  77  WS-UPDT-SW             PIC X            VALUE 'N'.
 00072  77  WS-STAT-SW             PIC X            VALUE 'N'.
 00073  77  WS-MAST-EOF-SW         PIC X            VALUE 'N'.
 00074      88  MASTER-EOF                          VALUE 'Y'.
 00075  77  WS-COE-EOF-SW          PIC X            VALUE 'N'.
 00076      88  COE-EOF                             VALUE 'Y'.
 00077  77  WS-MAST2-EOF-SW        PIC X            VALUE 'N'.
 00078      88  MAST2-EOF                           VALUE 'Y'.
 00079  77  WS-PRIOR-SW            PIC X            VALUE 'N'.
 00080      88  GOT-PRIOR-RECORD                    VALUE 'Y'.
 00081  01  WS-RECORD.
 00082      05  WS-COE-KEY.
 00083          10  WS-COE-VOL     PIC S9(3)   COMP-3 VALUE 0.
 00084          10  WS-COE-PROP    PIC S9(15)  COMP-3 VALUE 0.
 00085          10  WS-COE-TYPE    PIC X              VALUE ' '.
 00086      05  WS-MST-KEY.
 00087          10  WS-MST-VOL     PIC S9(3)   COMP-3 VALUE 0.
 00088          10  WS-MST-PROP    PIC S9(15)  COMP-3 VALUE 0.
 00089          10  WS-MST-TYPE    PIC X              VALUE ' '.
 00090      05  WS-PRI-KEY.
 00091          10  WS-PRI-VOL     PIC S9(3)   COMP-3 VALUE 0.
 00092          10  WS-PRI-PROP    PIC S9(15)  COMP-3 VALUE 0.
 00093          10  WS-PRI-TYPE    PIC X              VALUE ' '.
 00094      05  CURRENT-DATE-RETURN.
 00095          10  SYSTEM-DATE PIC 9(8).
 00096
 00097  LINKAGE SECTION.
 00098  01  LK-RECORD.
 00099      05 LK-LENGTH      PIC S9(4)  BINARY.
 00100         88 VALID-LK-LGTH                          VALUE +12.
 00101      05 LK-MEDIAN-R    PIC X(6).
 00102      05 LK-MEDIAN   REDEFINES LK-MEDIAN-R    PIC 9V9(5).
 00103      05 LK-PRIOR-EQ-R  PIC X(5).
 00104      05 LK-PRIOR    REDEFINES LK-PRIOR-EQ-R  PIC 9V9(4).
 00105      05 LK-IND         PIC X.
 00106
 00107  PROCEDURE DIVISION USING LK-RECORD.
 00108  0000-HOUSEKEEPING.
 00109      PERFORM 1100-DATE THRU 1100-EXIT.
 00110      PERFORM 1200-CHECK-PARMS THRU 1200-EXIT.
 00111      IF RETURN-CODE = 16
 00112         STOP RUN.
 00113
 00114 *    OPEN INPUT MASTER-IN, COE-FILE,
 00115      OPEN INPUT MASTER-IN, MASTER2-IN, COE-FILE,
 00116           OUTPUT MASTER-OUT-FILE.
 00117      PERFORM 0200-READ-MASTER-IN THRU 0200-EXIT.
 00118      PERFORM 0300-READ-COE-FILE THRU 0300-EXIT
 00119 *       UNTIL WS-COE-KEY = M-VOLPROP
 00120         UNTIL WS-COE-KEY = WS-MST-KEY
 00121 *          OR WS-COE-KEY > M-VOLPROP
 00122            OR WS-COE-KEY > WS-MST-KEY
 00123            OR COE-EOF.
 00124      MOVE ZERO TO AS-PROP.
 00125      PERFORM 0100-MAINLINE THRU 0100-EXIT
 00126              UNTIL MASTER-EOF
 00127                    OR RETURN-CODE = 16.
 00128      PERFORM 1300-DISPLAYS THRU 1300-EXIT.
 00129      CLOSE MASTER-IN
 00130            MASTER2-IN
 00131            COE-FILE
 00132            MASTER-OUT-FILE
 00133      STOP RUN.
 00134
 00135  0100-MAINLINE.
 00136      IF NOT MASTER-EOF
 00137         IF RETURN-CODE NOT EQUAL TO 16
 00138 *          IF M-VOLPROP = WS-COE-KEY
 00139            IF WS-MST-KEY = WS-COE-KEY
 00140               PERFORM 0400-MATCHED THRU 0400-EXIT
 00141            ELSE
 00142 *             PERFORM 0450-CALCS THRU 0450-EXIT
 00143 *             IF M-VOLPROP > WS-COE-KEY
 00144               IF WS-MST-KEY > WS-COE-KEY
 00145                  PERFORM 0500-MASTER-HIGH THRU 0500-EXIT
 00146               ELSE
 00147 *                IF M-VOLPROP < WS-COE-KEY
 00148                  IF WS-MST-KEY < WS-COE-KEY
 00149                     PERFORM 0600-NO-CHANGE THRU 0600-EXIT
 00150                  END-IF
 00151               END-IF
 00152            END-IF
 00153         END-IF
 00154      END-IF.
 00155  0100-EXIT.
 00156      EXIT.
 00157
 00158  0200-READ-MASTER-IN.
 00159      READ MASTER-IN
 00160           AT END
 00161              MOVE 'Y' TO WS-MAST-EOF-SW.
 00162      IF NOT MASTER-EOF
 00163         ADD 1 TO WS-MAST-IN-CNT
 00164         MOVE M-VOL   TO WS-MST-VOL
 00165         MOVE M-PROP  TO WS-MST-PROP
 00166         MOVE M-TXTYP TO WS-MST-TYPE
 00167      END-IF.
 00168  0200-EXIT.
 00169      EXIT.
 00170
 00171  0300-READ-COE-FILE.
 00172      READ COE-FILE
 00173           AT END
 00174              MOVE 'Y' TO WS-COE-EOF-SW.
 00175      IF NOT COE-EOF
 00176         ADD 1 TO WS-COE-CNT
 00177         MOVE C515-VOL   TO WS-COE-VOL
 00178         MOVE C515-PROP  TO WS-COE-PROP
 00179         MOVE C515-TXTYP TO WS-COE-TYPE
 00180      ELSE
 00181         MOVE HIGH-VALUES TO WS-COE-KEY
 00182      END-IF.
 00183  0300-EXIT.
 00184      EXIT.
 00185
 00186  0400-MATCHED.
 00187      MOVE 0   TO WS-M-VALUE.
 00188      MOVE ' ' TO WS-UPDT-SW.
 00189      PERFORM 0700-UPDATE-ROUTINE THRU 0700-EXIT.
 00190  0400-EXIT.
 00191      EXIT.
 00192
 00193 *0450-CALCS.
 00194 *    IF M-VALUE (4) > 0
 00195 *       MOVE 0 TO WS-M-VALUE
 00196 *       MOVE 'Y' TO WS-UPDT-SW
 00197 *       ADD 1 TO WS-UPDT-CNT-2
 00198 **      COMPUTE WS-HELP ROUNDED =
 00199 **         M-VALUE (4) / LK-MEDIAN
 00200 **      COMPUTE WS-HELPX ROUNDED =
 00201 **         WS-HELP / LK-PRIOR
 00202 *       COMPUTE WS-HELP ROUNDED =
 00203 *          M-VALUE (9) * LK-MEDIAN
 00204 **      COMPUTE WS-HELPX ROUNDED =
 00205 **         WS-HELP * LK-PRIOR
 00206 **      COMPUTE WS-HELPY ROUNDED =
 00207 **         WS-HELPX / LK-PRIOR
 00208 *       MOVE WS-HELPY TO WS-M-VALUE
 00209 *    END-IF.
 00210 *0450-EXIT.
 00211 *    EXIT.
 00212 *
 00213  0500-MASTER-HIGH.
 00214      IF LK-IND = 'N'
 00215         CONTINUE
 00216      ELSE
 00217         IF M-VALUE (4) > 0
 00218            PERFORM 0550-PRIOR THRU 0550-EXIT
 00219         END-IF
 00220      END-IF
 00221      PERFORM 1000-WRITE-OUTPUT   THRU 1000-EXIT
 00222      PERFORM 0200-READ-MASTER-IN THRU 0200-EXIT
 00223      PERFORM 0300-READ-COE-FILE  THRU 0300-EXIT.
 00224  0500-EXIT.
 00225      EXIT.
 00226
 00227  0550-PRIOR.
 00228      MOVE ' ' TO WS-UPDT-SW
 00229      PERFORM 0800-READ-PRIOR THRU 0800-EXIT
 00230      IF GOT-PRIOR-RECORD
 00231 *       IF AS-VALUE-1 (4) > 0
 00232            MOVE 0 TO WS-M-VALUE
 00233            MOVE 'Y' TO WS-UPDT-SW
 00234            MOVE 'H' TO WS-STAT-SW
 00235            ADD 1 TO WS-UPDT-CNT-2
 00236 *          COMPUTE WS-HELP ROUNDED =
 00237 *             AS-VALUE-1 (4) / LK-MEDIAN
 00238 *          COMPUTE WS-HELPX ROUNDED =
 00239 *             WS-HELP / LK-PRIOR
 00240            COMPUTE WS-HELP ROUNDED =
 00241 *             M-VALUE (9) * LK-MEDIAN
 00242               AS-VALUE-1 (9) * LK-MEDIAN
 00243 *          COMPUTE WS-HELPX ROUNDED =
 00244 *             WS-HELP * LK-PRIOR
 00245 *          COMPUTE WS-HELPY ROUNDED =
 00246 *             WS-HELPX / LK-PRIOR
 00247               MOVE WS-HELP  TO WS-M-VALUE
 00248 *       END-IF
 00249      END-IF.
 00250  0550-EXIT.
 00251      EXIT.
 00252
 00253  0600-NO-CHANGE.
 00254      IF LK-IND = 'N'
 00255         CONTINUE
 00256      ELSE
 00257         IF M-VALUE (4) > 0
 00258            PERFORM 0550-PRIOR THRU 0550-EXIT
 00259         END-IF
 00260      END-IF
 00261      PERFORM 1000-WRITE-OUTPUT   THRU 1000-EXIT.
 00262      PERFORM 0200-READ-MASTER-IN THRU 0200-EXIT.
 00263  0600-EXIT.
 00264      EXIT.
 00265
 00266  0700-UPDATE-ROUTINE.
 00267 *    IF C515-TYPE > 0 AND C515-TYPE < 7
 00268 *       MOVE 'Y' TO WS-UPDT-SW
 00269 *       ADD 1 TO WS-UPDT-CNT
 00270 *       MOVE C515-RASSVAL TO WS-M-VALUE
 00271 *    ELSE
 00272       IF C515-TYPE = 7 OR C515-TYPE = 9 OR C515-TYPE = 10
 00273         IF C515-STAT = 7 OR C515-STAT = 8
 00274            IF C515-REASON NOT = 13 AND 14 AND 26
 00275               MOVE 'Y' TO WS-UPDT-SW
 00276               MOVE 'C' TO WS-STAT-SW
 00277 *             DISPLAY 'C515-PROP  COE   '      C515-PROP
 00278               ADD 1 TO WS-UPDT-CNT
 00279               MOVE C515-RASSVAL TO WS-M-VALUE
 00280 *             MOVE C515-AADJTXAMT TO WS-M-VALUE
 00281            END-IF
 00282         END-IF
 00283       END-IF
 00284 *    END-IF
 00285      IF LK-IND = 'N'
 00286         CONTINUE
 00287      ELSE
 00288         IF M-VALUE (4) > 0
 00289            PERFORM 0550-PRIOR THRU 0550-EXIT
 00290         END-IF
 00291      END-IF
 00292      PERFORM 1000-WRITE-OUTPUT    THRU 1000-EXIT
 00293      PERFORM 0200-READ-MASTER-IN  THRU 0200-EXIT
 00294      PERFORM 0300-READ-COE-FILE   THRU 0300-EXIT.
 00295  0700-EXIT.
 00296      EXIT.
 00297
 00298  0800-READ-PRIOR.
 00299      PERFORM 0900-GET-MASTER2 THRU 0900-EXIT
 00300 *      UNTIL AS-PROP IS NOT LESS THAN M-PROP
 00301 *       UNTIL AS-VOLPROP IS NOT LESS THAN M-VOLPROP
 00302         UNTIL WS-PRI-KEY IS NOT LESS THAN WS-MST-KEY
 00303            OR MAST2-EOF
 00304 *   IF AS-PROP = M-PROP
 00305 *    IF AS-VOLPROP = M-VOLPROP
 00306      IF WS-PRI-KEY = WS-MST-KEY
 00307         MOVE 'Y' TO WS-PRIOR-SW
 00308      ELSE
 00309         MOVE 'N' TO WS-PRIOR-SW
 00310      END-IF.
 00311  0800-EXIT.
 00312      EXIT.
 00313
 00314  0900-GET-MASTER2.
 00315      READ MASTER2-IN
 00316           AT END
 00317              MOVE 'Y' TO WS-MAST2-EOF-SW
 00318              MOVE 99999999999999 TO AS-PROP.
 00319      IF NOT MAST2-EOF
 00320         ADD 1 TO WS-MAST2-IN-CNT
 00321         MOVE AS-VOL   TO WS-PRI-VOL
 00322         MOVE AS-PROP  TO WS-PRI-PROP
 00323         MOVE AS-TXTYP TO WS-PRI-TYPE
 00324      END-IF.
 00325  0900-EXIT.
 00326      EXIT.
 00327
 00328  1000-WRITE-OUTPUT.
 00329      MOVE MSTR-IN-REC TO MSTR-OUT-REC
 00330      IF WS-UPDT-SW = 'Y'
 00331         MOVE ' ' TO WS-UPDT-SW
 00332 *       MOVE WS-M-VALUE TO MA-VALUE-1 (6)
 00333         MOVE WS-M-VALUE TO MA-VALUE-1 (9)
 00334         IF WS-STAT-SW = 'C'
 00335            MOVE 'C'  TO MA-NPHE-STATUS
 00336         ELSE
 00337            IF WS-STAT-SW = 'H'
 00338               MOVE 'H' TO MA-NPHE-STATUS
 00339            END-IF
 00340         END-IF
 00341         MOVE SPACES TO WS-STAT-SW
 00342      END-IF
 00343      WRITE MSTR-OUT-REC
 00344      ADD 1 TO WS-MAST-OUT-CNT.
 00345  1000-EXIT.
 00346       EXIT.
 00347  1100-DATE.
 00348      MOVE FUNCTION CURRENT-DATE  TO CURRENT-DATE-RETURN.
 00349      COMPUTE  CURRENT-DATE   = SYSTEM-DATE * MDCY-OR-CYMD.
 00350      DISPLAY
 00351      'THIS PROGRAM IS ASHMA831 .. RUN DATE IS ' CURRENT-DATE.
 00352  1100-EXIT.
 00353      EXIT.
 00354
 00355  1200-CHECK-PARMS.
 00356      DISPLAY ' LK-MEDIAN-R   = ' LK-MEDIAN-R.
 00357      DISPLAY ' LK-PRIOR-EQ-R = ' LK-PRIOR-EQ-R.
 00358      DISPLAY ' LK-INDICATOR  = ' LK-IND.
 00359      IF NOT VALID-LK-LGTH
 00360         DISPLAY 'INVALID LINK LENGTH = ' LK-LENGTH
 00361         DISPLAY 'VALID LINK LENGTH = 12'
 00362         MOVE 16 TO RETURN-CODE
 00363      END-IF.
 00364      IF LK-MEDIAN NOT NUMERIC OR LK-MEDIAN = 0
 00365         DISPLAY 'MEDIAN ERROR    ' LK-MEDIAN
 00366         MOVE 16 TO RETURN-CODE
 00367      END-IF.
 00368      IF LK-PRIOR-EQ-R NOT NUMERIC OR LK-PRIOR-EQ-R = 0
 00369         DISPLAY 'PRIOR ERROR     ' LK-PRIOR
 00370         MOVE 16 TO RETURN-CODE
 00371      END-IF.
 00372      IF (LK-IND = SPACE) OR (LK-IND IS NUMERIC)
 00373         MOVE 16 TO RETURN-CODE
 00374         DISPLAY 'INDICATOR ERROR  ' LK-IND
 00375      END-IF.
 00376      IF LK-IND NOT = 'Y' AND 'N'
 00377         MOVE 16 TO RETURN-CODE
 00378         DISPLAY 'INDICATOR ERROR2  ' LK-IND
 00379      END-IF.
 00380  1200-EXIT.
 00381      EXIT.
 00382
 00383  1300-DISPLAYS.
 00384      DISPLAY ' MASTER RECORDS READ    ... ' WS-MAST-IN-CNT.
 00385      DISPLAY ' COE RECORDS READ       ... ' WS-COE-CNT.
 00386      DISPLAY ' PRIOR MASTER RECORDS READ  ' WS-MAST2-IN-CNT.
 00387      DISPLAY ' MASTER RECORDS WRITTEN ... ' WS-MAST-OUT-CNT.
 00388      DISPLAY ' MASTER RECORDS UPD - COE . ' WS-UPDT-CNT.
 00389      DISPLAY ' MASTER RECORDS UPD - PRIOR ' WS-UPDT-CNT-2.
 00390  1300-EXIT.
 00391      EXIT.
