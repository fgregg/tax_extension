 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID. ASHMA855.
 00003 *AUTHOR. LEE/MOLIS.
 00004 *DATE-WRITTEN. 06/00.
 00005 *REMARKS. ASHMA855 - BASE YEAR VALUE CALCULATION FOR COE RECORDS
 00006 *
 00007
 00008  ENVIRONMENT DIVISION.
 00009  CONFIGURATION SECTION.
 00010  SOURCE-COMPUTER. IBM-370.
 00011  OBJECT-COMPUTER. IBM-370.
 00012  INPUT-OUTPUT SECTION.
 00013  FILE-CONTROL.
 00014      SELECT ASCOEPRW        ASSIGN TO UT-S-ASCOEPRW.
 00015      SELECT EQUALFCT        ASSIGN TO DA-EQUALFCT
 00016                             RECORD KEY IS EQ-KEY
 00017                             ORGANIZATION IS INDEXED
 00018                             ACCESS IS RANDOM
 00019                             FILE STATUS IS EQUAL-STATUS
 00020                                            EQUAL-STATUS1.
 00021      SELECT OUTPUT-FILE     ASSIGN TO UT-S-OUTFILE.
 00022      SELECT PRINT-FILE-1    ASSIGN TO UT-S-PRINT1.
 00023      SELECT PRINT-FILE-2    ASSIGN TO UT-S-PRINT2.
 00024
 00025  DATA DIVISION.
 00026  FILE SECTION.
 00027  ++INCLUDE ASCOEPRWF1
 00028
 00029  FD  EQUALFCT
 00030      RECORD CONTAINS 21 CHARACTERS
 00031      BLOCK CONTAINS 0 RECORDS
 00032      LABEL RECORDS ARE STANDARD
 00033      DATA RECORD IS EQUAL-REC.
 00034  01  EQUAL-REC.
 00035  ++INCLUDE REBEQFRD01
 00036
 00037  FD  PRINT-FILE-1
 00038      RECORDING MODE IS F
 00039      RECORD CONTAINS 133 CHARACTERS
 00040      BLOCK CONTAINS 0 RECORDS
 00041      LABEL RECORDS ARE STANDARD
 00042      DATA RECORD IS PRINT-REC-1.
 00043  01  PRINT-REC-1            PIC X(133).
 00044
 00045  FD  PRINT-FILE-2
 00046      RECORDING MODE IS F
 00047      RECORD CONTAINS 133 CHARACTERS
 00048      BLOCK CONTAINS 0 RECORDS
 00049      LABEL RECORDS ARE STANDARD
 00050      DATA RECORD IS PRINT-REC-2.
 00051  01  PRINT-REC-2            PIC X(133).
 00052
 00053  FD  OUTPUT-FILE
 00054      RECORDING MODE IS F
 00055      RECORD CONTAINS 400 CHARACTERS
 00056      BLOCK CONTAINS 0 RECORDS
 00057      LABEL RECORDS ARE STANDARD
 00058      DATA RECORD IS SF-RECORD.
 00059  01  SF-RECORD.
 00060  ++INCLUDE ASHMASFR01
 00061
 00062  WORKING-STORAGE SECTION.
 00063  77  WS-COE-READ-CTR        PIC S9(8) VALUE +0     BINARY.
 00064  77  WS-SF-ZERO-CTR         PIC S9(8) VALUE +0     BINARY.
 00065  77  WS-SF-CALC-CTR         PIC S9(8) VALUE +0     BINARY.
 00066  77  WS-UNMATCH-CTR         PIC S9(8) VALUE +0     BINARY.
 00067  77  WS-OUTPUT-CTR          PIC S9(8) VALUE +0     BINARY.
 00068  77  ZERO-LN-CTR            PIC S9(4) VALUE +99    BINARY.
 00069  77  WS-EQ-LN-CTR           PIC S9(4) VALUE +99    BINARY.
 00070  77  EQ-PG-CTR-1            PIC S9(4) VALUE +0     BINARY.
 00071  77  ZERO-PG-CTR-1          PIC S9(4) VALUE +0     BINARY.
 00072  77  BLNK                   PIC X     VALUE SPACE.
 00073  77  WK-PROP                PIC 9(15) VALUE ZEROS.
 00074  77  NO-CALC-SW             PIC 9     VALUE ZEROES.
 00075      88 NO-CALC                       VALUE 1.
 00076  77  COE-EOF-SW             PIC 9     VALUE ZEROES.
 00077      88 COE-EOF                       VALUE 1.
 00078  77  EQUAL-EOF-SW           PIC 9     VALUE ZEROES.
 00079      88 EQUAL-EOF                     VALUE 1.
 00080  77  SAVE-PROP              PIC 9(15) VALUE ZEROS.
 00081  77  EQUAL-STATUS            PIC 99.
 00082      88 EQ-NORM-STATUS                VALUE ZEROES.
 00083      88 EQ-NOT-FND                    VALUE 23.
 00084      88 EQ-FND                        VALUE 00.
 00085  77  CURRENT-DATE           PIC 99/99/9(4).
 00086  77  CURRENT-TIME           PIC 99,99,99.
 00087  77  ERR-SW                 PIC X     VALUE 'N'.
 00088      88 ERR                           VALUE 'Y'.
 00089
 00090  01  WORK-STORAGE.
 00091      03 WS-SF-ZERO-MSG      PIC X(41)
 00092         VALUE 'SENIOR FREEZE BASE VALUE IS EQUAL TO ZERO'.
 00093      03 WS-SF-CALC-MSG      PIC X(50)
 00094        VALUE 'COMPUTED SENIOR FREEZE BASE VALUE IS EQUAL TO ZERO'
 00095      03 WS-SF-ZERO-HDG-MSG  PIC X(37)
 00096         VALUE 'SENIOR FREEZE BASE VALUE ERROR REPORT'.
 00097      03 WS-EQUAL-MSG        PIC X(31)
 00098         VALUE 'NO MATCHING EQUALIZATION FACTOR'.
 00099      03 WS-EQUAL-HDG-MSG    PIC X(25)
 00100         VALUE 'EQUALIZATION ERROR REPORT'.
 00101      03 WS-TOT-SF-ZERO   PIC X(26)
 00102            VALUE 'TOTAL SENIOR FREEZE = ZERO'.
 00103      03 WS-TOT-CALC-ZERO   PIC X(35)
 00104            VALUE 'TOTAL COMPUTED SENIOR FREEZE = ZERO'.
 00105      03 WS-BASE-YEAR        PIC 9(15)       VALUE ZEROS.
 00106      03 WS-YEAR-X.
 00107         05  WS-CC           PIC 9(2)        VALUE ZEROS.
 00108         05  WS-YR           PIC 9(2)        VALUE ZEROS.
 00109      03 WS-YEAR REDEFINES WS-YEAR-X   PIC 9(4).
 00110
 00111      03 MDCY-OR-CYMD        PIC S9(5)V9(4)  VALUE +10000.0001.
 00112      03 CURRENT-DATE-RETURN                 VALUE SPACES.
 00113         05  SYSTEM-DATE     PIC 9(8).
 00114         05  SYSTEM-TIME     PIC 9(6).
 00115         05  FILLER          PIC X(7).
 00116
 00117      03 EQUAL-STATUS1.
 00118         05 EQ-RETURN        PIC S9(4)              BINARY.
 00119         05 EQ-FUNC          PIC S9(4)              BINARY.
 00120         05 EQ-FEEDBACK      PIC S9(4)              BINARY.
 00121      03 CURR-KEY.
 00122         05 CURR-PROP        PIC 9(15)     VALUE ZEROS.
 00123         05 CURR-PROCYR      PIC XX        VALUE ZEROS.
 00124      03 PREV-KEY.
 00125         05 PREV-PROP        PIC 9(15)     VALUE ZEROS.
 00126         05 PREV-PROCYR      PIC 99        VALUE ZEROS.
 00127
 00128      03 HEAD1.
 00129         05 FILLER           PIC XX    VALUE SPACES.
 00130         05 H1-CURR-DTE      PIC X(10).
 00131         05 FILLER           PIC X(39) VALUE SPACES.
 00132         05 FILLER           PIC X(19) VALUE 'OFFICE OF THE COOK '
 00133         05 FILLER           PIC X(15) VALUE 'COUNTY ASSESSOR'.
 00134         05 FILLER           PIC X(31) VALUE SPACES.
 00135         05 FILLER           PIC X(5)  VALUE 'PAGE '.
 00136         05 H1-PAGE          PIC ZZZ,ZZ9.
 00137
 00138      03 HEAD2.
 00139         05 FILLER           PIC X(11) VALUE '  ASHMA855-'.
 00140         05 H2-REPORT-TYP    PIC X.
 00141         05 FILLER           PIC X(40) VALUE SPACES.
 00142         05 H2-REPORT-TITLE  PIC X(70).
 00143
 00144      03 HEAD3.
 00145         05 FILLER           PIC X(05) VALUE SPACES.
 00146         05 FILLER           PIC X(08) VALUE 'PROPERTY'.
 00147         05 FILLER           PIC X(07) VALUE SPACES.
 00148         05 FILLER           PIC X(07) VALUE 'PROCESS'.
 00149         05 FILLER           PIC X(03) VALUE SPACES.
 00150         05 FILLER           PIC X(03) VALUE 'TAX'.
 00151         05 FILLER           PIC X(03) VALUE SPACES.
 00152         05 FILLER           PIC X(03) VALUE 'TAX'.
 00153         05 FILLER           PIC X(05) VALUE SPACES.
 00154         05 FILLER           PIC X(03) VALUE 'COE'.
 00155
 00156      03 HEAD4.
 00157         05 FILLER           PIC X(05) VALUE SPACES.
 00158         05 FILLER           PIC X(06) VALUE 'NUMBER'.
 00159         05 FILLER           PIC X(10) VALUE SPACES.
 00160         05 FILLER           PIC X(04) VALUE 'YEAR'.
 00161         05 FILLER           PIC X(04) VALUE SPACES.
 00162         05 FILLER           PIC X(04) VALUE 'YEAR'.
 00163         05 FILLER           PIC X(02) VALUE SPACES.
 00164         05 FILLER           PIC X(04) VALUE 'TYPE'.
 00165         05 FILLER           PIC X(04) VALUE SPACES.
 00166         05 FILLER           PIC X(06) VALUE 'NUMBER'.
 00167
 00168      03 DETAIL1.
 00169         05 FILLER           PIC XX    VALUE SPACES.
 00170         05 DL1-PROP         PIC 99B99B999B999B9(4).
 00171         05 FILLER           PIC X(3)  VALUE SPACES.
 00172         05 DL1-PROCYR       PIC X(4).
 00173         05 FILLER           PIC X(3)  VALUE SPACES.
 00174         05 DL1-TXYR         PIC X(4).
 00175         05 FILLER           PIC X(3)  VALUE SPACES.
 00176         05 DL1-TXTYP        PIC X(4).
 00177         05 FILLER           PIC X(2)  VALUE SPACES.
 00178         05 DL1-COENO        PIC 9(7).
 00179         05 FILLER           PIC X(3)  VALUE SPACES.
 00180         05 DL1-MSG          PIC X(63).
 00181
 00182      03 TOTAL-LINE.
 00183         05 FILLER           PIC XX    VALUE SPACES.
 00184         05 TOT-MSG          PIC X(40) VALUE SPACES.
 00185         05 FILLER           PIC X(3)  VALUE SPACES.
 00186         05 TOT-CNT          PIC ZZ,ZZ9.
 00187         05 FILLER           PIC X(4)  VALUE SPACES.
 00188
 00189  LINKAGE SECTION.
 00190
 00191  PROCEDURE DIVISION.
 00192  010-BEGIN.
 00193      MOVE FUNCTION CURRENT-DATE TO CURRENT-DATE-RETURN.
 00194      COMPUTE CURRENT-DATE = SYSTEM-DATE * MDCY-OR-CYMD.
 00195      MOVE SYSTEM-TIME TO CURRENT-TIME.
 00196      INSPECT CURRENT-TIME REPLACING ALL ',' BY ':'.
 00197      DISPLAY 'PROGRAM ASHMA855 DATE AND TIME OR RUN = '
 00198              CURRENT-DATE '  ' CURRENT-TIME.
 00199      OPEN INPUT EQUALFCT
 00200      IF NOT EQ-NORM-STATUS
 00201         DISPLAY 'EQUALFCT OPEN FAILED  ' EQUAL-STATUS
 00202                 '   RETURN  '   EQ-RETURN
 00203                 '   FUNCTION  ' EQ-FUNC
 00204                 '   FEEDBACK  ' EQ-FEEDBACK
 00205         MOVE 16 TO RETURN-CODE
 00206         STOP RUN.
 00207      MOVE CURRENT-DATE           TO H1-CURR-DTE
 00208      OPEN INPUT  ASCOEPRW
 00209           OUTPUT PRINT-FILE-1
 00210                  PRINT-FILE-2
 00211                  OUTPUT-FILE
 00212      WRITE PRINT-REC-1 FROM BLNK AFTER ADVANCING PAGE
 00213      WRITE PRINT-REC-2 FROM BLNK AFTER ADVANCING PAGE
 00214
 00215      PERFORM 100-MAINLINE UNTIL COE-EOF OR ERR.
 00216
 00217  070-END.
 00218      MOVE WS-TOT-SF-ZERO TO TOT-MSG
 00219      MOVE WS-SF-ZERO-CTR TO TOT-CNT
 00220      WRITE PRINT-REC-1 FROM TOTAL-LINE AFTER ADVANCING 2 LINES
 00221      MOVE WS-TOT-CALC-ZERO TO TOT-MSG
 00222      MOVE WS-SF-CALC-CTR TO TOT-CNT
 00223      WRITE PRINT-REC-1 FROM TOTAL-LINE AFTER ADVANCING 1 LINES
 00224      DISPLAY 'TOTAL COE RECORDS READ                  '
 00225              WS-COE-READ-CTR
 00226      DISPLAY 'TOTAL RECORDS WITH SF = ZERO            '
 00227              WS-SF-ZERO-CTR
 00228      DISPLAY 'TOTAL RECORDS WITH COMPUTED SF = ZERO   '
 00229              WS-SF-CALC-CTR
 00230      DISPLAY 'TOTAL UNMATCHED EQUALIZATION            '
 00231              WS-UNMATCH-CTR
 00232      DISPLAY 'TOTAL OUTPUT RECORDS CREATED            '
 00233              WS-OUTPUT-CTR
 00234      CLOSE EQUALFCT
 00235      IF NOT EQ-NORM-STATUS
 00236         DISPLAY 'EQUALFCT CLOSE FAILED  ' EQUAL-STATUS
 00237                 '   RETURN  '   EQ-RETURN
 00238                 '   FUNCTION  ' EQ-FUNC
 00239                 '   FEEDBACK  ' EQ-FEEDBACK
 00240         MOVE 16 TO RETURN-CODE.
 00241      CLOSE ASCOEPRW
 00242            PRINT-FILE-1
 00243            PRINT-FILE-2
 00244      STOP RUN.
 00245
 00246  100-MAINLINE.
 00247      PERFORM 175-READ-COE-FILE
 00248      IF NOT COE-EOF
 00249         IF ((C515-STAT = 6 OR 7 OR 8) AND
 00250            (C515-TYPE = 20 OR 21))
 00251 *          IF C515-PROP = WK-PROP
 00252 *             CONTINUE
 00253 *          ELSE
 00254               MOVE C515-PROCYR TO EQ-YEAR
 00255               MOVE  1          TO EQ-QUAD
 00256               PERFORM 200-READ-EQUAL-FILE
 00257               IF EQ-FND
 00258                  IF C515-RSF-VALUE NOT NUMERIC
 00259                     MOVE ZEROS TO C515-RSF-VALUE
 00260                  END-IF
 00261                  IF C515-RSF-VALUE = ZERO
 00262                     MOVE WS-SF-ZERO-MSG TO DL1-MSG
 00263                     PERFORM 400-SF-ZERO-RTN
 00264 *                   MOVE C515-PROP TO WK-PROP
 00265                     ADD +1 TO WS-SF-ZERO-CTR
 00266                  ELSE
 00267                     MOVE ZEROS TO WS-BASE-YEAR
 00268                     COMPUTE WS-BASE-YEAR ROUNDED = ((C515-RASSVAL
 00269                        * EQ-FACTOR) - C515-RSF-VALUE)
 00270                     IF WS-BASE-YEAR > 0
 00271                        PERFORM 300-CRT-OUTPUT-FILE
 00272 *                      MOVE C515-PROP TO WK-PROP
 00273                     ELSE
 00274                        IF WS-BASE-YEAR = ZERO
 00275                           MOVE WS-SF-CALC-MSG TO DL1-MSG
 00276                           PERFORM 400-SF-ZERO-RTN
 00277                           ADD +1 TO WS-SF-CALC-CTR
 00278 *                         MOVE C515-PROP TO WK-PROP
 00279                        END-IF
 00280                  END-IF
 00281               ELSE
 00282                  PERFORM 500-NO-EQUAL-RTN
 00283                  ADD +1 TO WS-UNMATCH-CTR
 00284 *                MOVE C515-PROP TO WK-PROP
 00285               END-IF.
 00286 *          END-IF.
 00287
 00288  175-READ-COE-FILE.
 00289      READ ASCOEPRW
 00290         AT END
 00291           MOVE '1' TO COE-EOF-SW
 00292           MOVE HIGH-VALUES TO CURR-KEY
 00293         NOT AT END
 00294           ADD +1 TO WS-COE-READ-CTR
 00295           MOVE C515-PROP TO CURR-PROP
 00296           MOVE C515-PROCYR TO CURR-PROCYR
 00297           IF CURR-KEY < PREV-KEY
 00298              DISPLAY 'INPUT FILE RECORDS IS OUT OF SEQUENCE  '
 00299              DISPLAY 'CURRENT KEY   '  CURR-KEY
 00300              DISPLAY 'PREVIOUS KEY  '  PREV-KEY
 00301              MOVE 16 TO RETURN-CODE
 00302              MOVE 'Y' TO ERR-SW
 00303           ELSE
 00304              MOVE CURR-KEY TO PREV-KEY
 00305           END-IF
 00306        END-READ.
 00307
 00308  200-READ-EQUAL-FILE.
 00309      READ EQUALFCT
 00310      IF EQ-FND OR EQ-NOT-FND
 00311         NEXT SENTENCE
 00312      ELSE
 00313         DISPLAY 'EQUALFCT READ FAILED  ' EQUAL-STATUS
 00314                 '   RETURN  '   EQ-RETURN
 00315                 '   FUNCTION  ' EQ-FUNC
 00316                 '   FEEDBACK  ' EQ-FEEDBACK
 00317         MOVE 16 TO RETURN-CODE.
 00318
 00319  300-CRT-OUTPUT-FILE.
 00320      MOVE C515-TOWN    TO  SF-TOWN
 00321      MOVE C515-VOL     TO  SF-VOL
 00322      MOVE C515-PROP    TO  SF-PROP
 00323      MOVE C515-PROCYR  TO  WS-YR
 00324      IF WS-YR > 60
 00325         MOVE 19        TO  WS-CC
 00326      ELSE
 00327         MOVE 20        TO  WS-CC
 00328      END-IF
 00329      MOVE WS-YEAR      TO  SF-PROC-YR
 00330      SUBTRACT 1 FROM WS-YEAR
 00331      MOVE WS-YEAR      TO  SF-BASVALYR, SF-BASE-YR
 00332      MOVE ZEROS        TO  WS-YEAR
 00333      MOVE C515-TXYR    TO  WS-YR
 00334      IF WS-YR > 60
 00335         MOVE 19        TO  WS-CC
 00336      ELSE
 00337         MOVE 20        TO  WS-CC
 00338      END-IF
 00339      MOVE WS-YEAR      TO  SF-TAX-YR
 00340      MOVE C515-TXTYP   TO  SF-TAX-TYPE
 00341      MOVE SPACES       TO  SF-RECCODE SF-BVYNOCALCIND
 00342                            SF-BVYMANCALCIND
 00343                            SF-CYFARMIND
 00344                            SF-CALC-TYP
 00345                            SF-BIRTHDTE
 00346                            SF-SFSTAT
 00347                            SF-LIFECARE
 00348                            SF-BSYR-IND
 00349      MOVE ZEROS        TO  SF-BLDGUNITS
 00350                            SF-BLDGSHARES
 00351                            SF-PROPCT
 00352                            SF-KEYPCL
 00353                            SF-SPLTCD
 00354                            SF-OCCFAC
 00355                            SF-288EXPAV
 00356                            SF-288EXPEV
 00357                            SF-288OVRLIMAV
 00358                            SF-288OVRLIMEV
 00359                            SF-BVYCLS
 00360                            SF-BVYFULLAV
 00361                            SF-BVYEV
 00362                            SF-BVYELCOMPFULLAV
 00363                            SF-BVYTOTELCOMPEV
 00364                            SF-CYCLS
 00365                            SF-CYFULLAV
 00366                            SF-CYELCOMPAV
 00367                            SF-CYELCOMPEV
 00368                            SF-CYNOTELAV
 00369                            SF-CYNOTELEV
 00370                            SF-CYFNLEVDIFF
 00371                            SF-HOUNITS
 00372                            SF-HSUNITS
 00373                            SF-SFSHARES
 00374                            SF-CYFULLEV
 00375                            SF-DENIALDTE
 00376                            SF-FSTAPPDTE
 00377                            SF-LSTAPPDTE
 00378                            SF-QUALDTE
 00379                            SF-COOPSENSHRS
 00380                            SF-PCTSENSHRS
 00381                            SF-BSYR-EQVAL
 00382                            SF-OTHBEN
 00383                            SF-VETBEN
 00384      MOVE C515-RASSVAL TO SF-BVYFULLAV
 00385      COMPUTE SF-BVYEV ROUNDED = (C515-RASSVAL * EQ-FACTOR)
 00386      COMPUTE SF-BVYFULLAV ROUNDED = (C515-RASSVAL * EQ-FACTOR)
 00387      COMPUTE SF-BVYELCOMPFULLAV ROUNDED = (C515-RASSVAL
 00388                    * EQ-FACTOR)
 00389      COMPUTE SF-BVYTOTELCOMPEV ROUNDED = ((C515-RASSVAL
 00390                        * EQ-FACTOR) - C515-RSF-VALUE)
 00391      COMPUTE SF-BSYR-EQVAL ROUNDED = ((C515-RASSVAL
 00392                        * EQ-FACTOR) - C515-RSF-VALUE)
 00393      WRITE SF-RECORD
 00394      ADD +1 TO WS-OUTPUT-CTR.
 00395
 00396  400-SF-ZERO-RTN.
 00397      IF ZERO-LN-CTR > +62
 00398         PERFORM 450-ZERO-HDG-RTN
 00399      END-IF
 00400      MOVE C515-PROP   TO DL1-PROP
 00401      INSPECT DL1-PROP REPLACING ALL SPACES BY '-'
 00402      MOVE C515-PROCYR TO DL1-PROCYR
 00403      MOVE C515-TXTYP  TO DL1-TXTYP
 00404      MOVE C515-TXYR   TO DL1-TXYR
 00405      MOVE C515-BACERTNO TO DL1-COENO
 00406      WRITE PRINT-REC-1 FROM DETAIL1 AFTER ADVANCING 2 LINES
 00407      ADD +2 TO ZERO-LN-CTR.
 00408
 00409  500-NO-EQUAL-RTN.
 00410      IF WS-EQ-LN-CTR > +62
 00411         PERFORM 550-EQUAL-HDG-RTN
 00412      END-IF
 00413      MOVE C515-PROP   TO DL1-PROP
 00414      INSPECT DL1-PROP REPLACING ALL SPACES BY '-'
 00415      MOVE C515-PROCYR TO DL1-PROCYR
 00416      MOVE C515-TXYR   TO DL1-TXYR
 00417      MOVE C515-BACERTNO  TO DL1-COENO
 00418      MOVE WS-EQUAL-MSG TO DL1-MSG
 00419      WRITE PRINT-REC-2 FROM DETAIL1 AFTER ADVANCING 2 LINES
 00420      ADD +2 TO WS-EQ-LN-CTR.
 00421
 00422  450-ZERO-HDG-RTN.
 00423      ADD 1         TO ZERO-PG-CTR-1
 00424      MOVE ZERO-PG-CTR-1 TO H1-PAGE
 00425      MOVE  '1'     TO H2-REPORT-TYP
 00426      MOVE WS-SF-ZERO-HDG-MSG TO H2-REPORT-TITLE
 00427      WRITE PRINT-REC-1 FROM HEAD1 AFTER ADVANCING PAGE
 00428      WRITE PRINT-REC-1 FROM HEAD2 AFTER ADVANCING 2
 00429      WRITE PRINT-REC-1 FROM HEAD3 AFTER ADVANCING 2
 00430      WRITE PRINT-REC-1 FROM HEAD4 AFTER ADVANCING 1
 00431      MOVE 6        TO ZERO-LN-CTR.
 00432
 00433  550-EQUAL-HDG-RTN.
 00434      ADD 1         TO EQ-PG-CTR-1
 00435      MOVE EQ-PG-CTR-1 TO H1-PAGE
 00436      MOVE  '2'     TO H2-REPORT-TYP
 00437      MOVE WS-EQUAL-HDG-MSG TO H2-REPORT-TITLE
 00438      WRITE PRINT-REC-2 FROM HEAD1 AFTER ADVANCING PAGE
 00439      WRITE PRINT-REC-2 FROM HEAD2 AFTER ADVANCING 2
 00440      WRITE PRINT-REC-2 FROM HEAD3 AFTER ADVANCING 2
 00441      WRITE PRINT-REC-2 FROM HEAD4 AFTER ADVANCING 1
 00442      MOVE 6        TO WS-EQ-LN-CTR.
