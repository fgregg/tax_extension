 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID. ASREA002.
 00003  AUTHOR. GEORGE ZINTAK.
 00004 *REMARKS.
 00005 ****THIS PROGRAM IS FOR  THE NEW ASSESSMENT SYSTEM ****
 00006 *    THIS IS A CALLED PROGRAM THAT WILL CALULATE
 00007 *    VALUATION FOR TYPES 1,2,3,4,5 RECORDS FOR THE
 00008 *    DETAIL FILE. JUST PASS THE DETAIL SEGMENT TO BE CALULATED.
 00009 *
 00010 * MAP='RES,DYN' MUST BE UTILIZED WHEN COMPILING/PERMSTOWING
 00011 *
 00012  ENVIRONMENT DIVISION.
 00013  CONFIGURATION SECTION.
 00014  SOURCE-COMPUTER. IBM-370.
 00015  OBJECT-COMPUTER. IBM-370.
 00016  INPUT-OUTPUT SECTION.
 00017  FILE-CONTROL.
 00018  DATA DIVISION.
 00019  FILE SECTION.
 00020  WORKING-STORAGE SECTION.
 00021  77  WS-VAL            PIC 9(11)    COMP-3 VALUE ZERO.
 00022  77  D1-DEC            PIC 9               VALUE ZERO.
 00023  01  WORK-AREA.
 00024      05 WS-FF0         PIC 9(7)       VALUE ZERO.
 00025      05 WS-FF1 REDEFINES WS-FF0  PIC 9(6)V9.
 00026      05 WS-FF2 REDEFINES WS-FF0  PIC 9(5)V99.
 00027      05 WS-FF3 REDEFINES WS-FF0  PIC 9(4)V999.
 00028      05 WS-FF4 REDEFINES WS-FF0  PIC 999V9(4).
 00029      05 WS-FF5 REDEFINES WS-FF0  PIC 99V9(5).
 00030      05 WS-P-ASSED               PIC V9(7).
 00031      05 WS-P-ASSEDX REDEFINES WS-P-ASSED PIC 99V9(5).
 00032      05 WS-COND                  PIC V999.
 00033      05 WS-CONDX    REDEFINES WS-COND    PIC 99V9.
 00034      05 WS-CONDY REDEFINES WS-COND PIC 999.
 00035      05  WS-AR0         PIC 9(7)       VALUE ZERO.
 00036      05  WS-AR1 REDEFINES WS-AR0  PIC 9(6)V9.
 00037      05  WS-AR2 REDEFINES WS-AR0  PIC 9(5)V99.
 00038      05  WS-AR3 REDEFINES WS-AR0  PIC 9(4)V999.
 00039      05  WS-AR4 REDEFINES WS-AR0  PIC 9(3)V9(4).
 00040      05  WS-AR5 REDEFINES WS-AR0  PIC 99V9(5).
 00041      05  WS-UPR         PIC 9(5)V99 VALUE ZERO.
 00042  LINKAGE SECTION.
 00043  ++INCLUDE ASDTLQRD01
 00044  PROCEDURE DIVISION USING D1-REC.
 00045      MOVE ZEROS TO WS-VAL.
 00046      MOVE ZEROS TO D1-DEC.
 00047      IF   D1-TYPE = 1
 00048           PERFORM 550-TYPE-1 THRU 570-EXIT.
 00049      IF   D2-TYPE = 2
 00050           PERFORM 600-TYPE-2 THRU 620-EXIT.
 00051      IF   D2-TYPE = 3
 00052           PERFORM 650-TYPE-3 THRU 670-EXIT.
 00053      IF      D1-TYPE = 4 OR 5
 00054             PERFORM 700-TYPE-4-5 THRU 720-EXIT.
 00055      GO TO 800-STOP.
 00056  550-TYPE-1.
 00057      IF D1-UNIT-MEASURE = 'EX' OR 'RR'
 00058          MOVE ZEROS TO D1-VALUATION
 00059          GO TO 570-EXIT.
 00060      MOVE    D1-FRONT-FOOTAGE TO WS-FF0.
 00061      MOVE    D1-DECIMAL TO D1-DEC.
 00062      IF      D1-DEC = 0 MULTIPLY WS-FF0 BY D1-UNIT-PRICE
 00063                                            GIVING WS-VAL
 00064         ELSE IF D1-DEC = 1 MULTIPLY WS-FF1 BY D1-UNIT-PRICE
 00065                                            GIVING WS-VAL
 00066           ELSE IF D1-DEC = 2 MULTIPLY WS-FF2 BY D1-UNIT-PRICE
 00067                                            GIVING WS-VAL
 00068             ELSE IF D1-DEC = 3 MULTIPLY WS-FF3 BY D1-UNIT-PRICE
 00069                                            GIVING WS-VAL
 00070               ELSE IF D1-DEC = 4 MULTIPLY WS-FF4 BY D1-UNIT-PRICE
 00071                                            GIVING WS-VAL
 00072             ELSE IF D1-DEC = 5 MULTIPLY WS-FF5 BY D1-UNIT-PRICE
 00073                                            GIVING WS-VAL.
 00074      IF     D1-DEPTH-FACTOR > 0
 00075             MULTIPLY D1-DEPTH-FACTOR BY WS-VAL.
 00076      IF      D1-CORNER-FACTOR > 0
 00077              MULTIPLY D1-CORNER-FACTOR BY WS-VAL.
 00078      IF     D1-PERCENT-ASSESSED > 0
 00079             MOVE D1-PERCENT-ASSESSED TO WS-P-ASSEDX
 00080             MULTIPLY WS-P-ASSED BY WS-VAL.
 00081      IF      D1-EX-CORNER-FACTOR > 0
 00082              MULTIPLY D1-EX-CORNER-FACTOR BY WS-VAL.
 00083      IF     D1-BUFF-NO > 0
 00084             MOVE D1-BUFF-NO TO WS-CONDY
 00085             MULTIPLY WS-COND BY WS-VAL.
 00086      IF     WS-VAL < 1  MOVE 1 TO WS-VAL.
 00087      MOVE   WS-VAL TO D1-VALUATION.
 00088  570-EXIT.
 00089      EXIT.
 00090  600-TYPE-2.
 00091      MOVE   D2-CONDITION TO WS-CONDX.
 00092      MOVE      D2-DECIMAL TO D1-DEC
 00093      MOVE      D2-AREA TO WS-AR0
 00094      MOVE      D2-UNIT-PRICE TO WS-UPR
 00095      IF D1-DEC = 0
 00096          MULTIPLY WS-AR0 BY WS-UPR
 00097                    GIVING D2-PRODUCT
 00098         ELSE
 00099           IF D1-DEC = 1
 00100               MULTIPLY WS-AR1 BY WS-UPR
 00101                         GIVING D2-PRODUCT
 00102              ELSE
 00103                IF D1-DEC = 2
 00104                    MULTIPLY WS-AR2 BY WS-UPR
 00105                              GIVING D2-PRODUCT
 00106                   ELSE
 00107                     IF D1-DEC = 3
 00108                         MULTIPLY WS-AR3 BY WS-UPR
 00109                                   GIVING D2-PRODUCT
 00110                        ELSE
 00111                          IF D1-DEC = 4
 00112                              MULTIPLY WS-AR4 BY WS-UPR
 00113                                        GIVING D2-PRODUCT
 00114                               ELSE
 00115                                 IF D1-DEC = 5
 00116                                     MULTIPLY WS-AR5 BY WS-UPR
 00117                                               GIVING D2-PRODUCT.
 00118      MULTIPLY D2-PRODUCT BY WS-COND GIVING WS-VAL.
 00119      IF     D2-PERCENT-ASSESSED > 0
 00120             MOVE D2-PERCENT-ASSESSED TO WS-P-ASSEDX
 00121             MULTIPLY WS-P-ASSED BY WS-VAL.
 00122      IF     WS-VAL < 1  MOVE 1 TO WS-VAL.
 00123      MOVE   WS-VAL TO D2-VALUATION.
 00124  620-EXIT.
 00125      EXIT.
 00126  650-TYPE-3.
 00127      IF D3-IMPV-YR NOT GREATER THAN 78
 00128          NEXT SENTENCE
 00129      ELSE
 00130          GO TO 660-CHECK-YEAR79.
 00131      IF D3-MAJ-MI-CLASS = 288
 00132         AND D3-REPROD-COST < 15001
 00133          MOVE ZERO TO D3-VALUATION
 00134          GO TO 670-EXIT.
 00135      IF D3-MAJ-MI-CLASS = 288
 00136          SUBTRACT 15000 FROM D3-REPROD-COST GIVING WS-VAL
 00137      ELSE
 00138          MOVE D3-REPROD-COST TO WS-VAL.
 00139      IF D3-CONDITION GREATER THAN 0
 00140          MOVE D3-CONDITION TO WS-CONDX
 00141          MULTIPLY WS-COND BY WS-VAL.
 00142      IF D3-PERCENT-ASSESSED GREATER THAN 0
 00143          MOVE D3-PERCENT-ASSESSED TO WS-P-ASSEDX
 00144          MULTIPLY WS-P-ASSED BY WS-VAL.
 00145      IF WS-VAL < 1 MOVE 1 TO WS-VAL.
 00146      MOVE WS-VAL TO D3-VALUATION.
 00147      GO TO 670-EXIT.
 00148  660-CHECK-YEAR79.
 00149      IF D3-MAJ-MI-CLASS = 288
 00150         AND D3-IMPV-YR < 84
 00151         AND D3-REPROD-COST < 25001
 00152          MOVE ZERO TO D3-VALUATION
 00153          GO TO 670-EXIT.
 00154      IF D3-MAJ-MI-CLASS = 288
 00155         AND D3-IMPV-YR > 83
 00156         AND D3-REPROD-COST < 30001
 00157          MOVE ZERO TO D3-VALUATION
 00158          GO TO 670-EXIT.
 00159      IF D3-MAJ-MI-CLASS = 288
 00160          AND D3-IMPV-YR < 84
 00161          SUBTRACT 25000 FROM D3-REPROD-COST GIVING WS-VAL
 00162      ELSE
 00163          IF D3-MAJ-MI-CLASS = 288
 00164              AND D3-IMPV-YR > 83
 00165              SUBTRACT 30000 FROM D3-REPROD-COST GIVING WS-VAL
 00166          ELSE
 00167              MOVE D3-REPROD-COST TO WS-VAL.
 00168      IF D3-CONDITION GREATER THAN 0
 00169          MOVE D3-CONDITION TO WS-CONDX
 00170          MULTIPLY WS-COND BY WS-VAL.
 00171      IF D3-PERCENT-ASSESSED GREATER THAN 0
 00172          MOVE D3-PERCENT-ASSESSED TO WS-P-ASSEDX
 00173          MULTIPLY WS-P-ASSED BY WS-VAL.
 00174      IF WS-VAL < 1 MOVE 1 TO WS-VAL.
 00175      MOVE WS-VAL TO D3-VALUATION.
 00176  670-EXIT.
 00177      EXIT.
 00178  700-TYPE-4-5.
 00179      IF      D4-CONDITION > 0
 00180              MOVE D4-CONDITION TO WS-CONDX
 00181              MULTIPLY WS-COND BY D4-REPROD-COST GIVING WS-VAL.
 00182      IF      D4-PERCENT-ASSESSED > 0
 00183              MOVE D4-PERCENT-ASSESSED TO WS-P-ASSEDX
 00184              MULTIPLY WS-P-ASSED BY WS-VAL.
 00185      IF      WS-VAL < 1  MOVE 1 TO WS-VAL.
 00186      MOVE    WS-VAL TO D4-VALUATION.
 00187  720-EXIT.
 00188      EXIT.
 00189  800-STOP.
 00190      ENTER LINKAGE.
 00191      GOBACK.
 00192      ENTER COBOL.
