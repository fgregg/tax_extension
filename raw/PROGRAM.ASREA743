 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID. ASREA743.
 00003 *AUTHOR. RUGIS - MOLIS.
 00004 *DATE-WRITTEN. AUGUST 7, 1992.
 00005 *REMARKS. THE PURPOSE OF THIS PROGRAM IS TO CREATE THE FROZEN
 00006 *         VALUATION ACCUMULATED DIVISION AND DIVISION TAXCODE
 00007 *         FILES.
 00008  SKIP2
 00009  ENVIRONMENT DIVISION.
 00010  CONFIGURATION SECTION.
 00011  SOURCE-COMPUTER. IBM-370.
 00012  OBJECT-COMPUTER. IBM-370.
 00013  SKIP1
 00014  INPUT-OUTPUT SECTION.
 00015  FILE-CONTROL.
 00016      SELECT FRZVAL-SEL-FILE  ASSIGN UT-S-FRZSELC.
 00017      SELECT FRZVAL-DIV-FILE  ASSIGN UT-S-FRZDIVN.
 00018      SELECT FRZVAL-TXCD-FILE ASSIGN UT-S-FRZTXCD.
 00019  SKIP1
 00020  DATA DIVISION.
 00021  FILE SECTION.
 00022  SKIP1
 00023  ++INCLUDE CLFRZSELF1
 00024  SKIP1
 00025  ++INCLUDE CLFRZDIVF1
 00026  SKIP1
 00027  ++INCLUDE CLFRZTAXF1
 00028  SKIP3
 00029  WORKING-STORAGE SECTION.
 00030      SKIP1
 00031  77  RECS-IN-CNTR       PIC S9(7)     VALUE +0  PACKED-DECIMAL.
 00032      88  FIRST-REC      VALUE +1.
 00033  77  DIV-WRIT-CNTR      PIC S9(7)     VALUE +0  PACKED-DECIMAL.
 00034  77  TXCD-WRIT-CNTR     PIC S9(7)     VALUE +0  PACKED-DECIMAL.
 00035  77  DIV-CUR-288        PIC S9(13)    VALUE +0  PACKED-DECIMAL.
 00036  77  DIV-1ST-TIME       PIC S9(13)    VALUE +0  PACKED-DECIMAL.
 00037  77  TXCD-CUR-288       PIC S9(13)    VALUE +0  PACKED-DECIMAL.
 00038  77  TXCD-1ST-TIME      PIC S9(13)    VALUE +0  PACKED-DECIMAL.
 00039  77  TXCD-CNT           PIC S9(13)    VALUE +0  PACKED-DECIMAL.
 00040  77  SUB                PIC S9(4)     VALUE +0  BINARY.
 00041      SKIP1
 00042  77  DET-EOF-SW         PIC X         VALUE 'N'.
 00043      88  DET-EOF                      VALUE 'Y'.
 00044  77  ERR-SW             PIC X         VALUE 'N'.
 00045      88  ERR                          VALUE 'Y'.
 00046      SKIP1
 00047  01  WORK-AREA.
 00048      SKIP1
 00049      05  CURR-DET-KEY.
 00050          10  CD-DIVNO    PIC 9(15).
 00051          10  CD-TXCD     PIC 9(5).
 00052      SKIP1
 00053      05  PREV-DET-KEY    PIC X(20)    VALUE LOW-VALUES.
 00054      SKIP2
 00055      05  SAVE-DET-KEY.
 00056          10  SV-DIVNO    PIC 9(15).
 00057          10  SV-TXCD     PIC 9(5).
 00058      EJECT
 00059  PROCEDURE DIVISION.
 00060      SKIP1
 00061  010-HOUSEKEEPING.
 00062      SKIP1
 00063      OPEN INPUT FRZVAL-SEL-FILE
 00064          OUTPUT FRZVAL-DIV-FILE  FRZVAL-TXCD-FILE.
 00065      SKIP1
 00066      PERFORM 020-MAINLINE
 00067         UNTIL DET-EOF OR ERR.
 00068      SKIP1
 00069      IF TXCD-1ST-TIME GREATER ZEROES
 00070          PERFORM 050-MOVE-WRITE-TXCD-REC.
 00071      SKIP1
 00072      IF DIV-1ST-TIME GREATER ZEROES
 00073          PERFORM 040-MOVE-WRITE-DIV-REC.
 00074      SKIP1
 00075      DISPLAY 'TOTAL DETAIL RECORDS READ      ' RECS-IN-CNTR.
 00076      DISPLAY 'TOTAL DIVISION RECORDS WRITTEN ' DIV-WRIT-CNTR.
 00077      DISPLAY 'TOTAL TAX CODE RECORDS WRITTEN ' TXCD-WRIT-CNTR.
 00078      SKIP1
 00079      CLOSE FRZVAL-SEL-FILE  FRZVAL-DIV-FILE
 00080            FRZVAL-TXCD-FILE.
 00081      STOP RUN.
 00082      SKIP3
 00083  020-MAINLINE.
 00084      PERFORM 030-READ-DETAIL.
 00085      IF NOT (DET-EOF OR ERR)
 00086          IF CD-DIVNO EQUAL SV-DIVNO
 00087              IF CD-TXCD EQUAL SV-TXCD
 00088                  ADD FVS-CURR-288-AV-VAL TO DIV-CUR-288
 00089                                            TXCD-CUR-288
 00090                  ADD FVS-FRST-AV-VAL TO DIV-1ST-TIME
 00091                                        TXCD-1ST-TIME
 00092              ELSE
 00093                  PERFORM 050-MOVE-WRITE-TXCD-REC
 00094                  MOVE FVS-TXCD TO SV-TXCD
 00095                  MOVE ZEROES TO TXCD-CUR-288 TXCD-1ST-TIME
 00096                  ADD FVS-CURR-288-AV-VAL TO DIV-CUR-288
 00097                                            TXCD-CUR-288
 00098                  ADD FVS-FRST-AV-VAL TO DIV-1ST-TIME
 00099                                        TXCD-1ST-TIME
 00100                  ADD +1 TO TXCD-CNT
 00101          ELSE
 00102              PERFORM 040-MOVE-WRITE-DIV-REC
 00103              PERFORM 050-MOVE-WRITE-TXCD-REC
 00104              MOVE FVS-DIVNO TO SV-DIVNO
 00105              MOVE FVS-TXCD  TO SV-TXCD
 00106              MOVE ZEROES TO DIV-CUR-288 DIV-1ST-TIME
 00107                  TXCD-CUR-288 TXCD-1ST-TIME TXCD-CNT
 00108              ADD FVS-CURR-288-AV-VAL TO DIV-CUR-288
 00109                                        TXCD-CUR-288
 00110              ADD FVS-FRST-AV-VAL TO DIV-1ST-TIME
 00111                                    TXCD-1ST-TIME
 00112              ADD +1 TO TXCD-CNT.
 00113      SKIP2
 00114  030-READ-DETAIL.
 00115      READ FRZVAL-SEL-FILE
 00116        AT END
 00117          MOVE 'Y' TO DET-EOF-SW.
 00118      IF NOT DET-EOF
 00119          ADD +1 TO RECS-IN-CNTR
 00120          MOVE FVS-DIVNO TO CD-DIVNO
 00121          MOVE FVS-TXCD  TO CD-TXCD
 00122          IF CURR-DET-KEY LESS PREV-DET-KEY
 00123              DISPLAY 'FROZEN VALUE SELECTION FILE OUT OF SEQUENCE'
 00124              DISPLAY 'CURRENT  KEY IS ' CURR-DET-KEY
 00125              DISPLAY 'PREVIOUS KEY IS ' PREV-DET-KEY
 00126              MOVE 16  TO RETURN-CODE
 00127              MOVE 'Y' TO ERR-SW
 00128          ELSE
 00129              MOVE CURR-DET-KEY TO PREV-DET-KEY
 00130              IF FIRST-REC
 00131                 MOVE CD-DIVNO TO SV-DIVNO
 00132                 MOVE CD-TXCD  TO SV-TXCD
 00133                 ADD +1 TO TXCD-CNT.
 00134      SKIP3
 00135  040-MOVE-WRITE-DIV-REC.
 00136      MOVE SPACES TO FRZVAL-DIV-REC.
 00137      MOVE SV-DIVNO     TO FVD-DIVNO.
 00138      MOVE SV-TXCD      TO FVD-TXCD.
 00139      MOVE DIV-CUR-288  TO FVD-CURR-288-VAL.
 00140      MOVE DIV-1ST-TIME TO FVD-FRST-VAL.
 00141      MOVE TXCD-CNT     TO FVD-TXCD-COUNT.
 00142      WRITE FRZVAL-DIV-REC.
 00143      ADD +1 TO DIV-WRIT-CNTR.
 00144      SKIP3
 00145  050-MOVE-WRITE-TXCD-REC.
 00146      MOVE SPACES TO FRZVAL-TXCD-REC.
 00147      MOVE SV-DIVNO      TO FVT-DIVNO.
 00148      MOVE SV-TXCD       TO FVT-TXCD.
 00149      MOVE TXCD-CUR-288  TO FVT-CURR-288-VAL.
 00150      MOVE TXCD-1ST-TIME TO FVT-FRST-VAL.
 00151      WRITE FRZVAL-TXCD-REC.
 00152      ADD +1 TO TXCD-WRIT-CNTR.
