 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID.    ASREA852.
 00003 *AUTHOR.        LINDSAY - MOLIS.
 00004 *DATE-WRITTEN.  JAN 20,1982.
 00005 *REMARKS. THIS PROGRAM WILL CREATE THE NEW YEAR
 00006 *         HOMEOWNER'S FILE.
 00007 *MODIFICATION ON FEB 23,1988 - SQUILLACE
 00008 *         LOGIC ADDED TO COMPUTE 'OCCUPANCY FACTOR'.
 00009 *MODIFICATION ON JAN 29,1990 - LIAUBA
 00010 *         LOGIC ADDED TO CHECK FOR CLASS 300-899 AND A VALUATION
 00011 *         OF 1000 OR LESS.
 00012 *MODIFICATION ON DEC 26,1991 - HERING
 00013 *         LOGIC ADDED FOR CLASS 9
 00014 *         CONVERTED TO COBOL II
 00015 *MODIFICATION ON OCT 28,1997 - BROWN
 00016 *         LOGIC ADDED TO CHECK FOR GARAGE CDU
 00017 *MODIFICATION ON 02/06/01 - KLF
 00018 *         LOGIC ADDED TO WS-HOMEOWNER-CLS TO INCLUDE
 00019 *         CLASSESS 218 219 220 221
 00020 *MODIFICATION ON 02/14/01 - KLF
 00021 *         MODIFIED WS-TYPE2-3-SW TO WS-TYPE2-3-4-SW
 00022 *         ADDED CHECK FOR D4-TYPE4 TO SET SWITCH
 00023 *MODIFICATION ON 08/30/2006 - TJM
 00024 *         CHANGED TO INCLUDE '4000-5999' FOR CONDO SELECTION.
 00025      SKIP2
 00026  ENVIRONMENT DIVISION.
 00027  INPUT-OUTPUT SECTION.
 00028  FILE-CONTROL.
 00029      SELECT  MASTER-IN
 00030               ASSIGN  TO UT-S-MASTER.
 00031      SELECT  HOMEOWNER-MAST
 00032               ASSIGN TO UT-S-HOMEIN.
 00033      SELECT  HOMEOWNER-MAST2
 00034               ASSIGN TO UT-S-HOMEOUT.
 00035      SELECT  PRNT-FILE
 00036               ASSIGN  TO UT-S-PRNTOUT.
 00037      SKIP2
 00038  DATA DIVISION.
 00039  FILE SECTION.
 00040  ++INCLUDE ASREASFD01
 00041      SKIP2
 00042  ++INCLUDE HOMOWNFD01
 00043      SKIP2
 00044  ++INCLUDE HOMOWNFD02
 00045      SKIP2
 00046  FD  PRNT-FILE
 00047      BLOCK CONTAINS 0 RECORDS
 00048      RECORD CONTAINS 133 CHARACTERS
 00049      RECORDING MODE IS F
 00050      DATA RECORD IS PRNT-REC.
 00051  01  PRNT-REC               PIC X(133).
 00052      EJECT
 00053  WORKING-STORAGE SECTION.
 00054  77  FILLER            PIC X(58)  VALUE
 00055      '*************  WORKING STORAGE STARTS HERE  **************'.
 00056      SKIP1
 00057  77  WS-PROG           PIC X(8)  VALUE 'ASREA003'.
 00058  77  PAGE-CNTR         PIC S9(5) VALUE +0     PACKED-DECIMAL.
 00059  77  LINE-CNTR         PIC S9(3) VALUE +54    PACKED-DECIMAL.
 00060      88  PRINT-HEADER            VALUE +54 THRU +59.
 00061  77  MSTR-READ         PIC S9(7) VALUE +0     PACKED-DECIMAL.
 00062  77  HOME-READ         PIC S9(7) VALUE +0     PACKED-DECIMAL.
 00063  77  HOME-WRITTEN      PIC S9(7) VALUE +0     PACKED-DECIMAL.
 00064  77  SUB               PIC S9(4) VALUE +0     BINARY.
 00065  77  WS-TOTL-PRORATE   PIC 99V9(5) VALUE 0    PACKED-DECIMAL.
 00066  77  WS-TOTL-PRORATER REDEFINES WS-TOTL-PRORATE
 00067                        PIC V9(7)              PACKED-DECIMAL.
 00068  77  WS-PRORATE        PIC S9(4)V9(5) VALUE +0 PACKED-DECIMAL.
 00069  77  PRORATE-CNTR      PIC S9(3) VALUE +0     PACKED-DECIMAL.
 00070      88  PRORATE-CNTR-ZERO       VALUE +0.
 00071      88  PRORATE-CNTR-ONE        VALUE +1.
 00072  77  SET-HOME-SW       PIC X     VALUE 'N'.
 00073      88  READ-NEXT-HOMEOWNER     VALUE 'N'.
 00074  77  SET-MSTR-SW       PIC X     VALUE 'N'.
 00075      88  READ-NEXT-MSTR          VALUE 'N'.
 00076      88  VALID-REC               VALUE 'Y'.
 00077  77  CHK-VALUE-LESS-1001-SW  PIC X    VALUE 'N'.
 00078      88  CHK-VALUE-LESS-1001     VALUE 'Y'.
 00079  77  CHK-CLS-299       PIC X     VALUE 'N'.
 00080      88 CLS-299-PRESENT          VALUE 'Y'.
 00081  77  CHK-CLS-300-899   PIC X     VALUE 'N'.
 00082      88 CLS-300-899-PRESENT      VALUE 'Y'.
 00083  77  CHK-HOME-CLS      PIC X     VALUE 'N'.
 00084      88  HOME-SEG-PRESENT        VALUE 'Y'.
 00085  77  CHK-VALU-CLS      PIC X     VALUE 'N'.
 00086      88  VALU-SEG-PRESENT        VALUE 'Y'.
 00087  77  WS-TYPE2-3-4-SW   PIC X     VALUE 'N'.
 00088      88  WS-TYPE2-3-4-ON         VALUE 'Y'.
 00089  77  WS-TYPE5-SW       PIC X     VALUE 'N'.
 00090      88  WS-TYPE5-ON             VALUE 'Y'.
 00091      EJECT
 00092  01  WORK-AREA.
 00093      05  WS-DATE.
 00094          10  YY                      PIC 9(2)    VALUE ZEROS.
 00095          10  MM                      PIC 9(2)    VALUE ZEROS.
 00096          10  DD                      PIC 9(2)    VALUE ZEROS.
 00097      05  WS-DATE-MMDDYY.
 00098          10  MM                      PIC 9(2)    VALUE ZEROS.
 00099          10  FILLER                  PIC X       VALUE '/'.
 00100          10  DD                      PIC 9(2)    VALUE ZEROS.
 00101          10  FILLER                  PIC X       VALUE '/'.
 00102          10  YY                      PIC 9(2)    VALUE ZEROS.
 00103      05  ADD-MESG      PIC X(12) VALUE 'PARCEL ADDED'.
 00104      05  BYPASS-MESG   PIC X(25) VALUE
 00105                                      'PARCEL IS NON-RESIDENTIAL'.
 00106      05  DELETE-MESG   PIC X(23) VALUE 'PARCEL NO LONGER EXISTS'.
 00107      05  ADD-PRCL-MESG PIC X(23) VALUE 'PARCEL ADDED CLASS 297 '.
 00108      05  GARAGE-MESG   PIC X(35) VALUE
 00109                                'PARCEL CONTAINS GR (GARAGE) CDU'.
 00110      SKIP1
 00111      05  HOME-KEY                VALUE LOW-VALUES.
 00112          88  HOME-EOF            VALUE HIGH-VALUES.
 00113          10  HK-VOL         PIC 9(3).
 00114          10  HK-PROP        PIC 9(15).
 00115      SKIP1
 00116      05  MSTR-KEY                VALUE LOW-VALUES.
 00117          88  MSTR-EOF            VALUE HIGH-VALUES.
 00118          10  MK-VOL         PIC 9(3).
 00119          10  MK-PROP        PIC 9(15).
 00120      SKIP1
 00121      05  WS-OCCFAC          PIC 9999V9    VALUE 0.
 00122      SKIP1
 00123      05  WS-PROP            PIC 9(15).
 00124      05  FILLER REDEFINES WS-PROP.
 00125          10 FILLER          PIC 9(11).
 00126          10 WS-PROP-4       PIC 9(4).
 00127      SKIP1
 00128      05  WS-CLS             PIC 9(3).
 00129          88 WS-HOMEOWNER-CLS     VALUE 202 THRU 213 218 219
 00130                                        220 221 234 236 278
 00131                                        294 295.
 00132          88 WS-VALUATION-CLS     VALUE 297 299.
 00133          88 WS-297-CLS           VALUE 297.
 00134          88 WS-299-CLS           VALUE 299.
 00135          88 WS-300-899-CLS       VALUE 300  THRU   997.
 00136      SKIP1
 00137      05  HDR-LINE1.
 00138          10  FILLER    PIC X(3)  VALUE SPACES.
 00139          10  H1-DATE   PIC X(8).
 00140          10  FILLER    PIC X(44) VALUE SPACES.
 00141          10  FILLER    PIC X(19) VALUE 'OFFICE OF THE COOK'.
 00142          10  FILLER    PIC X(46) VALUE 'COUNTY ASSESSOR'.
 00143          10  FILLER    PIC X(5)  VALUE 'PAGE '.
 00144          10  H1-PAGE   PIC ZZ,ZZ9.
 00145      SKIP1
 00146      05  HDR-LINE2.
 00147          10  FILLER    PIC X(53) VALUE '   ASREA852'.
 00148          10  FILLER    PIC X(24) VALUE 'HOMEOWNER FILE CREATION '.
 00149          10  FILLER    PIC X(12) VALUE 'ERROR REPORT'.
 00150      SKIP1
 00151      05  HDR-LINE3.
 00152          10  FILLER    PIC X(31) VALUE '   VOL   PROPERTY NUMBER'.
 00153          10  FILLER    PIC X(19) VALUE 'TAX    HOMEOWNER'.
 00154          10  FILLER    PIC X(6)  VALUE 'MASTER'.
 00155      SKIP1
 00156      05  HDR-LINE4.
 00157          10  FILLER    PIC X(30) VALUE SPACES.
 00158          10  FILLER    PIC X(10) VALUE 'CODE'.
 00159          10  FILLER    PIC X(11) VALUE 'CLASS'.
 00160          10  FILLER    PIC X(5)  VALUE 'CLASS'.
 00161      SKIP1
 00162      05  DETAIL-LINE             VALUE SPACES.
 00163          10  FILLER    PIC X(3).
 00164          10  D-VOL     PIC 999.
 00165          10  FILLER    PIC XX.
 00166          10  D-PROP    PIC 99B99B999B999B9999.
 00167          10  FILLER    PIC X(3).
 00168          10  D-TXCD    PIC 9(5).
 00169          10  FILLER    PIC X(7).
 00170          10  D-H-CLS   PIC 9B99.
 00171          10  FILLER    PIC X(7).
 00172          10  D-M-CLS   PIC 9B99.
 00173          10  FILLER    PIC X(5).
 00174          10  D-MESG    PIC X(50).
 00175      SKIP1
 00176      05  FINAL-LINE.
 00177          10  FILLER    PIC X(9)  VALUE '   TOTAL '.
 00178          10  FIN-MSG   PIC X(26).
 00179          10  FIN-TOT   PIC Z,ZZZ,ZZZ,ZZ9.
 00180      SKIP1
 00181  01  FILLER            PIC X(58)  VALUE
 00182      '*************  WORKING STORAGE   ENDS HERE  **************'.
 00183      EJECT
 00184  LINKAGE SECTION.
 00185  01  LINK-INFO.
 00186      05  LI-LENGTH      PIC S9(4)  BINARY.
 00187          88  LI-VALID-LENGTH       VALUE +1.
 00188      05  LI-NEWYEAR     PIC X.
 00189          88  LI-VALID-NEWYR        VALUE 'N' 'Y'.
 00190          88  LI-NEWYEAR-YES        VALUE 'Y'.
 00191      SKIP2
 00192  PROCEDURE DIVISION USING LINK-INFO.
 00193  010-CHECK-LINKAGE.
 00194      IF  NOT LI-VALID-LENGTH
 00195           DISPLAY  'LINKAGE LENGTH INVALID'
 00196           MOVE  16 TO RETURN-CODE
 00197           STOP RUN
 00198      ELSE
 00199           IF  NOT LI-VALID-NEWYR
 00200                DISPLAY  'NEW YR PARM INVALID = ' LI-NEWYEAR
 00201                MOVE  16 TO RETURN-CODE
 00202                STOP RUN.
 00203      SKIP2
 00204  020-BEGIN.
 00205      OPEN  INPUT  MASTER-IN
 00206            INPUT  HOMEOWNER-MAST
 00207      OPEN  OUTPUT HOMEOWNER-MAST2
 00208            OUTPUT PRNT-FILE
 00209      ACCEPT WS-DATE  FROM DATE
 00210      MOVE CORR WS-DATE TO WS-DATE-MMDDYY
 00211      MOVE WS-DATE-MMDDYY TO H1-DATE
 00212      SKIP1
 00213      PERFORM  700-READ-HOMEOWNER
 00214      PERFORM  750-READ-MASTER
 00215                UNTIL  MSTR-EOF
 00216                     OR
 00217                       VALID-REC
 00218      SKIP1
 00219      PERFORM  100-MAINLINE
 00220                UNTIL  (MSTR-EOF AND  HOME-EOF)
 00221      SKIP1
 00222      PERFORM 850-PRINT-TOTALS
 00223      SKIP1
 00224      CLOSE   MASTER-IN
 00225              HOMEOWNER-MAST
 00226              HOMEOWNER-MAST2
 00227              PRNT-FILE
 00228      STOP RUN.
 00229      EJECT
 00230  100-MAINLINE.
 00231      IF  MSTR-KEY GREATER HOME-KEY
 00232           MOVE  'N' TO SET-HOME-SW
 00233           PERFORM  200-MSTR-HIGH-ROUTINE
 00234      ELSE
 00235           MOVE  'N' TO SET-MSTR-SW
 00236           IF  NOT M-EXEMPT
 00237             AND
 00238               NOT M-RAILROAD
 00239                MOVE  'N' TO CHK-CLS-299 CHK-VALU-CLS CHK-HOME-CLS
 00240                             WS-TYPE2-3-4-SW WS-TYPE5-SW
 00241                        CHK-VALUE-LESS-1001-SW   CHK-CLS-300-899
 00242                MOVE  ZERO TO PRORATE-CNTR WS-PRORATE
 00243                                           WS-OCCFAC
 00244                PERFORM 300-SEARCH-SEGMENTS
 00245                 VARYING SUB FROM 1 BY 1
 00246                  UNTIL  SUB GREATER THAN M-DTL-QST-CTR
 00247 **********************OR
 00248 ************************(HOME-SEG-PRESENT AND CLS-299-PRESENT)
 00249                IF  HOME-SEG-PRESENT
 00250                  OR
 00251                    VALU-SEG-PRESENT
 00252                  OR
 00253                  (CLS-300-899-PRESENT AND
 00254                     CHK-VALUE-LESS-1001)
 00255                     IF  MSTR-KEY EQUAL HOME-KEY
 00256                          MOVE 'N' TO SET-HOME-SW
 00257                          IF WS-TYPE2-3-4-ON OR
 00258                             WS-TYPE5-ON
 00259                             PERFORM 400-MATCH-ROUTINE
 00260                          ELSE
 00261                             CONTINUE
 00262                     ELSE
 00263                          IF WS-TYPE2-3-4-ON OR
 00264                             WS-TYPE5-ON
 00265                             PERFORM 500-MSTR-LOW-ROUTINE
 00266                          ELSE
 00267                             CONTINUE
 00268                ELSE
 00269                     PERFORM  600-BYPASS-MSTR-ROUTINE
 00270           ELSE
 00271                PERFORM  600-BYPASS-MSTR-ROUTINE.
 00272      SKIP1
 00273      IF  READ-NEXT-HOMEOWNER
 00274           PERFORM  700-READ-HOMEOWNER.
 00275      SKIP1
 00276      PERFORM  750-READ-MASTER
 00277       UNTIL  MSTR-EOF
 00278            OR
 00279              VALID-REC.
 00280      SKIP3
 00281  200-MSTR-HIGH-ROUTINE.
 00282      MOVE  HO-VOL TO D-VOL
 00283      MOVE  HO-PROP TO D-PROP
 00284      MOVE  HO-CLASS TO D-H-CLS
 00285      MOVE  HO-TXCD TO D-TXCD
 00286      MOVE  DELETE-MESG TO D-MESG
 00287      PERFORM  800-PRINT-DETAIL.
 00288      EJECT
 00289  300-SEARCH-SEGMENTS.
 00290      IF  D2-TYPE2-5 (SUB)
 00291           MOVE  D1-CLS (SUB) TO  WS-CLS
 00292           IF  WS-VALUATION-CLS
 00293               IF  D4-TYPE4 (SUB)
 00294                    PERFORM  350-RECOMPUTE-TYPE4
 00295               ELSE
 00296                    IF  D5-TYPE5 (SUB)
 00297                         PERFORM  370-RECOMPUTE-TYPE5.
 00298      SKIP1
 00299      IF  D2-TYPE2-5 (SUB)
 00300          IF  WS-HOMEOWNER-CLS
 00301               MOVE  'Y' TO CHK-HOME-CLS
 00302               IF  D2-PCASSD (SUB) GREATER THAN ZERO
 00303                    ADD  +1 TO PRORATE-CNTR
 00304                    ADD  D2-PCASSD (SUB) TO WS-PRORATE
 00305               ELSE
 00306                    NEXT SENTENCE
 00307          ELSE
 00308            IF (WS-297-CLS
 00309              AND
 00310                D2-VAL (SUB) GREATER THAN 1000)
 00311                OR
 00312                  (WS-299-CLS
 00313                  AND
 00314                   D2-VAL (SUB) GREATER THAN 0)
 00315                    MOVE 'Y' TO CHK-VALU-CLS
 00316                    IF D2-PCASSD (SUB) GREATER THAN ZERO
 00317                       ADD +1 TO PRORATE-CNTR
 00318                       ADD D2-PCASSD (SUB) TO WS-PRORATE
 00319                    ELSE
 00320                        NEXT SENTENCE
 00321            ELSE
 00322                IF WS-297-CLS
 00323                  AND
 00324                       D2-VAL (SUB) LESS THAN 1001
 00325                        MOVE 'Y' TO CHK-VALUE-LESS-1001-SW
 00326                        IF D2-PCASSD (SUB) GREATER THAN ZERO
 00327                           ADD +1 TO PRORATE-CNTR
 00328                           ADD D2-PCASSD (SUB) TO WS-PRORATE.
 00329      SKIP1
 00330      IF  D2-TYPE2-5 (SUB)
 00331        AND
 00332          WS-299-CLS
 00333           MOVE 'Y' TO CHK-CLS-299.
 00334      SKIP1
 00335      IF  D2-TYPE2-5 (SUB)
 00336        AND
 00337             WS-300-899-CLS
 00338              MOVE 'Y'  TO  CHK-CLS-300-899.
 00339      SKIP1
 00340      IF (D2-TYPE2 (SUB) OR D3-TYPE3 (SUB) OR D4-TYPE4(SUB))
 00341           MOVE 'Y' TO WS-TYPE2-3-4-SW
 00342      ELSE
 00343        IF D5-TYPE5 (SUB)
 00344 *         IF D5-AGE (SUB) = +001
 00345              MOVE 'Y' TO WS-TYPE5-SW.
 00346      SKIP1
 00347      IF D4-TYPE4 (SUB)
 00348         IF D4-OCCFAC (SUB) GREATER THAN WS-OCCFAC
 00349            MOVE D4-OCCFAC (SUB) TO WS-OCCFAC.
 00350      SKIP1
 00351      IF D5-TYPE5 (SUB)
 00352         IF D5-OCCFAC (SUB) GREATER THAN WS-OCCFAC
 00353            MOVE D5-OCCFAC (SUB) TO WS-OCCFAC.
 00354      SKIP1
 00355      IF  D2-TYPE2-5 (SUB)
 00356          IF  D2-CLS (SUB) = 299 AND D2-CDU (SUB) = 'GR'
 00357              MOVE M-PROP TO WS-PROP
 00358              IF  WS-PROP-4 >= 1000 AND <= 2999
 00359 *            IF ((WS-PROP-4 >= 1000 AND <= 2999) OR
 00360 *                (WS-PROP-4 >= 4000 AND <= 5999))
 00361                  MOVE 'N' TO CHK-HOME-CLS
 00362                  MOVE 'N' TO CHK-VALU-CLS
 00363                  MOVE  M-VOL TO D-VOL
 00364                  MOVE  M-PROP TO D-PROP
 00365                  MOVE  M-TXCD TO D-TXCD
 00366                  MOVE  M-CLS TO D-H-CLS
 00367                  MOVE  GARAGE-MESG TO D-MESG
 00368                  PERFORM  800-PRINT-DETAIL
 00369              END-IF
 00370          END-IF
 00371      END-IF.
 00372      SKIP1
 00373      IF  D2-TYPE2-5 (SUB)
 00374        AND
 00375          D2-QUES-CLASS (SUB)
 00376           ADD +1 TO SUB.
 00377      SKIP3
 00378  350-RECOMPUTE-TYPE4.
 00379      MOVE  D4-TOTVAL (SUB) TO D4-REPCST (SUB)
 00380      CALL  WS-PROG    USING M-DTL-QST (SUB).
 00381      SKIP3
 00382  370-RECOMPUTE-TYPE5.
 00383      IF  D5-OCCFAC (SUB) NOT EQUAL ZERO
 00384           COMPUTE  D5-REPCST (SUB) =
 00385            (D5-REPCST (SUB) * 100) / D5-OCCFAC (SUB)
 00386           CALL  WS-PROG    USING M-DTL-QST (SUB).
 00387      EJECT
 00388  400-MATCH-ROUTINE.
 00389      MOVE  HO-REC  TO  HO-REC2
 00390      IF  LI-NEWYEAR-YES
 00391           IF HO-NPHE-STATUS-2 = 'TR'
 00392              CONTINUE
 00393           ELSE
 00394              MOVE 'AB'   TO HO-NPHE-STATUS-2
 00395           END-IF
 00396           MOVE  ZERO TO HO-STAT1-2 HO-STAT2-2 HO-H-V2.
 00397      SKIP1
 00398      IF WS-TYPE2-3-4-ON
 00399         MOVE 0100.0 TO HO-OCCFAC2
 00400      ELSE
 00401         IF WS-TYPE5-ON
 00402            MOVE WS-OCCFAC TO HO-OCCFAC2
 00403         ELSE
 00404            MOVE 0100.0 TO HO-OCCFAC2.
 00405      SKIP1
 00406      PERFORM  550-COMPUTE-PRORATE
 00407      MOVE  M-CLS TO  HO-CLASS2
 00408      MOVE  M-TXCD TO  HO-TXCD2
 00409      PERFORM 900-WRITE-HOMEOWNER.
 00410      SKIP3
 00411  500-MSTR-LOW-ROUTINE.
 00412      MOVE  SPACES TO HO-REC2
 00413      MOVE  M-VOL TO  HO-VOL2
 00414      MOVE  M-PROP TO  HO-PROP2
 00415      MOVE  M-CLS TO  HO-CLASS2
 00416      MOVE  M-TXCD TO  HO-TXCD2
 00417      MOVE  ZERO TO HO-STAT1-2 HO-STAT2-2 HO-YRAPPL2
 00418            HO-H-V2 HO-ASSDVAL2 HO-EQVAL2 HO-ZIP2
 00419            HO-NPHE2 HO-NPHE-BSYR2 HO-EQFCTR2
 00420      MOVE  '00001' TO HO-COOPQTY2
 00421 *    MOVE  1.4153  TO HO-EQFCTR2
 00422      IF WS-TYPE2-3-4-ON
 00423         MOVE 0100.0 TO HO-OCCFAC2
 00424      ELSE
 00425         IF WS-TYPE5-ON
 00426            MOVE WS-OCCFAC TO HO-OCCFAC2
 00427         ELSE
 00428            MOVE 0100.0 TO HO-OCCFAC2.
 00429      PERFORM  550-COMPUTE-PRORATE
 00430      PERFORM  570-MOVE-MSTR-TO-PRNT
 00431      PERFORM  800-PRINT-DETAIL
 00432      PERFORM  900-WRITE-HOMEOWNER.
 00433      SKIP3
 00434  550-COMPUTE-PRORATE.
 00435      IF  CLS-299-PRESENT
 00436        OR
 00437          PRORATE-CNTR-ZERO
 00438           MOVE 1 TO HO-PRORATE2
 00439      ELSE
 00440           IF  PRORATE-CNTR-ONE
 00441                MOVE  WS-PRORATE TO WS-TOTL-PRORATE
 00442                MOVE  WS-TOTL-PRORATER TO HO-PRORATE2
 00443           ELSE
 00444                COMPUTE  WS-TOTL-PRORATE ROUNDED =
 00445                             WS-PRORATE / PRORATE-CNTR
 00446                MOVE  WS-TOTL-PRORATER TO HO-PRORATE2.
 00447      SKIP3
 00448  570-MOVE-MSTR-TO-PRNT.
 00449      MOVE  M-VOL TO  D-VOL
 00450      MOVE  M-PROP TO D-PROP
 00451      MOVE  M-TXCD TO D-TXCD
 00452      MOVE  M-CLS TO D-H-CLS
 00453      MOVE  ADD-MESG TO D-MESG.
 00454      IF CLS-300-899-PRESENT AND
 00455                     CHK-VALUE-LESS-1001
 00456      MOVE  ADD-PRCL-MESG TO D-MESG.
 00457      EJECT
 00458  600-BYPASS-MSTR-ROUTINE.
 00459      IF  MSTR-KEY EQUAL HOME-KEY
 00460           MOVE  'N' TO SET-HOME-SW
 00461           MOVE  HO-VOL TO D-VOL
 00462           MOVE  HO-PROP TO D-PROP
 00463           MOVE  HO-TXCD TO D-TXCD
 00464           MOVE  HO-CLASS TO D-H-CLS
 00465           MOVE  M-CLS TO D-M-CLS
 00466           INSPECT  D-M-CLS REPLACING ALL ' ' BY '-'
 00467           MOVE  BYPASS-MESG TO D-MESG
 00468           PERFORM  800-PRINT-DETAIL.
 00469      SKIP3
 00470  700-READ-HOMEOWNER.
 00471      READ  HOMEOWNER-MAST
 00472             AT END  MOVE HIGH-VALUE TO HOME-KEY.
 00473      IF  NOT HOME-EOF
 00474           ADD +1 TO HOME-READ
 00475           MOVE HO-VOL  TO  HK-VOL
 00476           MOVE HO-PROP TO  HK-PROP.
 00477      SKIP1
 00478      MOVE  'Y' TO SET-HOME-SW.
 00479      SKIP3
 00480  750-READ-MASTER.
 00481      READ  MASTER-IN
 00482             AT END MOVE HIGH-VALUES TO MSTR-KEY.
 00483      IF  NOT MSTR-EOF
 00484           ADD +1 TO MSTR-READ
 00485           IF  M-RE-VOL
 00486                MOVE  M-VOL   TO MK-VOL
 00487                MOVE  M-PROP  TO MK-PROP
 00488                MOVE  'Y' TO SET-MSTR-SW.
 00489      EJECT
 00490  800-PRINT-DETAIL.
 00491      IF  PRINT-HEADER
 00492           PERFORM  870-HDR-ROUTINE.
 00493      SKIP1
 00494      INSPECT  D-PROP REPLACING ALL SPACES BY '-'
 00495      INSPECT  D-H-CLS REPLACING ALL SPACES BY '-'
 00496      WRITE  PRNT-REC FROM DETAIL-LINE AFTER ADVANCING   2
 00497      ADD  +2 TO LINE-CNTR
 00498      MOVE  SPACES TO DETAIL-LINE.
 00499      SKIP3
 00500  850-PRINT-TOTALS.
 00501      IF  PRINT-HEADER
 00502           PERFORM 870-HDR-ROUTINE.
 00503      SKIP1
 00504      MOVE  'MASTER RECORDS READ' TO FIN-MSG
 00505      MOVE  MSTR-READ TO  FIN-TOT
 00506      WRITE  PRNT-REC FROM FINAL-LINE AFTER ADVANCING   3
 00507      MOVE  'HOMEOWNERS RECORDS READ' TO FIN-MSG
 00508      MOVE  HOME-READ TO FIN-TOT
 00509      WRITE  PRNT-REC FROM FINAL-LINE AFTER ADVANCING   1
 00510      MOVE  'HOMEOWNERS RECORDS WRITTEN' TO FIN-MSG
 00511      MOVE  HOME-WRITTEN TO  FIN-TOT
 00512      WRITE  PRNT-REC FROM FINAL-LINE AFTER ADVANCING   1.
 00513      SKIP3
 00514  870-HDR-ROUTINE.
 00515      ADD  +1 TO PAGE-CNTR
 00516      MOVE  PAGE-CNTR TO H1-PAGE
 00517      MOVE  +6 TO LINE-CNTR
 00518      WRITE  PRNT-REC FROM HDR-LINE1 AFTER ADVANCING   PAGE
 00519      WRITE  PRNT-REC FROM HDR-LINE2 AFTER ADVANCING   2
 00520      WRITE  PRNT-REC FROM HDR-LINE3 AFTER ADVANCING   2
 00521      WRITE  PRNT-REC FROM HDR-LINE4 AFTER ADVANCING   1.
 00522      SKIP3
 00523  900-WRITE-HOMEOWNER.
 00524 *    IF HO-OCCFAC2 NOT LESS THAN .1 AND HO-OCCFAC2 NOT GREATER
 00525 *       THAN 99.9
 00526 *       MOVE 2 TO HO-STAT1-2
 00527 *    ELSE
 00528         IF HO-OCCFAC2 EQUAL 100.0
 00529                  AND
 00530            HO-STAT1-2 EQUAL 2
 00531               MOVE 0 TO HO-STAT1-2.
 00532      WRITE  HO-REC2
 00533      ADD  +1 TO HOME-WRITTEN.
