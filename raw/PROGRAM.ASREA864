 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID. ASREA864.
 00003  AUTHOR. RUSS DOBER - STAN MOLIS.
 00004  DATE-WRITTEN. MAY 31,1979.
 00005  DATE-COMPILED.
 00006 *REMARKS.  THIS PROGRAM WILL UPDATE THE HOMESTEAD
 00007 *          EQUALIZED VALUATION FILE WITH MAINTENANCE
 00008 *          RECORDS.
 00009 *    MODIFICATION 001.  JAN, 1989.
 00010 *                       VINCENT FALK -- STANLEY MOLIS.
 00011 *MODIFICATION ON 08/31/04 - TJM
 00012 *         MOVE HE-AVAL AND HE-NPHE-AMT TO OUTPUT.
 00013 *MODIFICATION ON 04/26/05 - KLF
 00014 *         TO ACCOMODATE HOEXEMP FILE EXPANSION TO 130
 00015 *COPIED ASREA865 TO ASREA864 TO ACCOMODATE HOMEXEMP RECORD LENGTH
 00016 *   OF 140  04/05  KLF
 00017 *MODIFICATION ON 08/24/05 - TJM
 00018 *    HE-NPHE-EXAMT MAY BE ZERO AS WELL AS GREATER THAN ZERO.
 00019 *MODIFICATION ON 07/03/08 - NTF
 00020 *    MODIFIED PROGRAM TO READ THE LANDMARK FILE AND ADJUST
 00021 *    NPHE-AMT ACCORDINGLY.  ADDED LINKAGE SECTION.
 00022 *MODIFICATION ON 07/29/09 - TJM
 00023 * IN 070-CHANGE-ROUTINE, MOVE (ASSD-VAL) HE-AVAL > 0 TO OUT-ASSDVA
 00024 *MODIFICATION ON 08/24/2011 - TJM
 00025 * IN 070-CHANGE-ROUTINE, IF LANDMARK AND CO-OP, DO NOT MOVE 6000
 00026 *               TO NPHE AMOUNT AND DO NOT MOVE 'LN' TO STATUS.
 00027 * IN 110-HOMEXEMP-FLD-CHK, IF LANDMARK AND CO-OP, DO NOT MOVE 'LN'
 00028 *               STATUS.
 00029      SKIP3
 00030  ENVIRONMENT DIVISION.
 00031  CONFIGURATION SECTION.
 00032  SOURCE-COMPUTER. IBM-370.
 00033  OBJECT-COMPUTER. IBM-370.
 00034  INPUT-OUTPUT SECTION.
 00035  FILE-CONTROL.
 00036      SELECT HOMEXEMP          ASSIGN TO UT-S-HOMEXEMP.
 00037      SELECT HOMESTEAD-FILE    ASSIGN TO UT-S-HOMESTD.
 00038      SELECT EQUALFCT          ASSIGN TO UT-S-EQFACT.
 00039      SELECT PRINT-FILE        ASSIGN TO UT-S-PRINT.
 00040      SELECT ERROR-FILE        ASSIGN TO UT-S-ERROR.
 00041      SELECT OUTHOME-FILE      ASSIGN TO UT-S-OUTHOME.
 00042      SELECT LNDMRK-PROP-FILE  ASSIGN TO DA-LANDMARK
 00043                               ORGANIZATION IS INDEXED
 00044                               ACCESS MODE IS RANDOM
 00045                               RECORD KEY IS LDMK-KEY
 00046                               FILE STATUS IS FILE-STATUS
 00047                                   FILE-STATUS2.
 00048      SKIP3
 00049  DATA DIVISION.
 00050  FILE SECTION.
 00051      SKIP1
 00052  ++INCLUDE HEXMNTFD02
 00053      SKIP2
 00054  ++INCLUDE HOMEXEFD01
 00055      SKIP2
 00056  ++INCLUDE REBEQFFD01
 00057      SKIP2
 00058  FD  PRINT-FILE
 00059      LABEL RECORDS ARE STANDARD
 00060      BLOCK CONTAINS 0 RECORDS
 00061      RECORD CONTAINS 133 CHARACTERS
 00062      RECORDING MODE IS F
 00063      DATA RECORD IS PRINT-REC.
 00064  01  PRINT-REC              PIC X(133).
 00065      SKIP2
 00066  FD  ERROR-FILE
 00067      LABEL RECORDS ARE STANDARD
 00068      BLOCK CONTAINS 0 RECORDS
 00069      RECORD CONTAINS 133 CHARACTERS
 00070      RECORDING MODE IS F
 00071      DATA RECORD IS ERR-REC.
 00072  01  ERR-REC                PIC X(133).
 00073      SKIP2
 00074  FD  OUTHOME-FILE
 00075      LABEL RECORDS ARE STANDARD
 00076      BLOCK CONTAINS 0 RECORDS
 00077      RECORD CONTAINS 130 CHARACTERS
 00078      RECORDING MODE IS F
 00079      DATA RECORD IS OUTHOME-REC.
 00080  01  OUTHOME-REC.
 00081      05  OUT-VOL       PIC 999    COMP-3.
 00082      05  OUT-PROP      PIC 9(15)  COMP-3.
 00083      05  OUT-TXCD      PIC 9(5)   COMP-3.
 00084      05  OUT-STAT1     PIC 9.
 00085      05  OUT-STAT2     PIC 9.
 00086      05  FILLER        PIC X.
 00087      05  OUT-CLS       PIC 999    COMP-3.
 00088      05  OUT-YR        PIC 99.
 00089      05  OUT-NPHE-STATUS  PIC XX.
 00090      05  OUT-PRORAT    PIC 9V9(6) COMP-3.
 00091      05  OUT-COOP      PIC 9(5)   COMP-3.
 00092      05  OUT-OCCFAC    PIC 9999V9   COMP-3.
 00093      05  FILLER        PIC X.
 00094      05  OUT-EQFACT    PIC 9V9(4)     COMP-3.
 00095      05  OUT-HIV       PIC 9.
 00096      05  OUT-ASSDVAL   PIC 9(9)   COMP-3.
 00097      05  OUT-EQVAL     PIC 9(9)   COMP-3.
 00098      05  OUT-NPHE-EXAMT PIC 9(9)   COMP-3.
 00099      05  OUT-NPHE-BSYR  PIC 9(4).
 00100      05  FILLER        PIC X(01).
 00101      05  OUT-NAME      PIC X(22).
 00102      05  OUT-ADDRESS   PIC X(22).
 00103      05  OUT-CITY      PIC X(12).
 00104      05  OUT-STATE     PIC XX.
 00105      05  OUT-ZIP       PIC 9(9)   COMP-3.
 00106      05  FILLER        PIC X(10).
 00107      SKIP2
 00108  ++INCLUDE ASLNDMRKF1
 00109      SKIP2
 00110  WORKING-STORAGE SECTION.
 00111  77  CURRENT-DATE      PIC 99/99/9(4).
 00112  77  CURRENT-TIME      PIC 99,99,99.
 00113  77  MDCY-OR-CYMD PIC S9(5)V9(4) PACKED-DECIMAL VALUE +10000.0001.
 00114  77  FILLER            PIC X(58)  VALUE
 00115      '*************  WORKING STORAGE STARTS HERE  **************'.
 00116      SKIP1
 00117  77  PG-CNT                 PIC S9(5) VALUE +0       COMP-3.
 00118  77  ERR-PG-CNT             PIC S9(5) VALUE +0       COMP-3.
 00119  77  LINE-CNT               PIC S9(3)                COMP-3.
 00120  77  ERR-LINE-CNT           PIC S9(3)                COMP-3.
 00121  77  HOME-OUTCNT            PIC S9(7) VALUE +0       COMP-3.
 00122  77  HOME-INCNT             PIC S9(7) VALUE +0       COMP-3.
 00123  77  ERR-CNT                PIC S9(7) VALUE +0       COMP-3.
 00124  77  DELT-CNT               PIC S9(7) VALUE +0       COMP-3.
 00125  77  CHG-CNT                PIC S9(7) VALUE +0       COMP-3.
 00126  77  ADD-CNT                PIC S9(7) VALUE +0       COMP-3.
 00127  77  EXEMP-INCNT            PIC S9(7) VALUE +0       COMP-3.
 00128  77  WS-LDMK-CNT            PIC S9(7) VALUE +0       COMP-3.
 00129  77  D-MESG            PIC X(7)   VALUE 'DELETED'.
 00130  77  C-MESG            PIC X(7)   VALUE 'CHANGED'.
 00131  77  A-MESG            PIC X(6)  VALUE 'ADDED'.
 00132  77  ADD-ERR-MESG           PIC X(33) VALUE
 00133          'INVALID ADD RECORD ALREADY EXISTS'.
 00134  77  NO-MTCH-MESG           PIC X(33) VALUE
 00135          'NO MATCHING EXEMPTION RECORD'.
 00136  77  NO-EQFT-MESG           PIC X(33) VALUE
 00137          'NO EQUALIZATION FACTOR FOR YR APL'.
 00138  77  EXEMP-MESG             PIC X(30) VALUE
 00139          'TOTAL MAINTENANCE RECORDS READ'.
 00140  77  TADD-MESG              PIC X(10) VALUE 'TOTAL ADDS'.
 00141  77  CHG-MESG               PIC X(13) VALUE 'TOTAL CHANGES'.
 00142  77  DELT-MESG              PIC X(13) VALUE 'TOTAL DELETES'.
 00143  77  ERR-MESG               PIC X(12) VALUE 'TOTAL ERRORS'.
 00144  77  HOME-MESG              PIC X(28) VALUE
 00145          'TOTAL HOMEOWNER RECORDS READ'.
 00146  77  HOME-OUTMESG           PIC X(31) VALUE
 00147          'TOTAL HOMEOWNER RECORDS WRITTEN'.
 00148  77  LDMK-OUTMESG           PIC X(31) VALUE
 00149          'TOTAL LANDMARK  RECORDS WRITTEN'.
 00150  77  SAVE-QUAD              PIC  9.
 00151  77  ERR-CHK                PIC  9    VALUE 0.
 00152      88 ERR                           VALUE 1.
 00153  77  NO-MATCH-CHK           PIC  9    VALUE 0.
 00154      88 NO-MATCH                      VALUE 1.
 00155  77  EXEMP-EOF              PIC 9     VALUE 0.
 00156      88 END-OF-EXEMP-FILE             VALUE 1.
 00157  77  HOME-EOF               PIC 9     VALUE 0.
 00158      88 END-OF-HOME-FILE              VALUE 1.
 00159  77  EQFACT-EOF             PIC 9     VALUE 0.
 00160      88 END-OF-EQFACT-FILE            VALUE 1.
 00161  77  SUB                    PIC 9(4)                 COMP.
 00162  77  VSAM-ERR-SW            PIC X     VALUE 'N'.
 00163      88  VSAM-ERR                     VALUE 'Y'.
 00164  77  WS-LDMK-SW             PIC X     VALUE 'N'.
 00165  77  LDMK-EOF-SW            PIC X     VALUE 'N'.
 00166      88  LDMK-EOF                     VALUE 'Y'.
 00167      SKIP2
 00168  01  WORK-VARIABLES.
 00169      05  CURRENT-DATE-RETURN.
 00170          10  SYSTEM-DATE  PIC 9(8).
 00171          10  SYSTEM-TIME  PIC 9(6).
 00172          10  FILLER       PIC X(7).
 00173      05  WS-CHANGE-ZIP      PIC 9(9).
 00174      05  WS-C-Z  REDEFINES  WS-CHANGE-ZIP.
 00175          10  WS-ZIP-FIVE    PIC 9(5).
 00176          10  FILLER         PIC X(4).
 00177      05  FILE-STATUS        PIC 9(2)  VALUE 0.
 00178          88  GOOD-STATUS              VALUE 00.
 00179          88  LAND-NOTFND              VALUE 23.
 00180      05  FILE-STATUS2                           BINARY.
 00181          10  FILE2-RETURN   PIC 99    VALUE 0.
 00182          10  FILE2-FUNCTION PIC 9     VALUE 0.
 00183          10  FILE2-FEEDBACK PIC 999   VALUE 0.
 00184      05  ERR-HDR.
 00185          10  FILLER         PIC X(3)  VALUE SPACES.
 00186          10  ERR-DATE       PIC X(8).
 00187          10  FILLER         PIC X(44) VALUE SPACES.
 00188          10  FILLER         PIC X(14) VALUE 'OFFICE OF THE '.
 00189          10  FILLER         PIC X(51) VALUE 'ASSESSOR'.
 00190          10  FILLER         PIC X(5)  VALUE 'PAGE '.
 00191          10  ERR-PG         PIC ZZ,ZZ9.
 00192      SKIP1
 00193      05  ERR-HDR2.
 00194          10  FILLER         PIC X(39) VALUE '   ASREA864'.
 00195          10  FILLER      PIC X(21) VALUE 'HOMEOWNER MAINTENANCE'.
 00196          10  FILLER         PIC X(13) VALUE ' UPDATE ERROR'.
 00197          10  FILLER         PIC X(07) VALUE ' REPORT'.
 00198      SKIP1
 00199      05  ERR-TIT.
 00200          10  FILLER         PIC X(16) VALUE '  ACTION  TOWN  '.
 00201          10  FILLER         PIC X(16) VALUE 'VOL    PROPERTY '.
 00202          10  FILLER         PIC X(18) VALUE 'NUMBER    CLASS   '.
 00203          10  FILLER         PIC X(19) VALUE 'PRO-RATION     COOP'.
 00204          10  FILLER         PIC X(11) VALUE '     YEAR  '.
 00205          10  FILLER         PIC X(17) VALUE 'ASSESSED     OCCP'.
 00206      SKIP1
 00207      05  ERR-TIT2.
 00208          10  FILLER         PIC X(42) VALUE SPACES.
 00209          10  FILLER      PIC X(21) VALUE 'MA/MI    FACTOR      '.
 00210          10  FILLER         PIC X(17) VALUE 'QUANTITY   APPL  '.
 00211          10  FILLER         PIC X(18) VALUE 'VALUATION   FACTOR'.
 00212          10  FILLER         PIC X(5)  VALUE SPACES.
 00213          10  FILLER         PIC X(14) VALUE 'ERROR MESSAGES'.
 00214      SKIP1
 00215      05  ERR-DETAIL.
 00216          10  FILLER         PIC X(4)  VALUE SPACES.
 00217          10  E-ACTION       PIC X.
 00218          10  FILLER         PIC X(6)  VALUE SPACES.
 00219          10  E-TOWN         PIC 99.
 00220          10  FILLER         PIC X(3)  VALUE SPACES.
 00221          10  E-VOL          PIC 999.
 00222          10  FILLER         PIC X(4)  VALUE SPACES.
 00223          10  E-PROP         PIC 99B99B999B999B9999.
 00224          10  FILLER         PIC X(2)  VALUE SPACES.
 00225          10  E-CLS          PIC 9B99.
 00226          10  FILLER         PIC X(4)  VALUE SPACES.
 00227          10  E-PRO          PIC Z.999999.
 00228          10  FILLER         PIC X(3)  VALUE SPACES.
 00229          10  E-COOP         PIC ZZ,ZZ9.
 00230          10  FILLER         PIC X(4)  VALUE SPACES.
 00231          10  E-YR           PIC 99.
 00232          10  FILLER         PIC X(1)  VALUE SPACES.
 00233          10  E-AVAL         PIC ZZZ,ZZZ,ZZ9.
 00234          10  FILLER         PIC X     VALUE SPACES.
 00235          10  E-OCCFAC       PIC Z999.9.
 00236          10  FILLER         PIC X(1)  VALUE SPACES.
 00237 *        10  E-NPHE-EXAMT   PIC X(5)  VALUE SPACES.
 00238          10  E-NPHE-EXAMT   PIC X(7)  VALUE SPACES.
 00239          10  FILLER         PIC X(1)  VALUE SPACES.
 00240          10  E-MESG         PIC X(33).
 00241      SKIP1
 00242      05  PRT-HDR.
 00243          10  FILLER         PIC X(3)  VALUE SPACES.
 00244          10  PRT-DATE       PIC X(8).
 00245          10  FILLER         PIC X(44) VALUE SPACES.
 00246          10  FILLER         PIC X(19) VALUE 'OFFICE OF THE ASSES'.
 00247          10  FILLER         PIC X(46) VALUE 'SOR'.
 00248          10  FILLER         PIC X(5)  VALUE 'PAGE '.
 00249          10  PRT-PG         PIC ZZ,ZZ9.
 00250      SKIP1
 00251      05  PRT-HDR2.
 00252          10  FILLER         PIC X(42) VALUE '   ASREA864'.
 00253          10  FILLER      PIC X(21) VALUE 'HOMEOWNER MAINTENANCE'.
 00254          10  FILLER         PIC X(14) VALUE ' UPDATE REPORT'.
 00255      SKIP1
 00256      05  PRT-TIT.
 00257          10  FILLER         PIC X(02) VALUE SPACES.
 00258          10  FILLER         PIC X(12) VALUE 'TOWN  VOL   '.
 00259          10  FILLER         PIC X(16) VALUE 'PROPERTY NUMBER'.
 00260          10  FILLER         PIC X(05) VALUE SPACES.
 00261          10  FILLER         PIC X(13) VALUE 'CLASS YEAR   '.
 00262          10  FILLER         PIC X(14) VALUE 'EQ   PRO-RATN '.
 00263          10  FILLER         PIC X(22) VALUE ' COOP    ASSESSED  '.
 00264          10  FILLER         PIC X(20) VALUE 'OCCP    EQUALIZED  '.
 00265          10  FILLER         PIC X(12) VALUE 'NAME/ADDRESS'.
 00266          10  FILLER         PIC X(09) VALUE SPACES.
 00267          10  FILLER         PIC X(6)  VALUE 'ACTION'.
 00268      SKIP1
 00269      05  PRT-TIT2.
 00270          10  FILLER         PIC X(35) VALUE SPACES.
 00271          10  FILLER         PIC X(11) VALUE 'MA/MI APPL '.
 00272          10  FILLER         PIC X(14) VALUE 'FACTOR  FACTOR'.
 00273          10  FILLER         PIC XXX   VALUE SPACES.
 00274          10  FILLER         PIC X(18) VALUE 'QNTY   VALUATION  '.
 00275          10  FILLER      PIC X(20) VALUE '   FACT    VALUATION'.
 00276      SKIP1
 00277      05  PRT-DETAIL.
 00278          10  FILLER         PIC XXX   VALUE SPACES.
 00279          10  P-TOWN         PIC 99.
 00280          10  FILLER         PIC XXX   VALUE SPACES.
 00281          10  P-VOL          PIC 999.
 00282          10  FILLER         PIC XX    VALUE SPACES.
 00283          10  P-PROP         PIC 99B99B999B999B9999.
 00284          10  FILLER         PIC X(4)  VALUE SPACES.
 00285          10  P-CLS          PIC 9B99.
 00286          10  FILLER         PIC X(3)  VALUE SPACES.
 00287          10  P-YR           PIC 99.
 00288          10  FILLER         PIC XX    VALUE SPACES.
 00289          10  P-EQFACT       PIC 9.9999.
 00290          10  FILLER         PIC X     VALUE SPACES.
 00291          10  P-PROFACT      PIC Z.ZZZZZZ.
 00292          10  FILLER         PIC X     VALUE SPACES.
 00293          10  P-COOP         PIC ZZ,ZZZ.
 00294          10  FILLER         PIC X     VALUE SPACES.
 00295          10  P-ASSVAL       PIC ZZZ,ZZZ,ZZ9.
 00296          10  FILLER         PIC XX    VALUE SPACES.
 00297          10  P-OCCFAC       PIC Z999.9.
 00298          10  FILLER         PIC X     VALUE SPACES.
 00299          10  P-EQVAL        PIC ZZZ,ZZZ,ZZ9.
 00300          10  FILLER         PIC XX    VALUE SPACES.
 00301          10  P-NA-NAME.
 00302              15  P-NAME     PIC X(22).
 00303          10  P-NA-ADDRESS REDEFINES P-NA-NAME.
 00304              15  P-ADDRESS  PIC X(22).
 00305          10  P-NA-CSZ REDEFINES P-NA-NAME.
 00306              15  P-CITY     PIC X(12).
 00307              15  FILLER     PIC X.
 00308              15  P-STATE    PIC XX.
 00309              15  FILLER     PIC XX.
 00310              15  P-ZIP      PIC 9(5).
 00311          10  FILLER         PIC X     VALUE SPACES.
 00312          10  P-MESG         PIC X(7).
 00313      SKIP1
 00314      05  UNP-HM-TXCD        PIC 9(5)  VALUE ZEROS.
 00315      05  FILLER REDEFINES UNP-HM-TXCD.
 00316          10  UNP-HTWN      PIC 99.
 00317          10  UNP-HCD        PIC 999.
 00318      SKIP1
 00319
 00320      05  UNP-OUT-TXCD       PIC 9(5)  VALUE ZEROS.
 00321      05  FILLER REDEFINES UNP-OUT-TXCD.
 00322          10  UNP-OTWN       PIC 99.
 00323          10  UNP-OCD        PIC 999.
 00324      SKIP1
 00325      05  TOT-LINE.
 00326          10  FILLER         PIC X(3)  VALUE SPACES.
 00327          10  TOT-MESG       PIC X(31).
 00328          10  FILLER         PIC X     VALUE SPACE.
 00329          10  TOT-CNT        PIC Z,ZZZ,ZZ9.
 00330      SKIP1
 00331      05  HOME-PROP.
 00332          10 H-PROP          PIC 9(15)                COMP-3.
 00333      SKIP1
 00334      05  EXEMP-PROP.
 00335          10 EX-PROP         PIC 9(15)                COMP-3.
 00336      SKIP1
 00337      05  SAVE-HOME.
 00338          10 S-H-PROP        PIC 9(15) VALUE 0        COMP-3.
 00339      SKIP1
 00340      05  SAVE-EXEMP.
 00341          10 S-E-PROP        PIC 9(14) VALUE 0.
 00342      SKIP1
 00343      05  CURR-HOME.
 00344          10  C-H-PROP       PIC 9(15)                COMP-3.
 00345      SKIP1
 00346      05  CURR-EXEMP.
 00347          10  C-E-PROP       PIC 9(14).
 00348      SKIP1
 00349      05  YRQUAD.
 00350          10  M-YR           PIC  99.
 00351          10  M-QUAD         PIC  9.
 00352      05  YRQUAD-RD REDEFINES YRQUAD   PIC 9(3).
 00353      SKIP1
 00354      05  SAVE-YRQUAD.
 00355          10  S-YEAR         PIC 99    VALUE 0.
 00356          10  S-QUAD         PIC 9     VALUE 0.
 00357      SKIP1
 00358      05  CURR-YRQUAD.
 00359          10  C-YEAR         PIC 99.
 00360          10  C-QUAD         PIC 9.
 00361      SKIP1
 00362      05  EQUAL-FACT-AREA.
 00363          10  EQFACT-TABLE OCCURS 320 TIMES
 00364                             ASCENDING KEY IS T-YRQUAD
 00365                             INDEXED BY I.
 00366              15  T-YRQUAD   PIC 9(3).
 00367              15  T-EQFACT   PIC 9V9(4).
 00368              15  FILLER     PIC X(13).
 00369      SKIP1
 00370  ++INCLUDE TWNINFOTBL
 00371      SKIP1
 00372      05  FILLER        PIC X(58)  VALUE
 00373      '*************  WORKING STORAGE   ENDS HERE  **************'.
 00374      SKIP2
 00375  LINKAGE SECTION.
 00376      SKIP1
 00377  01  LINK-INFO.
 00378      05  LINK-LENGTH        PIC S9(4) COMP.
 00379          88  VALID-LINK-LENGTH    VALUE +9.
 00380      05  LINK-YEAR          PIC 9(4).
 00381      05  LINK-HOMIN         PIC 9(5).
 00382      SKIP2
 00383  PROCEDURE DIVISION USING LINK-INFO.
 00384  010-BEGIN.
 00385      MOVE FUNCTION CURRENT-DATE TO CURRENT-DATE-RETURN.
 00386      COMPUTE CURRENT-DATE = SYSTEM-DATE * MDCY-OR-CYMD.
 00387      MOVE SYSTEM-TIME TO CURRENT-TIME.
 00388      INSPECT CURRENT-TIME REPLACING ALL ',' BY ':'.
 00389      DISPLAY 'PROGRAM ASREA864 DATE AND TIME OF RUN = '
 00390          CURRENT-DATE ' ' CURRENT-TIME.
 00391      OPEN INPUT LNDMRK-PROP-FILE.
 00392      IF NOT GOOD-STATUS
 00393          DISPLAY 'LANDMARK FILE OPEN ERR'
 00394          DISPLAY 'FILE STATUS IS ' FILE-STATUS
 00395          DISPLAY 'FILE STATUS FEEDBACK  ' FILE2-RETURN
 00396              ' ' FILE2-FUNCTION ' ' FILE2-FEEDBACK
 00397          MOVE 'Y' TO VSAM-ERR-SW
 00398          MOVE 16 TO RETURN-CODE
 00399          STOP RUN.
 00400      OPEN    INPUT  HOMEXEMP
 00401              INPUT  HOMESTEAD-FILE
 00402              INPUT  EQUALFCT
 00403              OUTPUT PRINT-FILE
 00404              OUTPUT ERROR-FILE
 00405              OUTPUT OUTHOME-FILE
 00406      PERFORM 137-LOAD-IT THRU 138-EXIT
 00407      IF NOT VALID-LINK-LENGTH
 00408          DISPLAY 'LINKAGE LENGTH INVALID'
 00409          MOVE 0016 TO RETURN-CODE
 00410          STOP RUN.
 00411      IF (LINK-YEAR NOT NUMERIC) OR
 00412         (LINK-YEAR NOT GREATER THAN '0000')
 00413             DISPLAY 'INVALID TAX YEAR PARAMETER  ' LINK-YEAR
 00414             MOVE 0016 TO RETURN-CODE
 00415             STOP RUN.
 00416      IF (LINK-HOMIN NOT NUMERIC) OR
 00417         (LINK-HOMIN = ZERO)
 00418             DISPLAY 'INVALID H/O MIN AMOUNT      ' LINK-HOMIN
 00419             MOVE 0016 TO RETURN-CODE
 00420             STOP RUN.
 00421      MOVE    CURRENT-DATE  TO  ERR-DATE
 00422                                PRT-DATE
 00423      PERFORM 160-PRT-HDR THRU 165-EXIT
 00424      PERFORM 170-ERR-HDR THRU 175-EXIT
 00425      PERFORM 040-READ-HOMESTEAD THRU 045-EXIT
 00426      PERFORM 050-READ-HOMEXEMP  THRU 055-EXIT
 00427      PERFORM 030-MAINLINE       THRU 035-EXIT
 00428              UNTIL (END-OF-EXEMP-FILE AND END-OF-HOME-FILE)
 00429               OR   ERR.
 00430      SKIP3
 00431  020-EOJ.
 00432      DISPLAY 'LANDMARK RECORDS  ' WS-LDMK-CNT
 00433      MOVE    EXEMP-INCNT  TO  TOT-CNT
 00434      MOVE    EXEMP-MESG  TO  TOT-MESG
 00435      WRITE   PRINT-REC FROM TOT-LINE
 00436                        AFTER ADVANCING 2 LINES
 00437      WRITE   ERR-REC   FROM TOT-LINE
 00438                        AFTER ADVANCING 2 LINES
 00439      MOVE    ADD-CNT      TO  TOT-CNT
 00440      MOVE    TADD-MESG    TO  TOT-MESG
 00441      WRITE   PRINT-REC FROM TOT-LINE
 00442                        AFTER ADVANCING 1 LINE
 00443      WRITE   ERR-REC   FROM TOT-LINE
 00444                        AFTER ADVANCING 1 LINE
 00445      MOVE    CHG-CNT      TO  TOT-CNT
 00446      MOVE    CHG-MESG     TO  TOT-MESG
 00447      WRITE   PRINT-REC FROM TOT-LINE
 00448                        AFTER ADVANCING 1 LINE
 00449      WRITE   ERR-REC   FROM TOT-LINE
 00450                        AFTER ADVANCING 1 LINE
 00451      MOVE    DELT-CNT     TO  TOT-CNT
 00452      MOVE    DELT-MESG    TO  TOT-MESG
 00453      WRITE   PRINT-REC FROM TOT-LINE
 00454                        AFTER ADVANCING 1 LINE
 00455      WRITE   ERR-REC   FROM TOT-LINE
 00456                        AFTER ADVANCING 1 LINE
 00457      MOVE    ERR-CNT      TO  TOT-CNT
 00458      MOVE    ERR-MESG     TO  TOT-MESG
 00459      WRITE   PRINT-REC FROM TOT-LINE
 00460                        AFTER ADVANCING 1 LINE
 00461      WRITE   ERR-REC   FROM TOT-LINE
 00462                        AFTER ADVANCING 1 LINE
 00463      MOVE    HOME-INCNT   TO  TOT-CNT
 00464      MOVE    HOME-MESG    TO  TOT-MESG
 00465      WRITE   PRINT-REC FROM TOT-LINE
 00466                        AFTER ADVANCING 1 LINE
 00467      WRITE   ERR-REC   FROM TOT-LINE
 00468                        AFTER ADVANCING 1 LINE
 00469      MOVE    HOME-OUTCNT  TO  TOT-CNT
 00470      MOVE    HOME-OUTMESG TO  TOT-MESG
 00471      WRITE   PRINT-REC FROM TOT-LINE
 00472                       AFTER ADVANCING 1 LINE
 00473      WRITE   ERR-REC   FROM TOT-LINE
 00474                       AFTER ADVANCING 1 LINE
 00475      MOVE    WS-LDMK-CNT  TO  TOT-CNT
 00476      MOVE    LDMK-OUTMESG TO  TOT-MESG
 00477      WRITE   PRINT-REC FROM TOT-LINE
 00478                       AFTER ADVANCING 1 LINE
 00479      WRITE   ERR-REC   FROM TOT-LINE
 00480                       AFTER ADVANCING 1 LINE
 00481      CLOSE   HOMEXEMP
 00482              HOMESTEAD-FILE
 00483              EQUALFCT
 00484              PRINT-FILE
 00485              ERROR-FILE
 00486              OUTHOME-FILE
 00487      CLOSE LNDMRK-PROP-FILE
 00488      IF NOT GOOD-STATUS
 00489          DISPLAY 'ASREA864'
 00490          DISPLAY 'LDMKFILE CLOSE ERR - FILE STATUS IS '
 00491              FILE-STATUS
 00492          DISPLAY 'FILE STATUS FEEDBACK  ' FILE2-RETURN
 00493            ' ' FILE2-FUNCTION ' ' FILE2-FEEDBACK
 00494          MOVE 'Y' TO VSAM-ERR-SW
 00495          MOVE 16 TO RETURN-CODE.
 00496      STOP RUN.
 00497      SKIP3
 00498  030-MAINLINE.
 00499      IF   HOME-PROP LESS THAN EXEMP-PROP
 00500           MOVE HOME-REC TO OUTHOME-REC
 00501           PERFORM 120-WRITE-OUTHOME-FILE THRU 125-EXIT
 00502           PERFORM 040-READ-HOMESTEAD THRU 045-EXIT
 00503      ELSE
 00504           IF   HOME-PROP EQUAL EXEMP-PROP
 00505                IF   HE-ACT EQUAL 'D'
 00506                     PERFORM 060-DELETE-ROUTINE THRU 065-EXIT
 00507                     PERFORM 040-READ-HOMESTEAD THRU 045-EXIT
 00508                     PERFORM 050-READ-HOMEXEMP  THRU 055-EXIT
 00509                ELSE
 00510                     IF   HE-ACT EQUAL 'C'
 00511                          PERFORM 070-CHANGE-ROUTINE THRU 075-EXIT
 00512                          PERFORM 040-READ-HOMESTEAD THRU 045-EXIT
 00513                          PERFORM 050-READ-HOMEXEMP  THRU 055-EXIT
 00514                     ELSE
 00515                          MOVE ADD-ERR-MESG TO E-MESG
 00516                          PERFORM 130-MOVE-WRITE-ERR THRU 135-EXIT
 00517                          MOVE HOME-REC TO OUTHOME-REC
 00518                          PERFORM 120-WRITE-OUTHOME-FILE THRU
 00519                                  125-EXIT
 00520                          PERFORM 040-READ-HOMESTEAD THRU 045-EXIT
 00521                          PERFORM 050-READ-HOMEXEMP  THRU 055-EXIT
 00522           ELSE
 00523 *              IF   HE-ACT EQUAL 'A'
 00524 *                   PERFORM 080-ADD-ROUTINE   THRU 085-EXIT
 00525 *                   PERFORM 050-READ-HOMEXEMP THRU 055-EXIT
 00526 *              ELSE
 00527                     MOVE NO-MTCH-MESG TO E-MESG
 00528                     PERFORM 130-MOVE-WRITE-ERR THRU 135-EXIT
 00529                     PERFORM 050-READ-HOMEXEMP  THRU 055-EXIT.
 00530      SKIP1
 00531  035-EXIT.
 00532      EXIT.
 00533      SKIP3
 00534  040-READ-HOMESTEAD.
 00535      READ    HOMESTEAD-FILE
 00536              AT END MOVE 1 TO HOME-EOF
 00537                     MOVE ALL '9' TO HOME-PROP.
 00538      IF      NOT END-OF-HOME-FILE
 00539              MOVE  HM-PROP  TO  C-H-PROP
 00540              IF    CURR-HOME LESS THAN SAVE-HOME
 00541                    MOVE 16 TO RETURN-CODE
 00542                    DISPLAY 'HOMESTEAD FILE SEQUENCE ERROR'
 00543                      DISPLAY 'PREVIOUS RECORD ' S-H-PROP
 00544                      DISPLAY 'CURRENT  RECORD ' C-H-PROP
 00545                    MOVE   1 TO ERR-CHK
 00546              ELSE
 00547                    MOVE    CURR-HOME TO SAVE-HOME
 00548                    MOVE    HM-PROP   TO H-PROP
 00549                    ADD     +1        TO HOME-INCNT.
 00550      SKIP1
 00551  045-EXIT.
 00552      EXIT.
 00553      SKIP3
 00554  050-READ-HOMEXEMP.
 00555      READ    HOMEXEMP
 00556              AT END MOVE 1 TO EXEMP-EOF
 00557                     MOVE ALL '9' TO EXEMP-PROP.
 00558      IF      NOT END-OF-EXEMP-FILE
 00559              MOVE  HE-PROP  TO  C-E-PROP
 00560              IF    CURR-EXEMP LESS THAN SAVE-EXEMP
 00561                    MOVE 16 TO RETURN-CODE
 00562                    DISPLAY 'HOMEXEMP FILE SEQUENCE ERROR'
 00563                    DISPLAY 'PREVIOUS RECORD ' SAVE-EXEMP
 00564                    DISPLAY 'CURRENT RECORD  ' CURR-EXEMP
 00565                    MOVE    1 TO ERR-CHK
 00566              ELSE
 00567                    MOVE    CURR-EXEMP  TO SAVE-EXEMP
 00568                    MOVE    HE-PROP     TO EX-PROP
 00569                    ADD     +1          TO EXEMP-INCNT.
 00570      SKIP1
 00571  055-EXIT.
 00572      EXIT.
 00573      SKIP3
 00574  060-DELETE-ROUTINE.
 00575      MOVE    D-MESG  TO  P-MESG
 00576      PERFORM 100-MOVE-WRITE-HOME-FLDS THRU 105-EXIT
 00577      ADD     +1  TO  DELT-CNT.
 00578      SKIP1
 00579  065-EXIT.
 00580      EXIT.
 00581      SKIP3
 00582  070-CHANGE-ROUTINE.
 00583      MOVE HOME-REC  TO  OUTHOME-REC
 00584      MOVE OUT-PROP  TO   LDMK-PROP
 00585      MOVE LINK-YEAR TO   LDMK-YEAR
 00586      MOVE 'N'       TO   WS-LDMK-SW
 00587      PERFORM 180-READ-LANDMARK-FILE
 00588      IF WS-LDMK-SW = 'Y'
 00589        IF HE-STATUS IS NOT EQUAL TO 'CP'
 00590          MOVE LINK-HOMIN TO  OUT-NPHE-EXAMT
 00591          MOVE 'LN'     TO  OUT-NPHE-STATUS
 00592          ADD +1 TO WS-LDMK-CNT
 00593        ELSE
 00594          IF HE-NPHE-EXAMT NUMERIC
 00595             MOVE 'CP'          TO OUT-NPHE-STATUS
 00596             MOVE HE-NPHE-EXAMT TO OUT-NPHE-EXAMT
 00597          END-IF
 00598        END-IF
 00599      ELSE
 00600         IF HE-NPHE-EXAMT NUMERIC
 00601            MOVE HE-NPHE-EXAMT TO OUT-NPHE-EXAMT
 00602         END-IF
 00603      END-IF
 00604      IF HE-AVAL > ZERO
 00605         MOVE HE-AVAL       TO OUT-ASSDVAL
 00606      END-IF
 00607      IF HE-EQVAL > ZERO OR HE-EQVAL = ZERO
 00608         MOVE HE-EQVAL      TO OUT-EQVAL
 00609      END-IF
 00610 *    IF HE-NPHE-EXAMT > ZERO
 00611 *    IF HE-NPHE-EXAMT NUMERIC
 00612 *       MOVE HE-NPHE-EXAMT TO OUT-NPHE-EXAMT
 00613 *    END-IF
 00614      IF HE-NPHE-BSYR > ZERO
 00615         MOVE HE-NPHE-BSYR       TO OUT-NPHE-BSYR
 00616         MOVE HE-NPHE-BSYR(3:2)  TO OUT-YR
 00617      END-IF
 00618      MOVE    0 TO NO-MATCH-CHK
 00619      MOVE    HM-STAT1 TO OUT-STAT1
 00620      PERFORM 090-TOWN-EQFACT-SEARCHES THRU 095-EXIT
 00621      IF      NOT ERR
 00622         AND  NOT NO-MATCH
 00623              MOVE    C-MESG TO P-MESG
 00624              PERFORM 100-MOVE-WRITE-HOME-FLDS THRU 105-EXIT
 00625              PERFORM 110-HOMEXEMP-FLD-CHK THRU 115-EXIT
 00626 *            MULTIPLY  OUT-ASSDVAL BY OUT-EQFACT GIVING OUT-EQVAL
 00627 *                                                       ROUNDED
 00628              MOVE OUT-TXCD TO UNP-OUT-TXCD
 00629              MOVE UNP-OTWN TO P-TOWN
 00630              MOVE  OUT-VOL     TO  P-VOL
 00631              MOVE  OUT-PROP    TO  P-PROP
 00632              MOVE  OUT-CLS     TO  P-CLS
 00633              MOVE  OUT-YR      TO  P-YR
 00634              MOVE  OUT-EQFACT  TO  P-EQFACT
 00635              MOVE  OUT-PRORAT  TO  P-PROFACT
 00636              MOVE  OUT-OCCFAC  TO  P-OCCFAC
 00637              MOVE  OUT-COOP    TO  P-COOP
 00638              MOVE  OUT-ASSDVAL TO  P-ASSVAL
 00639              MOVE  OUT-EQVAL   TO  P-EQVAL
 00640              INSPECT P-PROP REPLACING ALL SPACES BY '-'
 00641              INSPECT P-CLS  REPLACING ALL SPACES BY '-'
 00642              ADD  +1 TO CHG-CNT
 00643              MOVE  OUT-NAME    TO  P-NAME
 00644              MOVE  SPACES      TO  P-MESG
 00645              WRITE PRINT-REC FROM PRT-DETAIL
 00646                              AFTER ADVANCING 2 LINES
 00647              ADD  +2 TO LINE-CNT
 00648              MOVE  SPACES      TO  PRT-DETAIL
 00649              MOVE  OUT-ADDRESS TO  P-ADDRESS
 00650              WRITE PRINT-REC FROM PRT-DETAIL
 00651                              AFTER ADVANCING 1 LINE
 00652              ADD +1 TO LINE-CNT
 00653              MOVE  SPACES      TO  PRT-DETAIL
 00654              MOVE  OUT-CITY    TO  P-CITY
 00655              MOVE  OUT-STATE   TO  P-STATE
 00656 *            MOVE  OUT-ZIP     TO  P-ZIP
 00657              MOVE  OUT-ZIP     TO  WS-CHANGE-ZIP
 00658              MOVE  WS-ZIP-FIVE TO  P-ZIP
 00659              WRITE PRINT-REC FROM PRT-DETAIL
 00660                              AFTER ADVANCING 1 LINE
 00661              ADD +1 TO LINE-CNT
 00662              PERFORM 120-WRITE-OUTHOME-FILE THRU 125-EXIT
 00663      ELSE
 00664              IF   NO-MATCH
 00665                   PERFORM 120-WRITE-OUTHOME-FILE THRU 125-EXIT
 00666                   MOVE NO-EQFT-MESG TO E-MESG
 00667                   PERFORM 130-MOVE-WRITE-ERR THRU 135-EXIT.
 00668      SKIP1
 00669  075-EXIT.
 00670      EXIT.
 00671      SKIP3
 00672  080-ADD-ROUTINE.
 00673      MOVE    0 TO NO-MATCH-CHK
 00674      MOVE    SPACES TO OUTHOME-REC
 00675      MOVE    1 TO OUT-STAT1
 00676      MOVE    0 TO OUT-STAT2
 00677      PERFORM 090-TOWN-EQFACT-SEARCHES THRU 095-EXIT
 00678      IF      NOT ERR
 00679         AND  NOT NO-MATCH
 00680              MOVE  HE-TWN     TO  P-TOWN
 00681              MOVE  HE-VOL     TO  OUT-VOL
 00682                                   P-VOL
 00683              MOVE  HE-PROP    TO  OUT-PROP
 00684                                   P-PROP
 00685              MOVE  HE-CLS     TO  OUT-CLS
 00686                                   P-CLS
 00687              MOVE  HE-PRO     TO  OUT-PRORAT
 00688                                   P-PROFACT
 00689              MOVE  HE-COOP    TO  OUT-COOP
 00690                                   P-COOP
 00691              MOVE  HE-AVAL    TO  OUT-ASSDVAL
 00692                                   P-ASSVAL
 00693              MOVE  OUT-ASSDVAL TO P-ASSVAL
 00694              MOVE  HE-YR      TO  OUT-YR
 00695                                   P-YR
 00696              MOVE  HE-STATUS  TO  OUT-NPHE-STATUS
 00697 *                                 P-STATUS
 00698              MOVE  HE-OCCFAC  TO  OUT-OCCFAC
 00699                                   P-OCCFAC
 00700              MOVE HE-EQVAL  TO OUT-EQVAL
 00701                                  P-EQVAL
 00702              MOVE HE-NPHE-EXAMT TO OUT-NPHE-EXAMT
 00703              MOVE HE-NPHE-BSYR  TO OUT-NPHE-BSYR
 00704              MOVE HE-NPHE-BSYR(3:2)  TO OUT-YR
 00705              MOVE  OUT-EQFACT TO  P-EQFACT
 00706              MOVE  A-MESG     TO  P-MESG
 00707              MOVE  HE-TWN     TO  UNP-OTWN
 00708              MOVE  0          TO  UNP-OCD
 00709              MOVE  UNP-OUT-TXCD  TO OUT-TXCD
 00710 *            MULTIPLY  OUT-ASSDVAL BY OUT-EQFACT GIVING OUT-EQVAL
 00711 *                                                       ROUNDED
 00712              MOVE  OUT-EQVAL  TO  P-EQVAL
 00713              INSPECT P-PROP REPLACING ALL SPACES BY '-'
 00714              INSPECT P-CLS  REPLACING ALL SPACES BY '-'
 00715              MOVE HE-NAME TO OUT-NAME P-NAME
 00716              IF LINE-CNT GREATER THAN +56
 00717                 PERFORM 160-PRT-HDR THRU 165-EXIT
 00718                 PERFORM 086-ACSZ-RTN THRU 087-EXIT
 00719              ELSE
 00720                 PERFORM 086-ACSZ-RTN THRU 087-EXIT
 00721      ELSE
 00722              IF   NO-MATCH
 00723                   MOVE HOME-REC TO OUTHOME-REC
 00724                   PERFORM 120-WRITE-OUTHOME-FILE THRU 125-EXIT
 00725                   MOVE NO-EQFT-MESG TO E-MESG
 00726                   PERFORM 130-MOVE-WRITE-ERR THRU 135-EXIT.
 00727      SKIP1
 00728  085-EXIT.
 00729      EXIT.
 00730      SKIP3
 00731  086-ACSZ-RTN.
 00732      WRITE PRINT-REC FROM PRT-DETAIL AFTER ADVANCING 3 LINES
 00733      ADD +3 TO LINE-CNT
 00734      MOVE SPACES TO PRT-DETAIL
 00735      MOVE HE-ADDR TO P-ADDRESS
 00736 *    MOVE HE-ADDR TO OUT-ADDRESS P-ADDRESS
 00737      WRITE PRINT-REC FROM PRT-DETAIL AFTER ADVANCING 1 LINE
 00738      ADD +1 TO LINE-CNT
 00739      MOVE SPACES TO PRT-DETAIL
 00740      MOVE HE-CITY TO P-CITY
 00741 *    MOVE HE-CITY TO OUT-CITY P-CITY
 00742      MOVE HE-STATE TO P-STATE
 00743 *    MOVE HE-STATE TO OUT-STATE P-STATE
 00744      IF HE-ZIP EQUAL TO SPACES
 00745          MOVE ZEROES TO P-ZIP
 00746      ELSE
 00747          MOVE HE-ZIP TO P-ZIP.
 00748      WRITE PRINT-REC FROM PRT-DETAIL AFTER ADVANCING 1 LINE
 00749      ADD +1 TO LINE-CNT
 00750      PERFORM 120-WRITE-OUTHOME-FILE THRU 125-EXIT
 00751      ADD +1 TO ADD-CNT.
 00752  087-EXIT.
 00753      EXIT.
 00754      SKIP1
 00755  090-TOWN-EQFACT-SEARCHES.
 00756      IF      HE-TWN LESS THAN 70
 00757              SUBTRACT 9 FROM HE-TWN GIVING SUB
 00758      ELSE
 00759              SUBTRACT 39 FROM HE-TWN GIVING SUB.
 00760      MOVE    TOWN-QUAD (SUB) TO SAVE-QUAD
 00761      IF      NOT ERR
 00762              MOVE SAVE-QUAD TO  M-QUAD
 00763              IF    HE-YR NUMERIC
 00764                    MOVE  HE-YR TO M-YR
 00765              ELSE
 00766                    MOVE  HM-YR TO M-YR.
 00767      IF      NOT ERR
 00768              SEARCH ALL EQFACT-TABLE
 00769                         AT END MOVE 1 TO NO-MATCH-CHK
 00770                         WHEN  T-YRQUAD (I) EQUAL YRQUAD-RD
 00771                                MOVE T-EQFACT (I) TO OUT-EQFACT.
 00772      SKIP1
 00773  095-EXIT.
 00774      EXIT.
 00775      SKIP3
 00776  100-MOVE-WRITE-HOME-FLDS.
 00777      MOVE    HM-TXCD      TO  UNP-HM-TXCD
 00778      MOVE    UNP-HTWN     TO  P-TOWN
 00779      MOVE    HM-VOL      TO  P-VOL
 00780      MOVE    HM-PROP     TO  P-PROP
 00781      INSPECT P-PROP      REPLACING ALL SPACES BY '-'
 00782      MOVE    HM-CLS      TO  P-CLS
 00783      INSPECT P-CLS       REPLACING ALL SPACES BY '-'
 00784      MOVE    HM-YR       TO  P-YR
 00785      MOVE    HM-EQFACT   TO  P-EQFACT
 00786      MOVE    HM-PRORAT   TO  P-PROFACT
 00787      MOVE    HM-COOP     TO  P-COOP
 00788      MOVE    HM-OCCFAC   TO  P-OCCFAC
 00789      MOVE    HM-ASSDVAL  TO  P-ASSVAL
 00790      MOVE    HM-EQVAL    TO  P-EQVAL
 00791      MOVE    HM-NAME     TO  P-NAME.
 00792      IF      LINE-CNT GREATER THAN +56
 00793              PERFORM 160-PRT-HDR THRU 165-EXIT.
 00794      WRITE   PRINT-REC FROM PRT-DETAIL AFTER ADVANCING 3 LINES
 00795      ADD     +3 TO LINE-CNT
 00796      MOVE    SPACES TO PRT-DETAIL
 00797      MOVE    HM-ADDRESS TO P-ADDRESS
 00798      WRITE   PRINT-REC FROM PRT-DETAIL AFTER ADVANCING 1 LINE
 00799      ADD     +1 TO LINE-CNT
 00800      MOVE    SPACES TO PRT-DETAIL
 00801      MOVE    HM-CITY TO P-CITY
 00802      MOVE    HM-STATE TO P-STATE
 00803 *    MOVE    HM-ZIP   TO P-ZIP
 00804      MOVE    HM-ZIP   TO WS-CHANGE-ZIP
 00805      MOVE WS-ZIP-FIVE TO P-ZIP
 00806      WRITE   PRINT-REC FROM PRT-DETAIL AFTER ADVANCING 1 LINE
 00807      ADD     +1 TO LINE-CNT.
 00808      SKIP1
 00809  105-EXIT.
 00810      EXIT.
 00811      SKIP3
 00812  110-HOMEXEMP-FLD-CHK.
 00813 *    IF      HE-CLS NUMERIC
 00814 *            MOVE HE-CLS TO OUT-CLS.
 00815 *    IF      HE-PRO NUMERIC
 00816 *            MOVE HE-PRO TO OUT-PRORAT.
 00817 *    IF      HE-COOP NUMERIC
 00818 *            MOVE HE-COOP TO OUT-COOP.
 00819 *    IF      HE-AVAL NUMERIC
 00820 *            MOVE HE-AVAL TO OUT-ASSDVAL.
 00821      IF      HE-YR NUMERIC
 00822              MOVE HE-YR TO OUT-YR.
 00823      IF WS-LDMK-SW = 'Y'
 00824        IF HE-STATUS IS NOT EQUAL TO 'CP'
 00825          MOVE 'LN' TO OUT-NPHE-STATUS
 00826        ELSE
 00827          MOVE 'CP' TO OUT-NPHE-STATUS
 00828        END-IF
 00829      ELSE
 00830         IF   HE-STATUS > SPACES
 00831              MOVE HE-STATUS TO OUT-NPHE-STATUS
 00832         END-IF
 00833      END-IF
 00834 *    IF      HE-OCCFAC-R NOT EQUAL SPACES
 00835 *            MOVE HE-OCCFAC  TO OUT-OCCFAC.
 00836 *    IF      HE-NAME NOT EQUAL SPACES
 00837 *            MOVE HE-NAME TO OUT-NAME.
 00838 *    IF      HE-ADDR NOT EQUAL SPACES
 00839 *            MOVE HE-ADDR TO OUT-ADDRESS.
 00840 *    IF      HE-CITY NOT EQUAL SPACES
 00841 *            MOVE HE-CITY TO OUT-CITY.
 00842 *    IF      HE-STATE NOT EQUAL SPACES
 00843 *            MOVE HE-STATE TO OUT-STATE.
 00844 *    IF      HE-ZIP NUMERIC AND HE-ZIP NOT EQUAL ZEROS
 00845 *            MOVE HE-ZIP TO OUT-ZIP.
 00846 **   IF      HE-NPHE-EXAMT NUMERIC AND
 00847 **           HE-NPHE-EXAMT > ZEROS
 00848 *    IF      HE-NPHE-EXAMT NUMERIC
 00849 *            MOVE HE-NPHE-EXAMT TO OUT-NPHE-EXAMT.
 00850      IF      HE-NPHE-BSYR NUMERIC
 00851              MOVE HE-NPHE-BSYR TO OUT-NPHE-BSYR
 00852              MOVE HE-NPHE-BSYR(3:2) TO OUT-YR.
 00853 *----001--------------------------------*
 00854 *    PERFORM 116-OCCFAC-CHECK THRU 117-EXIT.
 00855 *---------------------------------------*
 00856              SKIP1
 00857  115-EXIT.
 00858      EXIT.
 00859      SKIP3
 00860 *----001--------------------------------*
 00861 *116-OCCFAC-CHECK.
 00862 *    IF HE-OCCFAC-R NOT EQUAL SPACES
 00863 *       IF HE-OCCFAC EQUAL 100.0
 00864 *           IF HM-STAT1 EQUAL 2
 00865 *               MOVE 0 TO OUT-STAT1
 00866 *           ELSE
 00867 *               NEXT SENTENCE
 00868 *       ELSE
 00869 *           IF HE-OCCFAC-R NOT EQUAL SPACES
 00870 *               MOVE 2 TO OUT-STAT1.
 00871 *117-EXIT.
 00872 *    EXIT.
 00873 *---------------------------------------*
 00874      SKIP3
 00875  120-WRITE-OUTHOME-FILE.
 00876      WRITE   OUTHOME-REC
 00877      ADD     +1 TO HOME-OUTCNT.
 00878      SKIP1
 00879  125-EXIT.
 00880      EXIT.
 00881      SKIP3
 00882  130-MOVE-WRITE-ERR.
 00883      MOVE    HE-ACT   TO  E-ACTION
 00884      MOVE    HE-TWN   TO  E-TOWN
 00885      MOVE    HE-VOL   TO  E-VOL
 00886      MOVE    HE-PROP  TO  E-PROP
 00887      MOVE    HE-CLS   TO  E-CLS
 00888      MOVE    HE-PRO   TO  E-PRO
 00889      MOVE    HE-COOP  TO  E-COOP
 00890      MOVE    HE-OCCFAC TO E-OCCFAC
 00891      MOVE    HE-YR    TO  E-YR
 00892      MOVE    HE-AVAL  TO  E-AVAL
 00893      MOVE    HE-NPHE-EXAMT TO  E-NPHE-EXAMT
 00894      INSPECT E-PROP   REPLACING ALL SPACES BY '-'
 00895      INSPECT E-CLS    REPLACING ALL SPACES BY '-'
 00896      IF      ERR-LINE-CNT GREATER THAN +56
 00897              PERFORM 170-ERR-HDR THRU 175-EXIT.
 00898      WRITE   ERR-REC FROM ERR-DETAIL
 00899               AFTER ADVANCING 2 LINES
 00900      ADD     +2 TO ERR-LINE-CNT
 00901      ADD     +1 TO ERR-CNT.
 00902      SKIP1
 00903  135-EXIT.
 00904      EXIT.
 00905      SKIP3
 00906  137-LOAD-IT.
 00907      MOVE    ALL '9' TO EQUAL-FACT-AREA
 00908      PERFORM 140-READ-LOAD-EQFACT-FILE THRU 145-EXIT
 00909              VARYING I FROM 1 BY 1
 00910               UNTIL END-OF-EQFACT-FILE
 00911                OR    ERR
 00912                OR    I GREATER THAN 321.
 00913 *              OR    I GREATER THAN 161.
 00914      SKIP1
 00915  138-EXIT.
 00916      EXIT.
 00917      SKIP3
 00918  140-READ-LOAD-EQFACT-FILE.
 00919      READ    EQUALFCT
 00920              AT END MOVE 1 TO EQFACT-EOF.
 00921      IF      NOT END-OF-EQFACT-FILE
 00922              MOVE  EQ-YEAR  TO  C-YEAR
 00923              MOVE  EQ-QUAD  TO  C-QUAD
 00924              IF    CURR-YRQUAD NOT GREATER THAN SAVE-YRQUAD
 00925                    MOVE 16 TO RETURN-CODE
 00926                    DISPLAY 'EQUAL FACTOR FILE SEQUENCE ERROR '
 00927                    DISPLAY 'PREVIOUS RECORD ' SAVE-YRQUAD
 00928                    DISPLAY 'CURRENT RECORD  ' CURR-YRQUAD
 00929                    MOVE 1 TO ERR-CHK
 00930              ELSE
 00931                    MOVE CURR-YRQUAD TO SAVE-YRQUAD
 00932                    MOVE FACT-REC    TO EQFACT-TABLE (I).
 00933      SKIP1
 00934  145-EXIT.
 00935      EXIT.
 00936      SKIP3
 00937  160-PRT-HDR.
 00938      ADD     +1 TO PG-CNT
 00939      MOVE    PG-CNT TO PRT-PG
 00940      WRITE   PRINT-REC FROM PRT-HDR
 00941                        AFTER ADVANCING PAGE
 00942      WRITE   PRINT-REC FROM PRT-HDR2
 00943                        AFTER ADVANCING 2 LINES
 00944      WRITE   PRINT-REC FROM PRT-TIT
 00945                        AFTER ADVANCING 2 LINES
 00946      WRITE   PRINT-REC FROM PRT-TIT2
 00947                        AFTER ADVANCING 1 LINE
 00948      MOVE    +6 TO LINE-CNT.
 00949      SKIP1
 00950  165-EXIT.
 00951      EXIT.
 00952      SKIP3
 00953  170-ERR-HDR.
 00954      ADD     +1 TO ERR-PG-CNT
 00955      MOVE    ERR-PG-CNT TO ERR-PG
 00956      WRITE   ERR-REC FROM ERR-HDR
 00957                      AFTER ADVANCING PAGE
 00958      WRITE   ERR-REC FROM ERR-HDR2
 00959                      AFTER ADVANCING 2 LINES
 00960      WRITE   ERR-REC FROM ERR-TIT
 00961                      AFTER ADVANCING 2 LINES
 00962      WRITE   ERR-REC FROM ERR-TIT2
 00963                      AFTER ADVANCING 1 LINE
 00964      MOVE    +6 TO ERR-LINE-CNT.
 00965      SKIP1
 00966  175-EXIT.
 00967      EXIT.
 00968      SKIP3
 00969  180-READ-LANDMARK-FILE.
 00970      READ LNDMRK-PROP-FILE.
 00971      IF GOOD-STATUS
 00972         MOVE 'Y' TO WS-LDMK-SW
 00973      ELSE
 00974          IF LAND-NOTFND
 00975             MOVE 'Y' TO LDMK-EOF-SW
 00976             MOVE 'N' TO WS-LDMK-SW
 00977          ELSE
 00978              DISPLAY 'ASREA864'
 00979              DISPLAY 'LDMKFILE READ ERR - FILE STATUS IS '
 00980                  FILE-STATUS
 00981              DISPLAY 'FILE STATUS FEEDBACK ' FILE2-RETURN
 00982                  ' ' FILE2-FUNCTION ' ' FILE2-FEEDBACK
 00983              MOVE 16 TO RETURN-CODE
 00984              MOVE 'Y' TO VSAM-ERR-SW.
