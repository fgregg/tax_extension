 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID.  ASREA868.
 00003  AUTHOR.  SQUILLACE-KISTINGER
 00004 *DATE-WRITTEN. 12/2/92.
 00005  SKIP1
 00006 *REMARKS---------------------------------------------------------*
 00007 *     THE PURPOSE OF THIS PROGRAM IS TO CREATE THE ASSESSOR'S H/O
 00008 *        RECORDS WITH MISSING BASE VALUATIONS REPORT.
 00009 *
 00010 *----------------------------------------------------------------*
 00011  SKIP1
 00012  ENVIRONMENT DIVISION.
 00013  CONFIGURATION SECTION.
 00014  SOURCE-COMPUTER. IBM-370.
 00015  OBJECT-COMPUTER. IBM-370.
 00016  INPUT-OUTPUT SECTION.
 00017  FILE-CONTROL.
 00018  SKIP1
 00019      SELECT  HOMEOWNER-MAST   ASSIGN TO UT-S-MAST.
 00020      SELECT  PRINT-FILE       ASSIGN TO UT-S-PRINT.
 00021  SKIP1
 00022  DATA DIVISION.
 00023  FILE SECTION.
 00024  FD  HOMEOWNER-MAST
 00025      BLOCK CONTAINS 0 RECORDS
 00026      RECORD CONTAINS 130 CHARACTERS
 00027      LABEL RECORDS ARE STANDARD
 00028      DATA RECORD IS HO-REC.
 00029  01  HO-REC.
 00030  ++INCLUDE HOMEXERD01
 00031      SKIP2
 00032  ++INCLUDE PRINTFILE
 00033      EJECT
 00034  WORKING-STORAGE SECTION.
 00035  77  REC-COUNT               PIC S9(9) COMP-3 VALUE +0.
 00036  77  LINE-CNTR               PIC S9(9) COMP-3 VALUE +75.
 00037  77  PAGE-CNTR               PIC S9(9) COMP-3 VALUE +0.
 00038  77  RECS-PRINTED            PIC S9(9) COMP-3 VALUE +0.
 00039  77  RECS-BYPASS             PIC S9(9) COMP-3 VALUE +0.
 00040  77  EOF-SW                  PIC X            VALUE 'N'.
 00041      88  EOF                                  VALUE 'Y'.
 00042  77  ERR-SW                  PIC X            VALUE 'N'.
 00043      88  ERR                                  VALUE 'Y'.
 00044  77  RUN-DATE                PIC X(8).
 00045  77  BLNK                    PIC X         VALUE SPACE.
 00046  77  WS-OCCUFAC          PIC S9999V9       VALUE 0.
 00047      88  VALID-OCCUFAC                     VALUE +0100.0.
 00048  77  WS-STAT2            PIC 9.
 00049      88  VALID-STAT2                       VALUE 1.
 00050       EJECT
 00051  01  WORK-AREA.
 00052      05  CURR-KEY.
 00053          10  CURR-TOWN                PIC 9(2).
 00054          10  CURR-VOL                 PIC 9(3).
 00055          10  CURR-PROP                PIC 9(14).
 00056          10  CURR-TXTYP               PIC X.
 00057          SKIP1
 00058      05  PREV-KEY                     PIC X(20) VALUE LOW-VALUES.
 00059          SKIP1
 00060      05  SAVE-TOWN                    PIC 99.
 00061      SKIP1
 00062      05  WS-TWN.
 00063          10  WS-TOWN             PIC 99.
 00064             88  SV-SUBURBAN                    VALUE 10 THRU 39.
 00065             88  SV-CITY                        VALUE 70 THRU 77.
 00066      05  WS-TOWN-N REDEFINES WS-TWN  PIC 9(2).
 00067      05  W-UNPK-TXCDE  PIC 9(5).
 00068      05  FILLER REDEFINES W-UNPK-TXCDE.
 00069          10 W-TOWN     PIC 9(2).
 00070       EJECT
 00071  01  PRINT-LINES.
 00072      05 HEADING-1.
 00073         10  FILLER          PIC X(54)  VALUE ' ASREA868'.
 00074         10  FILLER          PIC X(21)  VALUE
 00075             'OFFICE OF THE ASSESOR'.
 00076         10  FILLER          PIC X(37)  VALUE SPACES.
 00077         10  FILLER          PIC X(9)   VALUE 'PAGE NO.'.
 00078         10  H1-PAGE         PIC Z,ZZZ.
 00079      SKIP1
 00080      05 HEADING-2.
 00081         10  FILLER          PIC X(2)   VALUE SPACES.
 00082         10  H2-DATE         PIC X(8).
 00083         10  FILLER          PIC X(37)  VALUE SPACES.
 00084         10  FILLER          PIC X(53) VALUE
 00085      'HOMEOWNER RECORDS WITH MISSING BASE VALUATIONS REPORT'.
 00086      SKIP1
 00087      05 HEADING-3.
 00088         10  FILLER          PIC X(3)   VALUE SPACES.
 00089         10  FILLER          PIC X(29)  VALUE
 00090             'VOLUME  PROPERTY INDEX NO.'.
 00091         10  FILLER          PIC X(50)  VALUE
 00092             'TAX-CODE  CLASS  BASE-YEAR  ASSESSED VALUATION'.
 00093         10  FILLER          PIC X(19)  VALUE
 00094             'EQUALIZED VALUATION'.
 00095      SKIP1
 00096      05  DETAIL-LINE                   VALUE SPACES.
 00097          10  FILLER         PIC X(4).
 00098          10  DL-VOL         PIC 9(3).
 00099          10  FILLER         PIC X(4).
 00100          10  DL-PROP        PIC 99,99,999,999,9999.
 00101          10  FILLER         PIC X(5).
 00102          10  DL-TXCD        PIC 99999.
 00103          10  FILLER         PIC X(4).
 00104          10  DL-CLASS       PIC 9,99.
 00105          10  FILLER         PIC X(6).
 00106          10  DL-BASE-YR     PIC 99.
 00107          10  FILLER         PIC X(8).
 00108          10  DL-ASSVAL      PIC ZZZ,ZZZ,ZZ9.
 00109          10  FILLER         PIC X(12).
 00110          10  DL-EQVAL       PIC ZZZ,ZZZ,ZZ9.
 00111  SKIP1
 00112      05 TOT-LN.
 00113         10  FILLER          PIC X(5)   VALUE SPACES.
 00114         10  TL-MSG          PIC X(34)  VALUE SPACES.
 00115         10  TL-TOTALS       PIC Z,ZZZ,ZZ9.
 00116  SKIP1
 00117      05  UNPK-PROP           PIC 9(15).
 00118      05  UNPK-PROPX REDEFINES UNPK-PROP.
 00119          10 HIGH-PROP        PIC 9.
 00120          10 REST-PROP        PIC 9(14).
 00121  SKIP1
 00122      05  DSP-TIME                   PIC 99,99.
 00123      05  DSP-DATE.
 00124          10  DSP-MO                 PIC 99.
 00125          10  FILLER                 PIC X         VALUE '/'.
 00126          10  DSP-DA                 PIC 99.
 00127          10  FILLER                 PIC X         VALUE '/'.
 00128          10  DSP-YR                 PIC 99.
 00129      05  ACPT-DATE                  PIC 9(6).
 00130      05  ACPT-DATE-X REDEFINES ACPT-DATE.
 00131          10  ACPT-YR                PIC 99.
 00132          10  ACPT-MO                PIC 99.
 00133          10  ACPT-DA                PIC 99.
 00134      05  ACPT-TIME-HOLD             PIC 9(8).
 00135      05  ACPT-TIME-HOLD-X REDEFINES ACPT-TIME-HOLD.
 00136          10  ACPT-TIME              PIC 9(4).
 00137          10  FILLER                 PIC 9(4).
 00138      05  WS-TIME                    PIC 99,99,99.
 00139      EJECT
 00140  PROCEDURE DIVISION.
 00141  000-HOUSEKEEPING.
 00142      ACCEPT ACPT-TIME-HOLD FROM TIME.
 00143      MOVE ACPT-TIME TO DSP-TIME.
 00144      INSPECT DSP-TIME REPLACING ALL ',' BY ':'.
 00145      ACCEPT ACPT-DATE FROM DATE.
 00146      MOVE ACPT-MO TO DSP-MO.
 00147      MOVE ACPT-DA TO DSP-DA.
 00148      MOVE ACPT-YR TO DSP-YR.
 00149      DISPLAY 'PROGRAM ASREA868 DATE AND TIME OF RUN = '
 00150        DSP-DATE ' ' DSP-TIME.
 00151      OPEN INPUT HOMEOWNER-MAST
 00152           OUTPUT PRINT-FILE.
 00153  SKIP1
 00154      PERFORM  010-MAINLINE  UNTIL  EOF OR ERR.
 00155  SKIP1
 00156      IF LINE-CNTR GREATER THAN +55
 00157         PERFORM 500-HEADING-ROUTINE.
 00158      DISPLAY 'TOTAL NUMBER OF RECORDS READ     '  REC-COUNT
 00159      MOVE    'TOTAL NUMBER OF RECORDS READ     '  TO TL-MSG
 00160      MOVE REC-COUNT TO TL-TOTALS
 00161      WRITE PRINT-REC FROM TOT-LN      AFTER ADVANCING 3
 00162      DISPLAY 'TOTAL NUMBER OF RECORDS PRINTED  ' RECS-PRINTED
 00163      MOVE    'TOTAL NUMBER OF RECORDS PRINTED  ' TO TL-MSG
 00164      MOVE RECS-PRINTED TO TL-TOTALS
 00165      WRITE PRINT-REC FROM TOT-LN      AFTER ADVANCING 1
 00166      DISPLAY 'TOTAL NUMBER OF RECORDS BYPASSED ' RECS-BYPASS
 00167      MOVE    'TOTAL NUMBER OF RECORDS BYPASSED ' TO TL-MSG
 00168      MOVE RECS-BYPASS  TO TL-TOTALS
 00169      WRITE PRINT-REC FROM TOT-LN      AFTER ADVANCING 1.
 00170  SKIP1
 00171      CLOSE  HOMEOWNER-MAST
 00172             PRINT-FILE.
 00173      STOP RUN.
 00174  SKIP3
 00175  010-MAINLINE.
 00176      PERFORM 100-READ-HOMEOWNER-FILE
 00177         IF NOT (EOF OR ERR)
 00178            IF HM-YR = 00
 00179               PERFORM 200-PRINT-DETAIL
 00180            ELSE
 00181               ADD   +1  TO  RECS-BYPASS.
 00182  SKIP3
 00183  100-READ-HOMEOWNER-FILE.
 00184      READ HOMEOWNER-MAST
 00185      AT END
 00186        MOVE HIGH-VALUES TO CURR-KEY
 00187        MOVE 'Y'         TO EOF-SW.
 00188      IF NOT  EOF
 00189          MOVE HM-TXCD     TO    W-UNPK-TXCDE
 00190          MOVE W-TOWN      TO    CURR-TOWN
 00191          MOVE HM-VOL      TO    CURR-VOL
 00192          MOVE HM-PROP     TO    CURR-PROP
 00193          MOVE HM-TXTYP    TO    CURR-TXTYP
 00194          IF  CURR-KEY  LESS PREV-KEY
 00195              MOVE  16  TO  RETURN-CODE
 00196              MOVE  'Y' TO  ERR-SW
 00197              DISPLAY 'HOMEOWNER SEQUENCE ERROR'
 00198              DISPLAY 'CURRENT  KEY = ' CURR-KEY
 00199              DISPLAY 'PREVIOUS KEY = ' PREV-KEY
 00200           ELSE
 00201              ADD +1  TO  REC-COUNT
 00202              MOVE CURR-KEY TO PREV-KEY.
 00203  SKIP3
 00204  200-PRINT-DETAIL.
 00205      IF LINE-CNTR GREATER THAN +55
 00206         PERFORM 500-HEADING-ROUTINE.
 00207      SKIP1
 00208      MOVE  HM-TXCD          TO DL-TXCD
 00209      MOVE  HM-VOL           TO DL-VOL
 00210      MOVE  HM-PROP          TO DL-PROP
 00211      INSPECT DL-PROP REPLACING ALL ',' BY '-'
 00212      MOVE  HM-YR            TO DL-BASE-YR
 00213      MOVE  HM-CLS           TO DL-CLASS
 00214      INSPECT DL-CLASS REPLACING ALL ',' BY '-'
 00215      MOVE  HM-ASSDVAL       TO DL-ASSVAL
 00216      MOVE  HM-EQVAL         TO DL-EQVAL
 00217      WRITE PRINT-REC FROM DETAIL-LINE AFTER ADVANCING 2
 00218      ADD  +1 TO RECS-PRINTED
 00219      ADD  +2 TO LINE-CNTR.
 00220      SKIP3
 00221  500-HEADING-ROUTINE.
 00222      ADD +1 TO PAGE-CNTR.
 00223      MOVE PAGE-CNTR TO H1-PAGE.
 00224      WRITE PRINT-REC FROM HEADING-1 AFTER ADVANCING PAGE.
 00225      WRITE PRINT-REC FROM HEADING-2 AFTER ADVANCING 2.
 00226      WRITE PRINT-REC FROM HEADING-3 AFTER ADVANCING 3.
 00227      WRITE PRINT-REC FROM BLNK      AFTER ADVANCING 2.
 00228      MOVE +8 TO LINE-CNTR.
