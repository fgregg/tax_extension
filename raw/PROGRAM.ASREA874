 00001  IDENTIFICATION DIVISION.
 00002  PROGRAM-ID. ASREA874.
 00003 *AUTHOR. PROGRAMMMER - NANCY T. FLEMING.
 00004 *        ANALYST     - KATHY LEE FOLAN.
 00005 *DATE-WRITTEN. APRIL 8, 2008.
 00006 *REMARKS. THIS PROGRAM CREATES A DISABLED EXEMPTION FILE.
 00007 *11/09/15 BOSWELL: MODIFIED PROGRAM FOR 2015 TAX YEAR EXEMPTION
 00008 *****************************************************************
 00009  ENVIRONMENT DIVISION.
 00010  CONFIGURATION SECTION.
 00011  SOURCE-COMPUTER. IBM-370.
 00012  OBJECT-COMPUTER. IBM-370.
 00013  INPUT-OUTPUT SECTION.
 00014  FILE-CONTROL.
 00015      SELECT ASSESSOR-IN   ASSIGN TO UT-S-INFILE.
 00016      SELECT VOLFILE       ASSIGN TO DA-VOLFILE
 00017                           ACCESS MODE IS SEQUENTIAL
 00018                           ORGANIZATION IS INDEXED
 00019                           FILE STATUS IS FILE-STATUS FILE-STATUS-2
 00020                           RECORD KEY IS VOL-KEY.
 00021      SELECT DISEXMPT-FILE ASSIGN TO UT-S-DISEXMPT.
 00022      SELECT PRINT-FILE    ASSIGN TO UT-S-PRINTFLE.
 00023  DATA DIVISION.
 00024  FILE SECTION.
 00025      SKIP1
 00026  FD  ASSESSOR-IN
 00027      BLOCK CONTAINS  0   RECORDS
 00028      RECORD CONTAINS 46 CHARACTERS
 00029      LABEL RECORDS   ARE STANDARD
 00030      RECORDING MODE  IS  F.
 00031  01  ASSESSOR-IN-REC.
 00032      05 ASSR-PROP              PIC 9(14) PACKED-DECIMAL.
 00033 *                                          PROPERTY NO.     1-8
 00034      05 ASSR-BATCHNO           PIC 9(5)  PACKED-DECIMAL.
 00035 *                                          BATCH NO.        9-11
 00036      05 ASSR-TOWN              PIC X(2).
 00037 *                                          TOWN NO.        12-13
 00038      05 ASSR-VOL               PIC 9(3).
 00039 *                                          VOL             14-16
 00040      05 ASSR-RETVET-IND        PIC X.
 00041 *                                          RETURNING VET   17-17
 00042      05 ASSR-RETVET-EAV        PIC 9(5)  PACKED-DECIMAL.
 00043 *                                          RETURNING EAV   18-20
 00044      05 ASSR-DISPER-IND        PIC X.
 00045 *                                          DIS PERSON IND  21-21
 00046      05 ASSR-DISPER-EAV        PIC 9(5)  PACKED-DECIMAL.
 00047 *                                          DIS PERSON EAV  22-24
 00048      05 ASSR-DISVET-IND-1      PIC X.
 00049 *                                          DIS VET IND-L75 25-25
 00050      05 ASSR-DISVET-EAV-1      PIC 9(5)  PACKED-DECIMAL.
 00051 *                                          DIS VET EAV-L75 26-28
 00052      05 ASSR-DISVET-IND-2      PIC X.
 00053 *                                          DIS VET IND-G75 29-29
 00054      05 ASSR-DISVET-EAV-2      PIC 9(5)  PACKED-DECIMAL.
 00055 *                                          DIS VET EAV-G75 30-32
 00056      05 ASSR-USERID            PIC 9(6)  PACKED-DECIMAL.
 00057 *                                          USERID          33-36
 00058      05 ASSR-ENT-DATE          PIC 9(6)  PACKED-DECIMAL.
 00059 *                                          DATE ENTERED    37-40
 00060      05 ASSR-INDICATOR         PIC 9.
 00061 *                                          INDICATOR       41-41
 00062      05 ASSR-DISVET-IND-3      PIC X.
 00063 *                                          INDICATOR       42-42
 00064      05 ASSR-DISVET-EAV-3      PIC 9(7)  PACKED-DECIMAL.
 00065 *                                          INDICATOR       43-46
 00066      SKIP3
 00067  FD  VOLFILE
 00068      BLOCK  CONTAINS 0   RECORDS
 00069      RECORD CONTAINS 10  CHARACTERS
 00070      LABEL  RECORDS  ARE STANDARD.
 00071  01  VOL-REC.
 00072      05 VOL-KEY.
 00073         10 V-TWN       PIC 9(3)       PACKED-DECIMAL.
 00074         10 V-VOL       PIC 9(3)       PACKED-DECIMAL.
 00075      05 V-PROP-1       PIC 9(5)       PACKED-DECIMAL.
 00076      05 V-PROP-2       PIC 9(5)       PACKED-DECIMAL.
 00077      SKIP3
 00078  ++INCLUDE ASREASDVF1
 00079      SKIP3
 00080  FD  PRINT-FILE
 00081      BLOCK  CONTAINS 0   RECORDS
 00082      RECORD CONTAINS 133 CHARACTERS
 00083      LABEL  RECORDS  ARE STANDARD
 00084      RECORDING MODE  IS  F.
 00085  01  PRINT-REC         PIC X(133).
 00086      EJECT
 00087  WORKING-STORAGE SECTION.
 00088  77  MDCY-OR-CYMD PIC S9(5)V9(4) PACKED-DECIMAL VALUE +10000.0001.
 00089  77  CURRENT-DATE      PIC 99/99/9(4).
 00090  77  CURRENT-TIME      PIC 99,99,99.
 00091  77  DATE-PROD         PIC X(8)       VALUE 'DPDPP142'.
 00092  77  LNE-CNT           PIC S9(3) PACKED-DECIMAL VALUE +99.
 00093  77  REST-OF-LNE-CNT   PIC S9(3) PACKED-DECIMAL VALUE +0.
 00094  77  PAGE-CNT          PIC S9(3) PACKED-DECIMAL VALUE +0.
 00095  77  IN-CNT            PIC S9(5) PACKED-DECIMAL VALUE +0.
 00096  77  WS-ERR-CNT        PIC S9(5) PACKED-DECIMAL VALUE +0.
 00097  77  DISEX-CNT         PIC S9(5) PACKED-DECIMAL VALUE +0.
 00098  77  WRIT-CNT          PIC S9(5) PACKED-DECIMAL VALUE +0.
 00099  77  WS-BYPASS-CNT     PIC S9(5) PACKED-DECIMAL VALUE +0.
 00100  77  VALID-RECORD-SW   PIC X.
 00101      88  VALID-RECORD      VALUE  'Y'.
 00102      88  NOT-VALID-RECORD  VALUE 'N'.
 00103  77  REC-SW            PIC S9         VALUE +3.
 00104      88 GOOD-REC       VALUE +0.
 00105      88 DUP            VALUE +1 +2.
 00106      88 NEW-KEY        VALUE +3.
 00107  77  WS-ERR-SW         PIC X          VALUE 'N'.
 00108      88 ERR            VALUE 'Y'.
 00109  77  VSAM-ERR-SW       PIC X          VALUE 'N'.
 00110      88 VSAM-ERR       VALUE 'Y'.
 00111  77  VOL-EOF-SW        PIC X          VALUE 'N'.
 00112      88 VOL-EOF        VALUE 'Y'.
 00113  77  ASSR-IN-EOF-SW    PIC X          VALUE 'N'.
 00114      88 ASSR-IN-EOF    VALUE 'Y'.
 00115  77  ASSR-IN-EOD-SW    PIC X          VALUE 'N'.
 00116      88 ASSR-IN-EOD    VALUE 'Y'.
 00117
 00118  01  WORK-VARIABLES.
 00119      05  CURRENT-DATE-RETURN.
 00120          10  SYSTEM-DATE    PIC 9(8).
 00121          10  SYSTEM-TIME    PIC 9(6).
 00122          10  FILLER         PIC X(7).
 00123  01  WORK-KEY.
 00124      05 W-TWN          PIC 9(3)       VALUE 0.
 00125      05 W-VOL          PIC 9(3)       VALUE 0.
 00126  01  WORK-KEY-R REDEFINES WORK-KEY  PIC 9(6).
 00127      SKIP1
 00128  01  VOL-WORK-KEY.
 00129      05 VW-TWN         PIC 9(3)       VALUE 0.
 00130      05 VW-VOL         PIC 9(3)       VALUE 0.
 00131  01  VOL-WORK-KEY-R REDEFINES VOL-WORK-KEY  PIC 9(6).
 00132      SKIP1
 00133  01  WORK-PROP.
 00134      05 W-PROP-5       PIC 9(5).
 00135      05 FILLER         PIC 9(9).
 00136  01  WORK-PROP-R REDEFINES WORK-PROP  PIC 9(14).
 00137      EJECT
 00138  01  WORK-AREA.
 00139      05  WS-DATE.
 00140          10  CCYY                    PIC 9(4)    VALUE ZEROS.
 00141          10  MM                      PIC 9(2)    VALUE ZEROS.
 00142          10  DD                      PIC 9(2)    VALUE ZEROS.
 00143      05  WS-DATE-MMDDCCYY.
 00144          10  MM                      PIC 9(2)    VALUE ZEROS.
 00145          10  FILLER                  PIC X       VALUE '/'.
 00146          10  DD                      PIC 9(2)    VALUE ZEROS.
 00147          10  FILLER                  PIC X       VALUE '/'.
 00148          10  CCYY                    PIC 9(4)    VALUE ZEROS.
 00149      05  FILE-STATUS       PIC 9(2)   VALUE 0.
 00150          88 GOOD-STATUS    VALUE 00.
 00151      05  FILE-STATUS-2                          BINARY.
 00152          10 FILE2-RETURN   PIC 99     VALUE 0.
 00153          10 FILE2-FUNCTION PIC 9      VALUE 0.
 00154          10 FILE2-FEEDBACK PIC 999    VALUE 0.
 00155  01  HDR-1.
 00156      05 FILLER         PIC X.
 00157      05 FILLER         PIC X(8)  VALUE 'ASREA874'.
 00158      05 FILLER         PIC X(18) VALUE SPACE.
 00159      05 H1-DATE        PIC X(10).
 00160      05 FILLER         PIC X(20) VALUE SPACE.
 00161      05 FILLER         PIC X(20) VALUE 'COOK COUNTY ASSESSOR'.
 00162      05 FILLER         PIC X(48) VALUE SPACE.
 00163      05 FILLER         PIC X(5)  VALUE 'PAGE '.
 00164      05 H1-PAGE        PIC ZZ9.
 00165      SKIP1
 00166  01  HDR-2.
 00167      05 FILLER         PIC X.
 00168      05 FILLER         PIC X(52) VALUE SPACE.
 00169      05 FILLER         PIC X(31) VALUE
 00170         'DISABLED EXEMPTION ERROR REPORT'.
 00171      SKIP1
 00172  01  HDR-3.
 00173      05 FILLER PIC X.
 00174      05 FILLER PIC X(4)  VALUE 'TOWN'.
 00175      05 FILLER PIC X(5)  VALUE SPACE.
 00176      05 FILLER PIC X(6)  VALUE 'VOLUME'.
 00177      05 FILLER PIC X(8)  VALUE SPACE.
 00178      05 FILLER PIC X(8)  VALUE 'PROPERTY'.
 00179      05 FILLER PIC X(8)  VALUE SPACE.
 00180      05 FILLER PIC X(9)  VALUE 'RETURNING'.
 00181      05 FILLER PIC X(4)  VALUE SPACE.
 00182      05 FILLER PIC X(8)  VALUE 'DISABLED'.
 00183      05 FILLER PIC X(5)  VALUE SPACE.
 00184      05 FILLER PIC X(8)  VALUE 'DISABLED'.
 00185      05 FILLER PIC X(9)  VALUE SPACE.
 00186      05 FILLER PIC X(8)  VALUE 'DISABLED'.
 00187      05 FILLER PIC X(17) VALUE SPACE.
 00188      05 FILLER PIC X(13) VALUE 'ERROR MESSAGE'.
 00189      SKIP1
 00190  01  HDR-4.
 00191      05 FILLER PIC X.
 00192      05 FILLER PIC X(40) VALUE   SPACE.
 00193      05 FILLER PIC X(7)  VALUE   'VETERAN'.
 00194      05 FILLER PIC X(5)  VALUE   SPACE.
 00195      05 FILLER PIC X(6)  VALUE   'PERSON'.
 00196      05 FILLER PIC X(7)  VALUE   SPACE.
 00197      05 FILLER PIC X(7)  VALUE   'VETERAN'.
 00198      05 FILLER PIC X(10) VALUE   SPACE.
 00199      05 FILLER PIC X(7)  VALUE   'VETERAN'.
 00200      SKIP1
 00201  01  HDR-5.
 00202      05 FILLER PIC X.
 00203      05 FILLER PIC X(24) VALUE   SPACE.
 00204      05 FILLER PIC X(6)  VALUE   'NUMBER'.
 00205      05 FILLER PIC X(10) VALUE   SPACE.
 00206      05 FILLER PIC X(7)  VALUE   'IND/EAV'.
 00207      05 FILLER PIC X(5)  VALUE   SPACE.
 00208      05 FILLER PIC X(7)  VALUE   'IND/EAV'.
 00209      05 FILLER PIC X(4)  VALUE   SPACE.
 00210      05 FILLER PIC X(12) VALUE   'IND/<75% EAV'.
 00211      05 FILLER PIC X(5)  VALUE   SPACE.
 00212      05 FILLER PIC X(12) VALUE   'IND/>75% EAV'.
 00213      SKIP1
 00214  01  DET-LINE.
 00215      05 FILLER         PIC X.
 00216      05 FILLER         PIC X          VALUE SPACE.
 00217      05 DET-TWN        PIC 99.
 00218      05 FILLER         PIC X(4)       VALUE SPACE.
 00219      05 DET-VOL        PIC 9(3).
 00220      05 FILLER         PIC X(4)       VALUE SPACE.
 00221      05 DET-PROP       PIC 99,99,999,999,9999.
 00222      05 FILLER         PIC X(3)       VALUE SPACE.
 00223      05 DET-ASSR-RETVET-IND  PIC X.
 00224      05 FILLER         PIC X          VALUE SPACE.
 00225      05 FILLER         PIC X          VALUE '/'.
 00226      05 FILLER         PIC X          VALUE SPACE.
 00227      05 DET-ASSR-RETVET-EAV  PIC 9(5).
 00228      05 FILLER         PIC X(3)       VALUE SPACE.
 00229      05 DET-ASSR-DISPER-IND  PIC X.
 00230      05 FILLER         PIC X          VALUE SPACE.
 00231      05 FILLER         PIC X          VALUE '/'.
 00232      05 FILLER         PIC X          VALUE SPACE.
 00233      05 DET-ASSR-DISPER-EAV  PIC 9(5).
 00234      05 FILLER         PIC X(3)       VALUE SPACE.
 00235      05 DET-ASSR-DISVET-IND-1    PIC X.
 00236      05 FILLER         PIC X          VALUE SPACE.
 00237      05 FILLER         PIC X          VALUE '/'.
 00238      05 FILLER         PIC X          VALUE SPACE.
 00239      05 DET-ASSR-DISVET-EAV-1    PIC 9(5).
 00240      05 FILLER         PIC X(3)       VALUE SPACE.
 00241      05 DET-ASSR-DISVET-IND-2    PIC X.
 00242      05 FILLER         PIC X          VALUE SPACE.
 00243      05 FILLER         PIC X          VALUE '/'.
 00244      05 FILLER         PIC X          VALUE SPACE.
 00245      05 DET-ASSR-DISVET-EAV-2    PIC 9(5).
 00246      05 FILLER         PIC X(3)       VALUE SPACE.
 00247      05 DET-ASSR-DISVET-IND-3    PIC X.
 00248      05 FILLER         PIC X          VALUE SPACE.
 00249      05 FILLER         PIC X          VALUE '/'.
 00250      05 FILLER         PIC X          VALUE SPACE.
 00251      05 DET-ASSR-DISVET-EAV-3    PIC 9(5).
 00252      05 FILLER         PIC X(3)       VALUE SPACE.
 00253      05 DET-MSG        PIC X(33).
 00254      SKIP1
 00255  01  TOT-1.
 00256      05 FILLER         PIC X.
 00257      05 FILLER         PIC X(5)  VALUE SPACE.
 00258      05 FILLER         PIC X(24) VALUE
 00259         'TOTAL RECORDS READ  '.
 00260      05 T1-READ        PIC ZZ,ZZ9.
 00261  01  TOT-2.
 00262      05 FILLER         PIC X.
 00263      05 FILLER         PIC X(5)  VALUE SPACE.
 00264      05 FILLER         PIC X(24) VALUE
 00265         'TOTAL RECORDS IN ERROR '.
 00266      05 T2-ERROR       PIC ZZ,ZZ9.
 00267  01  TOT-3.
 00268      05 FILLER         PIC X.
 00269      05 FILLER         PIC X(5)  VALUE SPACE.
 00270      05 FILLER         PIC X(24) VALUE
 00271         'TOTAL RECORDS WRITTEN '.
 00272      05 T3-WRITTEN     PIC ZZ,ZZ9.
 00273
 00274  01  FILLER.
 00275  ++INCLUDE STATETABLE
 00276      SKIP2
 00277  PROCEDURE DIVISION.
 00278  STEP0-INITIALIZE.
 00279      MOVE FUNCTION CURRENT-DATE TO CURRENT-DATE-RETURN.
 00280      COMPUTE CURRENT-DATE = SYSTEM-DATE * MDCY-OR-CYMD.
 00281      MOVE SYSTEM-TIME TO CURRENT-TIME.
 00282      MOVE SYSTEM-DATE TO WS-DATE.
 00283      MOVE CORRESPONDING WS-DATE TO WS-DATE-MMDDCCYY.
 00284      MOVE WS-DATE-MMDDCCYY TO H1-DATE.
 00285      INSPECT CURRENT-TIME REPLACING ALL ',' BY ':'.
 00286      DISPLAY 'PROGRAM ASREA874 DATE AND TIME OF RUN = '
 00287          CURRENT-DATE ' ' CURRENT-TIME.
 00288
 00289  STEP1-OPEN-FILES.
 00290      OPEN INPUT VOLFILE.
 00291      IF NOT GOOD-STATUS
 00292         DISPLAY 'VOLFILE OPEN ERR - FILE STATUS IS ' FILE-STATUS
 00293         DISPLAY 'FILE STATUS FEEDBACK  ' FILE2-RETURN
 00294           ' ' FILE2-FUNCTION  ' ' FILE2-FEEDBACK
 00295         MOVE 16 TO RETURN-CODE
 00296         STOP RUN.
 00297      OPEN INPUT  ASSESSOR-IN
 00298           OUTPUT DISEXMPT-FILE
 00299                  PRINT-FILE.
 00300
 00301  STEP2.
 00302      PERFORM STEP3-MAINLINE UNTIL ASSR-IN-EOF OR ERR.
 00303      IF ASSR-IN-EOF
 00304          MOVE 'Y' TO ASSR-IN-EOD-SW.
 00305      IF ERR
 00306          MOVE 16 TO RETURN-CODE.
 00307      IF NOT VSAM-ERR
 00308         CLOSE VOLFILE
 00309            IF NOT GOOD-STATUS
 00310               DISPLAY 'ASREA874'
 00311               DISPLAY 'VOLFILE CLOSE ERR - FILE STATUS IS '
 00312                  FILE-STATUS
 00313               DISPLAY 'FILE STATUS FEEDBACK  ' FILE2-RETURN
 00314                 ' ' FILE2-FUNCTION ' ' FILE2-FEEDBACK
 00315               MOVE 'Y' TO WS-ERR-SW
 00316               MOVE 16 TO RETURN-CODE.
 00317      IF NOT ERR AND ASSR-IN-EOF
 00318         DISPLAY 'ASSR-IN RECORDS                 ' IN-CNT
 00319         DISPLAY 'RECORDS IN ERROR                ' WS-ERR-CNT
 00320         DISPLAY 'DISABLED EXEMPT RECORDS         ' DISEX-CNT
 00321      END-IF.
 00322      IF LNE-CNT > 60
 00323         PERFORM STEP10-PRINT-HEADINGS
 00324         MOVE IN-CNT TO T1-READ
 00325         WRITE PRINT-REC FROM TOT-1 AFTER ADVANCING 2 LINES
 00326         MOVE WS-ERR-CNT TO T2-ERROR
 00327         WRITE PRINT-REC FROM TOT-2 AFTER ADVANCING 2 LINES
 00328         MOVE DISEX-CNT TO T3-WRITTEN
 00329         WRITE PRINT-REC FROM TOT-3 AFTER ADVANCING 2 LINES
 00330      ELSE
 00331         MOVE IN-CNT TO T1-READ
 00332         SUBTRACT LNE-CNT FROM 60 GIVING REST-OF-LNE-CNT
 00333         WRITE PRINT-REC FROM TOT-1 AFTER ADVANCING
 00334                                             REST-OF-LNE-CNT
 00335         MOVE WS-ERR-CNT TO T2-ERROR
 00336         WRITE PRINT-REC FROM TOT-2 AFTER ADVANCING 2 LINES
 00337         MOVE DISEX-CNT TO T3-WRITTEN
 00338         WRITE PRINT-REC FROM TOT-3 AFTER ADVANCING 2 LINES
 00339      END-IF.
 00340      CLOSE ASSESSOR-IN
 00341            DISEXMPT-FILE
 00342            PRINT-FILE.
 00343      STOP RUN.
 00344
 00345  STEP3-MAINLINE.
 00346      PERFORM STEP4-READ-ASSR-IN-FILE.
 00347      IF NOT ASSR-IN-EOF
 00348          PERFORM STEP5-EDIT-RECORD
 00349          IF VALID-RECORD
 00350              PERFORM STEP11-WRITE-DISEXMPT-REC
 00351          END-IF
 00352      END-IF.
 00353
 00354  STEP4-READ-ASSR-IN-FILE.
 00355      READ ASSESSOR-IN
 00356          AT END MOVE 'Y' TO ASSR-IN-EOF-SW.
 00357      IF NOT ASSR-IN-EOF
 00358          ADD +1 TO IN-CNT
 00359      END-IF.
 00360
 00361  STEP5-EDIT-RECORD.
 00362      IF ASSR-TOWN NUMERIC AND
 00363          (ASSR-TOWN > 09 AND ASSR-TOWN < 40)
 00364              OR (ASSR-TOWN > 69 AND ASSR-TOWN < 78)
 00365                  CONTINUE
 00366      ELSE
 00367          ADD +1 TO WS-ERR-CNT
 00368          MOVE 'TOWN ERROR     ' TO DET-MSG
 00369          MOVE 'Y' TO WS-ERR-SW
 00370      END-IF.
 00371      IF ASSR-VOL NUMERIC AND ASSR-VOL > 0
 00372          CONTINUE
 00373      ELSE
 00374          ADD +1 TO WS-ERR-CNT
 00375          MOVE 'VOLUME ERROR   ' TO DET-MSG
 00376          MOVE 'Y' TO WS-ERR-SW
 00377      END-IF.
 00378      IF ASSR-PROP NUMERIC AND ASSR-PROP > 0
 00379          CONTINUE
 00380      ELSE
 00381          ADD +1 TO WS-ERR-CNT
 00382          MOVE 'PROPERTY NUMBER ERROR  ' TO DET-MSG
 00383          MOVE 'Y' TO WS-ERR-SW
 00384      END-IF.
 00385      MOVE ZEROES TO WS-BYPASS-CNT.
 00386 *->  IF ASSR-RETVET-IND = 'X' AND
 00387 *->     ASSR-DISPER-IND = 'X'
 00388 *->     ADD +1 TO WS-ERR-CNT
 00389 *->     MOVE 'RET/VET & DISABLED PERSON INVALID ' TO DET-MSG
 00390 *->     MOVE 'Y' TO WS-ERR-SW
 00391 *->     MOVE ASSR-RETVET-IND TO DET-ASSR-RETVET-IND
 00392 *->     MOVE ASSR-RETVET-EAV TO DET-ASSR-RETVET-EAV
 00393 *->     MOVE ASSR-DISPER-IND TO DET-ASSR-DISPER-IND
 00394 *->     MOVE ASSR-DISPER-EAV TO DET-ASSR-DISPER-EAV
 00395 *->  ELSE
 00396         IF ASSR-DISPER-IND = 'X' AND
 00397            ASSR-DISVET-IND-1 = 'X'
 00398            ADD +1 TO WS-ERR-CNT
 00399            MOVE 'DIS PERSON & DIS VET <75 INVALID ' TO DET-MSG
 00400            MOVE 'Y' TO WS-ERR-SW
 00401            MOVE ASSR-DISPER-IND TO DET-ASSR-DISPER-IND
 00402            MOVE ASSR-DISPER-EAV TO DET-ASSR-DISPER-EAV
 00403            MOVE ASSR-DISVET-IND-1 TO DET-ASSR-DISVET-IND-1
 00404            MOVE ASSR-DISVET-EAV-1 TO DET-ASSR-DISVET-EAV-1
 00405         ELSE
 00406            IF ASSR-DISPER-IND = 'X' AND
 00407               ASSR-DISVET-IND-2 = 'X'
 00408               ADD +1 TO WS-ERR-CNT
 00409               MOVE 'DIS PERSON & DIS VET >75 INVALID ' TO DET-MSG
 00410               MOVE 'Y' TO WS-ERR-SW
 00411               MOVE ASSR-DISPER-IND TO DET-ASSR-DISPER-IND
 00412               MOVE ASSR-DISPER-EAV TO DET-ASSR-DISPER-EAV
 00413               MOVE ASSR-DISVET-IND-2 TO
 00414                    DET-ASSR-DISVET-IND-2
 00415               MOVE ASSR-DISVET-EAV-2 TO
 00416                    DET-ASSR-DISVET-EAV-2
 00417            ELSE
 00418               IF ASSR-DISPER-IND = 'X' AND
 00419                  ASSR-DISVET-IND-3 = 'X'
 00420                  ADD +1               TO WS-ERR-CNT
 00421                  MOVE 'DIS PERSON & DIS VET >75 INVALID '
 00422                                       TO DET-MSG
 00423                  MOVE 'Y'             TO WS-ERR-SW
 00424                  MOVE ASSR-DISPER-IND TO DET-ASSR-DISPER-IND
 00425                  MOVE ASSR-DISPER-EAV TO DET-ASSR-DISPER-EAV
 00426                  MOVE ASSR-DISVET-IND-3 TO
 00427                       DET-ASSR-DISVET-IND-3
 00428                  MOVE ASSR-DISVET-EAV-3 TO
 00429                       DET-ASSR-DISVET-EAV-3
 00430               ELSE
 00431                 IF ASSR-DISVET-IND-1 = 'X' AND
 00432                    ASSR-DISVET-IND-2 = 'X' AND
 00433                    ASSR-DISVET-IND-3 = 'X'
 00434                    ADD +1 TO WS-ERR-CNT
 00435                    MOVE 'DIS PERSON & DIS VET >75 INVALID '
 00436                          TO DET-MSG
 00437                    MOVE 'Y' TO WS-ERR-SW
 00438                    MOVE ASSR-DISPER-IND TO DET-ASSR-DISPER-IND
 00439                    MOVE ASSR-DISPER-EAV TO DET-ASSR-DISPER-EAV
 00440                    MOVE ASSR-DISVET-IND-2 TO
 00441                         DET-ASSR-DISVET-IND-2
 00442                    MOVE ASSR-DISVET-EAV-2 TO
 00443                         DET-ASSR-DISVET-EAV-2
 00444                  END-IF
 00445               END-IF
 00446            END-IF
 00447         END-IF
 00448 *->  END-IF
 00449      IF ASSR-RETVET-IND = ' '
 00450          ADD +1 TO WS-BYPASS-CNT
 00451      ELSE
 00452          IF ASSR-RETVET-IND = 'X' AND ASSR-RETVET-EAV = 5000
 00453              CONTINUE
 00454          ELSE
 00455              ADD +1 TO WS-ERR-CNT
 00456              MOVE 'RETURNING VETERAN ERROR   ' TO DET-MSG
 00457              MOVE 'Y' TO WS-ERR-SW
 00458              MOVE ASSR-RETVET-IND TO DET-ASSR-RETVET-IND
 00459              MOVE ASSR-RETVET-EAV TO DET-ASSR-RETVET-EAV
 00460          END-IF
 00461      END-IF.
 00462      IF ASSR-DISPER-IND = ' '
 00463          ADD +1 TO WS-BYPASS-CNT
 00464      ELSE
 00465          IF ASSR-DISPER-IND = 'X' AND ASSR-DISPER-EAV = 2000
 00466              CONTINUE
 00467          ELSE
 00468              ADD +1 TO WS-ERR-CNT
 00469              MOVE 'DISABLED PERSON ERROR  ' TO DET-MSG
 00470              MOVE 'Y' TO WS-ERR-SW
 00471              MOVE ASSR-DISPER-IND TO DET-ASSR-DISPER-IND
 00472              MOVE ASSR-DISPER-EAV TO DET-ASSR-DISPER-EAV
 00473          END-IF
 00474      END-IF.
 00475      IF ASSR-DISVET-IND-1 = ' '
 00476          ADD +1 TO WS-BYPASS-CNT
 00477      ELSE
 00478          IF ASSR-DISVET-IND-1 = 'X' AND ASSR-DISVET-EAV-1
 00479              = 2500
 00480                  CONTINUE
 00481          ELSE
 00482              ADD +1 TO WS-ERR-CNT
 00483              MOVE 'DISABLED VETERAN ERROR   ' TO DET-MSG
 00484              MOVE 'Y' TO WS-ERR-SW
 00485              MOVE ASSR-DISVET-IND-1 TO DET-ASSR-DISVET-IND-1
 00486              MOVE ASSR-DISVET-EAV-1 TO DET-ASSR-DISVET-EAV-1
 00487          END-IF
 00488      END-IF.
 00489      IF ASSR-DISVET-IND-2 = ' '
 00490          ADD +1 TO WS-BYPASS-CNT
 00491      ELSE
 00492          IF ASSR-DISVET-IND-2 = 'X' AND ASSR-DISVET-EAV-2
 00493              = 5000
 00494                  CONTINUE
 00495          ELSE
 00496              ADD +1 TO WS-ERR-CNT
 00497              MOVE 'DISABLED VETERAN ERROR   ' TO DET-MSG
 00498              MOVE 'Y' TO WS-ERR-SW
 00499              MOVE ASSR-DISVET-IND-2 TO DET-ASSR-DISVET-IND-2
 00500              MOVE ASSR-DISVET-EAV-2 TO DET-ASSR-DISVET-EAV-2
 00501          END-IF
 00502      END-IF.
 00503
 00504      IF ASSR-DISVET-IND-3 = ' '
 00505          ADD +1 TO WS-BYPASS-CNT
 00506      ELSE
 00507          IF ASSR-DISVET-IND-3 = 'X' AND ASSR-DISVET-EAV-3
 00508              = 250000
 00509                  CONTINUE
 00510          ELSE
 00511              ADD +1 TO WS-ERR-CNT
 00512              MOVE 'DISABLED VETERAN ERROR   ' TO DET-MSG
 00513              MOVE 'Y' TO WS-ERR-SW
 00514              MOVE ASSR-DISVET-IND-3 TO DET-ASSR-DISVET-IND-3
 00515              MOVE ASSR-DISVET-EAV-3 TO DET-ASSR-DISVET-EAV-3
 00516          END-IF
 00517      END-IF.
 00518
 00519      IF ASSR-DISVET-EAV-1 > 0 AND
 00520         ASSR-DISVET-EAV-2 > 0 AND
 00521         ASSR-DISVET-EAV-3 > 0
 00522          ADD +1 TO WS-ERR-CNT
 00523          MOVE 'DISABLED VET ALL EAV > 0 ' TO DET-MSG
 00524          MOVE 'Y' TO WS-ERR-SW
 00525      ELSE
 00526          CONTINUE
 00527      END-IF.
 00528
 00529      PERFORM STEP6-READ-VOLFILE.
 00530
 00531      IF WS-ERR-SW = 'Y'
 00532          PERFORM STEP9-PRINT-REPORT
 00533          MOVE SPACES TO WS-ERR-SW
 00534      ELSE
 00535 *        IF WS-BYPASS-CNT = 4
 00536          IF WS-BYPASS-CNT = 5
 00537              MOVE 'ALL EXEMPTIONS BYPASSED ' TO DET-MSG
 00538              PERFORM STEP9-PRINT-REPORT
 00539              MOVE ZEROES TO WS-BYPASS-CNT
 00540          ELSE
 00541              PERFORM STEP11-WRITE-DISEXMPT-REC
 00542          END-IF
 00543      END-IF.
 00544
 00545  STEP6-READ-VOLFILE.
 00546      MOVE ASSR-TOWN TO W-TWN.
 00547      MOVE ASSR-VOL  TO W-VOL.
 00548      PERFORM STEP7-READ-VOLFILE THRU STEP7-READ-VOLFILE-EXIT
 00549          UNTIL ERR
 00550              OR VOL-WORK-KEY-R NOT LESS THAN WORK-KEY-R.
 00551      PERFORM STEP8-VERIFY-PROP THRU STEP8-VERIFY-PROP-EXIT.
 00552
 00553  STEP7-READ-VOLFILE.
 00554      READ VOLFILE
 00555           AT END
 00556              DISPLAY 'ASREA874'
 00557              DISPLAY 'EOF ON VOLFILE'
 00558              MOVE 'Y' TO VOL-EOF-SW.
 00559      IF NOT VOL-EOF
 00560          IF NOT GOOD-STATUS
 00561              DISPLAY 'ASREA874'
 00562              DISPLAY 'VOLFILE READ ERR - FILE STATUS IS  '
 00563                  FILE-STATUS
 00564              DISPLAY 'FILE STATUS FEEDBACK  ' FILE2-RETURN
 00565                  ' ' FILE2-FUNCTION ' ' FILE2-FEEDBACK
 00566              MOVE 16 TO RETURN-CODE
 00567              MOVE 'Y' TO WS-ERR-SW
 00568          ELSE
 00569              MOVE V-TWN TO VW-TWN
 00570              MOVE V-VOL TO VW-VOL
 00571          END-IF
 00572      END-IF.
 00573  STEP7-READ-VOLFILE-EXIT.
 00574      EXIT.
 00575
 00576  STEP8-VERIFY-PROP.
 00577      IF NOT ERR
 00578          IF VOL-WORK-KEY-R NOT EQUAL WORK-KEY-R
 00579              ADD +1 TO WS-ERR-CNT
 00580              MOVE 'TOWN VOL AND/OR PROP NO. INVALID' TO DET-MSG
 00581              MOVE 'Y' TO WS-ERR-SW
 00582          ELSE
 00583              MOVE ASSR-PROP TO WORK-PROP-R
 00584              IF (W-PROP-5 LESS V-PROP-1)
 00585                  OR (W-PROP-5 GREATER V-PROP-2)
 00586                      ADD +1 TO WS-ERR-CNT
 00587                      MOVE 'TOWN VOL AND/OR PROP NO. INVALID'
 00588                          TO DET-MSG
 00589                      MOVE 'Y' TO WS-ERR-SW
 00590              END-IF
 00591          END-IF
 00592      END-IF.
 00593  STEP8-VERIFY-PROP-EXIT.
 00594      EXIT.
 00595
 00596  STEP9-PRINT-REPORT.
 00597      MOVE ASSR-TOWN TO DET-TWN.
 00598      MOVE ASSR-VOL  TO DET-VOL.
 00599      MOVE ASSR-PROP TO DET-PROP.
 00600      INSPECT DET-PROP REPLACING ALL ',' BY '-'.
 00601      MOVE ASSR-RETVET-IND TO DET-ASSR-RETVET-IND.
 00602      MOVE ASSR-RETVET-EAV TO DET-ASSR-RETVET-EAV.
 00603      MOVE ASSR-DISPER-IND TO DET-ASSR-DISPER-IND.
 00604      MOVE ASSR-DISPER-EAV TO DET-ASSR-DISPER-EAV.
 00605      MOVE ASSR-DISVET-IND-1 TO DET-ASSR-DISVET-IND-1.
 00606      MOVE ASSR-DISVET-EAV-1 TO DET-ASSR-DISVET-EAV-1.
 00607      MOVE ASSR-DISVET-IND-2 TO DET-ASSR-DISVET-IND-2.
 00608      MOVE ASSR-DISVET-EAV-2 TO DET-ASSR-DISVET-EAV-2.
 00609      MOVE ASSR-DISVET-IND-3 TO DET-ASSR-DISVET-IND-3.
 00610      MOVE ASSR-DISVET-EAV-3 TO DET-ASSR-DISVET-EAV-3.
 00611      IF LNE-CNT > +64
 00612          PERFORM STEP10-PRINT-HEADINGS
 00613      END-IF.
 00614      WRITE PRINT-REC FROM DET-LINE AFTER ADVANCING 2 LINES.
 00615      ADD +2 TO LNE-CNT.
 00616      ADD +1 TO WRIT-CNT.
 00617
 00618  STEP10-PRINT-HEADINGS.
 00619      ADD +1 TO PAGE-CNT.
 00620      MOVE PAGE-CNT TO H1-PAGE.
 00621      WRITE PRINT-REC FROM HDR-1 AFTER ADVANCING PAGE.
 00622      WRITE PRINT-REC FROM HDR-2 AFTER ADVANCING 1 LINE.
 00623      WRITE PRINT-REC FROM HDR-3 AFTER ADVANCING 2 LINES.
 00624      WRITE PRINT-REC FROM HDR-4 AFTER ADVANCING 1 LINES.
 00625      WRITE PRINT-REC FROM HDR-5 AFTER ADVANCING 1 LINES.
 00626      MOVE +6 TO LNE-CNT.
 00627
 00628  STEP11-WRITE-DISEXMPT-REC.
 00629      MOVE SPACES TO DISEXMPT-REC.
 00630      MOVE ASSR-TOWN TO DSEX-TOWN.
 00631      MOVE ASSR-VOL  TO DSEX-VOL.
 00632      MOVE ASSR-PROP TO DSEX-PROP.
 00633      IF ASSR-RETVET-IND = 'X'
 00634          MOVE ASSR-RETVET-EAV TO DSEX-RETVET-EAV
 00635      ELSE
 00636          MOVE ZEROES TO DSEX-RETVET-EAV
 00637      END-IF.
 00638      IF ASSR-DISPER-IND = 'X'
 00639          MOVE ASSR-DISPER-EAV TO DSEX-DISPER-EAV
 00640      ELSE
 00641          MOVE ZEROES TO DSEX-DISPER-EAV
 00642      END-IF.
 00643      IF ASSR-DISVET-IND-1 = 'X'
 00644          MOVE ASSR-DISVET-EAV-1 TO DSEX-DISVET-EAV-1
 00645      ELSE
 00646          MOVE ZEROES TO DSEX-DISVET-EAV-1
 00647      END-IF.
 00648      IF ASSR-DISVET-IND-2 = 'X'
 00649          MOVE ASSR-DISVET-EAV-2 TO DSEX-DISVET-EAV-2
 00650      ELSE
 00651          MOVE ZEROES TO DSEX-DISVET-EAV-2
 00652      END-IF.
 00653      IF ASSR-DISVET-IND-3 = 'X'
 00654          MOVE ASSR-DISVET-EAV-3 TO DSEX-DISVET-EAV-3
 00655      ELSE
 00656          MOVE ZEROES TO DSEX-DISVET-EAV-3
 00657      END-IF.
 00658      MOVE ASSR-BATCHNO  TO  DSEX-BATCH-NO
 00659      MOVE ASSR-USERID   TO  DSEX-USERID
 00660      MOVE ASSR-ENT-DATE TO  DSEX-ENT-DATE
 00661      IF DSEX-RETVET-EAV > 0 AND
 00662         DSEX-DISVET-EAV-1 > 0
 00663         MOVE 5    TO DSEX-INDICATOR
 00664      ELSE
 00665         IF DSEX-RETVET-EAV > 0 AND
 00666            DSEX-DISVET-EAV-2 > 0
 00667            MOVE 6  TO DSEX-INDICATOR
 00668         ELSE
 00669            IF DSEX-RETVET-EAV > 0 AND
 00670               DSEX-DISPER-EAV > 0
 00671               MOVE 7 TO DSEX-INDICATOR
 00672            ELSE
 00673               IF DSEX-RETVET-EAV > 0 AND
 00674                  DSEX-DISPER-EAV > 0
 00675                  MOVE 8 TO DSEX-INDICATOR
 00676               ELSE
 00677                  IF DSEX-RETVET-EAV > 0
 00678                     MOVE 1  TO DSEX-INDICATOR
 00679                  ELSE
 00680                     IF DSEX-DISPER-EAV > 0
 00681                        MOVE 2 TO DSEX-INDICATOR
 00682                     ELSE
 00683                        IF DSEX-DISVET-EAV-1 > 0
 00684                           MOVE 3 TO DSEX-INDICATOR
 00685                        ELSE
 00686                           IF DSEX-DISVET-EAV-2 > 0
 00687                              MOVE 4 TO DSEX-INDICATOR
 00688                           ELSE
 00689                              IF DSEX-DISVET-EAV-3 > 0
 00690                                 MOVE 8 TO DSEX-INDICATOR.
 00691      WRITE DISEXMPT-REC.
 00692      ADD +1 TO DISEX-CNT.
