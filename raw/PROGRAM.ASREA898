 *          DATA SET ASREA898E  AT LEVEL 007 AS OF 05/13/13  UPDATE SENIOR FREEZE WITH FILE FROM ASSESSOR
 MACRO
 FILE FILEA
 TOWN  1 2 N
 VOL   3 3 N
 PROP  6 14 N
 SENFREZ 20 9 N
 STAT    29 2 A

 FILE FILE2
 TOWN2 14 1 U
 VOL2   2 2 P
 PROP2  4 8 P
 MVAL1 57 5 P
 MVAL2 62 5 P
 MVAL3 67 5 P

 FILE FILEB SEQUENTIAL VBS (18710 6447)

 SFCNTR  W 5 N 0
 HLCNTR  W 5 N 0
 HSCNTR  W 5 N 0
 WSMVAL1 W 9 N 0

 JOB INPUT (FILEA KEY(VOL PROP) +
            FILE2 KEY(VOL2 PROP2)) FINISH(EOJ)

 IF MATCHED
    IF STAT = 'HF'
       SFCNTR = SFCNTR + 1
    ELSE
       IF STAT = 'LF'
          HLCNTR = HLCNTR + 1
       ELSE
          HSCNTR = HSCNTR + 1
       END-IF
    END-IF
    WSMVAL1 = WSMVAL1 + MVAL1
    MVAL1 = SENFREZ
    RECORD-LENGTH(FILEB) = RECORD-LENGTH(FILE2)
    PUT FILEB FROM FILE2
 ELSE
    IF FILE2
       RECORD-LENGTH(FILEB) = RECORD-LENGTH(FILE2)
       PUT FILEB FROM FILE2
    END-IF
 END-IF

 EOJ. PROC
 DISPLAY 'TOTAL SR. FREEZE RECORDS UPDATED = ' SFCNTR
 DISPLAY 'TOTAL LONG TIME  RECORDS UPDATED = ' HLCNTR
 DISPLAY 'TOTAL SUNSET     RECORDS UPDATED = ' HSCNTR
 DISPLAY 'TOTAL SR. FREEZE DOLLARS ZEROED  = ' WSMVAL1
 END-PROC
