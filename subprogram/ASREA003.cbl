00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID.    ASREA003.
00003 *AUTHOR.        GEORGE ZINTAK.
00004 *REMARKS.
00005 ****THIS PROGRAM IS FOR  THE NEW ASSESSMENT SYSTEM ****
00006 *    THIS IS A CALLED PROGRAM THAT WILL CALULATE
00007 *    VALUATION FOR TYPES 1,2,3,4,5 RECORDS FOR THE
00008 *    DETAIL FILE. JUST PASS THE DETAIL SEGMENT TO BE CALULATED.
00009 *04/00 MODIFIED PGM TO USE WINDOWING FOR FOUR POSITION YEAR
00010 *      COMPARISION.
00011 *08/04 MODIFIED PGM TO SUBTRACT 75000 FROM D3-REPROD-COST FOR
00012 *      TAX YEAR >= 2004 PARA:660-CHECK-YEAR79.
00013 *
00014  ENVIRONMENT DIVISION.
00015  INPUT-OUTPUT SECTION.
00016  DATA DIVISION.
00017  FILE SECTION.
00018  WORKING-STORAGE SECTION.
00019  77  WS-VAL            PIC 9(11)    VALUE ZERO PACKED-DECIMAL.
00020  77  D1-DEC            PIC 9               VALUE ZERO.
00021  01  WORK-AREA.
00022      05 WS-R3-YEAR     PIC 9(3)       VALUE ZEROS.
00023      05 WS-R3-YEAR-X REDEFINES WS-R3-YEAR.
00024         10  FILLER     PIC 9(1).
00025         10  WS-R3-YR   PIC 9(2).
00026      05 WK-R3-YEAR     PIC 9(4)       VALUE ZEROS.
00027      05 WK-R3-YEAR-X REDEFINES WK-R3-YEAR.
00028         10  WK-R3-CC   PIC 9(2).
00029         10  WK-R3-YR   PIC 9(2).
00030      05 WS-FF0         PIC 9(7)       VALUE ZERO.
00031      05 WS-FF1 REDEFINES WS-FF0  PIC 9(6)V9.
00032      05 WS-FF2 REDEFINES WS-FF0  PIC 9(5)V99.
00033      05 WS-FF3 REDEFINES WS-FF0  PIC 9(4)V999.
00034      05 WS-FF4 REDEFINES WS-FF0  PIC 999V9(4).
00035      05 WS-FF5 REDEFINES WS-FF0  PIC 99V9(5).
00036      05 WS-P-ASSED               PIC V9(7).
00037      05 WS-P-ASSEDX REDEFINES WS-P-ASSED PIC 99V9(5).
00038      05 WS-COND                  PIC V999.
00039      05 WS-CONDX    REDEFINES WS-COND    PIC 99V9.
00040      05 WS-CONDY REDEFINES WS-COND PIC 999.
00041      05  WS-AR0         PIC 9(7)       VALUE ZERO.
00042      05  WS-AR1 REDEFINES WS-AR0  PIC 9(6)V9.
00043      05  WS-AR2 REDEFINES WS-AR0  PIC 9(5)V99.
00044      05  WS-AR3 REDEFINES WS-AR0  PIC 9(4)V999.
00045      05  WS-AR4 REDEFINES WS-AR0  PIC 9(3)V9(4).
00046      05  WS-AR5 REDEFINES WS-AR0  PIC 99V9(5).
00047      05  WS-UPR         PIC 9(5)V99 VALUE ZERO.
00048  LINKAGE SECTION.
00049  COPY ASDTLQRD01.
00050  PROCEDURE DIVISION USING D1-REC.
00051      MOVE ZEROS TO WS-VAL.
00052      MOVE ZEROS TO D1-DEC.
00053      IF   D1-TYPE = 1
00054           PERFORM 550-TYPE-1 THRU 570-EXIT.
00055      IF   D2-TYPE = 2
00056           PERFORM 600-TYPE-2 THRU 620-EXIT.
00057      IF   D2-TYPE = 3
00058           PERFORM 650-TYPE-3 THRU 670-EXIT.
00059      IF      D1-TYPE = 4 OR 5
00060             PERFORM 700-TYPE-4-5 THRU 720-EXIT.
00061      GO TO 800-STOP.
00062  550-TYPE-1.
00063      IF D1-UNIT-MEASURE = 'EX' OR 'RR'
00064          MOVE ZEROS TO D1-VALUATION
00065          GO TO 570-EXIT.
00066      MOVE    D1-FRONT-FOOTAGE TO WS-FF0.
00067      MOVE    D1-DECIMAL TO D1-DEC.
00068      IF      D1-DEC = 0 MULTIPLY WS-FF0 BY D1-UNIT-PRICE
00069                                            GIVING WS-VAL
00070         ELSE IF D1-DEC = 1 MULTIPLY WS-FF1 BY D1-UNIT-PRICE
00071                                            GIVING WS-VAL
00072           ELSE IF D1-DEC = 2 MULTIPLY WS-FF2 BY D1-UNIT-PRICE
00073                                            GIVING WS-VAL
00074             ELSE IF D1-DEC = 3 MULTIPLY WS-FF3 BY D1-UNIT-PRICE
00075                                            GIVING WS-VAL
00076               ELSE IF D1-DEC = 4 MULTIPLY WS-FF4 BY D1-UNIT-PRICE
00077                                            GIVING WS-VAL
00078             ELSE IF D1-DEC = 5 MULTIPLY WS-FF5 BY D1-UNIT-PRICE
00079                                            GIVING WS-VAL.
00080      IF     D1-DEPTH-FACTOR > 0
00081             MULTIPLY D1-DEPTH-FACTOR BY WS-VAL.
00082      IF      D1-CORNER-FACTOR > 0
00083              MULTIPLY D1-CORNER-FACTOR BY WS-VAL.
00084      IF     D1-PERCENT-ASSESSED > 0
00085             MOVE D1-PERCENT-ASSESSED TO WS-P-ASSEDX
00086             MULTIPLY WS-P-ASSED BY WS-VAL.
00087      IF      D1-EX-CORNER-FACTOR > 0
00088              MULTIPLY D1-EX-CORNER-FACTOR BY WS-VAL.
00089      IF     D1-BUFF-NO > 0
00090             MOVE D1-BUFF-NO TO WS-CONDY
00091             MULTIPLY WS-COND BY WS-VAL.
00092      IF     WS-VAL < 1  MOVE 1 TO WS-VAL.
00093      MOVE   WS-VAL TO D1-VALUATION.
00094  570-EXIT.
00095      EXIT.
00096  600-TYPE-2.
00097      MOVE   D2-CONDITION TO WS-CONDX.
00098      MOVE      D2-DECIMAL TO D1-DEC
00099      MOVE      D2-AREA TO WS-AR0
00100      MOVE      D2-UNIT-PRICE TO WS-UPR
00101      IF D1-DEC = 0
00102          MULTIPLY WS-AR0 BY WS-UPR
00103                    GIVING D2-PRODUCT
00104         ELSE
00105           IF D1-DEC = 1
00106               MULTIPLY WS-AR1 BY WS-UPR
00107                         GIVING D2-PRODUCT
00108              ELSE
00109                IF D1-DEC = 2
00110                    MULTIPLY WS-AR2 BY WS-UPR
00111                              GIVING D2-PRODUCT
00112                   ELSE
00113                     IF D1-DEC = 3
00114                         MULTIPLY WS-AR3 BY WS-UPR
00115                                   GIVING D2-PRODUCT
00116                        ELSE
00117                          IF D1-DEC = 4
00118                              MULTIPLY WS-AR4 BY WS-UPR
00119                                        GIVING D2-PRODUCT
00120                               ELSE
00121                                 IF D1-DEC = 5
00122                                     MULTIPLY WS-AR5 BY WS-UPR
00123                                               GIVING D2-PRODUCT.
00124      MULTIPLY D2-PRODUCT BY WS-COND GIVING WS-VAL.
00125      IF     D2-PERCENT-ASSESSED > 0
00126             MOVE D2-PERCENT-ASSESSED TO WS-P-ASSEDX
00127             MULTIPLY WS-P-ASSED BY WS-VAL.
00128      IF     WS-VAL < 1  MOVE 1 TO WS-VAL.
00129      MOVE   WS-VAL TO D2-VALUATION.
00130  620-EXIT.
00131      EXIT.
00132  650-TYPE-3.
00133      MOVE D3-IMPV-YR TO WS-R3-YEAR.
00134      IF WS-R3-YR > 60
00135         MOVE 19 TO WK-R3-CC
00136      ELSE
00137         MOVE 20 TO WK-R3-CC.
00138      MOVE WS-R3-YR TO WK-R3-YR.
00139
00140      IF WK-R3-YEAR NOT GREATER THAN 1978
00141          NEXT SENTENCE
00142      ELSE
00143          GO TO 660-CHECK-YEAR79.
00144      IF D3-MAJ-MI-CLASS = 288
00145         AND D3-REPROD-COST < 15001
00146          MOVE ZERO TO D3-VALUATION
00147          GO TO 670-EXIT.
00148      IF D3-MAJ-MI-CLASS = 288
00149          SUBTRACT 15000 FROM D3-REPROD-COST GIVING WS-VAL
00150      ELSE
00151          MOVE D3-REPROD-COST TO WS-VAL.
00152      IF D3-CONDITION GREATER THAN 0
00153          MOVE D3-CONDITION TO WS-CONDX
00154          MULTIPLY WS-COND BY WS-VAL.
00155      IF D3-PERCENT-ASSESSED GREATER THAN 0
00156          MOVE D3-PERCENT-ASSESSED TO WS-P-ASSEDX
00157          MULTIPLY WS-P-ASSED BY WS-VAL.
00158      IF WS-VAL < 1 MOVE 1 TO WS-VAL.
00159      MOVE WS-VAL TO D3-VALUATION.
00160      GO TO 670-EXIT.
00161  660-CHECK-YEAR79.
00162      IF D3-MAJ-MI-CLASS = 288
00163         AND WK-R3-YEAR < 1984
00164         AND D3-REPROD-COST < 25001
00165          MOVE ZERO TO D3-VALUATION
00166          GO TO 670-EXIT.
00167      IF D3-MAJ-MI-CLASS = 288
00168         AND (WK-R3-YEAR > 1983 AND <= 1997)
00169         AND D3-REPROD-COST < 30001
00170          MOVE ZERO TO D3-VALUATION
00171          GO TO 670-EXIT.
00172      IF D3-MAJ-MI-CLASS = 288
00173         AND (WK-R3-YEAR > 1997 AND <= 2003)
00174         AND D3-REPROD-COST < 45001
00175          MOVE ZERO TO D3-VALUATION
00176          GO TO 670-EXIT.
00177      IF D3-MAJ-MI-CLASS = 288
00178         AND WK-R3-YEAR > 2003
00179         AND D3-REPROD-COST < 75001
00180          MOVE ZERO TO D3-VALUATION
00181          GO TO 670-EXIT.
00182      IF D3-MAJ-MI-CLASS = 288
00183          AND WK-R3-YEAR < 1984
00184          SUBTRACT 25000 FROM D3-REPROD-COST GIVING WS-VAL
00185      ELSE
00186          IF D3-MAJ-MI-CLASS = 288
00187              AND (WK-R3-YEAR > 1983 AND <= 1997)
00188              SUBTRACT 30000 FROM D3-REPROD-COST GIVING WS-VAL
00189          ELSE
00190             IF D3-MAJ-MI-CLASS = 288
00191                AND (WK-R3-YEAR > 1997 AND <= 2003)
00192                 SUBTRACT 45000 FROM D3-REPROD-COST GIVING WS-VAL
00193             ELSE
00194                IF D3-MAJ-MI-CLASS = 288
00195                   AND WK-R3-YEAR > 2003
00196                   SUBTRACT 75000 FROM D3-REPROD-COST
00197                          GIVING WS-VAL
00198                ELSE
00199                   MOVE D3-REPROD-COST TO WS-VAL.
00200      IF D3-CONDITION GREATER THAN 0
00201          MOVE D3-CONDITION TO WS-CONDX
00202          MULTIPLY WS-COND BY WS-VAL.
00203      IF D3-PERCENT-ASSESSED GREATER THAN 0
00204          MOVE D3-PERCENT-ASSESSED TO WS-P-ASSEDX
00205          MULTIPLY WS-P-ASSED BY WS-VAL.
00206      IF WS-VAL < 1 MOVE 1 TO WS-VAL.
00207      MOVE WS-VAL TO D3-VALUATION.
00208  670-EXIT.
00209      EXIT.
00210  700-TYPE-4-5.
00211      IF      D4-CONDITION > 0
00212              MOVE D4-CONDITION TO WS-CONDX
00213              MULTIPLY WS-COND BY D4-REPROD-COST GIVING WS-VAL.
00214      IF      D4-PERCENT-ASSESSED > 0
00215              MOVE D4-PERCENT-ASSESSED TO WS-P-ASSEDX
00216              MULTIPLY WS-P-ASSED BY WS-VAL.
00217      IF      WS-VAL < 1  MOVE 1 TO WS-VAL.
00218      MOVE    WS-VAL TO D4-VALUATION.
00219  720-EXIT.
00220      EXIT.
00221  800-STOP.
00222      GOBACK.
