00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASREA178.
00003  AUTHOR.  NANCY T. FLEMING
00004  DATE-WRITTEN. MAY 4, 2005.
00005  DATE-COMPILED.
00006 ****************************************************************
00007 *REMARKS. ASREA178 IS A COPY OF ASREA177. CHANGED PROGRAM TO   *
00008 *         ONLY CONVERT TYPE 5 DETAIL SEGMENTS TO TYPE 3 DETAIL *
00009 *         SEGMENTS.  REMOVE ALL LOGIC TO CONVERT TYPE 4        *
00010 *         SEGMENTS TO TYPE 3 SEGMENTS.                         *
00011 ****************************************************************
00012
00013  ENVIRONMENT DIVISION.
00014  CONFIGURATION SECTION.
00015  SOURCE-COMPUTER. IBM-370.
00016  OBJECT-COMPUTER. IBM-370.
00017  INPUT-OUTPUT SECTION.
00018
00019  FILE-CONTROL.
00020      SELECT MASTIN  ASSIGN TO UT-S-MIN.
00021      SELECT MASTOUT ASSIGN TO UT-S-MOUT.
00022      SELECT PRINTREC ASSIGN TO UT-S-PRINT.
00023
00024
00025
00026  DATA DIVISION.
00027  FILE SECTION.
00028
00029  FD  MASTIN
00030      RECORD CONTAINS 122 TO 18706 CHARACTERS
00031      BLOCK CONTAINS 0 CHARACTERS
00032      LABEL RECORDS ARE STANDARD
00033      RECORDING MODE IS S
00034      DATA RECORD IS MIN.
00035  01  MIN.
00036  COPY ASREASRD01.
00037
00038  FD  MASTOUT
00039      RECORD CONTAINS 122 TO 18706 CHARACTERS
00040      BLOCK CONTAINS 0 CHARACTERS
00041      LABEL RECORDS ARE STANDARD
00042      RECORDING MODE IS S
00043      DATA RECORD IS MOUT.
00044  01  MOUT.
00045  COPY ASREASRD02.
00046
00047  FD  PRINTREC
00048      LABEL RECORDS ARE STANDARD
00049      BLOCK CONTAINS 0 RECORDS
00050      RECORD CONTAINS 133 CHARACTERS
00051      RECORDING MODE IS F
00052      DATA RECORD IS PREC.
00053  01  PREC              PIC X(133).
00054
00055
00056
00057  WORKING-STORAGE SECTION.
00058  77  FILLER PIC X(36)
00059        VALUE 'ASREA178 WORKING STORAGE STARTS HERE'.
00060  77  WS-PGMNAME        PIC X(8) VALUE 'ASREA003'.
00061  77  M-READ            PIC S9(9) PACKED-DECIMAL VALUE ZEROES.
00062  77  M-WRITTEN         PIC S9(9) PACKED-DECIMAL VALUE ZEROES.
00063  77  CT-5              PIC S9(5) PACKED-DECIMAL VALUE ZEROES.
00064  77  ZERO-5            PIC S9(5) PACKED-DECIMAL VALUE ZEROES.
00065  77  CONVERT-5         PIC S9(5) PACKED-DECIMAL VALUE ZEROES.
00066  77  SUBT              PIC S999  PACKED-DECIMAL VALUE +1.
00067  77  LINE-CT           PIC S999  PACKED-DECIMAL VALUE +75.
00068  77  PAGE-CT           PIC S9(5) PACKED-DECIMAL VALUE ZEROES.
00069  77  EDIT-VAL-FAC      PIC S9(9)V9 PACKED-DECIMAL VALUE ZEROES.
00070  77  TOTAL-VAL1        PIC S9(9) PACKED-DECIMAL VALUE ZEROES.
00071  77  TOTAL-VAL2-5      PIC S9(9) PACKED-DECIMAL VALUE ZEROES.
00072  77  SUBS              PIC S999  PACKED-DECIMAL VALUE +1.
00073  77  SUB7              PIC S999  PACKED-DECIMAL VALUE +7.
00074  77  SUB8              PIC S999  PACKED-DECIMAL VALUE +8.
00075  77  SUB9              PIC S999  PACKED-DECIMAL VALUE +9.
00076  77  SUB12             PIC S999  PACKED-DECIMAL VALUE +12.
00077  77  PACKED-ZEROES     PIC S9(15) PACKED-DECIMAL
00078                        VALUE +000000000000000.
00079  77  TYPE-5-FOUND-SW   PIC X               VALUE 'N'.
00080      88 TYPE-5-FOUND                       VALUE 'Y'.
00081  77  QUEST-SW          PIC X               VALUE 'N'.
00082      88 QUEST                              VALUE 'Y'.
00083  77  EOF-MIN-SW        PIC X               VALUE 'N'.
00084      88 EOF-MIN                            VALUE 'Y'.
00085  77  CURRENT-DATE      PIC 99/99/9(4).
00086  77  CURRENT-TIME      PIC 99,99,99.
00087
00088  01  WORK-DET          PIC X(53)           VALUE SPACES.
00089  01  CONTROL-SECTION.
00090      05 EDIT-TYP       PIC 9.
00091         88 TYPE1            VALUE 1.
00092         88 TYPE2            VALUE 2.
00093         88 TYPE2-5          VALUE 2 THRU 5.
00094         88 TYPE4            VALUE 4.
00095         88 TYPE5            VALUE 5.
00096         88 TYPE-5           VALUE 5.
00097      05 EDIT-CLS       PIC 999.
00098      05 FILLER REDEFINES EDIT-CLS.
00099         10 MAJ-CLS     PIC 9.
00100         10 MIN-CLS     PIC 99.
00101            88 RES      VALUES ARE 02 THRU 12 34 78 95.
00102      05 UNPK-TXCD      PIC 9(5).
00103      05 FILLER REDEFINES UNPK-TXCD.
00104         10 M-TOWN      PIC 99.
00105         10 FILLER      PIC XXX.
00106
00107      05 WORK-3.
00108         10 W3-MC       PIC S999  PACKED-DECIMAL.
00109         10 W3-TYP      PIC X.
00110         10 W3-CD       PIC X.
00111         10 W3-5-7      PIC XXX.
00112         10 W3-CLS      PIC S999  PACKED-DECIMAL.
00113         10 W3-CDU      PIC XX.
00114         10 W3-REPCST   PIC S9(9) PACKED-DECIMAL.
00115         10 W3-17-22    PIC X(6).
00116         10 W3-YR       PIC S999  PACKED-DECIMAL.
00117         10 W3-AGE      PIC S999  PACKED-DECIMAL.
00118         10 W3-COND     PIC S99V9 PACKED-DECIMAL.
00119         10 W3-PCASSD   PIC S99V9(5)  PACKED-DECIMAL.
00120         10 W3-BUFF     PIC S999  PACKED-DECIMAL.
00121         10 W3-35-36    PIC XX.
00122         10 W3-VAL      PIC S9(9) PACKED-DECIMAL.
00123         10 W3-KEYPCL   PIC S9(15) PACKED-DECIMAL.
00124         10 W3-SC       PIC X.
00125         10 W3-51-53    PIC XXX.
00126      05  MDCY-OR-CYMD  PIC S9(5)V9(4)   VALUE +10000.0001.
00127      05  CURRENT-DATE-RETURN            VALUE SPACES.
00128          10  SYSTEM-DATE            PIC 9(8).
00129          10  SYSTEM-TIME            PIC 9(6).
00130          10  FILLER                 PIC X(7).
00131
00132  01  REPORT-SECTION.
00133      05 H1.
00134         10 FILLER      PIC XXX   VALUE SPACES.
00135         10 H1-DATE     PIC X(10) VALUE SPACES.
00136         10 FILLER      PIC X(42) VALUE SPACES.
00137         10 FILLER      PIC X(22) VALUE
00138      'OFFICE OF THE ASSESSOR'.
00139         10 FILLER      PIC X(44) VALUE SPACES.
00140         10 FILLER      PIC X(5)  VALUE 'PAGE '.
00141         10 H1-PAGE     PIC Z,ZZ9 VALUE ZEROES.
00142      05 H2.
00143         10 FILLER      PIC X(11) VALUE '   ASREA178'.
00144         10 FILLER      PIC X(10) VALUE SPACES.
00145         10 FILLER      PIC X(13) VALUE 'PROCESS YEAR '.
00146         10 H2-CENT     PIC 99.
00147         10 H2-YEAR     PIC 99.
00148         10 FILLER      PIC X(29) VALUE SPACES.
00149         10 FILLER      PIC X(47) VALUE
00150      'TYPE 5 DETAIL CONVERSION ERROR REPORT    '.
00151         10 H2-NAME     PIC X(13) VALUE SPACES.
00152      05 H3.
00153         10 FILLER      PIC X(56) VALUE
00154      ' VOL    PROPERTY NUMBER  MULT T C     FULL     OCCP  CDU'.
00155         10 FILLER      PIC X(58) VALUE
00156      '   REPRODCTV   CLASS  AGE  LVL    PRORATN  BUFF   ASSESSED'.
00157      05 H4.
00158         10 FILLER      PIC X(25) VALUE SPACES.
00159         10 FILLER      PIC X(51) VALUE
00160      'CODE       VALUATION  FACT          COST      MA-MI'.
00161         10 FILLER      PIC X(38) VALUE
00162      '       ASS     FACTOR   NO   VALUATION'.
00163
00164      05 PRINT-LINE.
00165         10 FILLER      PIC X.
00166         10 PL-VOL      PIC 999.
00167         10 FILLER      PIC X.
00168         10 PL-PROP     PIC Z99,99,999,999,9999.
00169         10 FILLER      PIC XX.
00170         10 PL-MC       PIC 999.
00171         10 FILLER      PIC X.
00172         10 PL-TYP      PIC 9.
00173         10 FILLER      PIC X.
00174         10 PL-CD       PIC 9.
00175         10 FILLER      PIC XXX.
00176         10 PL-TOTVAL   PIC Z(9).
00177         10 FILLER      PIC XX.
00178         10 PL-OCCFAC   PIC ZZ.Z.
00179         10 FILLER      PIC XXX.
00180         10 PL-CDU      PIC XX.
00181         10 FILLER      PIC XXX.
00182         10 PL-REPCST   PIC Z(9).
00183         10 FILLER      PIC X(4).
00184         10 PL-CLS      PIC 9,99.
00185         10 FILLER      PIC XX.
00186         10 PL-AGE      PIC 999.
00187         10 FILLER      PIC XX.
00188         10 PL-COND     PIC 999.
00189         10 FILLER      PIC XXX.
00190         10 PL-PCASSD   PIC ZZ.Z(5).
00191         10 FILLER      PIC XX.
00192         10 PL-BUFF     PIC ZZZ.
00193         10 FILLER      PIC XXX.
00194         10 PL-VAL      PIC Z(9).
00195
00196  01  TABLE-SECTION.
00197  COPY TWNINFOTBL.
00198 **************  ASREA178 WORKING STORAGE ENDS HERE ***************
00199  EJECT
00200  LINKAGE SECTION.
00201  01  INPUT-PARMS.
00202      05  PARM-LENGTH         PIC S9(4)       BINARY.
00203          88  VALID-LENGTH                    VALUE +2.
00204      05  PARM-YEAR           PIC 99.
00205
00206
00207  PROCEDURE DIVISION USING INPUT-PARMS.
00208      IF  VALID-LENGTH
00209          IF  PARM-YEAR NUMERIC
00210              NEXT SENTENCE
00211          ELSE
00212              DISPLAY 'PROCESS YEAR INVALID ' PARM-YEAR
00213              MOVE 16  TO   RETURN-CODE
00214          END-IF
00215      ELSE
00216          MOVE 16  TO   RETURN-CODE
00217          DISPLAY 'INVALID PARM LENGTH '  PARM-LENGTH
00218      END-IF.
00219      IF RETURN-CODE EQUAL 16
00220         STOP RUN.
00221      OPEN INPUT MASTIN.
00222      OPEN OUTPUT MASTOUT.
00223      OPEN OUTPUT PRINTREC.
00224      MOVE FUNCTION CURRENT-DATE TO CURRENT-DATE-RETURN.
00225      COMPUTE CURRENT-DATE = SYSTEM-DATE * MDCY-OR-CYMD.
00226      MOVE CURRENT-DATE TO H1-DATE.
00227      MOVE PARM-YEAR   TO H2-YEAR.
00228      IF PARM-YEAR > 60
00229         MOVE 19 TO H2-CENT
00230      ELSE
00231         MOVE 20 TO H2-CENT.
00232
00233  READ-MIN.
00234      READ MASTIN AT END MOVE 'Y' TO EOF-MIN-SW
00235                         GO TO RM-EXIT.
00236      ADD +1 TO M-READ.
00237  RM-EXIT. EXIT.
00238
00239  TOWN-LOOKUP.
00240      MOVE M-TXCD OF MIN TO UNPK-TXCD
00241      IF SUBT > TOWNTABLE-ENTRIES
00242         DISPLAY '*INPUT MASTER TXCD INVALID, JOB ENDS*'
00243                  M-TXCD OF MIN SPACE M-TOWN
00244         MOVE 16 TO RETURN-CODE
00245         GO TO STOP-RUN.
00246      IF TOWN-NO (SUBT) = M-TOWN
00247         MOVE TOWN-NAME (SUBT) TO H2-NAME
00248      ELSE
00249         ADD +1 TO SUBT
00250         GO TO TOWN-LOOKUP.
00251  TL-EXIT. EXIT.
00252
00253  MAINLINE.
00254      IF EOF-MIN GO TO END-OF-JOB.
00255
00256      MOVE 'N' TO TYPE-5-FOUND-SW
00257      PERFORM CHECK-SEGMENTS THRU CS-EXIT
00258              VARYING SUBS FROM 1 BY 1
00259              UNTIL SUBS > M-DTL-QST-CTR
00260
00261      IF TYPE-5-FOUND
00262         PERFORM RECOMPUTE-ROUTINE THRU RR-EXIT.
00263
00264      PERFORM WRITE-MOUT THRU WM-EXIT
00265      PERFORM READ-MIN THRU RM-EXIT
00266      GO TO MAINLINE.
00267
00268  CHECK-SEGMENTS.
00269      IF QUEST
00270         MOVE 'N' TO QUEST-SW
00271         GO TO CS-EXIT.
00272      MOVE D1-TYP OF MIN (SUBS) TO EDIT-TYP
00273      MOVE D1-CLS OF MIN (SUBS) TO EDIT-CLS
00274      IF TYPE2-5 AND RES
00275         MOVE 'Y' TO QUEST-SW.
00276
00277      IF TYPE2
00278         IF D2-CDU OF MIN (SUBS)
00279            EQUAL 'YR'
00280            MOVE SPACES TO D2-CDU OF MIN (SUBS).
00281      IF TYPE-5
00282         NEXT SENTENCE
00283      ELSE
00284         GO TO CS-EXIT.
00285
00286      IF TYPE5
00287         ADD +1 TO CT-5
00288         MOVE D5-OCCFAC OF MIN (SUBS) TO EDIT-VAL-FAC.
00289      IF EDIT-VAL-FAC > 0 NEXT SENTENCE
00290      ELSE
00291          PERFORM PRINT-0-TYPE-5 THRU P0T5-EXIT.
00292
00293      MOVE 'Y' TO TYPE-5-FOUND-SW
00294      PERFORM CONVERT-TYPE-5-TO-3 THRU CT5T3-EXIT.
00295  CS-EXIT. EXIT.
00296
00297  PRINT-0-TYPE-5.
00298      IF LINE-CT > +50
00299         MOVE ZEROES TO LINE-CT
00300         ADD +1 TO PAGE-CT
00301         MOVE PAGE-CT TO H1-PAGE
00302         WRITE PREC FROM H1 AFTER ADVANCING PAGE
00303         WRITE PREC FROM H2 AFTER ADVANCING 2 LINES
00304         WRITE PREC FROM H3 AFTER ADVANCING 2 LINES
00305         WRITE PREC FROM H4 AFTER ADVANCING 1 LINES.
00306
00307      MOVE M-VOL OF MIN TO PL-VOL
00308      MOVE M-PROP OF MIN TO PL-PROP
00309      INSPECT PL-PROP REPLACING ALL ',' BY '-'
00310      IF NOT TYPE4
00311         ADD +1 TO ZERO-5
00312         MOVE ZEROES                   TO PL-TOTVAL.
00313      WRITE PREC FROM PRINT-LINE AFTER ADVANCING 2 LINES
00314      ADD +2 TO LINE-CT.
00315  P0T5-EXIT. EXIT.
00316
00317  CONVERT-TYPE-5-TO-3.
00318      MOVE SPACES TO WORK-3
00319      IF TYPE5 AND
00320         EDIT-VAL-FAC > 0
00321         COMPUTE D5-REPCST OF MIN (SUBS) =
00322         (D5-REPCST OF MIN (SUBS) * 100 / D5-OCCFAC OF MIN (SUBS)).
00323      IF TYPE5
00324         ADD +1 TO CONVERT-5
00325         MOVE D5-MC     OF MIN (SUBS)  TO W3-MC
00326         MOVE '3'                      TO W3-TYP
00327         MOVE '2'                      TO W3-CD
00328         MOVE SPACES                   TO W3-5-7
00329         MOVE D5-CLS    OF MIN (SUBS)  TO W3-CLS
00330         MOVE D5-REPCST OF MIN (SUBS)  TO W3-REPCST
00331         MOVE SPACES                   TO W3-17-22
00332         MOVE PACKED-ZEROES            TO W3-YR
00333         MOVE D5-AGE    OF MIN (SUBS)  TO W3-AGE
00334         MOVE D5-COND   OF MIN (SUBS)  TO W3-COND
00335         MOVE D5-PCASSD OF MIN (SUBS)  TO W3-PCASSD
00336         MOVE D5-BUFF   OF MIN (SUBS)  TO W3-BUFF
00337         MOVE SPACES                   TO W3-35-36
00338         MOVE D5-VAL    OF MIN (SUBS)  TO W3-VAL
00339         MOVE D5-KEYPCL OF MIN (SUBS)  TO W3-KEYPCL
00340         MOVE D5-SC     OF MIN (SUBS)  TO W3-SC
00341         MOVE SPACES                   TO W3-51-53
00342      IF TYPE5
00343         IF D5-CDU OF MIN (SUBS) EQUAL 'YR'
00344            MOVE SPACES TO W3-CDU
00345         ELSE
00346            MOVE D5-CDU OF MIN (SUBS) TO W3-CDU
00347         END-IF
00348      END-IF.
00349      IF TYPE5
00350         MOVE WORK-3 TO D-TYP5 OF MIN (SUBS).
00351  CT5T3-EXIT. EXIT.
00352
00353  RECOMPUTE-ROUTINE.
00354      MOVE ZEROES TO TOTAL-VAL1
00355                     TOTAL-VAL2-5
00356
00357      PERFORM COMPUTE-VAL THRU CV-EXIT
00358              VARYING SUBS FROM 1 BY 1
00359              UNTIL SUBS > M-DTL-QST-CTR
00360
00361      MOVE M-VALUE (SUB9) TO M-VALUE (SUB12)
00362      MOVE TOTAL-VAL1     TO M-VALUE (SUB7)
00363      MOVE TOTAL-VAL2-5   TO M-VALUE (SUB8)
00364      COMPUTE M-VALUE (SUB9) = M-VALUE (SUB7) + M-VALUE (SUB8).
00365  RR-EXIT. EXIT.
00366
00367  COMPUTE-VAL.
00368      MOVE D-TYP1 OF MIN (SUBS) TO WORK-DET
00369      CALL WS-PGMNAME USING WORK-DET
00370      MOVE WORK-DET TO D-TYP1 OF MIN (SUBS)
00371
00372      IF D1-TYP OF MIN (SUBS) = 1
00373         ADD D1-VAL OF MIN (SUBS) TO TOTAL-VAL1
00374      ELSE
00375         ADD D1-VAL OF MIN (SUBS) TO TOTAL-VAL2-5.
00376
00377      MOVE D1-CLS OF MIN (SUBS) TO EDIT-CLS
00378      IF RES
00379         ADD +1 TO SUBS.
00380  CV-EXIT. EXIT.
00381
00382  WRITE-MOUT.
00383      WRITE MOUT FROM MIN
00384      ADD +1 TO M-WRITTEN.
00385  WM-EXIT. EXIT.
00386
00387  END-OF-JOB.
00388      DISPLAY '*************************'
00389      DISPLAY '      ASREA178 EOJ       '
00390      DISPLAY '   MASTERS               '
00391      DISPLAY '               READ      ' M-READ
00392      DISPLAY '               WRITTEN   ' M-WRITTEN
00393      DISPLAY '                         '
00394      DISPLAY '   TYP-5                 '
00395      DISPLAY '               HITS 5        ' CT-5
00396      DISPLAY '               ZERO 5        ' ZERO-5
00397      DISPLAY '               CONVERTED 5   ' CONVERT-5
00398      DISPLAY '                         '
00399      DISPLAY '*************************'.
00400
00401  STOP-RUN.
00402      CLOSE MASTIN
00403            MASTOUT
00404            PRINTREC
00405      STOP RUN.
