00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. ASREA853.
00003 *AUTHOR. LINDSAY - MOLIS.
00004 *DATE-WRITTEN.  JAN 20,1982.
00005 *REMARKS. THIS PROGRAM WILL CREATE THE NEW YEAR
00006 *         HOMEOWNER'S FILE.
00007 *MODIFICATION ON 2-19-88. SQUILLACE.
00008 *         LOGIC ADDED TO COMPUTE 'OCCUPANCY FACTOR'.
00009      SKIP2
00010  ENVIRONMENT DIVISION.
00011  CONFIGURATION SECTION.
00012  SOURCE-COMPUTER. IBM-370.
00013  OBJECT-COMPUTER. IBM-370.
00014  INPUT-OUTPUT SECTION.
00015  FILE-CONTROL.
00016      SELECT  MASTER-IN
00017               ASSIGN  TO UT-S-MASTER.
00018      SELECT  HOMEOWNER-MAST
00019               ASSIGN TO UT-S-HOMEIN.
00020      SELECT  HOMEOWNER-MAST2
00021               ASSIGN TO UT-S-HOMEOUT.
00022      SELECT  PRNT-FILE
00023               ASSIGN  TO UT-S-PRNTOUT.
00024      SKIP2
00025  DATA DIVISION.
00026  FILE SECTION.
00027  COPY ASREASFD01.
00028      SKIP2
00029  COPY HOMOWNFD01.
00030      SKIP2
00031  COPY HOMOWNFD02.
00032      SKIP2
00033  FD  PRNT-FILE
00034      BLOCK CONTAINS 0 RECORDS
00035      RECORD CONTAINS 133 CHARACTERS
00036      LABEL RECORDS ARE STANDARD
00037      RECORDING MODE IS F
00038      DATA RECORD IS PRNT-REC.
00039  01  PRNT-REC               PIC X(133).
00040      EJECT
00041  WORKING-STORAGE SECTION.
00042  77  FILLER            PIC X(58)  VALUE
00043      '*************  WORKING STORAGE STARTS HERE  **************'.
00044      SKIP1
00045  77  WS-PROG           PIC X(8)  VALUE 'ASREA003'.
00046  77  PAGE-CNTR         PIC S9(5) VALUE +0    PACKED-DECIMAL.
00047  77  LINE-CNTR         PIC S9(3) VALUE +54   PACKED-DECIMAL.
00048      88  PRINT-HEADER            VALUE +54 THRU +59.
00049  77  MSTR-READ         PIC S9(7) VALUE +0    PACKED-DECIMAL.
00050  77  HOME-READ         PIC S9(7) VALUE +0    PACKED-DECIMAL.
00051  77  HOME-WRITTEN      PIC S9(7) VALUE +0    PACKED-DECIMAL.
00052  77  SUB               PIC S9(4) VALUE +0    BINARY.
00053  77  WS-TOTL-PRORATE   PIC 99V9(5) VALUE 0   PACKED-DECIMAL.
00054  77  WS-TOTL-PRORATER REDEFINES WS-TOTL-PRORATE
00055                        PIC V9(7)             PACKED-DECIMAL.
00056  77  WS-PRORATE        PIC S9(4)V9(5) VALUE +0 PACKED-DECIMAL.
00057  77  PRORATE-CNTR      PIC S9(3) VALUE +0    PACKED-DECIMAL.
00058      88  PRORATE-CNTR-ZERO       VALUE +0.
00059      88  PRORATE-CNTR-ONE        VALUE +1.
00060  77  SET-HOME-SW       PIC X     VALUE 'N'.
00061      88  READ-NEXT-HOMEOWNER     VALUE 'N'.
00062  77  SET-MSTR-SW       PIC X     VALUE 'N'.
00063      88  READ-NEXT-MSTR          VALUE 'N'.
00064      88  VALID-REC               VALUE 'Y'.
00065  77  CHK-CLS-299       PIC X     VALUE 'N'.
00066      88 CLS-299-PRESENT          VALUE 'Y'.
00067  77  CHK-HOME-CLS      PIC X     VALUE 'N'.
00068      88  HOME-SEG-PRESENT        VALUE 'Y'.
00069  77  CHK-VALU-CLS      PIC X     VALUE 'N'.
00070      88  VALU-SEG-PRESENT        VALUE 'Y'.
00071  77  WS-TYPE2-3-SW     PIC X     VALUE 'N'.
00072      88  WS-TYPE2-3-ON           VALUE 'Y'.
00073  77  WS-TYPE5-SW       PIC X     VALUE 'N'.
00074      88  WS-TYPE5-ON             VALUE 'Y'.
00075      SKIP2
00076  01  WORK-AREA.
00077      05  WS-DATE.
00078          10  YY                      PIC 9(2)    VALUE ZEROS.
00079          10  MM                      PIC 9(2)    VALUE ZEROS.
00080          10  DD                      PIC 9(2)    VALUE ZEROS.
00081      05  WS-DATE-MMDDYY.
00082          10  MM                      PIC 9(2)    VALUE ZEROS.
00083          10  FILLER                  PIC X       VALUE '/'.
00084          10  DD                      PIC 9(2)    VALUE ZEROS.
00085          10  FILLER                  PIC X       VALUE '/'.
00086          10  YY                      PIC 9(2)    VALUE ZEROS.
00087      SKIP2
00088      05  ADD-MESG      PIC X(12) VALUE 'PARCEL ADDED'.
00089      05  BYPASS-MESG   PIC X(25) VALUE
00090                                      'PARCEL IS NON-RESIDENTIAL'.
00091      05  DELETE-MESG   PIC X(23) VALUE 'PARCEL NO LONGER EXISTS'.
00092      SKIP1
00093      05  HOME-KEY                VALUE LOW-VALUES.
00094          88  HOME-EOF            VALUE HIGH-VALUES.
00095          10  HK-VOL         PIC 9(3).
00096          10  HK-PROP        PIC 9(15).
00097      SKIP1
00098      05  MSTR-KEY                VALUE LOW-VALUES.
00099          88  MSTR-EOF            VALUE HIGH-VALUES.
00100          10  MK-VOL         PIC 9(3).
00101          10  MK-PROP        PIC 9(15).
00102      EJECT
00103      05  WS-OCCFAC          PIC 9999V9    VALUE 0.
00104      SKIP1
00105      05  WS-CLS             PIC 9(3).
00106          88 WS-HOMEOWNER-CLS     VALUE 100 THRU 899.
00107          88 WS-VALUATION-CLS     VALUE 999.
00108          88 WS-299-CLS           VALUE 299 399 489 499
00109                                        589 599
00110                                        679 689 789 799 889 899
00111                                        959.
00112      SKIP1
00113      05  HDR-LINE1.
00114          10  FILLER    PIC X(3)  VALUE SPACES.
00115          10  H1-DATE   PIC X(8).
00116          10  FILLER    PIC X(44) VALUE SPACES.
00117          10  FILLER    PIC X(19) VALUE 'OFFICE OF THE COOK'.
00118          10  FILLER    PIC X(46) VALUE 'COUNTY ASSESSOR'.
00119          10  FILLER    PIC X(5)  VALUE 'PAGE '.
00120          10  H1-PAGE   PIC ZZ,ZZ9.
00121      SKIP1
00122      05  HDR-LINE2.
00123          10  FILLER    PIC X(53) VALUE '   ASREA853'.
00124          10  FILLER    PIC X(24) VALUE 'HOMEOWNER FILE CREATION '.
00125          10  FILLER    PIC X(12) VALUE 'ERROR REPORT'.
00126      SKIP1
00127      05  HDR-LINE3.
00128          10  FILLER    PIC X(31) VALUE '   VOL   PROPERTY NUMBER'.
00129          10  FILLER    PIC X(19) VALUE 'TAX    HOMEOWNER'.
00130          10  FILLER    PIC X(6)  VALUE 'MASTER'.
00131      SKIP1
00132      05  HDR-LINE4.
00133          10  FILLER    PIC X(30) VALUE SPACES.
00134          10  FILLER    PIC X(10) VALUE 'CODE'.
00135          10  FILLER    PIC X(11) VALUE 'CLASS'.
00136          10  FILLER    PIC X(5)  VALUE 'CLASS'.
00137      SKIP1
00138      05  DETAIL-LINE             VALUE SPACES.
00139          10  FILLER    PIC X(3).
00140          10  D-VOL     PIC 999.
00141          10  FILLER    PIC XX.
00142          10  D-PROP    PIC 99B99B999B999B9999.
00143          10  FILLER    PIC X(3).
00144          10  D-TXCD    PIC 9(5).
00145          10  FILLER    PIC X(7).
00146          10  D-H-CLS   PIC 9B99.
00147          10  FILLER    PIC X(7).
00148          10  D-M-CLS   PIC 9B99.
00149          10  FILLER    PIC X(5).
00150          10  D-MESG    PIC X(50).
00151      SKIP1
00152      05  FINAL-LINE.
00153          10  FILLER    PIC X(9)  VALUE '   TOTAL '.
00154          10  FIN-MSG   PIC X(26).
00155          10  FIN-TOT   PIC Z,ZZZ,ZZZ,ZZ9.
00156      SKIP1
00157  01  FILLER            PIC X(58)  VALUE
00158      '*************  WORKING STORAGE   ENDS HERE  **************'.
00159      EJECT
00160  LINKAGE SECTION.
00161  01  LINK-INFO.
00162      05  LI-LENGTH      PIC S9(4)  BINARY.
00163          88  LI-VALID-LENGTH       VALUE +6.
00164      05  LI-NEWYEAR     PIC X.
00165          88  LI-VALID-NEWYR        VALUE 'N' 'Y'.
00166          88  LI-NEWYEAR-YES        VALUE 'Y'.
00167      05  LI-EQFCTR      PIC 9V9(4).
00168      SKIP2
00169  PROCEDURE DIVISION USING LINK-INFO.
00170  010-CHECK-LINKAGE.
00171      IF  NOT LI-VALID-LENGTH
00172           DISPLAY  'LINKAGE LENGTH INVALID'
00173           MOVE  16 TO RETURN-CODE
00174           STOP RUN
00175      ELSE
00176           IF  NOT LI-VALID-NEWYR
00177                DISPLAY  'NEW YR PARM INVALID = ' LI-NEWYEAR
00178                MOVE  16 TO RETURN-CODE
00179           ELSE
00180               IF  LI-EQFCTR = ZERO
00181                   DISPLAY  'INVALID EQ FACTOR = ' LI-EQFCTR
00182                   MOVE  16 TO RETURN-CODE
00183                   STOP RUN.
00184      SKIP2
00185  020-BEGIN.
00186      OPEN  INPUT  MASTER-IN
00187            INPUT  HOMEOWNER-MAST
00188      OPEN  OUTPUT HOMEOWNER-MAST2
00189            OUTPUT PRNT-FILE
00190      ACCEPT WS-DATE FROM DATE
00191      MOVE CORR WS-DATE TO WS-DATE-MMDDYY
00192      MOVE WS-DATE-MMDDYY TO H1-DATE
00193      SKIP1
00194      PERFORM  700-READ-HOMEOWNER
00195      PERFORM  750-READ-MASTER
00196                UNTIL  MSTR-EOF
00197                     OR
00198                       VALID-REC
00199      SKIP1
00200      PERFORM  100-MAINLINE
00201                UNTIL  (MSTR-EOF AND  HOME-EOF)
00202      SKIP1
00203      PERFORM 850-PRINT-TOTALS
00204      SKIP1
00205      CLOSE   MASTER-IN
00206              HOMEOWNER-MAST
00207              HOMEOWNER-MAST2
00208              PRNT-FILE
00209      STOP RUN.
00210      EJECT
00211  100-MAINLINE.
00212      IF  MSTR-KEY GREATER HOME-KEY
00213           MOVE  'N' TO SET-HOME-SW
00214           PERFORM  200-MSTR-HIGH-ROUTINE
00215      ELSE
00216           MOVE  'N' TO SET-MSTR-SW
00217           IF  NOT M-EXEMPT
00218             AND
00219               NOT M-RAILROAD
00220                MOVE  'N' TO CHK-CLS-299 CHK-VALU-CLS CHK-HOME-CLS
00221                             WS-TYPE2-3-SW WS-TYPE5-SW
00222                MOVE  ZERO TO PRORATE-CNTR WS-PRORATE
00223                                           WS-OCCFAC
00224                PERFORM 300-SEARCH-SEGMENTS
00225                 VARYING SUB FROM 1 BY 1
00226                  UNTIL  SUB GREATER THAN M-DTL-QST-CTR
00227 **********************OR
00228 ************************(HOME-SEG-PRESENT AND CLS-299-PRESENT)
00229                IF  HOME-SEG-PRESENT
00230                  OR
00231                    VALU-SEG-PRESENT
00232                     IF  MSTR-KEY EQUAL HOME-KEY
00233                          MOVE 'N' TO SET-HOME-SW
00234                          PERFORM  400-MATCH-ROUTINE
00235                     ELSE
00236                          PERFORM  500-MSTR-LOW-ROUTINE
00237                ELSE
00238                     PERFORM  600-BYPASS-MSTR-ROUTINE
00239           ELSE
00240                PERFORM  600-BYPASS-MSTR-ROUTINE.
00241      SKIP1
00242      IF  READ-NEXT-HOMEOWNER
00243           PERFORM  700-READ-HOMEOWNER.
00244      SKIP1
00245      PERFORM  750-READ-MASTER
00246       UNTIL  MSTR-EOF
00247            OR
00248              VALID-REC.
00249      SKIP3
00250  200-MSTR-HIGH-ROUTINE.
00251      MOVE  HO-VOL TO D-VOL
00252      MOVE  HO-PROP TO D-PROP
00253      MOVE  HO-CLASS TO D-H-CLS
00254      MOVE  HO-TXCD TO D-TXCD
00255      MOVE  DELETE-MESG TO D-MESG
00256      PERFORM  800-PRINT-DETAIL.
00257      EJECT
00258  300-SEARCH-SEGMENTS.
00259      IF  D2-TYPE2-5 (SUB)
00260           MOVE  D1-CLS (SUB) TO  WS-CLS
00261           IF  WS-VALUATION-CLS
00262               IF  D4-TYPE4 (SUB)
00263                    PERFORM  350-RECOMPUTE-TYPE4
00264               ELSE
00265                    IF  D5-TYPE5 (SUB)
00266                         PERFORM  370-RECOMPUTE-TYPE5.
00267      SKIP1
00268      IF  D2-TYPE2-5 (SUB)
00269          IF  WS-HOMEOWNER-CLS
00270               MOVE  'Y' TO CHK-HOME-CLS
00271               IF  D2-PCASSD (SUB) GREATER THAN ZERO
00272                    ADD  +1 TO PRORATE-CNTR
00273                    ADD  D2-PCASSD (SUB) TO WS-PRORATE
00274               ELSE
00275                    NEXT SENTENCE
00276          ELSE
00277               IF  WS-VALUATION-CLS
00278                 AND
00279                   D2-VAL (SUB) GREATER THAN ZERO
00280                    MOVE  'Y' TO CHK-VALU-CLS
00281                    IF  D2-PCASSD (SUB) GREATER THAN ZERO
00282                         ADD  +1 TO PRORATE-CNTR
00283                         ADD  D2-PCASSD (SUB) TO WS-PRORATE.
00284      SKIP1
00285      IF  D2-TYPE2-5 (SUB)
00286        AND
00287          WS-299-CLS
00288           MOVE 'Y' TO CHK-CLS-299.
00289      SKIP1
00290      IF (D2-TYPE2 (SUB) OR D3-TYPE3 (SUB))
00291           MOVE 'Y' TO WS-TYPE2-3-SW
00292      ELSE
00293        IF D5-TYPE5 (SUB)
00294 *         IF D5-AGE (SUB) = +001
00295              MOVE 'Y' TO WS-TYPE5-SW.
00296      SKIP1
00297      IF D4-TYPE4 (SUB)
00298         IF D4-OCCFAC (SUB) GREATER THAN WS-OCCFAC
00299            MOVE D4-OCCFAC (SUB) TO WS-OCCFAC.
00300      SKIP1
00301      IF D5-TYPE5 (SUB)
00302         IF D5-OCCFAC (SUB) GREATER THAN WS-OCCFAC
00303            MOVE D5-OCCFAC (SUB) TO WS-OCCFAC.
00304      SKIP1
00305      IF  D2-TYPE2-5 (SUB)
00306        AND
00307          D2-QUES-CLASS (SUB)
00308           ADD +1 TO SUB.
00309      SKIP3
00310  350-RECOMPUTE-TYPE4.
00311      MOVE  D4-TOTVAL (SUB) TO D4-REPCST (SUB)
00312      CALL  WS-PROG    USING M-DTL-QST (SUB).
00313      SKIP3
00314  370-RECOMPUTE-TYPE5.
00315      IF  D5-OCCFAC (SUB) NOT EQUAL ZERO
00316           COMPUTE  D5-REPCST (SUB) =
00317            (D5-REPCST (SUB) * 100) / D5-OCCFAC (SUB)
00318           CALL  WS-PROG    USING M-DTL-QST (SUB).
00319      EJECT
00320  400-MATCH-ROUTINE.
00321      MOVE  HO-REC  TO  HO-REC2
00322      IF  LI-NEWYEAR-YES
00323           MOVE  ZERO TO HO-STAT1-2 HO-STAT2-2 HO-H-V2.
00324      SKIP1
00325 *    PERFORM  550-COMPUTE-PRORATE
00326      MOVE  M-CLS TO  HO-CLASS2
00327      MOVE  M-TXCD TO  HO-TXCD2
00328      IF WS-TYPE2-3-ON
00329         MOVE 0100.0 TO HO-OCCFAC2
00330      ELSE
00331         IF WS-TYPE5-ON
00332            MOVE WS-OCCFAC TO HO-OCCFAC2
00333         ELSE
00334            MOVE 0100.0 TO HO-OCCFAC2.
00335      PERFORM 900-WRITE-HOMEOWNER.
00336      SKIP3
00337  500-MSTR-LOW-ROUTINE.
00338      MOVE  SPACES TO HO-REC2
00339      MOVE  M-VOL TO  HO-VOL2
00340      MOVE  M-PROP TO  HO-PROP2
00341      MOVE  M-CLS TO  HO-CLASS2
00342      MOVE  M-TXCD TO  HO-TXCD2
00343      MOVE  ZERO TO HO-STAT1-2 HO-STAT2-2 HO-YRAPPL2
00344            HO-H-V2 HO-ASSDVAL2 HO-EQVAL2 HO-ZIP2
00345            HO-NPHE2 HO-NPHE-BSYR2
00346      MOVE  '00001' TO HO-COOPQTY2
00347      MOVE  LI-EQFCTR TO HO-EQFCTR2
00348      IF WS-TYPE2-3-ON
00349         MOVE 0100.0 TO HO-OCCFAC2
00350      ELSE
00351         IF WS-TYPE5-ON
00352            MOVE WS-OCCFAC TO HO-OCCFAC2
00353         ELSE
00354            MOVE 0100.0 TO HO-OCCFAC2.
00355      PERFORM  550-COMPUTE-PRORATE
00356      PERFORM  570-MOVE-MSTR-TO-PRNT
00357      PERFORM  800-PRINT-DETAIL
00358      PERFORM  900-WRITE-HOMEOWNER.
00359      SKIP3
00360  550-COMPUTE-PRORATE.
00361      IF  CLS-299-PRESENT
00362        OR
00363          PRORATE-CNTR-ZERO
00364           MOVE 1 TO HO-PRORATE2
00365      ELSE
00366           IF  PRORATE-CNTR-ONE
00367                MOVE  WS-PRORATE TO WS-TOTL-PRORATE
00368                MOVE  WS-TOTL-PRORATER TO HO-PRORATE2
00369           ELSE
00370                COMPUTE  WS-TOTL-PRORATE ROUNDED =
00371                             WS-PRORATE / PRORATE-CNTR
00372                MOVE  WS-TOTL-PRORATER TO HO-PRORATE2.
00373      SKIP3
00374  570-MOVE-MSTR-TO-PRNT.
00375      MOVE  M-VOL TO  D-VOL
00376      MOVE  M-PROP TO D-PROP
00377      MOVE  M-TXCD TO D-TXCD
00378      MOVE  M-CLS TO D-H-CLS
00379      MOVE  ADD-MESG TO D-MESG.
00380      EJECT
00381  600-BYPASS-MSTR-ROUTINE.
00382      IF  MSTR-KEY EQUAL HOME-KEY
00383           MOVE  'N' TO SET-HOME-SW
00384           MOVE  HO-VOL TO D-VOL
00385           MOVE  HO-PROP TO D-PROP
00386           MOVE  HO-TXCD TO D-TXCD
00387           MOVE  HO-CLASS TO D-H-CLS
00388           MOVE  M-CLS TO D-M-CLS
00389           INSPECT  D-M-CLS REPLACING ALL ' ' BY '-'
00390           MOVE  BYPASS-MESG TO D-MESG
00391           PERFORM  800-PRINT-DETAIL.
00392      SKIP3
00393  700-READ-HOMEOWNER.
00394      READ  HOMEOWNER-MAST
00395             AT END  MOVE HIGH-VALUE TO HOME-KEY.
00396      IF  NOT HOME-EOF
00397           ADD +1 TO HOME-READ
00398           MOVE HO-VOL  TO  HK-VOL
00399           MOVE HO-PROP TO  HK-PROP.
00400      SKIP1
00401      MOVE  'Y' TO SET-HOME-SW.
00402      SKIP3
00403  750-READ-MASTER.
00404      READ  MASTER-IN
00405             AT END MOVE HIGH-VALUES TO MSTR-KEY.
00406      IF  NOT MSTR-EOF
00407           ADD +1 TO MSTR-READ
00408           IF  M-RE-VOL
00409                MOVE  M-VOL   TO MK-VOL
00410                MOVE  M-PROP  TO MK-PROP
00411                MOVE  'Y' TO SET-MSTR-SW.
00412      EJECT
00413  800-PRINT-DETAIL.
00414      IF  PRINT-HEADER
00415           PERFORM  870-HDR-ROUTINE.
00416      SKIP1
00417      INSPECT  D-PROP REPLACING ALL SPACES BY '-'
00418      INSPECT  D-H-CLS REPLACING ALL SPACES BY '-'
00419      WRITE  PRNT-REC FROM DETAIL-LINE AFTER ADVANCING   2
00420      ADD  +2 TO LINE-CNTR
00421      MOVE  SPACES TO DETAIL-LINE.
00422      SKIP3
00423  850-PRINT-TOTALS.
00424      IF  PRINT-HEADER
00425           PERFORM 870-HDR-ROUTINE.
00426      SKIP1
00427      MOVE  'MASTER RECORDS READ' TO FIN-MSG
00428      MOVE  MSTR-READ TO  FIN-TOT
00429      WRITE  PRNT-REC FROM FINAL-LINE AFTER ADVANCING   3
00430      MOVE  'HOMEOWNERS RECORDS READ' TO FIN-MSG
00431      MOVE  HOME-READ TO FIN-TOT
00432      WRITE  PRNT-REC FROM FINAL-LINE AFTER ADVANCING   1
00433      MOVE  'HOMEOWNERS RECORDS WRITTEN' TO FIN-MSG
00434      MOVE  HOME-WRITTEN TO  FIN-TOT
00435      WRITE  PRNT-REC FROM FINAL-LINE AFTER ADVANCING   1.
00436      SKIP3
00437  870-HDR-ROUTINE.
00438      ADD  +1 TO PAGE-CNTR
00439      MOVE  PAGE-CNTR TO H1-PAGE
00440      MOVE  +6 TO LINE-CNTR
00441      WRITE  PRNT-REC FROM HDR-LINE1 AFTER ADVANCING   PAGE
00442      WRITE  PRNT-REC FROM HDR-LINE2 AFTER ADVANCING   2
00443      WRITE  PRNT-REC FROM HDR-LINE3 AFTER ADVANCING   2
00444      WRITE  PRNT-REC FROM HDR-LINE4 AFTER ADVANCING   1.
00445      SKIP3
00446  900-WRITE-HOMEOWNER.
00447      WRITE  HO-REC2
00448      ADD  +1 TO HOME-WRITTEN.
